
	.file	"react8.asm"
	.title	"wrester reactions to hits"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"plyr.equ"
	.include	"anim.equ"
	.include	"audit.equ"
	.include	"sound.h"
	.include 	"ring.equ"
	.include	"lifebar.h"



	.ref	bam_2_head_hit_anim,bam_4_head_hit_anim
	.ref	bam_head_hit_dizzy_anim

	.ref	dnk_fall_back_anim,dnk_2_head_hit_anim
	.ref	dnk_head_hit_dizzy_anim,dnk_4_head_hit_anim

	.ref	hrt_fall_back_anim,hrt_4_head_hit_dizzy_anim
	.ref	hrt_4_head_hit_anim,hrt_2_head_hit_anim

	.ref	lex_fall_back_anim,lex_2_head_hit_anim
	.ref	lex_4_head_hit_anim,lex_head_hit_dizzy_anim

	.ref	rzr_fall_back_anim,rzr_2_head_hit_anim
	.ref	rzr_4_head_hit_dizzy_anim,rzr_4_head_hit_anim

	.ref	shn_fall_back_anim,shn_4_head_hit_dizzy_anim
	.ref	shn_2_head_hit_anim,shn_4_head_hit_anim

	.ref	und_fall_back_anim,und_head_hit_dizzy_anim
	.ref	und_2_head_hit_anim,und_4_head_hit_anim

	.ref	yok_fall_back_anim,yok_2_head_hit_anim
	.ref	yok_4_head_hit_dizzy_anim,yok_4_head_hit_anim

	.ref	block_hit_flail
	.ref	change_anim1a,wres_collis_off,block_hit,CALL_NASTY_MOVE
	.ref	set_getup_time
	.ref	wres_slave_anim,hit_flykick,hit_damage_pending





 SUBR	hit_shnbfkik

	;This has to be a separate attack mode so damage can be different,
	; but the reaction is same as for a flying kick.

	jauc	hit_flykick

 SUBR	hit_shnspdkik

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	lxkcnoblock


	calla	block_hit_flail
	rets

lxkcnoblock

	GETLIFE
	jrz	lxkcisdead


	WRSNDX	KICK_L1,KICK_L2


	SETMODE	NORMAL

lxkcdo_hit
	FACE24TBL lxkchead_hit
	calla	change_anim1a
lxkcisdead
	calla	wres_collis_off
	rets

lxkchead_hit
	.long	hrt_2_head_hit_anim,hrt_4_head_hit_anim			;0 Bret Hart
	.long	rzr_2_head_hit_anim,rzr_4_head_hit_anim			;1 Razor Ramon
	.long	und_2_head_hit_anim,und_4_head_hit_anim			;2 Undertaker
	.long	yok_2_head_hit_anim,yok_4_head_hit_anim			;3 Yokozuna
	.long	shn_2_head_hit_anim,shn_4_head_hit_anim			;4 Shawn Michaels
	.long	bam_2_head_hit_anim,bam_4_head_hit_anim			;5 Bam Bam
	.long	dnk_2_head_hit_anim,dnk_4_head_hit_anim			;6 Doink
	.long	dnk_2_head_hit_anim,dnk_4_head_hit_anim			;7 spare
	.long	lex_2_head_hit_anim,lex_4_head_hit_anim			;8 Lex Luger
	.long	dnk_2_head_hit_anim,dnk_4_head_hit_anim			;9 Referee


 SUBR	hit_shnspdkik2

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	afspnoblock

	calla	block_hit_flail
	rets

afspnoblock


	WRSNDX	FLYKICK_L1,FLYKICK_L2
	GETLIFE
	jrz	afspskip



	SETMODE	NORMAL
afspskip
	clr	a0
	move	a0,*a10(OBJ_ZVEL),L	;Zero zvel after collis

	move	a0,*a13(ROLL_POS)
	calla	set_getup_time

	FACETBL	afspfall_back
	calla	change_anim1a


	movi	[4,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	afspfall_right		;attacker on left
	neg	a2
afspfall_right
	move	a2,*a13(OBJ_XVEL),L

afspisdead
	calla	wres_collis_off
	rets

afspfall_back
	.long	hrt_fall_back_anim		;0 Bret Hart
	.long	rzr_fall_back_anim		;1 Razor Ramon
	.long	und_fall_back_anim		;2 Undertaker
	.long	yok_fall_back_anim		;3 Yokozuna
	.long	shn_fall_back_anim		;4 Shawn Michaels
	.ref	bam_fall_back_anim
	.long	bam_fall_back_anim		;5 Bam Bam
	.long	dnk_fall_back_anim		;6 Doink
	.long	dnk_fall_back_anim		;7 spare
	.long	lex_fall_back_anim		;8 Lex Luger
	.long	dnk_fall_back_anim		;9 Referee


 SUBR	hit_hitcheck

	;clear hit_damage_pending, just to be safe.
	clr	a14
	move	a14,@hit_damage_pending

	calla	wres_collis_off
	rets


 SUBR	hit_flyelbow

	;do a normal hit_flykick.  It'll return carry if the victim was
	; blocking, or clear carry if hit hit normally, in which case
	; it will also muck about with our x velocity, which we'll want
	; to undo.
	calla	hit_flykick
	jrc	qyafdone

	;okay, we've hit, done all that stuff, and had our velocity
	; negated and halfed.  negate it again, 'cuz we don't want to
	; bounce off with a flying elbow.  it looks goofy.
	move	*a10(OBJ_XVEL),a14,L
	neg	a14
	move	a14,*a10(OBJ_XVEL),L

qyafdone	rets


	.end

