**************************************************************
*
* Software:	Jason Skiles
* Initiated:	28 Apr 94
*
* COPYRIGHT (C) 1993 WILLIAMS ELECTRONICS GAMES, INC.
*
*.Last mod - 4/28/94 22:12
**************************************************************

	.file	"crowd.asm"
	.title	"crowd stuff"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"audit.equ"
	.include	"macros.h"
	.include	"link.equ"
	.include	"sound.h"

	.INCLUDE	"SOUND.EQU"
	.include	"crowdimg.tbl"
	.include	"crowdimg.glo"
	.include	"bgndtbl.glo"
	.include	"imgtbl.glo"

*****************************************************************************

	.ref	BAKLST,RNDRNG0,RNDPER,START_FLASHES,no_debris,reduce_bog

*****************************************************************************

DEF_ANISPD	equ	8
SPD_FOREVER	equ	7FFFh

CROWD_OBJS	equ	0h
CROWD_NORMANIM	equ	20h
CROWD_CHEERANIM	equ	40h

CANI_GOTO	equ	0 + 8000h
CANI_RNDWAIT	equ	1 + 8000h
CANI_END		equ	2 + 8000H
CANI_CODE	EQU	3 + 8000H
CANI_REPEAT	EQU	4 + 8000H
CANI_SHOULD_REPEAT EQU	5 + 8000H

*STRUCT BOBJANIM
SCPTR	.set	000	;UHL script pointer (next instruction)
TIME	.set	020h	;UHW time to hold
FRAME	.set	030h	;UHL current obj
CHEER1	.set	050h	;UHL cheer1 jump point
CHEER2	.set	070h	;UHL cheer2 jump point
REPEAT_P .SET	090H	;UHL REPEAT POINT TO JUMP BACK TO
REPEAT_N .SET	0B0H	;UHW REPEAT THIS NUMBER OF TIMES
*ENDSTRUCT
BSIZE		equ	REPEAT_N+10h

NUMCROWD	equ	30

HICROWDZ	equ	58/2		;high z (after shift)

	BSSX	CROWDDATA,(NUMCROWD+1)*BSIZE

	BSSX	crowd_process,32

*****************************************************************************
* Main crowd control process
*

;NOTE:  The background stuff is doing SOMETHING with ODXOFF and ODYOFF, so
;       I'm storing anim points in OXVEL and OYVEL instead.  Those seem safe.

 SUBR	crowd_anim

	move	a13,@crowd_process,L

	;initialize
	movi	NUMCROWD,a0
	movi	CROWD_ANIMS,a1
	movi	CROWDDATA,a2
fpclinitloop
	move	*a1+,a3,L
	move	*a3+,a14,W
	move	a14,*a2(TIME),W
	move	*a3+,a14,L
	move	a14,*a2(FRAME),L
	move	a3,*a2(SCPTR),L
	move	*a1+,a14,L
	move	a14,*a2(CHEER1),L
	move	*a1+,a14,L
	move	a14,*a2(CHEER2),L
	CLR	A14
	MOVE	A14,*A2(REPEAT_P),L
	MOVE	A14,*A2(REPEAT_N)
	addi	BSIZE,a2
	dsj	a0,fpclinitloop

	clr	a14
	move	a14,*a2(SCPTR),L

fpclmain_loop
	;if we're in reduce_bog mode, sleep until someone wakes us up.
	movk	1,a0
	move	@reduce_bog,a14
	jrz	fpclslp
	movi	7FFFh,a0
fpclslp	SLEEPR	a0

	;update CROWDDATA
	movi	CROWDDATA-BSIZE,a4
fpclcd_loop
	addi	BSIZE,a4
	move	*a4(SCPTR),a14,L
	jrz	fpclcd_done
	callr	animate_back
	jruc	fpclcd_loop


 SUBR	animate_back
	move	*a4(TIME),a14,W
	dec	a14
	jrz	fpclnext_command
	move	a14,*a4(TIME),W
	MOVK	1,A14
	rets


fpclnext_command
	move	*a4(SCPTR),a5,L
fpclnext_in_the_QUEUE
	move	*a5+,a14,W
	jrn	fpclcani_command
	move	a14,*a4(TIME),W
	move	*a5+,a14,L
	move	a14,*a4(FRAME),L
	move	a5,*a4(SCPTR),L

	;You think a simple CLR would do the job here?  Try it.  I dare you.
	MOVK	1,A14
	DEC	A14
	rets

fpclcani_command
	andi	7FFFh,a14
	X32	a14
	addi	fpclcani_commands,a14
	move	*a14,a14,L
	jump	a14

fpclcani_commands
	.long	fpcl_goto
	.long	fpcl_rndwait
	.LONG	fpcl_END_ANIM
	.LONG	fpcl_CODE
	.LONG	fpcl_RANDOM_REPEAT
	.LONG	fpcl_SHOULD_WE_WE_REPEAT

**********
fpcl_CODE
	MOVE	*A5+,A0,L
	MMTM	SP,A0,A1,A2,A3,A4,A5,A6,A7,A8,A9,A10,A11,A14
	CALL	A0
	MMFM	SP,A0,A1,A2,A3,A4,A5,A6,A7,A8,A9,A10,A11,A14
	jruc	fpclnext_in_the_QUEUE


fpcl_END_ANIM
	MOVI	-1,A14
	RETS

fpcl_goto
	move	*a5+,a5,L
	move	a5,*a4(SCPTR),L
	jruc	fpclnext_command

**********
fpcl_rndwait
	move	*a5+,a0,W
	calla	RNDRNG0
	move	a0,*a4(TIME),W
	move	a5,*a4(SCPTR),L
	rets

fpcl_RANDOM_REPEAT
	MOVE	*A5+,A0
	MOVE	A5,*A4(REPEAT_P),L
	CALLA	RNDRNG0
	MOVE	A0,*A4(REPEAT_N)
	jruc	fpclnext_in_the_QUEUE

fpcl_SHOULD_WE_WE_REPEAT
	MOVE	*A4(REPEAT_N),A0
	DEC	A0
	MOVE	A0,*A4(REPEAT_N)
	JRZ	REACHED_LAST_LOOP
	JRN	REACHED_LAST_LOOP
	MOVE	*A4(REPEAT_P),*A4(SCPTR),L
	MOVE	*A4(REPEAT_P),A5,L
	jruc	fpclnext_in_the_QUEUE
REACHED_LAST_LOOP
	CLR	A0
	MOVE	A0,*A4(REPEAT_N)
	jruc	fpclnext_in_the_QUEUE



fpclcd_done
	.ref	PCNT
	move	@PCNT,a14
	btst	0,a14
	jrz	fpclmain_loop


	;update BAKLST
	move	@BAKLST,a8,L

fpclbakloop
	move	*a8(OZPOS),a0,W
	srl	1,a0
	jrc	fpclbakskip
	cmpi	HICROWDZ,a0
	jrgt	fpclbakskip

	move	a0,a1
	X64	a1
	X128	a0
	add	a1,a0

	addi	CROWDDATA,a0
	move	*a0(FRAME),a0,L
	move	*a8(OIMG),a1,L
	cmp	a0,a1
	jreq	fpclbakskip

;	callr	anibobj
	move	*a8(OCTRL),a1,W

	move	*a8(OXPOS),a2,W
	move	*a8(OXVEL),a3,W
	move	*a0(IANIOFFX),a4,W
	clr	a5
	btst	B_FLIPH,a1
	jrz	fpclnoflip

	;obj is horz-flipped.  negate the Xanim point and adjust X pos
	; by XSZold-XSZnew
	neg	a4
	move	*a8(OSIZEX),a5,W
	move	*a0(ISIZEX),a14,W
	sub	a14,a5
fpclnoflip
	add	a3,a2
	sub	a4,a2
	add	a5,a2
	move	a2,*a8(OXPOS),W
	move	a4,*a8(OXVEL),W


	move	*a8(OYPOS),a2,W
	move	*a8(OYVEL),a3,W
	move	*a0(IANIOFFY),a4,W
	add	a3,a2
	sub	a4,a2
	move	a2,*a8(OYPOS),W
	move	a4,*a8(OYVEL),W

	move	a0,a2
	move	a1,a3

	move	a2,*a8(OIMG),L
	move	*a2(0),*a8(OSIZE),L
	move	*a2(ISAG),*a8(OSAG),L

	setf	5,0,0
	move	*a2(ICTRL+7),*a8(OCTRL+7) ;Write 5 z comp bits
;	setf	6,0,0
;	move	a3,*a8(OCTRL)		;Write 6 low bits
	setf	16,1,0
fpclbakskip
	move	*a8,a8,L
 	jrnz	fpclbakloop


	jruc	fpclmain_loop


;crowd anim starting points
CROWD_ANIMS
	.long	fpcljasoncrd_norm, fpcljasoncrd_cheer1,fpcljasoncrd_cheer2
	.long	fpcltonycrd_norm,  fpcltonycrd_cheer1 ,fpcltonycrd_cheer1
	.long	fpclvogel_norm,    fpclvogel_cheer1	 ,fpclvogel_cheer2
	.long	fpcltobias_norm,   fpcltobias_cheer1	 ,fpcltobias_cheer2
	.long	fpclalex_norm,     fpclalex_cheer1	 ,fpclalex_cheer2

	.long	fpclmarine_norm,   fpclmarine_cheer1	 ,fpclmarine_cheer2
	.long	fpcljake_norm,     fpcljake_cheer1	 ,fpcljake_cheer2
	.long	fpcljohnc_norm,    fpcljohnc_cheer1	 ,fpcljohnc_cheer2
	.long	fpcldewey_norm,    fpcldewey_cheer1	 ,fpcldewey_cheer2
	.long	fpcljason_norm,    fpcljason_cheer1	 ,fpcljason_cheer2

	.long	fpcljamie_norm,    fpcljamie_cheer1	 ,fpcljamie_cheer2
	.long	fpclsheridan_norm, fpclsheridan_cheer1,fpclsheridan_cheer2
	.long	fpclsal_norm,      fpclsal_cheer1	 ,fpclsal_cheer2
	.long	fpcljosh_norm,     fpcljosh_cheer1	 ,fpcljosh_cheer2
	.long	fpclmark_norm,     fpclmark_cheer1	 ,fpclmark_cheer1

	.long	fpcltony_norm,     fpcltony_cheer1	 ,fpcltony_cheer2
	.long	fpclcarlos_norm,   fpclcarlos_cheer1	 ,fpclcarlos_cheer2
	.long	fpclnewc_norm,     fpclnewc_cheer1	 ,fpclnewc_cheer2
	.long	fpcljoy_norm,      fpcljoy_cheer1	 ,fpcljoy_cheer1
	.long	fpclside1_norm,    fpclside1_cheer1	 ,fpclside1_cheer1

	.long	fpclside2_norm,    fpclside2_cheer1	 ,fpclside2_cheer1
	.long	fpclside3_norm,    fpclside3_cheer1	 ,fpclside3_cheer1
	.long	fpclgeorge_norm,   fpclgeorge_cheer1	 ,fpclgeorge_cheer1
	.long	fpcldave_norm,     fpcldave_cheer1	 ,fpcldave_cheer1
	.long	fpclsophia_norm,   fpclsophia_cheer1	 ,fpclsophia_cheer2

	.long	fpclmancow_norm,   fpclmancow_cheer1	 ,fpclmancow_cheer2
	.long	fpcljak2_norm,	fpcljak2_cheer1	 ,fpcljak2_cheer2
	.long	fpcleugene_norm,	fpcleugene_cheer1	 ,fpcleugene_cheer2
	.long	fpclpenacho_norm,	fpclpenacho_cheer1 ,fpclpenacho_cheer2
	.long	fpcljenni_norm,	fpcljenni_cheer1 	 ,fpcljenni_cheer2


**********
* SOPHIA

fpclSOPHIA_SPD	equ	7

fpclsophia_norm
	WL	SPD_FOREVER,SOFI1
	WL	CANI_GOTO,fpclsophia_norm

fpclsophia_cheer1
	WL	fpclSOPHIA_SPD,SOFI2
	WL	fpclSOPHIA_SPD,SOFI3
	WL	fpclSOPHIA_SPD,SOFI4
	WL	fpclSOPHIA_SPD,SOFI3
	WL	fpclSOPHIA_SPD,SOFI2
	WL	CANI_GOTO,fpclsophia_norm

fpclsophia_cheer2
	WL	fpclSOPHIA_SPD,SOFI2
	.WORD	CANI_REPEAT,4
	WL	fpclSOPHIA_SPD,SOFI3
	WL	fpclSOPHIA_SPD,SOFI4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclSOPHIA_SPD,SOFI3
	WL	fpclSOPHIA_SPD,SOFI2
	WL	CANI_GOTO,fpclsophia_norm

**********
* MANCOW

fpclMANCOW_SPD	equ	6

fpclmancow_norm
	WL	SPD_FOREVER,MANC1
	WL	CANI_GOTO,fpclmancow_norm

fpclmancow_cheer1
	WL	fpclMANCOW_SPD,MANC2
	WL	fpclMANCOW_SPD,MANC3
	WL	fpclMANCOW_SPD,MANC4
	WL	fpclMANCOW_SPD,MANC5
	WL	fpclMANCOW_SPD,MANC4
	WL	fpclMANCOW_SPD,MANC3
	WL	fpclMANCOW_SPD,MANC2
	WL	CANI_GOTO,fpclmancow_norm

fpclmancow_cheer2
	WL	fpclMANCOW_SPD,MANC2
	.WORD	CANI_REPEAT,6
	WL	fpclMANCOW_SPD,MANC3
	WL	fpclMANCOW_SPD,MANC4
	WL	fpclMANCOW_SPD,MANC5
	WL	fpclMANCOW_SPD,MANC4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclMANCOW_SPD,MANC3
	WL	fpclMANCOW_SPD,MANC2
	WL	CANI_GOTO,fpclmancow_norm

**********
* JAK

fpclJAK_SPD	equ	7

fpcljak2_norm
	WL	SPD_FOREVER,JAK1
	WL	CANI_GOTO,fpcljak2_norm

fpcljak2_cheer1
	WL	fpclJAK_SPD,JAK2
	WL	fpclJAK_SPD,JAK3
	WL	fpclJAK_SPD,JAK4
	WL	fpclJAK_SPD+3,JAK5
	WL	fpclJAK_SPD,JAK4
	WL	fpclJAK_SPD,JAK3
	WL	fpclJAK_SPD,JAK2
	WL	CANI_GOTO,fpcljak2_norm

fpcljak2_cheer2
	WL	fpclJAK_SPD,JAK2
	.WORD	CANI_REPEAT,7
	WL	fpclJAK_SPD,JAK3
	WL	fpclJAK_SPD,JAK4
	WL	fpclJAK_SPD+3,JAK5
	WL	fpclJAK_SPD,JAK4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclJAK_SPD,JAK3
	WL	fpclJAK_SPD,JAK2
	WL	CANI_GOTO,fpcljak2_norm


**********
* EUGENE

fpclEUGENE_SPD	equ	7

fpcleugene_norm
	WL	SPD_FOREVER,EUG1
	WL	CANI_GOTO,fpcleugene_norm

fpcleugene_cheer1
	WL	fpclEUGENE_SPD,EUG2
	WL	fpclEUGENE_SPD,EUG3
	WL	fpclEUGENE_SPD,EUG4
	WL	fpclEUGENE_SPD,EUG3
	WL	fpclEUGENE_SPD,EUG2
	WL	CANI_GOTO,fpcleugene_norm

fpcleugene_cheer2
	WL	fpclEUGENE_SPD,EUG2
	.WORD	CANI_REPEAT,3
	WL	fpclEUGENE_SPD,EUG3
	WL	fpclEUGENE_SPD,EUG4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclEUGENE_SPD,EUG3
	WL	fpclEUGENE_SPD,EUG2
	WL	CANI_GOTO,fpcleugene_norm

**********
* JENNI

fpclJENNI_SPD	equ	7

fpcljenni_norm
	WL	SPD_FOREVER,JENI1
	WL	CANI_GOTO,fpcljenni_norm

fpcljenni_cheer1
	WL	fpclJENNI_SPD,JENI2
	WL	fpclJENNI_SPD,JENI3
	WL	fpclJENNI_SPD,JENI4
	WL	fpclJENNI_SPD,JENI3
	WL	fpclJENNI_SPD,JENI2
	WL	CANI_GOTO,fpcljenni_norm

fpcljenni_cheer2
	WL	fpclJENNI_SPD,JENI2
	.WORD	CANI_REPEAT,3
	WL	fpclJENNI_SPD,JENI3
	WL	fpclJENNI_SPD,JENI4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclJENNI_SPD,JENI3
	WL	fpclJENNI_SPD,JENI2
	WL	CANI_GOTO,fpcljenni_norm

**********
* PENACHO

fpclPENACHO_SPD	equ	7

fpclpenacho_norm
	WL	SPD_FOREVER,PENA1
	WL	CANI_GOTO,fpclpenacho_norm

fpclpenacho_cheer1
	WL	fpclPENACHO_SPD,PENA2
	WL	fpclPENACHO_SPD,PENA3
	WL	fpclPENACHO_SPD,PENA4
	WL	fpclPENACHO_SPD,PENA3
	WL	fpclPENACHO_SPD,PENA2
	WL	CANI_GOTO,fpclpenacho_norm

fpclpenacho_cheer2
	.WORD	CANI_REPEAT,4
	WL	fpclPENACHO_SPD,PENA2
	WL	fpclPENACHO_SPD,PENA3
	WL	fpclPENACHO_SPD,PENA4
	WL	fpclPENACHO_SPD,PENA3
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclPENACHO_SPD,PENA2
	WL	CANI_GOTO,fpclpenacho_norm




**********
* Alex

fpclALEX_SPD	equ	6

fpclalex_norm
	WL	SPD_FOREVER,ALEX1
	WL	CANI_GOTO,fpclalex_norm

fpclalex_cheer1
	WL	fpclALEX_SPD,ALEX2
	WL	fpclALEX_SPD,ALEX3
	WL	fpclALEX_SPD,ALEX4
	WL	fpclALEX_SPD,ALEX2
	WL	CANI_GOTO,fpclalex_norm
fpclalex_cheer2
	WL	fpclALEX_SPD,ALEX2
	WL	fpclALEX_SPD,ALEX3
	.WORD	CANI_REPEAT,7
	WL	fpclALEX_SPD,ALEX4
	WL	fpclALEX_SPD,ALEX3
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclALEX_SPD,ALEX2
	WL	CANI_GOTO,fpclalex_norm

**********
* Carlos

fpclCARLOS_SPD	equ	7

fpclcarlos_norm
	WL	SPD_FOREVER,CARLO1
	WL	CANI_GOTO,fpclcarlos_norm

fpclcarlos_cheer1
	WL	fpclCARLOS_SPD,CARLO2
	WL	fpclCARLOS_SPD,CARLO3
	WL	fpclCARLOS_SPD,CARLO4
	WL	fpclCARLOS_SPD,CARLO2
	WL	CANI_GOTO,fpclcarlos_norm

fpclcarlos_cheer2
	WL	fpclCARLOS_SPD,CARLO2
	WL	fpclCARLOS_SPD,CARLO3
	WL	fpclCARLOS_SPD,CARLO4
	.WORD	CANI_REPEAT,7
	WL	fpclCARLOS_SPD,CARLO3
	WL	fpclCARLOS_SPD,CARLO4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclCARLOS_SPD,CARLO2
	WL	CANI_GOTO,fpclcarlos_norm

**********
* Dave

fpclDAVE_SPD	equ	8

fpcldave_norm
	WL	SPD_FOREVER,DAVEMSK1
	WL	CANI_GOTO,fpcldave_norm

fpcldave_cheer1
	WL	fpclDAVE_SPD,DAVEMSK2
	WL	fpclDAVE_SPD,DAVEMSK3
	WL	fpclDAVE_SPD,DAVEMSK4
	WL	fpclDAVE_SPD,DAVEMSK3
	WL	fpclDAVE_SPD,DAVEMSK2
	WL	CANI_GOTO,fpcldave_norm

fpcldave_cheer2
	.WORD	CANI_REPEAT,7
	WL	fpclDAVE_SPD,DAVEMSK2
	WL	fpclDAVE_SPD,DAVEMSK3
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclDAVE_SPD,DAVEMSK4
	WL	fpclDAVE_SPD,DAVEMSK3
	WL	fpclDAVE_SPD,DAVEMSK2
	WL	CANI_GOTO,fpcldave_norm

**********
* Dewey

fpclDEWEY_SPD	equ	7

fpcldewey_norm
	WL	fpclDEWEY_SPD,DEWY1
	.word	CANI_RNDWAIT,TSEC*10
	WL	fpclDEWEY_SPD,DEWY2
	WL	fpclDEWEY_SPD,DEWY3
	WL	fpclDEWEY_SPD,DEWY4
	WL	fpclDEWEY_SPD,DEWY3
	WL	fpclDEWEY_SPD,DEWY2
	WL	CANI_GOTO,fpcldewey_norm

fpcldewey_cheer1
	WL	fpclDEWEY_SPD,DEWY1
	WL	fpclDEWEY_SPD,DEWY2
	WL	fpclDEWEY_SPD,DEWY3
	WL	fpclDEWEY_SPD,DEWY4
	WL	fpclDEWEY_SPD,DEWY3
	WL	fpclDEWEY_SPD,DEWY2
	WL	CANI_GOTO,fpcldewey_norm

fpcldewey_cheer2
	WL	fpclDEWEY_SPD,DEWY1
	.WORD	CANI_REPEAT,7
	WL	fpclDEWEY_SPD,DEWY2
	WL	fpclDEWEY_SPD,DEWY3
	WL	fpclDEWEY_SPD,DEWY4
	WL	fpclDEWEY_SPD,DEWY3
	WL	fpclDEWEY_SPD,DEWY2
	WL	fpclDEWEY_SPD,DEWY3
	WL	fpclDEWEY_SPD,DEWY4
	WL	fpclDEWEY_SPD,DEWY3
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclDEWEY_SPD,DEWY2
	WL	CANI_GOTO,fpcldewey_norm

**********
* George

fpclGEORGE_SPD	equ	8

fpclgeorge_norm
	WL	fpclGEORGE_SPD,GEORGE1
	.word	CANI_RNDWAIT,TSEC*4
	WL	fpclGEORGE_SPD,GEORGE2
	WL	fpclGEORGE_SPD,GEORGE3
	WL	fpclGEORGE_SPD,GEORGE4
	WL	fpclGEORGE_SPD,GEORGE3
	WL	fpclGEORGE_SPD,GEORGE2
	WL	CANI_GOTO,fpclgeorge_norm

fpclgeorge_cheer1
	.WORD	CANI_REPEAT,2
	WL	fpclGEORGE_SPD,GEORGE1
	WL	fpclGEORGE_SPD,GEORGE2
	WL	fpclGEORGE_SPD,GEORGE3
	WL	fpclGEORGE_SPD,GEORGE4
	WL	fpclGEORGE_SPD,GEORGE3
	WL	fpclGEORGE_SPD,GEORGE2
	.WORD	CANI_SHOULD_REPEAT
	WL	CANI_GOTO,fpclgeorge_norm

**********
* Jake

fpclJAKE_SPD	equ	8

fpcljake_norm
	WL	SPD_FOREVER,JAKE1
	WL	CANI_GOTO,fpcljake_norm

fpcljake_cheer1
	WL	fpclJAKE_SPD,JAKE2
	WL	fpclJAKE_SPD,JAKE3
	WL	fpclJAKE_SPD,JAKE4
	WL	fpclJAKE_SPD,JAKE2
	WL	CANI_GOTO,fpcljake_norm

fpcljake_cheer2
	WL	fpclJAKE_SPD,JAKE2
	.WORD	CANI_REPEAT,4
	WL	fpclJAKE_SPD,JAKE3
	WL	fpclJAKE_SPD,JAKE4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclJAKE_SPD,JAKE2
	WL	CANI_GOTO,fpcljake_norm

**********
* Jamie

fpclJAMIE_SPD	equ	5

fpcljamie_norm
	WL	SPD_FOREVER,JAMIE1
	WL	CANI_GOTO,fpcljamie_norm

fpcljamie_cheer1
	WL	fpclJAMIE_SPD,JAMIE2
	WL	fpclJAMIE_SPD,JAMIE3
	WL	fpclJAMIE_SPD,JAMIE4
	WL	fpclJAMIE_SPD,JAMIE5
	WL	fpclJAMIE_SPD,JAMIE6
	WL	CANI_GOTO,fpcljamie_norm

fpcljamie_cheer2
	.WORD	CANI_REPEAT,7
	WL	fpclJAMIE_SPD,JAMIE2
	WL	fpclJAMIE_SPD,JAMIE3
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclJAMIE_SPD,JAMIE4
	WL	fpclJAMIE_SPD,JAMIE5
	WL	fpclJAMIE_SPD,JAMIE6
	WL	CANI_GOTO,fpcljamie_norm

**********
* Jason

fpclJASON_SPD	equ	6

fpcljason_norm
	WL	SPD_FOREVER,SERG1
	WL	CANI_GOTO,fpcljason_norm

fpcljason_cheer1
	WL	fpclJASON_SPD,SERG2
	WL	fpclJASON_SPD,SERG3
	WL	fpclJASON_SPD,SERG4
	WL	fpclJASON_SPD,SERG5
	WL	fpclJASON_SPD,SERG4
	WL	fpclJASON_SPD,SERG3
	WL	fpclJASON_SPD,SERG2
	WL	CANI_GOTO,fpcljason_norm

fpcljason_cheer2
	WL	fpclJASON_SPD,SERG2
	WL	fpclJASON_SPD,SERG3
	.WORD	CANI_REPEAT,7
	WL	fpclJASON_SPD,SERG4
	WL	fpclJASON_SPD,SERG5
	WL	fpclJASON_SPD,SERG4
	WL	fpclJASON_SPD,SERG3
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclJASON_SPD,SERG2
	WL	CANI_GOTO,fpcljason_norm


**********
* Jason crowd

fpclJCROWD_SPD	equ	8

fpcljasoncrd_norm
	WL	SPD_FOREVER,JASONCRD1
	WL	CANI_GOTO,fpcljasoncrd_norm

fpcljasoncrd_cheer1
	WL	fpclJCROWD_SPD,JASONCRD2
	WL	fpclJCROWD_SPD,JASONCRD3
	WL	fpclJCROWD_SPD,JASONCRD4
	WL	fpclJCROWD_SPD,JASONCRD5
	WL	CANI_GOTO,fpcljasoncrd_norm

fpcljasoncrd_cheer2
	WL	fpclJCROWD_SPD,JASONCRD2
	WL	fpclJCROWD_SPD,JASONCRD3
	.WORD	CANI_REPEAT,7
	WL	fpclJCROWD_SPD,JASONCRD4
	WL	fpclJCROWD_SPD,JASONCRD5
	.WORD	CANI_SHOULD_REPEAT
	WL	CANI_GOTO,fpcljasoncrd_norm


**********
* JohnC

fpclJOHNC_SPD	equ	8

fpcljohnc_norm
	WL	SPD_FOREVER,JOHNC1
	WL	CANI_GOTO,fpcljohnc_norm

fpcljohnc_cheer1
	WL	fpclJOHNC_SPD,JOHNC2
	WL	fpclJOHNC_SPD,JOHNC3
	WL	fpclJOHNC_SPD*3,JOHNC4
	WL	fpclJOHNC_SPD,JOHNC3
	WL	fpclJOHNC_SPD,JOHNC2
	WL	CANI_GOTO,fpcljohnc_norm

fpcljohnc_cheer2
	WL	fpclJOHNC_SPD,JOHNC2
	WL	fpclJOHNC_SPD,JOHNC3
	WL	fpclJOHNC_SPD,JOHNC4
	WL	fpclJOHNC_SPD,JOHNC3
	WL	fpclJOHNC_SPD*3,JOHNC4
	WL	fpclJOHNC_SPD,JOHNC3
	WL	fpclJOHNC_SPD,JOHNC2
	WL	CANI_GOTO,fpcljohnc_norm

**********
* Josh

fpclJOSH_SPD	equ	7

fpcljosh_norm
	WL	SPD_FOREVER,JOSH1
	WL	CANI_GOTO,fpcljosh_norm

fpcljosh_cheer1
	WL	fpclJOSH_SPD,JOSH2
	WL	fpclJOSH_SPD,JOSH3
	WL	fpclJOSH_SPD,JOSH4
	WL	fpclJOSH_SPD,JOSH3
	WL	fpclJOSH_SPD,JOSH4
	WL	fpclJOSH_SPD,JOSH2
	WL	CANI_GOTO,fpcljosh_norm

fpcljosh_cheer2
	WL	fpclJOSH_SPD,JOSH2
	WL	fpclJOSH_SPD,JOSH3
	WL	fpclJOSH_SPD,JOSH4
	.WORD	CANI_REPEAT,6
	WL	fpclJOSH_SPD,JOSH3
	WL	fpclJOSH_SPD,JOSH4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclJOSH_SPD,JOSH2
	WL	CANI_GOTO,fpcljosh_norm


**********
* Joy (side crowd)

fpclJOY_SPD	equ	8

fpcljoy_norm
	WL	SPD_FOREVER,JOY1
	WL	CANI_GOTO,fpcljoy_norm
fpcljoy_cheer1
	WL	fpclJOY_SPD,JOY1
	WL	fpclJOY_SPD,JOY2
	WL	fpclJOY_SPD,JOY3
	WL	fpclJOY_SPD,JOY4
	WL	fpclJOY_SPD,JOY5
	WL	fpclJOY_SPD,JOY4
	WL	fpclJOY_SPD,JOY3
	WL	CANI_GOTO,fpcljoy_norm

**********
* Marine

fpclMARINE_SPD	equ	8

fpclmarine_norm
	WL	SPD_FOREVER,MARINE1
	WL	CANI_GOTO,fpclmarine_norm

fpclmarine_cheer1
	WL	fpclMARINE_SPD,MARINE2
	WL	fpclMARINE_SPD,MARINE3
	WL	fpclMARINE_SPD,MARINE4
	WL	fpclMARINE_SPD,MARINE2
	WL	CANI_GOTO,fpclmarine_norm

fpclmarine_cheer2
	WL	fpclMARINE_SPD,MARINE2
	.WORD	CANI_REPEAT,6
	WL	fpclMARINE_SPD,MARINE3
	WL	fpclMARINE_SPD,MARINE4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclMARINE_SPD,MARINE3
	WL	fpclMARINE_SPD,MARINE2
	WL	CANI_GOTO,fpclmarine_norm

**********
* Mark

fpclMARK_SPD	equ	8

fpclmark_norm
	WL	SPD_FOREVER,TURMEL1
	WL	CANI_GOTO,fpclmark_norm

fpclmark_cheer1
	WL	fpclMARK_SPD,TURMEL2
	WL	fpclMARK_SPD,TURMEL3
	WL	20,TURMEL4
	WL	fpclMARK_SPD,TURMEL3
	WL	fpclMARK_SPD,TURMEL2
	WL	CANI_GOTO,fpclmark_norm

**********
* Newc

fpclNEWC_SPD	equ	9

fpclnewc_norm
	WL	SPD_FOREVER,NEWCOM1
	WL	CANI_GOTO,fpclnewc_norm

fpclnewc_cheer1
	WL	fpclNEWC_SPD,NEWCOM2
	WL	fpclNEWC_SPD,NEWCOM3
	WL	fpclNEWC_SPD,NEWCOM4
	WL	fpclNEWC_SPD,NEWCOM3
	WL	fpclNEWC_SPD,NEWCOM4
	WL	fpclNEWC_SPD,NEWCOM3
	WL	fpclNEWC_SPD,NEWCOM2
	WL	CANI_GOTO,fpclnewc_norm

fpclnewc_cheer2
	WL	fpclNEWC_SPD,NEWCOM2
	.WORD	CANI_REPEAT,6
	WL	fpclNEWC_SPD,NEWCOM3
	WL	fpclNEWC_SPD,NEWCOM4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclNEWC_SPD,NEWCOM2
	WL	CANI_GOTO,fpclnewc_norm

**********
* Sal

fpclSAL_SPD	equ	8

fpclsal_norm
	WL	SPD_FOREVER,SAL1
	WL	CANI_GOTO,fpclsal_norm

fpclsal_cheer1
	WL	fpclSAL_SPD,SAL2
	WL	fpclSAL_SPD,SAL3
	WL	fpclSAL_SPD,SAL4
	WL	fpclSAL_SPD,SAL3
	WL	fpclSAL_SPD,SAL4
	WL	fpclSAL_SPD,SAL3
	WL	fpclSAL_SPD,SAL2
	WL	CANI_GOTO,fpclsal_norm

fpclsal_cheer2
	WL	fpclSAL_SPD,SAL2
	.WORD	CANI_REPEAT,7
	WL	fpclSAL_SPD,SAL3
	WL	fpclSAL_SPD,SAL4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclSAL_SPD,SAL2
	WL	CANI_GOTO,fpclsal_norm

**********
* Sheridan

fpclSHERIDAN_SPD	equ	8

fpclsheridan_norm
	WL	1,SHERID1
	.word	CANI_RNDWAIT,TSEC*20
	WL	fpclSHERIDAN_SPD,SHERID6
	WL	fpclSHERIDAN_SPD,SHERID7
	WL	fpclSHERIDAN_SPD*2,SHERID8
	WL	fpclSHERIDAN_SPD,SHERID7
	WL	fpclSHERIDAN_SPD,SHERID6
	WL	CANI_GOTO,fpclsheridan_norm

fpclsheridan_cheer1
	WL	fpclSHERIDAN_SPD,SHERID1
	WL	fpclSHERIDAN_SPD,SHERID2
	WL	fpclSHERIDAN_SPD,SHERID3
	WL	fpclSHERIDAN_SPD,SHERID4
	WL	fpclSHERIDAN_SPD,SHERID5
	WL	fpclSHERIDAN_SPD,SHERID6
	WL	fpclSHERIDAN_SPD,SHERID7
	WL	fpclSHERIDAN_SPD*2,SHERID8
	WL	fpclSHERIDAN_SPD,SHERID7
	WL	fpclSHERIDAN_SPD,SHERID6
	WL	CANI_GOTO,fpclsheridan_norm

fpclsheridan_cheer2
	WL	fpclSHERIDAN_SPD,SHERID1
	WL	fpclSHERIDAN_SPD,SHERID2
	WL	fpclSHERIDAN_SPD,SHERID3
	.WORD	CANI_REPEAT,3
	WL	fpclSHERIDAN_SPD,SHERID4
	WL	fpclSHERIDAN_SPD,SHERID5
	.WORD	CANI_SHOULD_REPEAT
	WL	CANI_GOTO,fpclsheridan_norm

**********
* Side crowd

fpclSDCRD_SPD	equ	8

fpclside1_norm
	WL	SPD_FOREVER,SDCRD1A
	WL	CANI_GOTO,fpclside1_norm

fpclside1_cheer1
	WL	fpclSDCRD_SPD,SDCRD2A
	WL	fpclSDCRD_SPD,SDCRD3A
	WL	fpclSDCRD_SPD,SDCRD4A
	WL	fpclSDCRD_SPD,SDCRD3A
	WL	fpclSDCRD_SPD,SDCRD2A
	WL	CANI_GOTO,fpclside1_norm

fpclside2_norm
	WL	SPD_FOREVER,SDCRD1B
	WL	CANI_GOTO,fpclside2_norm

fpclside2_cheer1
	WL	fpclSDCRD_SPD,SDCRD2B
	WL	fpclSDCRD_SPD,SDCRD3B
	WL	fpclSDCRD_SPD,SDCRD4B
	WL	fpclSDCRD_SPD,SDCRD3B
	WL	fpclSDCRD_SPD,SDCRD2B
	WL	CANI_GOTO,fpclside2_norm

fpclside3_norm
	WL	SPD_FOREVER,SDCRD1C
	WL	CANI_GOTO,fpclside3_norm

fpclside3_cheer1
	WL	fpclSDCRD_SPD,SDCRD2C
	WL	fpclSDCRD_SPD,SDCRD3C
	WL	fpclSDCRD_SPD,SDCRD4C
	WL	fpclSDCRD_SPD,SDCRD3C
	WL	fpclSDCRD_SPD,SDCRD2C
	WL	CANI_GOTO,fpclside3_norm

**********
* Tobias

fpclTOBIAS_SPD	equ	7

fpcltobias_norm
	WL	SPD_FOREVER,TOBIAS1
	WL	CANI_GOTO,fpcltobias_norm

fpcltobias_cheer1
	WL	fpclTOBIAS_SPD,TOBIAS2
	WL	fpclTOBIAS_SPD,TOBIAS3
	WL	fpclTOBIAS_SPD,TOBIAS4
	WL	fpclTOBIAS_SPD,TOBIAS2
	WL	CANI_GOTO,fpcltobias_norm

fpcltobias_cheer2
	WL	fpclTOBIAS_SPD,TOBIAS2
	.WORD	CANI_REPEAT,7
	WL	fpclTOBIAS_SPD,TOBIAS3
	WL	fpclTOBIAS_SPD,TOBIAS4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclTOBIAS_SPD,TOBIAS2
	WL	CANI_GOTO,fpcltobias_norm

**********
* Tony

fpclTONY_SPD	equ	7

fpcltony_norm
	WL	SPD_FOREVER,CRAZY1
	WL	CANI_GOTO,fpcltony_norm

fpcltony_cheer1
	WL	fpclTONY_SPD,CRAZY2
	WL	fpclTONY_SPD,CRAZY3
	WL	fpclTONY_SPD,CRAZY4
	WL	fpclTONY_SPD,CRAZY5
	WL	fpclTONY_SPD,CRAZY4
	WL	fpclTONY_SPD,CRAZY5
	WL	fpclTONY_SPD,CRAZY4
	WL	fpclTONY_SPD,CRAZY3
	WL	fpclTONY_SPD,CRAZY2
	WL	CANI_GOTO,fpcltony_norm

fpcltony_cheer2
	WL	fpclTONY_SPD,CRAZY2
	WL	fpclTONY_SPD,CRAZY3
	.WORD	CANI_REPEAT,4
	WL	fpclTONY_SPD,CRAZY4
	WL	fpclTONY_SPD,CRAZY5
	WL	fpclTONY_SPD,CRAZY4
	WL	fpclTONY_SPD,CRAZY3
	WL	fpclTONY_SPD,CRAZY4
	WL	fpclTONY_SPD,CRAZY5
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclTONY_SPD,CRAZY4
	WL	fpclTONY_SPD,CRAZY3
	WL	fpclTONY_SPD,CRAZY2
	WL	CANI_GOTO,fpcltony_norm


**********
* Tony crowd

fpclTONYC_SPD	equ	8

fpcltonycrd_norm
	WL	SPD_FOREVER,TONYCRD1
	WL	CANI_GOTO,fpcltonycrd_norm

fpcltonycrd_cheer1
	WL	fpclTONYC_SPD,TONYCRD2
	WL	fpclTONYC_SPD,TONYCRD3
	WL	fpclTONYC_SPD,TONYCRD4
	WL	fpclTONYC_SPD,TONYCRD5
	WL	CANI_GOTO,fpcltonycrd_norm

**********
* Vogel

fpclVOGEL_SPD	equ	8

fpclvogel_norm
	WL	SPD_FOREVER,VOGEL1
	WL	CANI_GOTO,fpclvogel_norm

fpclvogel_cheer1
	WL	fpclVOGEL_SPD,VOGEL2
	WL	fpclVOGEL_SPD,VOGEL3
	WL	fpclVOGEL_SPD,VOGEL4
	WL	fpclVOGEL_SPD,VOGEL3
	WL	fpclVOGEL_SPD,VOGEL2
	WL	CANI_GOTO,fpclvogel_norm

fpclvogel_cheer2
	WL	fpclVOGEL_SPD,VOGEL2
	.WORD	CANI_REPEAT,7
	WL	fpclVOGEL_SPD,VOGEL3
	WL	fpclVOGEL_SPD,VOGEL4
	.WORD	CANI_SHOULD_REPEAT
	WL	fpclVOGEL_SPD,VOGEL2
	WL	CANI_GOTO,fpclvogel_norm

 SUBR	DO_CROWD_CHEER
	MOVK	C_OVERIDE|C_LONG,A3
	CLR	A4

*****************************************************************************
* Causes the crowd to cheer.

;INPUT A3 = BIT 0 1=LONG OR 0=SHORT TYPE OF ANIMATION
;           BIT 1 1=OVERRIDE WHAT IS ALREADY GOING ON OR NOT
;      	   BIT 2 1=DO RANDOM NUMBER OF PEOPLE IN AUDIENCE 
;      A4 = RNDPER VALUE IF BIT 2 SET IN A3
 SUBR	crowd_cheer
		   
	CLR	A7
	BTST	B_RANDOM,A3
	JRZ	NO_RANDOMNESS
	MOVE	A4,A7
NO_RANDOMNESS
	movi	CROWDDATA,a4
	movi	NUMCROWD,a5
	movk	1,a6
fpclloop
	MOVE	*A4(CHEER1),A1,L
	CMPI	fpclside2_cheer1,A1
	JREQ	DO_NEXT_PERSON
	CMPI	fpclside3_cheer1,A1
	JREQ	DO_NEXT_PERSON
	CMPI	fpcldave_cheer1,A1
	JREQ	DO_NEXT_PERSON
	MOVE	A7,A0
	JRZ	DO_THEM_ALL
	CALLA	RNDPER
	JRLS	DO_NEXT_PERSON
DO_THEM_ALL
	BTST	B_OVERRIDE,A3
	JRNZ	JUST_DO_IT_ANYWAY
	MOVE	*A4(CHEER1),A8,L
	MOVE	*A4(SCPTR),A9,L
	CMP	A8,A9
	JRGE	DO_NEXT_PERSON
JUST_DO_IT_ANYWAY
	move	*a4(CHEER1),*a4(SCPTR),L
	BTST	B_L_OR_S,A3
	JRZ	OKAY_DO_SHORT
	move	*a4(CHEER2),*a4(SCPTR),L
OKAY_DO_SHORT
	move	a6,*a4(TIME),W

;MAKE SURE WE DO ALL THE SIDE CROWD AT ONCE
	move	*a4(CHEER1),A1,L
	CMPI	fpclside1_cheer1,A1
	JRNE	DO_NEXT_PERSON
	addi	BSIZE,a4
	move	*a4(CHEER1),*a4(SCPTR),L
	move	a6,*a4(TIME),W
	addi	BSIZE,a4
	move	*a4(CHEER1),*a4(SCPTR),L
	move	a6,*a4(TIME),W
	addi	BSIZE*2,a4
	move	*a4(CHEER1),*a4(SCPTR),L
	move	a6,*a4(TIME),W

	;a5 is the loop counter...gotta dec it any time we add to a4.
	subk	4,a5
DO_NEXT_PERSON
	addi	BSIZE,a4
	dsj	a5,fpclloop
	
	CALLA	START_FLASHES

	rets


******************************************************************************

	.end

