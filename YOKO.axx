
	.file	"yoko.asm"
	.title	"Yokozuna specific game code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"jjxm.h"
	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"plyr.equ"
	.include	"anim.equ"
	.include	"audit.equ"
	.include	"sound.h"
	.include 	"ring.equ"
	.include	"lifebar.h"
	.include	"damage.equ"



	.ref	change_anim1a,change_anim2a,bounce_off_ropes
	.ref	check_secret_moves,climb_turnbuckle,raisearm_check
	.ref	execute_walk,get_punch_dtime,get_opp_plyrmode
	.ref	keep_attached,change_anim1,mode_dead,round_award
	.ref	get_opp_process,ck_ignore,do_taunt,process_ptrs
	.ref	FIND_AND_KILL_ENDLESS,JUMP_ROPES,CHECK_COMBO_GO
	.ref	yok_combo_jabs_anim,yok_combo_kick_anim,set_raisearm_bit
	.ref	BLOCK_WOOSH,drone_change_back,ck_teammate_pin
	.ref	CLIMB_ROPES,ADD_IF_SILENT,master_keep_attached
	.ref	change_anim2,can_pin
	.ref	blocking_off,std_taunt,std_walk_fast
	.ref	hyper_speed_on
	.ref	in_finish_move
	.ref	p1rounds,p2rounds
	.if NUM_YOKO_FINISHES
	.ref	yok_finish1_move
	.if NUM_YOKO_FINISHES > 1
	.ref	yok_finish2_move
	.endif
	.endif


	.ref	yok_2_jabs_anim
	.ref	yok_4_jabs_anim
	.ref	yok_4_uppercut_anim
	.ref	yok_2_slap2_anim
	.ref	yok_4_slap2_anim
	.ref	yok_2_superkick_anim
	.ref	yok_4_superkick_anim
	.ref	yok_2_grabfling_anim
	.ref	yok_4_grabfling_anim

	.ref	DO_REVERSAL,PCNT
	.ref	DO_REVERSAL_MESS

	.ref	yok_3_head_hold2_anim
	.ref	yok_3_head_hold_anim
	.ref	wres_slave_anim
	.ref	yok_zip_anim
	.ref	start_run_anim

	.ref	yok_stand2_anim,yok_stand8_anim
	.ref	yok_scissor_anim
	.ref	yok_stand4_anim,yok_stand6_anim
	.ref	yok_torso2_anim,yok_torso8_anim
	.ref	yok_torso4_anim,yok_torso6_anim

	.ref	yok_2_to_4_turn_anim,yok_8_to_6_turn_anim	;stand
	.ref	yok_4_to_2_turn_anim,yok_6_to_8_turn_anim
	.ref	yok_4_to_6_turn_anim,yok_6_to_4_turn_anim
	.ref	yok_2_to_8_turn_anim,yok_8_to_2_turn_anim
	.ref	yok_4_to_8_turn_anim,yok_6_to_2_turn_anim
	.ref	yok_2_to_6_turn_anim,yok_8_to_4_turn_anim

	.ref	yok_2_to_4_turn2_anim,yok_8_to_6_turn2_anim	;torso
	.ref	yok_4_to_2_turn2_anim,yok_6_to_8_turn2_anim
	.ref	yok_4_to_6_turn2_anim,yok_6_to_4_turn2_anim
	.ref	yok_2_to_8_turn2_anim,yok_8_to_2_turn2_anim
	.ref	yok_4_to_8_turn2_anim,yok_6_to_2_turn2_anim
	.ref	yok_2_to_6_turn2_anim,yok_8_to_4_turn2_anim

	.ref	yok_walk1_f2_anim,yok_walk2_f2_anim,yok_walk4_f2_anim
	.ref	yok_walk5_f2_anim,yok_walk6_f2_anim,yok_walk8_f2_anim

	.ref	yok_walk1_f4_anim,yok_walk2_f4_anim,yok_walk4_f4_anim
	.ref	yok_walk5_f4_anim,yok_walk6_f4_anim,yok_walk8_f4_anim

	.ref	yok_run2_anim

	.ref	yok_climb_up_anim,yok_climb_down_anim
	.ref	yok_3_butt_drop_anim,yok_tbukl_buttdrop_anim

	.ref	yok_2_punch_anim,yok_4_punch_anim
	.ref	yok_4_push_anim
	.ref	yok_2_lbowdrop_anim,yok_4_lbowdrop_anim
	.ref	yok_2_kick_anim,yok_4_kick_anim
	.ref	yok_2_stomp_anim,yok_4_stomp_anim
	.ref	yok_2_knee_anim,yok_4_knee_anim
	.ref	yok_2_gut_push_anim,yok_4_gut_push_anim
	.ref	yok_4_losebal_anim

	.ref	yok_4_block_anim

	.ref	yok_2_head_hit_anim,yok_4_head_hit_anim
	.ref	yok_2_body_hit_anim,yok_4_body_hit_anim
	.ref	yok_2_raise_arm_anim,yok_4_raise_arm_anim
	.ref	yok_2_pin_anim,yok_4_pin_anim

	.ref	yok_graboh_anim
	.ref	yok_2_hiptoss_anim
	.ref	yok_4_hiptoss_anim
	.ref	yok_2_hiptoss2_anim
	.ref	yok_4_hiptoss2_anim
	.ref	yok_vsuplex_anim

	.ref	yok_spinslam_anim
	.ref	yok_heldheadbutt_rpt_anim

	.ref	yok_2_hair_pickup_anim
	.ref	yok_4_salt_anim,yok_3_head_held_stand_anim
	.ref	yok_overhd_slam_anim,yok_2_run_slap_anim
	.ref	yok_4_hair_pickup_anim,yok_2_salt_anim
	.ref	yok_stndholdoh_anim,yok_4_run_slap_anim
	.ref	yok_fall_back_anim,yok_holdoh_anim

	.ref	BONUS_MESS




yok_secret_moves
	.long	ingtcharge_salt	;must have this one
	.long	ingtneck_grab
	.long	ingtgrab_fling
	.long	ingthip_toss
	.long	ingtgrab_fling2
	.long	ingthip_toss2
	.long	ingtscissors
	.long	ingtgut_push
	.long	ingtjabs
	.long	0


 SUBR	yok_smove_table
	.long	yok_hdhold_combo1
	.long	yok_hdhold_scissor
	.long	yok_hdhold_suplex
	.long	yok_salt_throw
	.long	yok_grab_toss_air
	.long	yok_hdhold_combo2
	.long	std_walk_fast
	.long	std_taunt

	.if NUM_YOKO_FINISHES
	.long	yok_finish_move1
	.endif
	.if NUM_YOKO_FINISHES > 1
	.long	yok_finish_move2
	.endif
	.long	0

ingtscissors
	.word	B_SKICK,		J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 32 		;max
	.long	ingtscrt_scis

ingtscrt_scis
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ingtrets
	move	*a13(PLYRMODE),a0
	CMPI	MODE_HEADHOLD,a0
	jrz	ingtrets

	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jreq	ingtrets
	cmpi	MODE_DEAD,a0
	jreq	ingtrets

	;don't do it if opponent is behind you
	calla	ck_ignore
	jrc	ingtrets
	
	CALLA	FIND_AND_KILL_ENDLESS
	movi	yok_scissor_anim,a0
	calla	change_anim1a
	WRSND	W_YOKO,GRABFLING_T1,GRABFLING_T2
ingtrets	rets


ingthip_toss

	.word	B_PUNCH,		J_ALL
	.word	J_AWAY,		J_REAL_LR
	.word	J_AWAY,		J_REAL_LR
	.word	8000h | 32		;max
	.long	ingtscrt_hiptoss

ingtscrt_hiptoss
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ingtout

	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	ingtout
	cmpi	MODE_DEAD,a0
	jrz	ingtout
	cmpi	MODE_HEADHELD,a0
	jrz	ingtout
	cmpi	MODE_INAIR,a0
	jrz	ingtdoit
	cmpi	MODE_INAIR2,a0
	jrz	ingtdoit

	move	*a13(CLOSEST_DIST),a0
	cmpi	70h,a0
	jrgt	ingtout

ingtdoit
	FACE24	yok,hiptoss_anim
	calla	change_anim1a

	WRSND	W_YOKO,HIPTOSS_T1,PUNCH_T2

	rets


ingtgrab_fling2
	.word	B_SPUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
	.word	8000h | 10		;max
	.long	ingtscrt_grabfling2

ingthip_toss2
	.word	B_PUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
	.word	8000h | 10		;max
	.long	ingtscrt_grabfling2

ingtscrt_grabfling2
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ingtout

	calla	get_opp_plyrmode
	cmpi	MODE_BOUNCING,a0
	jrz	ingtyes
	cmpi	MODE_RUNNING,a0
	jrz	ingtyes
	rets


ingtgrab_fling

	.word	B_SPUNCH,		J_ALL
	.word	J_AWAY,		J_REAL_LR
	.word	J_AWAY,		J_REAL_LR
	.word	8000h | 32		;max
	.long	ingtscrt_grabfling

ingtscrt_grabfling


	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ingtout

	calla	get_opp_plyrmode
	cmpi	MODE_BOUNCING,a0
	jrz	ingtyes
	cmpi	MODE_RUNNING,a0
	jrnz	ingtnope
ingtyes
	FACE24	yok,hiptoss_anim
	calla	change_anim1a

	WRSND	W_YOKO,HIPTOSS_T1,PUNCH_T2
	rets

ingtnope
	calla	get_opp_plyrmode
	cmpi	MODE_DEAD,a0
	jrz	ingtout
	cmpi	MODE_HEADHELD,a0
	jrz	ingtout
	cmpi	MODE_ONGROUND,a0
	jrz	ingtout
	cmpi	MODE_ATTACHED,a0
	jrz	ingtout


	FACE24	yok,grabfling_anim
	calla	change_anim1a

	WRSND	W_YOKO,GRABFLING_T1,PUNCH_T2
	rets


ingtneck_grab
	.word	B_SPUNCH,		J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 30		;max
	.long	ingtscrt_neck

ingtscrt_neck
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ingtout

	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	ingtout
	cmpi	MODE_DEAD,a0
	jrz	ingtout
	cmpi	MODE_HEADHELD,a0
	jrz	ingtout
	cmpi	MODE_ATTACHED,a0
	jrz	ingtout

	move	*a13(LAST_HEADHOLD),a14,L
	move	@PCNT,a0,L
	sub	a14,a0
	cmpi	2*60,a0
	jrge	ingtok2a

	.ref	yok_3_fake_hold_anim
	movi	yok_3_fake_hold_anim,a0
	calla	change_anim1a
	rets

ingtok2a
	move	*a13(CLOSEST_XDIST),a1
	cmpi	80,a1
	jrgt	ingtstd_grab

	movi	yok_3_head_hold2_anim,a0
	calla	change_anim1a
	rets

ingtstd_grab
	movi	yok_3_head_hold_anim,a0
	calla	change_anim1a
	rets


ingtcharge_salt

	move	*a13(BUT_VAL_UP),a0
	btst	PLAYER_PUNCH_BIT,a0
	jrz	ingtno_punch

	move	*a13(PLYRNUM),a0
	calla	get_punch_dtime
	cmpi	85,a0			;> min?
	jrlt	ingtno_punch

	CALLA	FIND_AND_KILL_ENDLESS
	callr	ingtscrt_salt
	setc
	rets

ingtno_punch
	clrc
	rets

ingtscrt_salt
	move	*a13(PLYRMODE),a0
	cmpi	MODE_HEADHELD,a0
	jrz	ingtout
	cmpi	MODE_HEADHOLD,a0
	jrz	ingtout

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ingtout

	calla	get_opp_plyrmode
	cmpi	MODE_ATTACHED,a0
	jrz	ingtout

	FACE24	yok,salt_anim
	calla	change_anim1a
	WRSND	W_YOKO,HDBUTT_T1,HDBUTT_T2

	clr	a0
	move	a0,*a13(RUN_TIME)

	SETMODE	NORMAL

	rets


ingtgut_push
	.word	B_PUNCH,		J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 40		;max
	.long	ingtscrt_gut_push

ingtscrt_gut_push

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ingtout
 
	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	ingtout
	cmpi	MODE_DEAD,a0
	jrz	ingtout
	cmpi	MODE_HEADHELD,a0
	jrz	ingtout
	cmpi	MODE_ATTACHED,a0
	jrz	ingtout
 
	FACE24	yok,gut_push_anim
	calla	change_anim1a
	WRSND	W_YOKO,GRABTHROW_T1,GRABTHROW_T2
ingtout
	rets



ingtjabs
	.word	B_PUNCH,		J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_DOWN_TOWARD,		J_REAL_LR
	.word	J_DOWN,			J_REAL_LR
	.word	8000h | 50 		;max
	.long	ingtscrt_jabs

ingtscrt_jabs
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ingtout

	move	*a13(PLYRMODE),a0
	cmpi	MODE_ONTURNBKL,a0
	jrz	ingtout

	calla	get_opp_plyrmode
	cmpi	MODE_HEADHELD,a0
	jrz	ingtout

	FACE24	yok,jabs_anim
	calla	change_anim1a

	WRSND	W_YOKO,HDBUTT_T1,HDBUTT_T2

	rets

	.if NUM_YOKO_FINISHES
 SUBRP	yok_finish_move1

whndTIMEOUT	.equ	TSEC

whndend_wait
	SLEEP		TSEC/4
	move		@p1rounds,a0			; Get player 1 rounds
	move		@p2rounds,a1			; Get player 2 rounds
	or		a1,a0				; Put em together
	cmpi		2,a0				; Anybody got 2?
	jrlt		whndend_wait			; br = no

whndreset
	clr		a11
	move		a11,@in_finish_move
	SLEEPK		1
	WAITSWITCH_DWN	J_UP,0,whndreset
	movi		whndTIMEOUT,a11
	move		a11,@in_finish_move
	WAITSWITCH_DWN	J_DOWN,0,whndreset
	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,whndreset
	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,whndreset
	WAITSWITCH_DWN	B_PUNCH,J_ALL,whndreset

	movi		yok_finish1_move,a0
	move		a0,*a8(SPECIAL_MOVE_ADDR),L
	DIE
	.endif
	

	.if NUM_YOKO_FINISHES > 1
 SUBRP	yok_finish_move2

qblrTIMEOUT	.equ	TSEC

qblrend_wait
	SLEEP		TSEC/4
	move		@p1rounds,a0			; Get player 1 rounds
	move		@p2rounds,a1			; Get player 2 rounds
	or		a1,a0				; Put em together
	cmpi		2,a0				; Anybody got 2?
	jrlt		qblrend_wait			; br = no

qblrreset
	clr		a11
	move		a11,@in_finish_move
	SLEEPK		1
	WAITSWITCH_DWN	J_UP,0,qblrreset
	movi		qblrTIMEOUT,a11
	move		a11,@in_finish_move
	WAITSWITCH_DWN	J_UP,0,qblrreset
	WAITSWITCH_DWN	J_RIGHT,J_UP,qblrreset
	WAITSWITCH_DWN	J_RIGHT,J_UP,qblrreset
	WAITSWITCH_DWN	B_SPUNCH,J_ALL,qblrreset

	movi		yok_finish2_move,a0
	move		a0,*a8(SPECIAL_MOVE_ADDR),L
	DIE
	.endif


yok_grab_toss_air

utswTIMEOUT	.equ	40

utswlp0
	SLEEPK	1
utswlp
	clr	a11

	WAITSWITCH_DWN	J_AWAY,0,utswlp

	movi	utswTIMEOUT,a11

	WAITSWITCH_DWN	J_AWAY,0,utswlp
	WAITSWITCH_DWN	B_PUNCH,J_ALL,utswlp

	move	*a8(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	utswlp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jreq	utswlp0

	SWAP	a8,a13
	calla	get_opp_plyrmode
	SWAP	a8,a13
	cmpi	MODE_ONGROUND,a0
	jrz	utswlp
	cmpi	MODE_DEAD,a0
	jrz	utswlp
	cmpi	MODE_INAIR,a0
	jrz	utswdoit2
	cmpi	MODE_INAIR2,a0
	jrz	utswdoit2

	move	*a8(CLOSEST_NUM),a0
	X32	a0
	addi	process_ptrs,a0
	move	*a0,a0,L
	move	*a0(ATTACK_TYPE),a0
	cmpi	AT_LEAPING,a0
	jrz	utswdoit2

	move	*a8(CLOSEST_DIST),a0
	cmpi	6ch,a0
	jrgt	utswlp

	FACE24	yok,hiptoss_anim
	move	a0,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_YOKO,GRABFLING_T1,PUNCH_T2

	SLEEPK	20
	jruc	utswlp

utswdoit2	FACE24	yok,hiptoss2_anim
	move	a0,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_YOKO,GRABFLING_T1,PUNCH_T2

	SLEEPK	20

	jruc	utswlp


yok_hdhold_suplex

greuTIMEOUT	.equ	60

greulp0
	SLEEPK	1
greulp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	greucont
	cmpi	MODE_HEADHELD,a0
	jrnz	greulp0
greucont
	clr	a11

	WAITSWITCH_DWN	J_DOWN,0,greulp

	movi	greuTIMEOUT,a11

	WAITSWITCH_DWN	J_DOWN,0,greulp
	WAITSWITCH_DWN	B_SKICK,J_ALL,greulp
	
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	greuslam
	cmpi	MODE_HEADHELD,a0
	jrnz	greulp0
	move	*a8(I_WILL_DIE),A14
	jrnz	greulp0


	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	greulp0				;ignore

	CALLA	DO_REVERSAL
	CALLA	DO_REVERSAL_MESS

	;target WHOHITME -- don't hit anyone else
	SMRTTGT	a8,WHOHITME

	;immobilize WHOHITME
	move	*a8(WHOHITME),a0,L
	jruc	greuis_reversal

greuslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	greulp0				;ignore

	movi	42,a10
	CREATE	MESSAGE_PID,BONUS_MESS

	;target WHOIHIT -- don't hit anyone else
	SMRTTGT	a8,WHOIHIT

	;immobilize WHOIHIT
	move	*a8(WHOIHIT),a0,L

greuis_reversal
	movk	30,a14
	move	a14,*a0(IMMOBILIZE_TIME)

	CALLA	FIND_AND_KILL_ENDLESS

	movi	yok_vsuplex_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_YOKO,GRABFLING_T1,GRABFLING_T2

	SLEEPK	20

	jruc	greulp

yok_hdhold_scissor

dzgdTIMEOUT	.equ	60

dzgdlp0
	SLEEPK	1
dzgdlp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	dzgdcont
	cmpi	MODE_HEADHELD,a0
	jrnz	dzgdlp0
dzgdcont
	clr	a11

	WAITSWITCH_DWN	J_TOWARD,0,dzgdlp

	movi	dzgdTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,dzgdlp
	WAITSWITCH_DWN	B_SPUNCH,J_ALL,dzgdlp
	
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	dzgdslam
	cmpi	MODE_HEADHELD,a0
	jrnz	dzgdlp0
	move	*a8(I_WILL_DIE),A14
	jrnz	dzgdlp0

	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	dzgdlp0				;ignore

	CALLA	DO_REVERSAL
	CALLA	DO_REVERSAL_MESS

	;target WHOHITME -- don't hit anyone else
	SMRTTGT	a8,WHOHITME

	;immobilize WHOHITME
	move	*a8(WHOHITME),a0,L
	jruc	dzgdis_reversal

dzgdslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	dzgdlp0				;ignore

	movi	34,a10
	CREATE0	BONUS_MESS

	;target WHOIHIT -- don't hit anyone else
	SMRTTGT	a8,WHOIHIT

	;immobilize WHOIHIT
	move	*a8(WHOIHIT),a0,L

dzgdis_reversal
	movk	32,a14
	move	a14,*a0(IMMOBILIZE_TIME)

	CALLA	FIND_AND_KILL_ENDLESS
	movi	yok_scissor_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L
	WRSND	W_YOKO,GRABFLING_T1,GRABFLING_T2

	SLEEPK	20

	jruc	dzgdlp

yok_hdhold_combo1

tihvTIMEOUT	.equ	60

tihvlp0
	SLEEPK	1
tihvlp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrnz	tihvlp0
	calla	CHECK_COMBO_GO
	jrlt	tihvlp0

	clr	a11

	WAITSWITCH_DWN	J_TOWARD,0,tihvlp

	movi	tihvTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,tihvlp
	WAITSWITCH_DWN	B_SPUNCH,J_ALL,tihvlp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	tihvslam
	cmpi	MODE_HEADHELD,a0
	jrnz	tihvlp0

tihvslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	tihvlp0				;ignore


	;target WHOIHIT
	SMRTTGT	a8,WHOIHIT

	CALLA	FIND_AND_KILL_ENDLESS
	movi	yok_combo_jabs_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_YOKO,UPRCUT_T1,UPRCUT_T2

	SLEEPK	20

	jruc	tihvlp

yok_hdhold_combo2

cotfTIMEOUT	.equ	60

cotflp0
	SLEEPK	1
cotflp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrnz	cotflp0
	calla	CHECK_COMBO_GO
	jrlt	cotflp0

	clr	a11

	WAITSWITCH_DWN	J_TOWARD,0,cotflp

	movi	cotfTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,cotflp
	WAITSWITCH_DWN	B_PUNCH,J_ALL,cotflp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	cotfslam
	cmpi	MODE_HEADHELD,a0
	jrnz	cotflp0

cotfslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	cotflp0				;ignore


	;target WHOIHIT
	SMRTTGT	a8,WHOIHIT

	CALLA	FIND_AND_KILL_ENDLESS
	movi	yok_combo_kick_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_YOKO,UPRCUT_T1,UPRCUT_T2

	SLEEPK	20

	jruc	cotflp


yok_salt_throw

usnqTIMEOUT	.equ	60

usnqlp0
	SLEEPK	1
usnqlp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	usnqcont
	cmpi	MODE_HEADHELD,a0
	jrnz	usnqlp0
usnqcont
	clr	a11

	WAITSWITCH_DWN	J_DOWN,0,usnqlp

	movi	usnqTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,usnqlp
	WAITSWITCH_DWN	B_PUNCH,J_ALL,usnqlp
	
	CALLA	FIND_AND_KILL_ENDLESS
	movi	yok_4_salt_anim,a0
	move	a0,*a8(SPECIAL_MOVE_ADDR),L
	WRSND	W_YOKO,LBOWDROP_T1,LBOWDROP_T2

	SLEEP	120

	jruc	usnqlp


 SUBR	yoko_ani_init

	move	*a13(FACING_DIR),a0
	btst	PLAYER_RIGHT_BIT,a0
	jrnz	hrgxp1
	movi	yok_stand4_anim,a0
	calla	change_anim1a

	movi	yok_torso4_anim,a0
	calla	change_anim2a

	PUSH	a10
	move	a13,a10

	CREATE	TAUNT_PID,do_taunt
	PULL	a10

	rets

hrgxp1
	movi	yok_stand2_anim,a0
	calla	change_anim1a

	movi	yok_torso2_anim,a0
	calla	change_anim2a

	rets



 SUBR	move_yoko



	movi	yok_secret_moves,a11
	calla	check_secret_moves

	move	*a13(PLYRMODE),a0
	sll	5,a0			;x 32
	addi	eezrmode_table,a0
	move	*a0,a0,L
	call	a0
	rets

eezrmode_table
	.long	mode_normal		;0
	.long	mode_running		;1
	.long	mode_inair		;2
	.long	mode_attached		;3
	.long	mode_onground		;4
	.long	mode_bouncing		;5
	.long	mode_onturnbkl		;6
	.long	mode_block		;7
	.long	mode_dizzy		;8
	.long	mode_dead		;9
	.long	mode_oppoverhead	;10
	.long	mode_climbturnbkl	;11
	.long	mode_waitanim		;12
	.long	mode_grapple		;13
	.long	mode_master		;14
	.long	mode_slave		;15
	.long	mode_headhold		;16
	.long	mode_puppet2		;17
	.long	mode_normal	;18
	.long	mode_headheld		;19
	REFLONG	mode_puppet		;20
	REFLONG	mode_inair2		;21
	.long	mode_normal	;22
	.long	mode_normal	;23
	.long	mode_chokehold		;24
	REFLONG	mode_choking		;25



mode_normal

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uwoyno_interrupt

	move	*a13(I_WILL_DIE),a0
	jrz	uwoynope
	move	*a13(IMMOBILIZE_TIME),a0
	jrnz	uwoynope
	movi	yok_fall_back_anim,a0
	calla	change_anim1a

	movi	-10,a0
	move	*a13(PLYRNUM),a1
	calla	adjust_health

	SETMODE	DEAD

	clr	a0
	move	a0,*a13(I_WILL_DIE)

	rets

uwoynope

	calla	get_opp_plyrmode
	cmpi	MODE_DEAD,a0
	jrne	uwoyopp_not_dead

	;if closest is a zombie, skip pin section.
	calla	get_opp_process
	move	*a0(STATUS_FLAGS),a14
	btst	B_ZOMBIE,a14
	jrnz	uwoyopp_not_dead
	
	;all opponents are dead.  Check for pins/raisearms

	;if we're attached to someone, don't interrupt it.
	move	*a13(ATTACH_PROC),a1,L
	jrz	uwoyno_attch
	move	*a1(ATTACH_PROC),a2,L
	cmp	a2,a13
	jreq	uwoyno_pin_yet
uwoyno_attch

	;if a teammate has pinned, raise yer arm.
	calla	ck_teammate_pin	
	jrc	uwoyraisearm

	;nobody has pinned.  if we're outside or all our opponents are
	; outside, do a raisearm.
	calla	raisearm_check
	jrc	uwoyraisearm

	;we're inside with a pinnable opponent.  any button pins.
	move	*a13(BUT_VAL_CUR),a0
	jrz	uwoyno_pin_yet

	;we have a press -- make sure opponent is on the ground
	calla	can_pin
	jrnc	uwoyopp_notgnd

	;opponent is on the ground.  pin him.
	FACE24	yok,pin_anim
	calla	change_anim1a

	;set the 'I did a pin' flag.
	move	*a13(STATUS_FLAGS),a14
	ori	M_DID_PIN,a14
	move	a14,*a13(STATUS_FLAGS)

	;if we're a temp drone for auto-pin purposes, turn back into
	; a normal player here.
	calla	drone_change_back

	rets

uwoyraisearm
	FACE24	yok,raise_arm_anim
	calla	change_anim1a
	calla	set_raisearm_bit

	;if we're a temp drone for auto-pin purposes, turn back into
	; a normal player here.
	calla	drone_change_back

	rets

uwoytoo_far_to_pin
uwoyopp_notgnd
uwoyopp_not_dead
uwoyno_pin_yet

	move	*a13(IMMOBILIZE_TIME),a0
	jrnz	uwoyimmobilized

	move	*a13(BUT_VAL_CUR),a0
	btst	PLAYER_BLOCK_BIT,a0	;maybe have to action tables?
	jrz	uwoynblk			;1 for down transitions
	callr	uwoyblock			;& 1 for current buttons
	jrc	uwoynblk
	clr	a0
	move	a0,*a13(ATTACK_TYPE)
	rets
uwoynblk
	move	*a13(BUT_VAL_CUR),a0
	andi	011111b,a0
	cmpi	PLAYER_PUNCH_VAL|PLAYER_KICK_VAL,a0
	jrz	uwoypunchkick

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	uwoyaction_table,a0
	move	*a0,a0,L
	call	a0

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uwoyno_interrupt


	move	*a13(STICK_VAL_CUR),a0
	move	a0,*a13(MOVE_DIR)
	calla	climb_turnbuckle
	jrnc	uwoyno_climb
	move	*a13(WRESTLERNUM),A5
	MOVI	CLIMB_ROPES,A2
	MOVI	1000,A0
	CALLA	ADD_IF_SILENT
	rets

uwoyimmobilized
	clr	a14
	move	a14,*a13(MOVE_DIR)
uwoyno_climb
	calla	execute_walk

uwoyno_interrupt
	rets

uwoyaction_table
	.long	uwoyz,uwoypunch,uwoyblock,uwoyblock			;0-3
	.long	uwoysuper_punch,uwoysuper_punch,uwoyblock,uwoyblock	;4-7
	.long	uwoykick,uwoypunchkick,uwoyblock,uwoyblock		;8-11
	.long	uwoysuper_punch,uwoypunchkick,uwoyblock,uwoyblock	;12-15
	.long	uwoysuper_kick,uwoysuper_kick,uwoyblock,uwoyblock	;16-19
	.long	uwoygraboh,uwoygraboh,uwoyblock,uwoyblock		;20-23
	.long	uwoysuper_kick,uwoypunchkick,uwoyblock,uwoyblock	;24-27
	.long	uwoygraboh,uwoygraboh,uwoyblock,uwoyblock		;28-31

uwoyz

	rets

uwoygraboh
	movi	yok_graboh_anim,a0
	calla	change_anim1a
	WRSND	W_YOKO,GRABHOLD_T1,GRABHOLD_T2
	rets

uwoypunch		;1

	JJXM_INIT
	JJXM	NORMAL,		 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	DIZZY,		 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	BLOCK,		 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	ONGROUND,	160,140,uwoypunch_lbdrop,uwoypunch_punch
	JJXM	RUNNING,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	BOUNCING,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	ONTURNBKL,	 	uwoypunch_punch
	JJXM	CLIMBTURNBKL,	 	uwoypunch_punch
	JJXM	PUPPET,		 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	PUPPET2,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	INAIR,		 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	ATTACHED,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	DEAD,		160,140,uwoypunch_lbdrop,uwoypunch_punch
	JJXM	OPPOVERHEAD,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	WAITANIM,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	GRAPPLE,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	MASTER,		 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	SLAVE,		 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	HEADHOLD,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	HEADHELD,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	CHOKEHOLD,	 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM	INAIR2,		 62, 95,uwoypunch_hdbutt,uwoypunch_punch
	JJXM_END

uwoypunch_punch
std_punch

	FACE24	yok,punch_anim
	calla	change_anim1a

	WRSND	W_YOKO,PUNCH_T1,PUNCH_T2
	rets

uwoypunch_hdbutt

	movi	yok_heldheadbutt_rpt_anim,a0
	calla	change_anim1a

	WRSND	W_YOKO,HDBUTT_T1,HDBUTT_T2
	rets

uwoypunch_lbdrop

	FACE24	yok,lbowdrop_anim
	calla	change_anim1a

	WRSND	W_YOKO,LBOWDROP_T1,LBOWDROP_T2
	rets

uwoyblock		;2

	clr	a0
	move	a0,*a13(OBJ_XVEL),L

std_block
	move	@blocking_off,a0
	jrnz	uwoyno_blocking

	RND_AWARD	a13,BLOCKS_AWD

	movi	yok_4_block_anim,a0
	calla	change_anim1a

	calla	BLOCK_WOOSH

	clr	a0
	move	a0,*a13(BLOCK_TIME)
	clrc
	rets

uwoyno_blocking
	setc
	rets


uwoysuper_punch	;4
	JJXM_INIT
	JJXM	NORMAL,		 95, 45,uwoyspunch_special,uwoyspunch_slap	;0
	JJXM	RUNNING,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;1
	JJXM	INAIR,		 95, 45,uwoyspunch_special,uwoyspunch_slap	;2
	JJXM	ATTACHED,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;3
	JJXM	ONGROUND,	160,140,uwoyspunch_lbowdrop,std_punch	;4
	JJXM	BOUNCING,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;0
	JJXM	ONTURNBKL,	 	uwoyspunch_slap			;6
	JJXM	BLOCK,		 95, 45,uwoyspunch_special,uwoyspunch_slap	;7
	JJXM	DIZZY,		 95, 45,uwoyspunch_special,uwoyspunch_slap	;8
	JJXM	DEAD,		160,140,uwoyspunch_lbowdrop,std_punch	;4
	JJXM	OPPOVERHEAD,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;10
	JJXM	CLIMBTURNBKL,		std_punch			;11
 	JJXM	WAITANIM,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;12
	JJXM	GRAPPLE,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;13
	JJXM	MASTER,		 95, 45,uwoyspunch_special,uwoyspunch_slap	;14
	JJXM	SLAVE, 		 95, 45,uwoyspunch_special,uwoyspunch_slap	;15
	JJXM	HEADHOLD,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;16
	JJXM	HEADHELD,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;19
	JJXM	CHOKEHOLD,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;16
	JJXM	PUPPET,		 95, 45,uwoyspunch_special,uwoyspunch_slap	;20
	JJXM	PUPPET2,	 95, 45,uwoyspunch_special,uwoyspunch_slap	;20
	JJXM	INAIR2,		 95, 45,uwoyspunch_special,uwoyspunch_slap	;21
	JJXM_END

uwoyspunch_rets
	rets

uwoyspunch_slap

	FACE24	yok,slap2_anim
	calla	change_anim1a

	WRSND	W_YOKO,PUNCH_T1,PUNCH_T2
	rets

uwoyspunch_special
	move	*a13(STICK_VAL_CUR),a0
	btst	MOVE_DOWN_BIT,a0
	jrnz	uwoyck_up


	FACE24	yok,jabs_anim
	calla	change_anim1a

	WRSND	W_YOKO,HDBUTT_T1,HDBUTT_T2
	rets

uwoyck_up
	movi	yok_4_uppercut_anim,a0
	calla	change_anim1

	WRSND	W_YOKO,HDBUTT_T1,HDBUTT_T2
	rets

uwoyspunch_lbowdrop


	move	*a13(CLOSEST_NUM),a14
	X32	a14

	addi	process_ptrs,a14
	move	*a14,a14,L
	move	*a14(PLYRMODE),a2
	cmpi	MODE_DEAD,a2
	jrz	uwoyno
	move	*a13(OBJ_XPOS),a1,L 
	move	*a14(OBJ_XPOS),a2,L
	sub	a2,a1
	abs	a1
	srl	16,a1
	cmpi	20h,a1	;28
 	jrlt	uwoyno


	move	*a14(OBJ_CONTROL),a14
	andi	M_FLIPH,a14
	move	*a13(OBJ_CONTROL),a0
	andi	M_FLIPH,a0
	cmp	a0,a14
	jrz	uwoyno


	FACE24	yok,hair_pickup_anim
	calla	change_anim1
	WRSND	W_YOKO,LBOWDROP_T1,LBOWDROP_T2
	rets

uwoyno
	FACE24	yok,lbowdrop_anim
	calla	change_anim1
	WRSND	W_YOKO,LBOWDROP_T1,LBOWDROP_T2
	rets


uwoykick		;8


	JJXM_INIT
	JJXM	NORMAL,		 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	DIZZY,		 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	BLOCK,		 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	ONGROUND,	160,140,uwoykick_stomp,uwoykick_kick
	JJXM	RUNNING,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	BOUNCING,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	ONTURNBKL,	 	uwoykick_kick
	JJXM	CLIMBTURNBKL,	 	uwoykick_kick
	JJXM	PUPPET,			uwoykick_kick
	JJXM	PUPPET2,		uwoykick_kick
	JJXM	INAIR,		 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	ATTACHED,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	DEAD,		160,140,uwoykick_stomp,uwoykick_kick
	JJXM	OPPOVERHEAD,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	WAITANIM,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	GRAPPLE,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	MASTER,		 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	SLAVE,		 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	HEADHOLD,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	HEADHELD,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	CHOKEHOLD,	 60, 50,uwoykick_knee,uwoykick_kick
	JJXM	INAIR2,		 	uwoykick_TB
	JJXM_END

uwoykick_TB

	.ref	yok_graboh_TB_anim
	movi	yok_graboh_TB_anim,a0
	calla	change_anim1a

	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

uwoykick_kick

	FACE24	yok,kick_anim
	calla	change_anim1a

	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

uwoykick_knee

	FACE24	yok,knee_anim
	calla	change_anim1a

	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

uwoykick_stomp

	FACE24	yok,stomp_anim
	calla	change_anim1a

	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets


uwoypunchkick	;9

	movi	start_run_anim,a0
	calla	change_anim1a

	rets


uwoysuper_kick	;16

	JJXM_INIT
	JJXM	NORMAL,		 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	DIZZY,		 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	BLOCK,		 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	ONGROUND,	160,140,uwoyskick_stomp,uwoyskick_kick
	JJXM	RUNNING,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	BOUNCING,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	ONTURNBKL,	 	uwoyskick_kick
	JJXM	CLIMBTURNBKL,	 	uwoyskick_kick
	JJXM	PUPPET,			uwoyskick_kick
	JJXM	PUPPET2,		uwoyskick_kick
	JJXM	INAIR,			uwoyskick_kick
	JJXM	ATTACHED,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	DEAD,		160,140,uwoyskick_stomp,uwoyskick_kick
	JJXM	OPPOVERHEAD,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	WAITANIM,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	GRAPPLE,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	MASTER,		 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	SLAVE,		 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	HEADHOLD,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	HEADHELD,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	CHOKEHOLD,	 60, 50,uwoyskick_knee,uwoyskick_kick
	JJXM	INAIR2,		 	uwoykick_TB
	JJXM_END


uwoyskick_kick

	FACE24	yok,superkick_anim
	calla	change_anim1a

	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

uwoyskick_knee

	FACE24	yok,knee_anim
	calla	change_anim1a

	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

uwoyskick_stomp

	FACE24	yok,stomp_anim
	calla	change_anim1a

	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

uwoyskick_scissor
	movi	yok_scissor_anim,a0
	calla	change_anim1a

	SETMODE	INAIR

	WRSND	W_YOKO,GRABFLING_T1,GRABFLING_T2

	rets



fuamXRUN_VAL	equ	YOK_XRUN
fuamZDRIFT_VAL	equ	YOK_ZDRIFT

mode_running

	move	*a13(RUN_TIME),a0		;Used for flying kick
	addk	1,a0
	move	a0,*a13(RUN_TIME)

	move	*a13(USR_VAR1),a0
	jrnz	fuamno_vel

	calla	bounce_off_ropes

	movi	fuamXRUN_VAL,a0
	move	*a13(MOVE_DIR),a14
	btst	PLAYER_RIGHT_BIT,a14
	jrnz	fuampos
	neg	a0
fuampos
	move	@hyper_speed_on,a14
	sll	a14,a0
	move	a0,*a13(OBJ_XVEL),L

fuamno_vel

	move	*a13(GETUP_TIME),a0		;delay b4 gaining control
	jrnz	fuamout_of_control

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	fuamno_interrupt

	move	*a13(STICK_VAL_CUR),a0
	move	*a13(MOVE_DIR),a1
	or	a0,a1
	andi	MOVE_LEFT|MOVE_RIGHT,a1
	cmpi	MOVE_LEFT|MOVE_RIGHT,a1
	jrne	fuamcontinue_run

	;braking with stick.  abort run.

	SETMODE	NORMAL

fuamcontinue_run
fuamno_interrupt

	move	*a13(STICK_VAL_CUR),a0
	clr	a1

	movi	-fuamZDRIFT_VAL,a1
	btst	PLAYER_UP_BIT,a0
	jrnz	fuamsetvel

	movi	fuamZDRIFT_VAL,a1
	btst	PLAYER_DOWN_BIT,a0
	jrnz	fuamsetvel

	clr	a1
fuamsetvel
	move	a1,*a13(OBJ_ZVEL),L

	move	*a13(DELAY_BUTNS),a0
	jrnz	fuamout_of_control


	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	fuamaction_table,a0
	move	*a0,a0,L
	call	a0
fuamout_of_control
	rets

fuamaction_table
	.long	fuamz,fuampunch,fuamblock,fuamblock			;0-3
	.long	fuamsuper_punch,fuamsuper_punch,fuamblock,fuamblock	;4-7
	.long	fuamkick,fuampunchkick,fuamblock,fuamblock		;8-11
	.long	fuamsuper_punch,fuampunchkick,fuamblock,fuamblock	;12-15
	.long	fuamsuper_kick,fuamsuper_kick,fuamblock,fuamblock	;16-19
	.long	fuamgraboh,fuamgraboh,fuamblock,fuamblock		;20-23
	.long	fuamsuper_kick,fuampunchkick,fuamblock,fuamblock	;24-27
	.long	fuamgraboh,fuamgraboh,fuamblock,fuamblock		;28-31

fuamz
	rets

fuampunch		;1
fuamsuper_punch	;4
fuampunchkick
fuamgraboh

	JJXM_INIT
	JJXM	NORMAL,			fuampunch_clobber
	JJXM	DIZZY,			fuampunch_clobber
	JJXM	BLOCK,			fuampunch_clobber
	JJXM	ONGROUND,		fuampunch_buttdrop
	JJXM	RUNNING,		fuampunch_clobber
	JJXM	BOUNCING,		fuampunch_clobber
	JJXM	ONTURNBKL,		fuampunch_clobber
	JJXM	CLIMBTURNBKL,		fuampunch_clobber
	JJXM	PUPPET,			fuampunch_clobber
	JJXM	PUPPET2,		fuampunch_clobber
	JJXM	INAIR,			fuampunch_clobber
	JJXM	ATTACHED,		fuampunch_clobber
	JJXM	DEAD,			fuampunch_buttdrop
	JJXM	OPPOVERHEAD,		fuampunch_clobber
	JJXM	WAITANIM,		fuampunch_clobber
	JJXM	GRAPPLE,		fuampunch_clobber
	JJXM	MASTER,			fuampunch_clobber
	JJXM	SLAVE,			fuampunch_clobber
	JJXM	HEADHOLD,		fuampunch_clobber
	JJXM	HEADHELD,		fuampunch_clobber
	JJXM	CHOKEHOLD,		fuampunch_clobber
	JJXM	INAIR2,			fuampunch_clobber
	JJXM_END

	rets

fuampunch_clobber

	calla	ck_ignore
	jrc	fuamrets

	clr	a0
	move	a0,*a13(RUN_TIME)

	SETMODE	NORMAL

	FACE24	yok,run_slap_anim
	calla	change_anim1a

	WRSND	W_YOKO,PUNCH_T1,PUNCH_T2
	rets


fuampunch_rets
	rets

fuampunch_buttdrop

	movi	yok_3_butt_drop_anim,a0
	calla	change_anim1a
	WRSND	W_YOKO,GRABTHROW_T1,GRABTHROW_T2
	rets


fuamblock		;2

	move	*a13(OBJ_XVEL),a0,L
	sra	1,a0
	move	a0,*a13(OBJ_XVEL),L
	SETMODE	NORMAL
	jruc	std_block

fuamkick		;8
fuamsuper_kick	;16

	JJXM_INIT
	JJXM	NORMAL,			fuamscissor
	JJXM	DIZZY,			fuamscissor
	JJXM	BLOCK,			fuamscissor
	JJXM	ONGROUND,		fuampunch_buttdrop
	JJXM	RUNNING,		fuamscissor
	JJXM	BOUNCING,		fuamscissor
	JJXM	ONTURNBKL,		fuamscissor
	JJXM	CLIMBTURNBKL,		fuamscissor
	JJXM	PUPPET,			fuamscissor
	JJXM	PUPPET2,		fuamscissor
	JJXM	INAIR,			fuamscissor
	JJXM	ATTACHED,		fuamscissor
	JJXM	DEAD,			fuampunch_buttdrop
	JJXM	OPPOVERHEAD,		fuamscissor
	JJXM	WAITANIM,		fuamscissor
	JJXM	GRAPPLE,		fuamscissor
	JJXM	MASTER,			fuamscissor
	JJXM	SLAVE,			fuamscissor
	JJXM	HEADHOLD,		fuamscissor
	JJXM	HEADHELD,		fuamscissor
	JJXM	CHOKEHOLD,		fuamscissor
	JJXM	INAIR2,			fuamscissor
	JJXM_END

	rets

fuamscissor
	calla	ck_ignore
	jrc	fuamrets

	movi	yok_scissor_anim,a0
	calla	change_anim1a

	SETMODE	INAIR

	WRSND	W_YOKO,GRABHOLD_T1,GRABHOLD_T2
fuamrets



mode_inair	;2

	rets


mode_attached	;3

	calla	keep_attached
	jrnz	cuzystill_attached

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	movi	MODE_NORMAL,a0
	move	a0,*a13(ANIMODE)

cuzystill_attached
	rets

mode_onground	;4

	rets



mode_bouncing	;5

	clr	a0
	move	a0,*a13(OBJ_XVEL),L
	move	a0,*a13(OBJ_ZVEL),L

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	ijhqnot_ended

	move	*a13(MOVE_DIR),a0
	xori	MOVE_LEFT+MOVE_RIGHT,a0	;flip LEFT/RIGHT
	move	a0,*a13(MOVE_DIR)

	move	*a13(NEW_FACING_DIR),a1
	andi	MOVE_UP+MOVE_DOWN,a1
	or	a0,a1			;LEFT or RIGHT
	move	a1,*a13(FACING_DIR)	;face same dir as run

	movi	yok_run2_anim,a0
	calla	change_anim1a

	SETMODE	RUNNING


ijhqnot_ended
	rets


mode_onturnbkl	;6


	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_DOWN_BIT,a0
	jrz	ugbeno_climbdown

	movi	yok_climb_down_anim,a0
	calla	change_anim1a

	SETMODE	CLIMBTURNBKL

ugbeno_interrupt
	rets

ugbeno_climbdown
	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	jrz	ugbez

	X32	a0
	addi	ugbeaction_table,a0
	move	*a0,a0,L
	call	a0

	move	*a13(PLYRNUM),a5
	movi	JUMP_ROPES,a2
	movi	1000,a0
	calla	ADD_IF_SILENT

	rets



ugbeaction_table
	.long	ugbez,ugbepunch,ugbeblock,ugbeblock			;0-3
	.long	ugbesuper_punch,ugbesuper_punch,ugbeblock,ugbeblock	;4-7
	.long	ugbekick,ugbepunchkick,ugbeblock,ugbeblock		;8-11
	.long	ugbesuper_punch,ugbepunchkick,ugbeblock,ugbeblock	;12-15
	.long	ugbesuper_kick,ugbesuper_kick,ugbeblock,ugbeblock	;16-19
	.long	ugbegraboh,ugbegraboh,ugbeblock,ugbeblock		;20-23
	.long	ugbesuper_kick,ugbepunchkick,ugbeblock,ugbeblock	;24-27
	.long	ugbegraboh,ugbegraboh,ugbeblock,ugbeblock		;28-31


ugbez
	rets

ugbepunch		;1
ugbeblock		;2
ugbesuper_punch	;4
ugbepunchkick	;5
ugbekick		;8
ugbesuper_kick	;16
ugbegraboh		;20

	SETMODE	INAIR

	movi	yok_tbukl_buttdrop_anim,a0
	calla	change_anim1a

	WRSND	W_YOKO,TURNDIVE_T1,TURNDIVE_T2

	rets


mode_block	;7

	move	*a13(BLOCK_TIME),a0
	inc	a0
	move	a0,*a13(BLOCK_TIME)
	cmpi	160,a0
	jrlt	upgzno

	calla	get_opp_plyrmode
	cmpi	MODE_BLOCK,a0
	jrnz	upgzno
	move	*a13(CLOSEST_XDIST),a0
	cmpi	61h,a0
	jrge	upgzno
	cmpi	45,a0
	jrlt	upgzno
	move	*a13(CLOSEST_ZDIST),a0
	cmpi	30,a0
	jrlt	upgzpunchblock

upgzno
	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	jrz	upgzno_interrupt

	;press.  if block isn't down, let mode_normal handle it
	move	a0,a2			;save a0 from ...val_down
	move	*a13(BUT_VAL_CUR),a0
	btst	PLAYER_BLOCK_BIT,a0
	jrz	upgzset_mode_normal
	move	a2,a0			;restore a0 from ...val_down

	X32	a0
	addi	upgzaction_table,a0
	move	*a0,a0,L
	call	a0

upgzno_interrupt
	rets

upgzset_mode_normal
	SETMODE	NORMAL
	jruc	mode_normal

upgzaction_table
	.long	upgzz,upgzpunchblock,upgzz	;0-2
	.long	upgzpunchblock		;3
	.long	upgzz,upgzz,upgzz,upgzz,upgzz,upgzz,upgzz	;4-10
	.long	upgzz,upgzz,upgzz,upgzz,upgzz,upgzz,upgzz	;11-17
	.long	upgzz,upgzz,upgzz,upgzz,upgzz,upgzz,upgzz	;18-24
	.long	upgzz,upgzz,upgzz,upgzz,upgzz,upgzz,upgzz	;25-31

upgzz
	rets

upgzpunchblock	;3


	SETMODE	NORMAL

	movi	yok_4_push_anim,a0
	calla	change_anim1a

	WRSND	W_YOKO,PUSH_T1,PUSH_T2

	rets


mode_dizzy	;8

	rets



mode_oppoverhead ;10

	move	*a13(ATTACH_PROC),a2,L		;proc attached to
	jrz	vgsanot_attached
	move	*a2(ATTACH_PROC),a0,L
	jrnz	vgsastill_attached

vgsanot_attached

	move	*a13(ANIMODE),a0		;* remove this????
	btst	MODE_UNINT_BIT,a0		;*
	jrnz	vgsano_interrupt			;*

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	CALLA	FIND_AND_KILL_ENDLESS
	SETMODE	NORMAL

	movi	MODE_NORMAL,a0
	move	a0,*a13(ANIMODE)

	rets

vgsastill_attached
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	vgsano_interrupt

	move	*a13(FACING_DIR),a0
	andni	MOVE_UP,a0
	ori	MOVE_DOWN,a0
	move	a0,*a13(FACING_DIR)
	move	a0,*a13(NEW_FACING_DIR)

	move	*a13(STICK_VAL_CUR),a0
	jrz	vgsastand

	move	a0,*a13(MOVE_DIR)
	calla	execute_walk

	movi	yok_holdoh_anim,a0
	calla	change_anim2
 	jruc	vgsack_butns
vgsastand
	clr	a0
	move	a0,*a13(MOVE_DIR)
	move	a0,*a13(OBJ_XVEL),L
	move	a0,*a13(OBJ_ZVEL),L

	movi	yok_stndholdoh_anim,a0
	calla	change_anim1

vgsack_butns

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	vgsaaction_table,a0
	move	*a0,a0,L
	call	a0

vgsano_interrupt
	rets

vgsaaction_table
	.long	vgsaz,vgsapunch,vgsablock,vgsablock			;0-3
	.long	vgsasuper_punch,vgsasuper_punch,vgsablock,vgsablock	;4-7
	.long	vgsakick,vgsapunchkick,vgsablock,vgsablock		;8-11
	.long	vgsasuper_punch,vgsapunchkick,vgsablock,vgsablock	;12-15
	.long	vgsasuper_kick,vgsasuper_kick,vgsablock,vgsablock	;16-19
	.long	vgsagraboh,vgsagraboh,vgsablock,vgsablock		;20-23
	.long	vgsasuper_kick,vgsapunchkick,vgsablock,vgsablock	;24-27
	.long	vgsagraboh,vgsagraboh,vgsablock,vgsablock		;28-31

vgsaz
	rets

vgsapunch		;1
vgsablock		;2
vgsakick		;8
vgsasuper_kick	;16
vgsapunchkick
vgsagraboh

	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_DOWN_BIT,a0
	jrz	vgsaslam

	movi	yok_spinslam_anim,a0
	calla	change_anim1a

	WRSND	W_YOKO,HIPTOSS_T1,HIPTOSS_T2

	rets
vgsaslam
	movi	yok_overhd_slam_anim,a0
	calla	change_anim1a

	WRSND	W_YOKO,HIPTOSS_T1,HIPTOSS_T2

	rets

vgsasuper_punch	;4

	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_UP_BIT,a0
	jrz	vgsapunch

	CALLA	FIND_AND_KILL_ENDLESS
	.ref	yok_overhd_slam2_anim
	movi	yok_overhd_slam2_anim,a0
	calla	change_anim1a

	WRSND	W_YOKO,HIPTOSS_T1,HIPTOSS_T2

	rets


mode_climbturnbkl	;11

	rets



mode_waitanim	;12

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	pcxvnot_ended

	move	*a13(CODE_ADDR),a0,L
	call	a0

pcxvnot_ended
	rets



mode_grapple	;13
	rets



mode_master	;14

	calla	master_keep_attached	;keep slave attached to me

	rets


mode_slave	;15
	rets


mode_headhold	;16

	.ref	bozo_check
	calla	bozo_check
	jrnc	rbsrfail

	WRSND	W_YOKO,GRABFLING_T1,GRABFLING_T2

	movi	yok_vsuplex_anim,a0
	move	@PCNT,a14
	btst	0,a14
	jrz	rbsrtag
	movi	yok_scissor_anim,a0
rbsrtag	calla	change_anim1a

	rets

rbsrfail
	move	*a13(WHOIHIT),a0,L
	move	*a0(PLYRMODE),a0
	cmpi	MODE_HEADHELD,a0
	jrnz	rbsrexit


	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	rbsrno_interrupt

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	rbsraction_table,a0
	move	*a0,a0,L
	call	a0

rbsrno_interrupt
	rets

rbsrexit
	move	*a13(OBJ_ZPOS),a14,L
	subi	[6,0],a14
	move	a14,*a13(OBJ_ZPOS),L

	movk	MOVE_DOWN_RIGHT,a1
	move	*a13(OBJ_CONTROL),a0
	btst	B_FLIPH,a0
	jrz	rbsrright
	movk	MOVE_DOWN_LEFT,a1
rbsrright	move	a1,*a13(FACING_DIR)
	move	a1,*a13(NEW_FACING_DIR)

	SETMODE	NORMAL

	rets

rbsraction_table
	.long	rbsrz,rbsrpunch,rbsrblock,rbsrblock			;0-3
	.long	rbsrsuper_punch,rbsrsuper_punch,rbsrblock,rbsrblock	;4-7
	.long	rbsrkick,rbsrpunchkick,rbsrblock,rbsrblock		;8-11
	.long	rbsrsuper_punch,rbsrpunchkick,rbsrblock,rbsrblock	;12-15
	.long	rbsrsuper_kick,rbsrsuper_kick,rbsrblock,rbsrblock	;16-19
	.long	rbsrgraboh,rbsrgraboh,rbsrblock,rbsrblock		;20-23
	.long	rbsrsuper_kick,rbsrpunchkick,rbsrblock,rbsrblock	;24-27
	.long	rbsrgraboh,rbsrgraboh,rbsrblock,rbsrblock		;28-31

rbsrblock
rbsrgraboh
rbsrz		;0
	rets


rbsrpunch		;1
	CALLA	FIND_AND_KILL_ENDLESS
	move	*a13(STICK_VAL_CUR),a0
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a0,a1
	jrnz	rbsrtag2
	.ref	yok_4_knee2_anim
	movi	yok_4_knee2_anim,a0
	calla	change_anim1a

	WRSND	W_YOKO,KICK_T1,KICK_T2

	rets


rbsrtag2
	movi	yok_4_knee2_anim,a0
	calla	change_anim1a
	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

rbsrsuper_punch	;4

rbsrupper
	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_DOWN_BIT,a0
	jrz	rbsrz

	CALLA	FIND_AND_KILL_ENDLESS

	SPCDMG	D_UPRCUT/2,15

	movi	yok_4_uppercut_anim,a0
	calla	change_anim1a
	WRSND	W_YOKO,UPRCUT_T1,UPRCUT_T2
	rets

rbsrpunchkick
rbsrkick
	CALLA	FIND_AND_KILL_ENDLESS
	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_DOWN_BIT,a0
	jrz	rbsrknee

	movi	yok_4_knee2_anim,a0
	calla	change_anim1a
	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

rbsrknee
std_4_knee
	movi	yok_4_knee2_anim,a0
	calla	change_anim1a
	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets

rbsrsuper_kick	;16
	CALLA	FIND_AND_KILL_ENDLESS
	move	*a13(STICK_VAL_CUR),a0
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a0,a1
	jrnz	rbsrkick

	movi	yok_4_knee2_anim,a0
	calla	change_anim1a
	WRSND	W_YOKO,KICK_T1,KICK_T2
	rets


mode_puppet2	;17

	rets


mode_headheld	;19

	move	*a13(ANIMODE),a0
	btst	MODE_NOGRAVITY_BIT,a0
	janz	mode_choking

	calla	bozo_check
	jrnc	pjhwfail

	PUSH	a8
	move	a13,a8
	CALLA	DO_REVERSAL
	CALLA	DO_REVERSAL_MESS
	PULL	a8

	WRSND	W_YOKO,GRABFLING_T1,GRABFLING_T2

	movi	yok_vsuplex_anim,a0
	move	@PCNT,a14
	btst	0,a14
	jrz	pjhwtag
	movi	yok_scissor_anim,a0
pjhwtag	calla	change_anim1a

	rets
pjhwfail

	move	*a13(ATTACH_PROC),a0,L
	jrnz	pjhwno_luck

	move	*a13(OBJ_YPOSINT),a0
	move	*a13(GROUND_Y),a1
	cmp	a1,a0			;a0-a1
	jrgt	pjhwno_luck


	movi	yok_3_head_held_stand_anim,a0
	calla	change_anim1a

pjhwno_luck
	rets

mode_chokehold	;24
	rets


swykVEL	equ	3a000h		;38000h	;30000h
swykDVEL	equ	31000h		;27978h	;21f0eh


 SUBR yok_velocity_table


	.long	0,	-swykVEL	;0 UP

	.long	swykDVEL,	-swykDVEL	;1 UP-RIGHT

	.long	swykVEL,	0	;2 RIGHT

	.long	swykDVEL,	swykDVEL	;3 DOWN-RIGHT

	.long	0,	swykVEL	;4 DOWN

	.long	-swykDVEL,	swykDVEL	;5 DOWN-LEFT

	.long	-swykVEL,	0	;6 LEFT

	.long	-swykDVEL,	-swykDVEL	;7 UP-LEFT



 SUBR yok_rotate_anims_table
	.long	yok_stand2_anim		;new = up right
	.long	yok_2_to_4_turn_anim	;    = down right
	.long	yok_2_to_6_turn_anim	;    = down left
	.long	yok_2_to_8_turn_anim	;    = up left

	.long	yok_4_to_2_turn_anim	;new = up right
	.long	yok_stand4_anim		;    = down right
	.long	yok_4_to_6_turn_anim	;    = down left
	.long	yok_4_to_8_turn_anim	;    = up left

	.long	yok_6_to_2_turn_anim	;new = up right
	.long	yok_6_to_4_turn_anim	;    = down right
	.long	yok_stand6_anim		;    = down left
	.long	yok_6_to_8_turn_anim	;    = up left

	.long	yok_8_to_2_turn_anim	;new = up right
	.long	yok_8_to_4_turn_anim	;    = down right
	.long	yok_8_to_6_turn_anim	;    = down left
	.long	yok_stand8_anim		;    = up left


 SUBR yok_leg_anims_table
	.long	yok_walk1_f2_anim	;1 (UP)
	.long	yok_walk1_f2_anim	;2 (UP_RIGHT)
	.long	yok_walk1_f4_anim	;3 (RIGHT)
	.long	yok_walk1_f4_anim	;4 (DOWN_RIGHT)
	.long	yok_walk1_f4_anim	;5 (DOWN)
	.long	yok_walk1_f4_anim	;6 (DOWN_LEFT)
	.long	yok_walk1_f2_anim	;7 (LEFT)
	.long	yok_walk1_f2_anim	;8 (UP-LEFT)

	.long	yok_walk2_f2_anim	;1 (UP)
	.long	yok_walk2_f2_anim	;2 (UP_RIGHT)
	.long	yok_walk2_f2_anim	;3 (RIGHT)
	.long	yok_walk2_f4_anim	;4 (DOWN_RIGHT)
	.long	yok_walk8_f4_anim	;5 (DOWN)
	.long	yok_walk8_f4_anim	;6 (DOWN_LEFT)
	.long	yok_walk4_f2_anim	;7 (LEFT)
	.long	yok_walk4_f2_anim	;8 (UP-LEFT)

	.long	yok_walk2_f2_anim	;1 (UP)
	.long	yok_walk2_f2_anim	;2 (UP_RIGHT)
	.long	yok_walk2_f2_anim	;3 (RIGHT)
	.long	yok_walk4_f4_anim	;4 (DOWN_RIGHT)
	.long	yok_walk4_f4_anim	;5 (DOWN)
	.long	yok_walk8_f4_anim	;6 (DOWN_LEFT)
	.long	yok_walk6_f2_anim	;7 (LEFT)
	.long	yok_walk6_f2_anim	;8 (UP-LEFT)


	.long	yok_walk2_f2_anim	;1 (UP)
	.long	yok_walk8_f2_anim	;2 (UP_RIGHT)
	.long	yok_walk4_f4_anim	;3 (RIGHT)
	.long	yok_walk4_f4_anim	;4 (DOWN_RIGHT)
	.long	yok_walk2_f4_anim	;5 (DOWN)
	.long	yok_walk6_f4_anim	;6 (DOWN_LEFT)
	.long	yok_walk2_f2_anim	;7 (LEFT)
	.long	yok_walk6_f2_anim	;8 (UP-LEFT)

	.long	yok_walk5_f2_anim	;1 (UP)
	.long	yok_walk5_f2_anim	;2 (UP_RIGHT)
	.long	yok_walk5_f4_anim	;3 (RIGHT)
	.long	yok_walk5_f4_anim	;4 (DOWN_RIGHT)
	.long	yok_walk5_f4_anim	;5 (DOWN)
	.long	yok_walk5_f4_anim	;6 (DOWN_LEFT)
	.long	yok_walk5_f2_anim	;7 (LEFT)
	.long	yok_walk5_f2_anim	;8 (UP-LEFT)

	.long	yok_walk2_f2_anim	;1 (UP)
	.long	yok_walk6_f2_anim	;2 (UP_RIGHT)
	.long	yok_walk2_f2_anim	;3 (RIGHT)
	.long	yok_walk6_f4_anim	;4 (DOWN_RIGHT)
	.long	yok_walk2_f4_anim	;5 (DOWN)
	.long	yok_walk4_f4_anim	;6 (DOWN_LEFT)
	.long	yok_walk2_f2_anim	;7 (LEFT)
	.long	yok_walk8_f2_anim	;8 (UP-LEFT)

	.long	yok_walk2_f2_anim	;1 (UP)
	.long	yok_walk6_f2_anim	;2 (UP_RIGHT)
	.long	yok_walk6_f2_anim	;3 (RIGHT)
	.long	yok_walk8_f4_anim	;4 (DOWN_RIGHT)
	.long	yok_walk4_f4_anim	;5 (DOWN)
	.long	yok_walk4_f4_anim	;6 (DOWN_LEFT)
	.long	yok_walk2_f2_anim	;7 (LEFT)
	.long	yok_walk2_f2_anim	;8 (UP-LEFT)

	.long	yok_walk2_f2_anim	;1 (UP)
	.long	yok_walk4_f2_anim	;2 (UP_RIGHT)
	.long	yok_walk6_f2_anim	;3 (RIGHT)
	.long	yok_walk8_f4_anim	;4 (DOWN_RIGHT)
	.long	yok_walk6_f4_anim	;5 (DOWN)
	.long	yok_walk2_f4_anim	;6 (DOWN_LEFT)
	.long	yok_walk2_f2_anim	;7 (LEFT)
	.long	yok_walk2_f2_anim	;8 (UP-LEFT)


 SUBR yok_torso_anims_table
	.long	yok_torso2_anim		;new = up right
	.long	yok_2_to_4_turn2_anim	;    = down right
	.long	yok_2_to_6_turn2_anim	;    = down left
	.long	yok_2_to_8_turn2_anim	;    = up left

	.long	yok_4_to_2_turn2_anim	;new = up right
	.long	yok_torso4_anim		;    = down right
	.long	yok_4_to_6_turn2_anim	;    = down left
	.long	yok_4_to_8_turn2_anim	;    = up left

	.long	yok_6_to_2_turn2_anim	;new = up right
	.long	yok_6_to_4_turn2_anim	;    = down right
	.long	yok_torso6_anim		;    = down left
	.long	yok_6_to_8_turn2_anim	;    = up left

	.long	yok_8_to_2_turn2_anim	;new = up right
	.long	yok_8_to_4_turn2_anim	;    = down right
	.long	yok_8_to_6_turn2_anim	;    = down left
	.long	yok_torso8_anim		;    = up left



	.end

