
	.file	"adam.asm"
	.title	"adam bomb specific game code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"plyr.equ"
	.include	"anim.equ"
	.include	"audit.equ"
	.include	"sound.h"
	.include	"ring.equ"
	.include	"lifebar.h"



	.ref	wres_get_but_val_down,wres_get_but_val_cur
	.ref	wres_get_stick_val_cur

	.ref	change_anim1,change_anim1a
	.ref	change_anim2
	.ref	process_ptrs
	.ref	keep_attached
	.ref	get_opp_plyrmode
	.ref	wres_collis_off
	.ref	convert_facing
	.ref	check_secret_moves
	.ref	change_walk_anim
	.ref	set_rotate_anim
	.ref	execute_walk
	.ref	start_run,bounce_off_ropes
	.ref	face_each_other
	.ref	lineup_grapple
	.ref	break_lockup
	.ref	climb_turnbuckle

	;from DCSSOUND.ASM
	.ref	triple_sound

	;from PAL.ASM
	.ref	pal_find,pal_set


	.ref	wres_slave_anim
	.ref	adm_zip_anim

	.ref	adm_stand2_anim,adm_stand8_anim
	.ref	adm_stand4_anim,adm_stand6_anim
	.ref	adm_torso2_anim,adm_torso8_anim
	.ref	adm_torso4_anim,adm_torso6_anim

	.ref	adm_2_to_4_turn_anim,adm_8_to_6_turn_anim	;stand
	.ref	adm_4_to_2_turn_anim,adm_6_to_8_turn_anim
	.ref	adm_4_to_6_turn_anim,adm_6_to_4_turn_anim
	.ref	adm_2_to_8_turn_anim,adm_8_to_2_turn_anim
	.ref	adm_4_to_8_turn_anim,adm_6_to_2_turn_anim
	.ref	adm_2_to_6_turn_anim,adm_8_to_4_turn_anim

	.ref	adm_2_to_4_turn2_anim,adm_8_to_6_turn2_anim	;torso
	.ref	adm_4_to_2_turn2_anim,adm_6_to_8_turn2_anim
	.ref	adm_4_to_6_turn2_anim,adm_6_to_4_turn2_anim
	.ref	adm_2_to_8_turn2_anim,adm_8_to_2_turn2_anim
	.ref	adm_4_to_8_turn2_anim,adm_6_to_2_turn2_anim
	.ref	adm_2_to_6_turn2_anim,adm_8_to_4_turn2_anim

	.ref	adm_roll_frames

	.ref	adm_walk1_f2_anim,adm_walk2_f2_anim,adm_walk4_f2_anim
	.ref	adm_walk5_f2_anim,adm_walk6_f2_anim,adm_walk8_f2_anim

	.ref	adm_walk1_f4_anim,adm_walk2_f4_anim,adm_walk4_f4_anim
	.ref	adm_walk5_f4_anim,adm_walk6_f4_anim,adm_walk8_f4_anim

	.ref	adm_2_punch_anim,adm_4_punch_anim

	.ref	adm_2_butt_anim,adm_4_butt_anim

	.ref	adm_2_kick_anim,adm_4_kick_anim

	.ref	adm_2_uprcut_anim,adm_4_uprcut_anim

	.ref	adm_run_anim,adm_run2_anim,adm_ocrun_anim,adm_run_anim,adm_bounce_anim

 	.ref	adm_2_lbowdrop_anim,adm_4_lbowdrop_anim

	.ref	adm_grab_throw_anim
	.ref	adm_2_grabfling_anim
	.ref	adm_4_grabfling_anim

	.ref	adm_2_graboh_anim,adm_heldoh_anim
	.ref	adm_4_graboh_anim

	.ref	adm_2_fly_cline_anim,adm_4_fly_cline_anim
	.ref	adm_flying_kick_anim

	.ref	adm_4_head_hit_anim
	.ref	adm_2_face_hit_anim,adm_4_face_hit_anim
	.ref	adm_2_head_hit_anim
	.ref	adm_2_body_hit_anim,adm_4_body_hit_anim
	.ref	adm_fall_back_anim
	.ref	adm_faceup_getup_anim
	.ref	adm_hitonground_anim
	.ref	adm_4_stomp_anim,adm_2_stomp_anim
	.ref	adm_2ndwind_anim
	.ref	adm_climb_up_anim,adm_climb_down_anim
	.ref	adm_2_hitblock_anim,adm_4_hitblock_anim
	.ref	adm_hiptoss_anim,adm_hiptossfall_anim
	.ref	adm_4_push_anim,adm_2_push_anim
	.ref	adm_4_bigboot_anim,adm_2_bigboot_anim
	.ref	adm_4_block_anim,adm_2_block_anim
	.ref	adm_4_losebal_anim,adm_2_losebal_anim
	.ref	adm_dizzy_anim
	.ref	adm_raisearm_anim
	.ref	adm_ohthrow1_anim

	.ref	adm_2_lockup_anim,adm_4_lockup_anim





adam_secret_moves
	.long	rvxwbutton_hold	;must have this one

	.long	rvxwgrab_fling
	.long	rvxw2nd_wind1
	.long	rvxw2nd_wind2
	.long	0

rvxwbutton_hold
	rets


rvxwgrab_fling
	.word	B_GRAB | J_AWAY,	J_UP | J_DOWN
	.word	8000h | 10		;max
	.long	scrt_grabfling

rvxwturbo_run
	.word	B_TURBO,		J_ALL
	.word	B_TURBO,		J_ALL
	.word	8000h | 16 		;max
	.long	scrt_run

rvxw2nd_wind1
	.word	J_UP,			0
	.word	J_UP_AWAY,		0
	.word	J_AWAY,			0
	.word	J_DOWN_AWAY,		0
	.word	J_DOWN,			0
	.word	J_DOWN_TOWARD,		0
	.word	J_TOWARD,		0
	.word	J_UP_TOWARD,		0
	.word	J_UP,			0
	.word	8000h | 50		;max
	.long	scrt_2ndwind

rvxw2nd_wind2
	.word	J_UP,			0
	.word	J_UP_TOWARD,		0
	.word	J_TOWARD,		0
	.word	J_DOWN_TOWARD,		0
	.word	J_DOWN,			0
	.word	J_DOWN_AWAY,		0
	.word	J_AWAY,			0
	.word	J_UP_AWAY,		0
	.word	J_UP,			0
	.word	8000h | 50 		;max
	.long	scrt_2ndwind


 .if 0
	J_UP
	J_UP_TOWARD
	J_TOWARD
	J_DOWN_TOWARD
	J_DOWN
	J_DOWN_AWAY
	J_AWAY
	J_UP_AWAY

	B_PUNCH
	B_KICK
	B_BLOCK
	B_GRAB
	B_TURBO
	B_RUN

 .endif


scrt_grabfling



	movi	adm_2_grabfling_anim,a0

	move	*a13(FACING_DIR),a1
	btst	MOVE_UP_BIT,a1
	jrnz	rvxw2_grb
	movi	adm_4_grabfling_anim,a0
rvxw2_grb
	calla	change_anim1a

	WRSND	W_ADAM,GRABFLING_T1,GRABFLING_T2

	rets

scrt_run

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	rvxwno_scrt_run
	move	*a13(PLYRMODE),a0

	move	*a13(PLYRMODE),a0
	cmpi	MODE_RUNNING,a0
	jreq	rvxwno_scrt_run
	cmpi	MODE_BOUNCING,a0
	jreq	rvxwno_scrt_run
	calla	start_run

rvxwno_scrt_run
	rets

scrt_2ndwind
	movi	adm_2ndwind_anim,a0
	calla	change_anim1a
	rets


 SUBR	adam_ani_init

	movi	adm_stand2_anim,a0
	calla	change_anim1

	movi	adm_torso2_anim,a0
	calla	change_anim2

	rets



 SUBR	move_adam

	movi	100h,a0			;temp fix!
	move	a0,*a13(ANI_SPEED)

	movi	adam_secret_moves,a11
	calla	check_secret_moves

	move	*a13(PLYRMODE),a0
	sll	5,a0			;x 32
	addi	rmaymode_table,a0
	move	*a0,a0,L
	call	a0
	rets

rmaymode_table
	.long	mode_normal		;0
	.long	mode_running		;1
	.long	mode_inair		;2
	.long	mode_attached		;3
	.long	mode_onground		;4
	.long	mode_bouncing		;5
	.long	mode_onturnbkl		;6
	.long	mode_block		;7
	.long	mode_dizzy		;8
	.long	mode_dead		;9
	.long	mode_oppoverhead	;10
	.long	mode_climbturnbkl	;11
	.long	mode_waitanim		;12
	.long	mode_grapple		;13
	.long	mode_master		;14
	.long	mode_slave		;15
	.long	mode_headhold		;16


mode_normal


	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ymhmno_interrupt

 .if 1
	calla	get_opp_plyrmode
	cmpi	MODE_DEAD,a0
	jrne	ymhmopp_not_dead

	movi	adm_raisearm_anim,a0
	calla	change_anim1a
	rets

ymhmopp_not_dead

	GETLIFE
	jruc	ymhmnot_dizzy
	jrnz	ymhmnot_dizzy

	SETMODE	DIZZY
	movi	adm_dizzy_anim,a0
	calla	change_anim1a
	rets

 .endif
ymhmnot_dizzy


	move	a13,a0
	calla	wres_get_but_val_cur
	btst	PLAYER_BLOCK_BIT,a0	;maybe have to action tables?
	jrz	ymhmnblk			;1 for down transitions
	callr	ymhmblock			;& 1 for current buttons
ymhmnblk
	move	a13,a0
	calla	wres_get_but_val_down
	andi	011111b,a0
	X32	a0
	addi	ymhmaction_table,a0
	move	*a0,a0,L
	call	a0

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ymhmno_interrupt

	move	a13,a0
	calla	wres_get_stick_val_cur
	move	a0,*a13(MOVE_DIR)
	calla	climb_turnbuckle
	jrnc	ymhmno_climb
	rets
ymhmno_climb

	calla	execute_walk

ymhmno_interrupt
	rets

ymhmaction_table
	.long	ymhmz			;0
	.long	ymhmpunch			;1
	.long	ymhmblock			;2
	.long	ymhmz			;3
	.long	ymhmsuper_punch		;4
	.long	ymhmz,ymhmz,ymhmz		;5-7
	.long	ymhmkick			;8
	.long	ymhmz,ymhmz,ymhmz,ymhmz,ymhmz,ymhmz,ymhmz	;9-15
	.long	ymhmsuper_kick		;16
	.long	ymhmz,ymhmz,ymhmz,ymhmz,ymhmz,ymhmz,ymhmz	;17-23
	.long	ymhmz,ymhmz,ymhmz,ymhmz,ymhmz,ymhmz,ymhmz,ymhmz	;24-31

ymhmz
	rets

ymhmpunch		;1

	JJXM_INIT
	JJXM	NORMAL,		 60, 92,ymhmpunch_hdbutt,ymhmpunch_punch
	JJXM	DIZZY,		 60, 92,ymhmpunch_hdbutt,ymhmpunch_punch
	JJXM	ONGROUND,	 96,112,ymhmpunch_lbdrop,ymhmpunch_punch
	JJXM	RUNNING,	100, 60,ymhmpunch_hiptoss,ymhmpunch_punch
	JJXM	BOUNCING,	100, 60,ymhmpunch_hiptoss,ymhmpunch_punch
	JJXM	ONTURNBKL,		ymhmpunch_punch
	JJXM	CLIMBTURNBKL,		ymhmpunch_punch

 .if DEBUG
	LOCKUP
	rets
 .endif

ymhmpunch_rets
	rets

ymhmpunch_punch

	FACE24	adm,punch_anim
	calla	change_anim1a

	WRSND	W_ADAM,PUNCH_T1,PUNCH_T2

	rets

ymhmpunch_hdbutt

	FACE24	adm,butt_anim
	calla	change_anim1a

	WRSND	W_ADAM,PUNCH_T1,PUNCH_T2

	rets

ymhmpunch_lbdrop

	FACE24	adm,lbowdrop_anim
	calla	change_anim1a

	WRSND	W_ADAM,PUNCH_T1,PUNCH_T2

	rets

ymhmpunch_hiptoss

	movi	adm_hiptoss_anim,a0
	calla	change_anim1a

	WRSND	W_ADAM,PUNCH_T1,PUNCH_T2

	rets

ymhmblock		;2

	rets

ymhmsuper_punch	;4

	FACE24	adm,uprcut_anim
	calla	change_anim1a

	WRSND	W_ADAM,PUNCH_T1,PUNCH_T2

	rets

ymhmkick		;8

	JJXM_INIT
	JJXM	NORMAL,			ymhmkick_kick
	JJXM	DIZZY,			ymhmkick_kick
	JJXM	ONGROUND,	 96,112,ymhmkick_stomp,ymhmkick_kick
	JJXM	RUNNING,		ymhmkick_bigboot
	JJXM	BOUNCING,		ymhmkick_bigboot
	JJXM	ONTURNBKL,		ymhmkick_kick
	JJXM	CLIMBTURNBKL,		ymhmkick_kick

 .if DEBUG
	LOCKUP
	rets
 .endif

ymhmkick_kick

	FACE24	adm,kick_anim
	calla	change_anim1

	WRSND	W_ADAM,KICK_T1,KICK_T2
	rets

ymhmkick_stomp
	FACE24	adm,stomp_anim
	calla	change_anim1

	WRSND	W_ADAM,KICK_T1,KICK_T2
	rets

ymhmkick_bigboot
	FACE24	adm,bigboot_anim
	calla	change_anim1

	WRSND	W_ADAM,KICK_T1,KICK_T2
	rets

ymhmsuper_kick	;16

	JJXM_INIT
	JJXM	NORMAL,			ymhmskick_kick
	JJXM	DIZZY,			ymhmskick_kick
	JJXM	ONGROUND,	 96,112,ymhmskick_stomp,ymhmskick_kick
	JJXM	RUNNING,		ymhmskick_bigboot
	JJXM	BOUNCING,		ymhmskick_bigboot
	JJXM	ONTURNBKL,		ymhmskick_kick
	JJXM	CLIMBTURNBKL,		ymhmskick_kick

 .if DEBUG
	LOCKUP
	rets
 .endif

ymhmskick_kick

	FACE24	adm,kick_anim
	calla	change_anim1

	WRSND	W_ADAM,KICK_T1,KICK_T2
	rets

ymhmskick_stomp
	FACE24	adm,stomp_anim
	calla	change_anim1

	WRSND	W_ADAM,KICK_T1,KICK_T2
	rets

ymhmskick_bigboot
	FACE24	adm,bigboot_anim
	calla	change_anim1

	WRSND	W_ADAM,KICK_T1,KICK_T2
	rets


vszkXRUN_VAL	equ	60000h		;50000h
vszkZDRIFT_VAL	equ	20000h		;18000h

mode_running

	move	*a13(GETUP_TIME),a0		;delay b4 gaining control
	jrz	vszknot_octrl

	calla	keep_attached			;if still being flung
	jrnz	vszkout_of_control			;(Z=0 - still attached)


vszknot_octrl

	calla	bounce_off_ropes

	move	*a13(MOVE_DIR),a0

	movi	vszkXRUN_VAL,a1
	btst	PLAYER_RIGHT_BIT,a0
	jrnz	vszksetxvel

	movi	-vszkXRUN_VAL,a1
vszksetxvel
	move	a1,*a13(OBJ_XVEL),L


	move	*a13(GETUP_TIME),a0		;delay b4 gaining control
	jrnz	vszkout_of_control

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	vszkno_interrupt

	move	a13,a0
	calla	wres_get_but_val_cur
	btst	PLAYER_TURBO_BIT,a0		;turbo down?
	jrnz	vszkrun_down

	;has released turbo - go back to walk mode

	SETMODE	NORMAL

vszkrun_down
vszkno_interrupt

	move	a13,a0
	calla	wres_get_stick_val_cur
	clr	a1

	movi	-vszkZDRIFT_VAL,a1
	btst	PLAYER_UP_BIT,a0
	jrnz	vszksetvel

	movi	vszkZDRIFT_VAL,a1
	btst	PLAYER_DOWN_BIT,a0
	jrnz	vszksetvel

	clr	a1
vszksetvel
	move	a1,*a13(OBJ_ZVEL),L


	move	a13,a0
	calla	wres_get_but_val_down
	andi	011111b,a0
	X32	a0
	addi	vszkaction_table,a0
	move	*a0,a0,L
	call	a0
vszkout_of_control
	rets

vszkaction_table
	.long	vszkz			;0
	.long	vszkpunch			;1 (punch)
	.long	vszkz			;2 (block)
	.long	vszkz			;3
	.long	vszkz			;4 (super punch)
	.long	vszkz,vszkz,vszkz		;5-7
	.long	vszkkick			;8 kick
	.long	vszkz,vszkz,vszkz,vszkz,vszkz,vszkz,vszkz	;9-15
	.long	vszkz			;16 (super kick)
	.long	vszkz,vszkz,vszkz,vszkz,vszkz,vszkz,vszkz	;17-23
	.long	vszkz,vszkz,vszkz,vszkz,vszkz,vszkz,vszkz,vszkz	;24-31

vszkz
	rets

vszkpunch		;1

	FACE24	adm,fly_cline_anim
	calla	change_anim1

	;halve his velocity
	move	*a13(OBJ_XVEL),a0,L
	sra	1,a0
	move	a0,*a13(OBJ_XVEL),L

	WRSND	W_ADAM,FLYKICK_T1,FLYKICK_T2
	rets

vszkkick		;vszk4


	movi	adm_flying_kick_anim,a0
	calla	change_anim1

	SETMODE	INAIR

	move	*a13(OBJ_XVEL),a0,L
	sra	1,a0
	move	a0,*a13(OBJ_XVEL),L

	WRSND	W_ADAM,FLYKICK_T1,FLYKICK_T2

	rets


mode_inair	;2

	rets


mode_attached	;3


	calla	keep_attached
	jrnz	wvgpstill_attached

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	movi	MODE_NORMAL,a0
	move	a0,*a13(ANIMODE)

wvgpstill_attached
	rets

iireZROLL_VAL	equ	20000h
iireROLL_SPEED	equ	50h
iireROLL_FRAMES	equ	9

mode_onground	;4


	move	*a13(GETUP_TIME),a0
	jrnz	iirenomove

	move	a13,a0
	calla	wres_get_stick_val_cur

	movi	-iireROLL_SPEED,a2
	movi	-iireZROLL_VAL,a1
	btst	PLAYER_UP_BIT,a0
	jrnz	iiresetvel

	movi	iireROLL_SPEED,a2
	movi	iireZROLL_VAL,a1
	btst	PLAYER_DOWN_BIT,a0
	jrnz	iiresetvel

	clr	a1
	clr	a2

	movi	adm_faceup_getup_anim,a0
	calla	change_anim1

	SETMODE	NORMAL

	rets

iiresetvel
	move	a1,*a13(OBJ_ZVEL),L

	move	*a13(ROLL_POS),a0
	add	a2,a0
	jrp	iireok1
	addi	iireROLL_FRAMES<<8,a0
iireok1
	cmpi	iireROLL_FRAMES<<8,a0
	jrlt	iireok2

	subi	iireROLL_FRAMES<<8,a0
iireok2

	move	a0,*a13(ROLL_POS)

	move	*a13(ROLL_POS),a0
	srl	8,a0
	sll	5,a0
	addi	adm_roll_frames,a0
	move	*a0,a0,L
	move	a0,*a13(CUR_FRAME),L

iirenomove
	rets



mode_bouncing	;5

	clr	a0
	move	a0,*a13(OBJ_XVEL),L
	move	a0,*a13(OBJ_ZVEL),L

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	aoujnot_ended

	move	*a13(MOVE_DIR),a0
	xori	MOVE_LEFT+MOVE_RIGHT,a0	;flip LEFT/RIGHT
	move	a0,*a13(MOVE_DIR)

	move	*a13(NEW_FACING_DIR),a1
	andi	MOVE_UP+MOVE_DOWN,a1
	or	a0,a1			;LEFT or RIGHT
	move	a1,*a13(FACING_DIR)	;face same dir as run

	movi	adm_run2_anim,a0
	calla	change_anim1a

	SETMODE	RUNNING


aoujnot_ended
	rets


mode_onturnbkl	;6

	move	a13,a0
	calla	wres_get_stick_val_cur
	btst	PLAYER_DOWN_BIT,a0
	jrz	uhlpno_climbdown

	movi	adm_climb_down_anim,a0
	calla	change_anim1

	SETMODE	CLIMBTURNBKL

	rets


uhlpno_climbdown
	move	a13,a0
	calla	wres_get_but_val_down
	andi	01111b,a0		;mask out turbo button
	jrz	uhlpno_exit

	SETMODE	INAIR

	.ref	adm_diveofftb_anim
	movi	adm_diveofftb_anim,a0
	calla	change_anim1

	rets

uhlpno_exit
	rets


mode_block	;7

	move	a13,a0
	calla	wres_get_but_val_cur
	btst	PLAYER_BLOCK_BIT,a0
	jrnz	zrjpstill_blocking

	SETMODE	NORMAL

zrjpstill_blocking
	rets


mode_dizzy	;8

	rets

mode_dead	;9

	rets


mode_oppoverhead ;10

	move	*a13(ATTACH_PROC),a2,L		;proc attached to
	jrz	whoynot_attached
	move	*a2(ATTACH_PROC),a0,L
	jrnz	whoystill_attached

whoynot_attached

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	whoyno_interrupt

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	movi	MODE_NORMAL,a0
	move	a0,*a13(ANIMODE)

whoyno_interrupt
	rets

whoystill_attached

	move	a13,a0
	calla	wres_get_but_val_down
	andi	011111b,a0
	X32	a0
	addi	whoyaction_table,a0
	move	*a0,a0,L
	call	a0
	rets

whoyaction_table
	.long	whoyz			;0
	.long	whoypunch			;1
	.long	whoyblock			;2
	.long	whoyz			;3
	.long	whoysuper_punch		;4
	.long	whoyz,whoyz,whoyz		;5-7
	.long	whoykick			;8
	.long	whoyz,whoyz,whoyz,whoyz,whoyz,whoyz,whoyz	;9-15
	.long	whoysuper_kick		;16
	.long	whoyz,whoyz,whoyz,whoyz,whoyz,whoyz,whoyz	;17-23
	.long	whoyz,whoyz,whoyz,whoyz,whoyz,whoyz,whoyz,whoyz	;24-31

whoyz
	rets

whoypunch		;1
whoyblock		;2
whoysuper_punch	;4
whoykick		;8
whoysuper_kick	;16
	movi	adm_ohthrow1_anim,a0
	calla	change_anim1

	rets


mode_climbturnbkl	;11

	rets



mode_waitanim	;12

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	nqjxnot_ended

	move	*a13(CODE_ADDR),a0,L
	call	a0

nqjxnot_ended
	rets


mode_grapple	;13

	move	*a13(GETUP_TIME),a0
	jrnz	plpgstill_going


	calla	break_lockup
	rets






	SETMODE	HEADHOLD

	SETMODE	NORMAL	;<---- temp!

	PUSH	a13
	move	*a13(ATTACH_PROC),a13,L
	movi	MODE_SLAVE,a0
	move	a0,*a13(PLYRMODE)

	PULL	a13

	rets


plpgstill_going

	calla	lineup_grapple

	rets



mode_master	;14

	.ref	master_keep_attached
	calla	master_keep_attached	;keep slave attached to me

	rets


mode_slave	;15
	rets


mode_headhold	;16

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	txsznot_ended

	clr	a0
	movi	MODE_NORMAL,a1
	move	a1,*a13(PLYRMODE)

	move	*a13(ATTACH_PROC),a10,L
	jrz	txszskp
	move	a1,*a10(PLYRMODE)
	move	a0,*a10(ATTACH_PROC),L
txszskp
	move	a0,*a13(ATTACH_PROC),L

txsznot_ended
	rets



aoatVEL	equ	30000h
aoatDVEL	equ	21f0eh


 SUBR adm_velocity_table


	.long	0,	-aoatVEL	;0 UP

	.long	aoatDVEL,	-aoatDVEL	;1 UP-RIGHT

	.long	aoatVEL,	0	;2 RIGHT

	.long	aoatDVEL,	aoatDVEL	;3 DOWN-RIGHT

	.long	0,	aoatVEL	;4 DOWN

	.long	-aoatDVEL,	aoatDVEL	;5 DOWN-LEFT

	.long	-aoatVEL,	0	;6 LEFT

	.long	-aoatDVEL,	-aoatDVEL	;7 UP-LEFT



 SUBR adm_rotate_anims_table
	.long	adm_stand2_anim		;new = up right
	.long	adm_2_to_4_turn_anim	;    = down right
	.long	adm_2_to_6_turn_anim	;    = down left ***
	.long	adm_2_to_8_turn_anim	;    = up left

	.long	adm_4_to_2_turn_anim	;new = up right
	.long	adm_stand4_anim		;    = down right
	.long	adm_4_to_6_turn_anim	;    = down left
	.long	adm_4_to_8_turn_anim	;    = up left ***

	.long	adm_6_to_2_turn_anim	;new = up right ***
	.long	adm_6_to_4_turn_anim	;    = down right
	.long	adm_stand6_anim		;    = down left
	.long	adm_6_to_8_turn_anim	;    = up left

	.long	adm_8_to_2_turn_anim	;new = up right
	.long	adm_8_to_4_turn_anim	;    = down right ***
	.long	adm_8_to_6_turn_anim	;    = down left
	.long	adm_stand8_anim		;    = up left


 SUBR adm_leg_anims_table
	.long	adm_walk1_f2_anim	;1 (UP)
	.long	adm_walk1_f2_anim	;2 (UP_RIGHT)
	.long	adm_walk1_f4_anim	;3 (RIGHT)
	.long	adm_walk1_f4_anim	;4 (DOWN_RIGHT)
	.long	adm_walk1_f4_anim	;5 (DOWN)
	.long	adm_walk1_f4_anim	;6 (DOWN_LEFT)
	.long	adm_walk1_f2_anim	;7 (LEFT)
	.long	adm_walk1_f2_anim	;8 (UP-LEFT)

	.long	adm_walk2_f2_anim	;1 (UP)
	.long	adm_walk2_f2_anim	;2 (UP_RIGHT)
	.long	adm_walk2_f2_anim	;3 (RIGHT)
	.long	adm_walk2_f4_anim	;4 (DOWN_RIGHT)
	.long	adm_walk8_f4_anim	;5 (DOWN)
	.long	adm_walk8_f4_anim	;6 (DOWN_LEFT)
	.long	adm_walk4_f2_anim	;7 (LEFT)
	.long	adm_walk4_f2_anim	;8 (UP-LEFT)

	.long	adm_walk2_f2_anim	;1 (UP)
	.long	adm_walk2_f2_anim	;2 (UP_RIGHT)
	.long	adm_walk2_f2_anim	;3 (RIGHT)
	.long	adm_walk4_f4_anim	;4 (DOWN_RIGHT)
	.long	adm_walk4_f4_anim	;5 (DOWN)
	.long	adm_walk8_f4_anim	;6 (DOWN_LEFT)
	.long	adm_walk6_f2_anim	;7 (LEFT)
	.long	adm_walk6_f2_anim	;8 (UP-LEFT)


	.long	adm_walk2_f2_anim	;1 (UP)
	.long	adm_walk8_f2_anim	;2 (UP_RIGHT)
	.long	adm_walk4_f4_anim	;3 (RIGHT)
	.long	adm_walk4_f4_anim	;4 (DOWN_RIGHT)
	.long	adm_walk2_f4_anim	;5 (DOWN)
	.long	adm_walk6_f4_anim	;6 (DOWN_LEFT)
	.long	adm_walk2_f2_anim	;7 (LEFT)
	.long	adm_walk6_f2_anim	;8 (UP-LEFT)

	.long	adm_walk5_f2_anim	;1 (UP)
	.long	adm_walk5_f2_anim	;2 (UP_RIGHT)
	.long	adm_walk5_f4_anim	;3 (RIGHT)
	.long	adm_walk5_f4_anim	;4 (DOWN_RIGHT)
	.long	adm_walk5_f4_anim	;5 (DOWN)
	.long	adm_walk5_f4_anim	;6 (DOWN_LEFT)
	.long	adm_walk5_f2_anim	;7 (LEFT)
	.long	adm_walk5_f2_anim	;8 (UP-LEFT)

	.long	adm_walk2_f2_anim	;1 (UP)
	.long	adm_walk6_f2_anim	;2 (UP_RIGHT)
	.long	adm_walk2_f2_anim	;3 (RIGHT)
	.long	adm_walk6_f4_anim	;4 (DOWN_RIGHT)
	.long	adm_walk2_f4_anim	;5 (DOWN)
	.long	adm_walk4_f4_anim	;6 (DOWN_LEFT)
	.long	adm_walk2_f2_anim	;7 (LEFT)
	.long	adm_walk8_f2_anim	;8 (UP-LEFT)

	.long	adm_walk2_f2_anim	;1 (UP)
	.long	adm_walk6_f2_anim	;2 (UP_RIGHT)
	.long	adm_walk6_f2_anim	;3 (RIGHT)
	.long	adm_walk8_f4_anim	;4 (DOWN_RIGHT)
	.long	adm_walk4_f4_anim	;5 (DOWN)
	.long	adm_walk4_f4_anim	;6 (DOWN_LEFT)
	.long	adm_walk2_f2_anim	;7 (LEFT)
	.long	adm_walk2_f2_anim	;8 (UP-LEFT)

	.long	adm_walk2_f2_anim	;1 (UP)
	.long	adm_walk4_f2_anim	;2 (UP_RIGHT)
	.long	adm_walk6_f2_anim	;3 (RIGHT)
	.long	adm_walk8_f4_anim	;4 (DOWN_RIGHT)
	.long	adm_walk6_f4_anim	;5 (DOWN)
	.long	adm_walk2_f4_anim	;6 (DOWN_LEFT)
	.long	adm_walk2_f2_anim	;7 (LEFT)
	.long	adm_walk2_f2_anim	;8 (UP-LEFT)


 SUBR adm_torso_anims_table
	.long	adm_torso2_anim		;new = up right
	.long	adm_2_to_4_turn2_anim	;    = down right
	.long	adm_2_to_6_turn2_anim	;    = down left ***
	.long	adm_2_to_8_turn2_anim	;    = up left

	.long	adm_4_to_2_turn2_anim	;new = up right
	.long	adm_torso4_anim		;    = down right
	.long	adm_4_to_6_turn2_anim	;    = down left
	.long	adm_4_to_8_turn2_anim	;    = up left ***

	.long	adm_6_to_2_turn2_anim	;new = up right ***
	.long	adm_6_to_4_turn2_anim	;    = down right
	.long	adm_torso6_anim		;    = down left
	.long	adm_6_to_8_turn2_anim	;    = up left

	.long	adm_8_to_2_turn2_anim	;new = up right
	.long	adm_8_to_4_turn2_anim	;    = down right ***
	.long	adm_8_to_6_turn2_anim	;    = down left
	.long	adm_torso8_anim		;    = up left



 SUBR	adam_hit

	move	*a10(ATTACK_MODE),a0	;type of attack that hit adam
	move	a0,a1
	sll	5,a1			;x 32
	addi	txfvhit_table,a1
	move	*a1,a1,L		;appropriate routine
	call	a1

	rets

txfvhit_table
	.long	_punch		;0
	.long	_hdbutt		;1
	.long	_kick		;2
	.long	_flykick	;3
	.long	_grabthrow	;4
	.long	_uprcut		;5
	.long	_lbowdrop	;6
	.long	_grabhold	;7
	.long	_grabfling	;8
	.long	_push		;9
	.long	_hiptoss	;10
	.long	_bigboot	;11
	.long	_knee		;12
	.long	_grapple	;13
	.long	_boxpunch	;14
	.long	_stomp		;15
	.long	_spinkick	;16
	.long	_cline		;17
	.long	_headhold	;18
	.long	_jumpkick	;19
	.long	_run		;20
	.long	_puppet		;21
	.long	_backhand	;22
	.long	_buzz		;23


_punch




	WRSND	W_ADAM,PUNCH_L1,PUNCH_L2

	GETLIFE
	jrnz	zzoenot_dizzy

	movi	adm_fall_back_anim,a0
	calla	change_anim1

	SETMODE	DEAD
	rets

zzoenot_dizzy

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jreq	zzoeblock

	DAMAGE	1

	SETMODE	NORMAL			;don't change if blocking

	movi	adm_2_head_hit_anim,a0
	move	*a13(FACING_DIR),a1
	btst	MOVE_UP_BIT,a1
	jrnz	zzoe2_hit
	movi	adm_4_head_hit_anim,a0
zzoe2_hit
	calla	change_anim1a
	calla	wres_collis_off
	rets

zzoeblock
	DAMAGE	1

	movi	adm_2_hitblock_anim,a0
	move	*a13(FACING_DIR),a1
	btst	MOVE_UP_BIT,a1
	jrnz	zzoe2_blk
	movi	adm_4_hitblock_anim,a0
zzoe2_blk
	calla	change_anim1a

	movi	[2,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1		;offx - defx
	jrgt	zzoeslide_right	;attacker on left
	neg	a2
zzoeslide_right
	move	a2,*a13(OBJ_XVEL),L

	calla	wres_collis_off
	rets


_hdbutt

	DAMAGE	10

	WRSND	W_ADAM,HDBUTT_L1,HDBUTT_L2

	movi	adm_2_hitblock_anim,a0
	move	*a13(FACING_DIR),a1
	btst	MOVE_UP_BIT,a1
	jrnz	lfzt2_blk
	movi	adm_4_hitblock_anim,a0
lfzt2_blk

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jreq	lfztblock1

	SETMODE	NORMAL

	movi	adm_2_face_hit_anim,a0
	move	*a13(FACING_DIR),a1
	btst	MOVE_UP_BIT,a1
	jrnz	lfzt2_noblk
	movi	adm_4_face_hit_anim,a0
lfzt2_noblk
	calla	change_anim1a

	movi	[1,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	lfztgo_rgt			;attacker on left
	neg	a2
lfztgo_rgt
	move	a2,*a13(OBJ_XVEL),L

	calla	wres_collis_off
	rets

lfztblock1
	calla	change_anim1a

	calla	wres_collis_off
	rets

_kick

	DAMAGE	10

	WRSND	W_ADAM,KICK_L1,KICK_L2

	movi	adm_2_hitblock_anim,a0
	move	*a13(FACING_DIR),a1
	btst	MOVE_UP_BIT,a1
	jrnz	fnqh2_blk
	movi	adm_4_hitblock_anim,a0
fnqh2_blk
	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jreq	fnqhanim

	SETMODE	NORMAL

	movi	adm_2_body_hit_anim,a0
	move	*a13(FACING_DIR),a1
	btst	MOVE_UP_BIT,a1
	jrnz	fnqhanim
	movi	adm_4_body_hit_anim,a0

fnqhanim
	calla	change_anim1a

	calla	wres_collis_off
	rets

_flykick

	DAMAGE	10

	WRSND	W_ADAM,FLYKICK_L1,FLYKICK_L2
	clr	a0
	move	a0,*a13(ROLL_POS)
	movi	TSEC*2,a0
	move	a0,*a13(GETUP_TIME)

	movi	adm_fall_back_anim,a0
	calla	change_anim1
	movi	[4,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1		;offx - defx
	jrgt	dphtfall_right	;attacker on left
	movi	[-4,0],a2
dphtfall_right
	move	a2,*a13(OBJ_XVEL),L

	SETMODE	NORMAL

	calla	wres_collis_off
	rets

_grabthrow

	DAMAGE	10

	SETMODE	ATTACHED

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

	movi	adm_heldoh_anim,a0
	calla	change_anim1

	calla	wres_collis_off
	rets

_uprcut

	DAMAGE	10

	WRSND	W_ADAM,UPRCUT_L1,UPRCUT_L2

	movi	adm_fall_back_anim,a0
	calla	change_anim1

	clr	a0
	move	a0,*a13(ROLL_POS)
	movi	TSEC*2,a0
	move	a0,*a13(GETUP_TIME)


	movi	[10,0],a0
	move	a0,*a13(OBJ_YVEL),L

	movi	[2,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1		;offx - defx
	jrgt	niaxfall_right2	;attacker on left
	movi	[-2,0],a2
niaxfall_right2
	move	a2,*a13(OBJ_XVEL),L

	calla	wres_collis_off
	rets

_lbowdrop

	DAMAGE	10

	WRSND	W_ADAM,LBOWDROP_L1,LBOWDROP_L2

	movi	adm_hitonground_anim,a0
	calla	change_anim1a

	calla	wres_collis_off
	rets
	
_grabhold

	DAMAGE	10

	WRSND	W_ADAM,GRABHOLD_L1,GRABHOLD_L2

	SETMODE	ATTACHED

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

	movi	adm_heldoh_anim,a0
	calla	change_anim1

	calla	wres_collis_off
	rets

_grabfling

	DAMAGE	10

	SETMODE	RUNNING

	movi	TSEC*2,a0
	move	a0,*a13(GETUP_TIME)		;delay b4 gaining control

	move	*a13(FACING_DIR),a0	;use facing
	andi	PLAYER_LEFT_VAL|PLAYER_RIGHT_VAL,a0
	move	a0,*a13(MOVE_DIR)

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

	movi	adm_ocrun_anim,a0
	calla	change_anim1

	calla	wres_collis_off
	rets

_push

	DAMAGE	10

	WRSND	W_ADAM,LBOWDROP_L1,LBOWDROP_L2

	SETMODE	NORMAL

	movi	adm_2_losebal_anim,a0
	move	*a13(FACING_DIR),a1
	btst	MOVE_UP_BIT,a1
	jrnz	chds2_loseb
	movi	adm_4_losebal_anim,a0
chds2_loseb
	calla	change_anim1

	movi	[5,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1		;offx - defx
	jrgt	chdsfall_rightlb	;attacker on left
	neg	a2
chdsfall_rightlb
	move	a2,*a13(OBJ_XVEL),L

	calla	wres_collis_off
	rets

_hiptoss

	DAMAGE	10

	WRSND	W_ADAM,HIPTOSS_L1,HIPTOSS_L2

	clr	a0
	move	a0,*a13(ROLL_POS)
	movi	TSEC*2,a0
	move	a0,*a13(GETUP_TIME)

	movi	adm_hiptossfall_anim,a0
	calla	change_anim1

	calla	wres_collis_off
	rets

_bigboot

	DAMAGE	10

	WRSND	W_ADAM,LBOWDROP_L1,LBOWDROP_L2

	clr	a0
	move	a0,*a13(ROLL_POS)
	movi	TSEC*2,a0
	move	a0,*a13(GETUP_TIME)

	movi	adm_fall_back_anim,a0
	calla	change_anim1
	movi	[4,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1		;offx - defx
	jrgt	vwmbfall_rightz	;attacker on left
	movi	[-4,0],a2
vwmbfall_rightz
	move	a2,*a13(OBJ_XVEL),L

	SETMODE	NORMAL

	calla	wres_collis_off
	rets

_knee
	calla	wres_collis_off
	rets


_grapple		;13


	calla	face_each_other

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

 	move	*a13(OBJ_ZPOSINT),a0
 	move	*a10(OBJ_ZPOSINT),a1
	add	a1,a0
	srl	1,a0
	sll	16,a0
 	move	a0,*a13(OBJ_ZPOS),L
 	move	a0,*a10(OBJ_ZPOS),L


	movi	MODE_GRAPPLE,a0
	move	a0,*a13(PLYRMODE)		;slave
	move	a0,*a10(PLYRMODE)		;master

	movi	20*3,a0				;20 button presses
	movi	1*3,a0				;1 button press !!! temp !!!
	move	a0,*a13(GETUP_TIME)
	move	a0,*a10(GETUP_TIME)

	movi	wres_slave_anim,a0
	calla	change_anim1a

	calla	wres_collis_off
	rets


_boxpunch	;14
_stomp		;15
_spinkick	;16
_cline		;17
_headhold	;18
_jumpkick	;19
_run		;20
_puppet		;21
_backhand	;22
_buzz		;23
	rets

	.end

