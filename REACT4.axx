
	.file	"react4.asm"
	.title	"wrester reactions to hits"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"plyr.equ"
	.include	"anim.equ"
	.include	"audit.equ"
	.include	"sound.h"
	.include 	"ring.equ"
	.include	"lifebar.h"



	.ref	change_anim1a
	.ref	wres_collis_off
	.ref	block_hit_flail
	.ref	block_hit
	.ref	face_each_other
	.ref	CALL_NASTY_MOVE
	.ref	set_getup_time
	.ref	fall_back_tbl
	.ref	triple_sound
	.ref	ADD_IF_SILENT
	.ref	CALL_DROP_KICK,CALL_AVERAGE_MOVE
	.ref	DO_SCREAM
	.ref	shn_combo_run_stomp_anim
	.ref	shn_run_stomp_anim



	.ref	bam_2_head_hit2_anim,bam_4_head_hit2_anim
	.ref	bam_3_butt_drop_anim

	.ref	dnk_2_head_hit2_anim,dnk_4_head_hit2_anim
	.ref	dnk_knockdwn_anim
	.ref	dnk_fall_back_anim
	.ref	dnk_head_hit_dizzy_anim
	.ref	dnk_3_head_held_anim
	.ref	dnk_belly_anim

	.ref	hrt_2_head_hit2_anim,hrt_4_head_hit2_anim
	.ref	hrt_4_head_hit_dizzy_anim
	.ref	hrt_running_ground_punch_anim

	.ref	lex_4_head_hit2_anim
	.ref	lex_head_hit_dizzy_anim
	.ref	lex_2_head_hit2_anim
	.ref	lex_flying_ground_punch_anim

	.ref	rzr_2_head_hit2_anim,rzr_4_head_hit2_anim
	.ref	rzr_2_head_hit3_anim,rzr_4_head_hit3_anim


	.ref	shn_2_head_hit3_anim,shn_4_head_hit3_anim
	.ref	shn_4_head_hit_dizzy_anim

	.ref	und_2_head_hit2_anim,und_4_head_hit2_anim
	.ref	und_fall_back_anim
	.ref	und_flying_butt_drop_anim

	.ref	yok_2_head_hit2_anim,yok_4_head_hit2_anim
	.ref	yok_2_head_hit3_anim,yok_4_head_hit3_anim

	.ref	hit_damage_pending,hitonground_tbl



 SUBR	hit_stomp
 SUBR	hit_buttstomp

	;if we're standing, ignore this
	move	*a13(PLYRMODE),a0
	cmpi	MODE_NORMAL,a0

	jreq	zixamode_normal

	cmpi	MODE_BLOCK,a0
	jreq	zixamode_normal




	calla	wres_collis_off

	move	*a13(PLYRMODE),a0
	cmpi	MODE_ONGROUND,a0
	jrz	zixaon_grnd
	cmpi	MODE_DEAD,a0
	jrnz	zixaisdead

zixaon_grnd
	FACETBL hitonground_tbl
	calla	change_anim1a



	move	*a10(ANIBASE),a0,L
	cmpi	shn_combo_run_stomp_anim,a0
	jreq	zixado_unnormal_sounds
	cmpi	shn_run_stomp_anim,a0
	jrne	zixanormal_sounds
zixado_unnormal_sounds
	calla	DO_SCREAM
	jruc	zixaunnormal_sounds

zixanormal_sounds
	WRSNDX	LBOWDROP_L1,LBOWDROP_L2
	movi	43h,a0
	calla	triple_sound

zixaunnormal_sounds
	move	*a10(ANIBASE),a0,L

	cmpi	dnk_belly_anim,a0
	jreq	zixabounce

	cmpi	und_flying_butt_drop_anim,a0
	jreq	zixabounce

	cmpi	lex_flying_ground_punch_anim,a0
	jreq	zixabounce3


	jruc	zixaisdead

zixabounce2
	;REALLY NASTY HACK!  Watch out if you modify this in any way.
	callr	zixabounce
	movi	-10000h,a0
	move	a0,*a10(OBJ_ZVEL),L
	rets

zixabounce3
	;REALLY NASTY HACK!  Watch out if you modify this in any way.
	callr	zixabounce
	movi	40000h,a0
	move	a0,*a10(OBJ_YVEL),L
	rets

zixabounce
	movi	50000h,a0
	move	a0,*a10(OBJ_YVEL),L
	movi	10000h,a0
	move	a0,*a10(OBJ_ZVEL),L
	clr	a0
	move	a0,*a10(OBJ_XVEL),L

	.ref	shake_all_ropes
	calla	shake_all_ropes
	PUSH	a10
	movi	8,a10
	.ref	SHAKER2
	calla	SHAKER2
	PULL	a10

zixaisdead
	calla	wres_collis_off
	rets

zixamode_normal
	;OOPS!  Don't do any damage.
	clr	a14
	move	a14,@hit_damage_pending
	jruc	zixaisdead


 SUBR	hit_bstomp

	calla	DO_SCREAM
	calla	wres_collis_off

	move	*a13(PLYRMODE),a0
	cmpi	MODE_ONGROUND,a0
	jrz	zgbfon_grnd
	cmpi	MODE_DEAD,a0
	jrz	zgbfon_grnd


	calla	DO_SCREAM
	movi	43h,a0
	calla	triple_sound

	calla	set_getup_time

	SETMODE	NORMAL
	move	*a13(OBJ_YPOSINT),a14
	move	*a13(GROUND_Y),a0
	sub	a0,a14
	cmpi	20,a14
	jrge	zgbffall_back

zgbfkdwn
	FACETBL knockdwn
	calla	change_anim1a
	calla	wres_collis_off

	rets

zgbffall_back
	move	*a10(WRESTLERNUM),a0
	cmpi	5,a0
	jrz	zgbfkdwn
	FACETBL	fall_back_tbl

	calla	change_anim1a
	calla	wres_collis_off

	movi	[2,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	zgbffall_right		;attacker on left
	neg	a2
zgbffall_right
	move	a2,*a13(OBJ_XVEL),L

	movi	-40000h,a0
	move	a0,*a13(OBJ_YVEL),L

	rets

zgbfon_grnd
	FACETBL hitonground_tbl
	calla	change_anim1a
zgbfisdead
	calla	wres_collis_off
	rets

knockdwn
	.ref	hrt_knockdwn_anim
	.ref	rzr_knockdwn_anim
	.ref	und_knockdwn_anim
	.ref	yok_knockdwn_anim
	.ref	shn_knockdwn_anim
	.ref	bam_knockdwn_anim
	.ref	dnk_knockdwn_anim
	.ref	lex_knockdwn_anim

	.long	hrt_knockdwn_anim		;0 Bret Hart
	.long	rzr_knockdwn_anim		;1 Razor Ramon
	.long	und_knockdwn_anim		;2 Undertaker
	.long	yok_knockdwn_anim		;3 Yokozuna
	.long	shn_knockdwn_anim		;4 Shawn Michaels
	.long	bam_knockdwn_anim		;5 Bam Bam
	.long	dnk_knockdwn_anim		;6 Doink
	.long	0				;7 spare
	.long	lex_knockdwn_anim		;8 Lex Luger
	.long	0				;9 Referee


 SUBR	hit_bstomp2

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	zgbfnoblock

	calla	block_hit_flail
	rets

zgbfnoblock
	WRSNDX	FLYKICK_L1,FLYKICK_L2
	calla	wres_collis_off

	move	*a13(PLYRMODE),a0
	MOVE	*A10(COMBO_COUNT),A1
	JRNZ	NOT_NORMAL_CHECK
	cmpi	MODE_ONGROUND,a0
	jrz	zgbfisdead
	cmpi	MODE_DEAD,a0
	jrz	zgbfisdead
	JRUC	NORMAL_CHECK
NOT_NORMAL_CHECK
	move	*a13(OBJ_CONTROL),a0
	xori	M_FLIPH,a0
	move	a0,*a13(OBJ_CONTROL)
	CLR	A0
	MOVE	A0,*A13(OBJ_YVEL),L
	MOVE	*A13(GROUND_Y),*A13(OBJ_YPOSINT)
NORMAL_CHECK

	calla	set_getup_time

	calla	DO_SCREAM
	movi	43h,a0
	calla	triple_sound
	calla	CALL_NASTY_MOVE
	FACETBL knockdwn
	calla	change_anim1a
	calla	wres_collis_off
	rets

 SUBR	hit_hammer

	movi	45h,a0
	calla	triple_sound
	calla	CALL_NASTY_MOVE

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	bwalnoblock

	calla	block_hit
	rets

bwalnoblock
	jruc	hit_bstomp

 SUBR	hit_spinkick


	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	aelknoblock

	calla	block_hit_flail
	rets

aelknoblock
	movi	43h,a0
	calla	triple_sound
	calla	CALL_AVERAGE_MOVE

	GETLIFE
	jrz	aelkisdead

	WRSNDX	KICK_L1,KICK_L2

	SETMODE	NORMAL
	move	*a13(OBJ_YPOSINT),a14
	move	*a13(GROUND_Y),a0
	sub	a0,a14
	cmpi	20,a14
	jrge	aelkfall_back


	FACE24TBL aelkhead_hit2
	calla	change_anim1a
aelkisdead
	calla	wres_collis_off

	rets

aelkfall_back
	FACETBL	fall_back_tbl

	calla	change_anim1a
	calla	wres_collis_off

	movi	[3,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	aelkfall_right		;attacker on left
	neg	a2
aelkfall_right
	move	a2,*a13(OBJ_XVEL),L

	rets

aelkhead_hit2
	REFLONG	hrt_2_head_hit3_anim,hrt_4_head_hit3_anim		;0 Bret Hart
	.long	rzr_2_head_hit3_anim,rzr_4_head_hit3_anim		;1 Razor Ramon
	.long	und_2_head_hit2_anim,und_4_head_hit2_anim		;2 Undertaker
	.long	yok_2_head_hit3_anim,yok_4_head_hit3_anim		;3 Yokozuna
	.long	shn_2_head_hit3_anim,shn_4_head_hit3_anim		;4 Shawn Michaels
	.long	bam_2_head_hit2_anim,bam_4_head_hit2_anim		;5 Bam Bam
	.long	dnk_2_head_hit2_anim,dnk_4_head_hit2_anim		;6 Doink
	.long	0,0							;7 spare
	REFLONG	lex_2_head_hit3_anim,lex_4_head_hit3_anim		;8 Lex Luger
	.long	0,0							;9 Referee

 SUBR	hit_cline


	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	ktkunoblock

	calla	block_hit_flail
	rets

ktkunoblock

	calla	CALL_DROP_KICK


	WRSNDX	FLYKICK_L1,FLYKICK_L2

	GETLIFE
	jrz	ktkuskip



	SETMODE	NORMAL

ktkuskip

	clr	a0
	move	a0,*a10(OBJ_ZVEL),L	;Zero zvel after collis

	move	*a10(OBJ_ZPOS),a0,L
	subi	[1,0],a0
	move	a0,*a13(OBJ_ZPOS),L

	move	a0,*a13(ROLL_POS)
	calla	set_getup_time

	FACETBL	ktkufall_back2
	calla	change_anim1a

	movi	[3,0],a2
	move	*a10(OBJ_XVEL),a0,L
	jrp	ktkufall_right
	neg	a2


ktkufall_right


	move	a2,*a13(OBJ_XVEL),L

ktkuisdead
	calla	wres_collis_off
	rets

ktkufall_back2
	REFLONG	hrt_fall_back2_anim		;0 Bret Hart
	REFLONG	rzr_fall_back2_anim		;1 Razor Ramon
	REFLONG	und_fall_back2_anim		;2 Undertaker
	REFLONG	yok_fall_back2_anim		;3 Yokozuna
	REFLONG	shn_fall_back2_anim		;4 Shawn Michaels
	REFLONG	bam_fall_back2_anim		;5 Bam Bam
	REFLONG	dnk_fall_back2_anim		;6 Doink
	.long	0				;7 spare
	REFLONG	lex_fall_back2_anim		;8 Lex Luger


 SUBR	hit_headhold

	rets

 SUBR	hit_jumpkick

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	wstmnoblock

	calla	block_hit_flail
	rets

wstmnoblock

	GETLIFE
	jrz	wstmisdead

	WRSNDX	KICK_L1,KICK_L2


	SETMODE	NORMAL
	move	*a13(OBJ_YPOSINT),a14
	move	*a13(GROUND_Y),a0
	sub	a0,a14
	cmpi	20,a14
	jrge	wstmfall_back


	FACE24TBL wstmhead_hit2
	calla	change_anim1a
wstmisdead
	calla	wres_collis_off

	rets

wstmfall_back
	FACETBL	fall_back_tbl

	calla	change_anim1a
	calla	wres_collis_off

	movi	[3,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	wstmfall_right		;attacker on left
	neg	a2
wstmfall_right
	move	a2,*a13(OBJ_XVEL),L

	rets
 

wstmhead_hit2
	.long	hrt_2_head_hit2_anim,hrt_4_head_hit2_anim		;0 Bret Hart
	.long	rzr_2_head_hit2_anim,rzr_4_head_hit2_anim		;1 Razor Ramon
	.long	und_2_head_hit2_anim,und_4_head_hit2_anim		;2 Undertaker
	.long	yok_2_head_hit2_anim,yok_4_head_hit2_anim		;3 Yokozuna
	.long	shn_2_head_hit3_anim,shn_4_head_hit3_anim		;4 Shawn Michaels
	.long	bam_2_head_hit2_anim,bam_4_head_hit2_anim		;5 Bam Bam
	.long	dnk_2_head_hit2_anim,dnk_4_head_hit2_anim		;6 Doink
	.long	dnk_2_head_hit2_anim,dnk_4_head_hit2_anim		;7 spare
	.long	lex_2_head_hit2_anim,lex_4_head_hit2_anim		;8 Lex Luger
	.long	dnk_2_head_hit2_anim,dnk_4_head_hit2_anim		;9 Referee


	.end

