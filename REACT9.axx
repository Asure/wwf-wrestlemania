
	.file	"react9.asm"
	.title	"wrester reactions to hits"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"plyr.equ"
	.include	"anim.equ"
	.include	"audit.equ"
	.include	"sound.h"
	.include 	"ring.equ"
	.include	"lifebar.h"



	.ref	fall_back_tbl,head_hit_dizzy_tbl,head_hit_tbl,head_hit2_tbl
	.ref	block_hit_flail,triple_sound,hit_damage_pending
	.ref	change_anim1a,wres_collis_off,block_hit
	.ref	CALL_FACE_HIT,hitonground_tbl
	.ref	knee_hit_tbl,burn_tbl







 SUBR	hit_rslash

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	vaoinoblock


	calla	block_hit_flail
	rets

vaoinoblock


	CALLA	CALL_FACE_HIT

	GETLIFE
	jrz	vaoiisdead


	WRSNDX	RSLASH_L1,RSLASH_L2


	SETMODE	NORMAL

vaoido_hit
	move	*a13(OBJ_YPOSINT),a14
	move	*a13(GROUND_Y),a0
	sub	a0,a14
	cmpi	20,a14
	jrge	vaoifall_back

	;normal punch
	FACE24TBL head_hit2_tbl
	calla	change_anim1a
vaoiisdead
	calla	wres_collis_off
	rets

vaoifall_back
	FACETBL fall_back_tbl

	calla	change_anim1a
	calla	wres_collis_off

	movi	[3,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	vaoifall_right		;attacker on left
	neg	a2
vaoifall_right
	move	a2,*a13(OBJ_XVEL),L

	rets


 SUBR	hit_headdslash

	WRSNDX	RSLASH_L1,RSLASH_L2

	GETLIFE
	jrz	fkhbisdead


	FACE24TBL knee_hit_tbl
	calla	change_anim1a

	movi	2C000h,a0
	move	a0,*a13(OBJ_YVEL),L
	clr	a0
	move	a0,*a13(OBJ_XVEL),L

	;move a little bit toward the attacker
	movk	5,a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1
	jrle	fkhbok
   	neg	a2
fkhbok
	add	a2,a1
	move	a1,*a13(OBJ_XPOSINT)

	calla	wres_collis_off
	rets

fkhbisdead

	FACETBL	fall_back_tbl
	calla	change_anim1a

	movi	[4,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	fkhbgo_rgt			;attacker on left
	neg	a2
fkhbgo_rgt
	move	a2,*a13(OBJ_XVEL),L

	calla	wres_collis_off
	rets


 SUBR	hit_headuslash

	WRSNDX	RSLASH_L1,RSLASH_L2

	GETLIFE
	jrz	rjdzisdead

	FACE24TBL knee_hit_tbl
	calla	change_anim1a

	movi	40000h,a0
	move	a0,*a13(OBJ_YVEL),L
	clr	a0
	move	a0,*a13(OBJ_XVEL),L

	calla	wres_collis_off
	rets

rjdzisdead

	FACETBL	fall_back_tbl
	calla	change_anim1a

	movi	[4,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	rjdzgo_rgt			;attacker on left
	neg	a2
rjdzgo_rgt
	move	a2,*a13(OBJ_XVEL),L

	calla	wres_collis_off
	rets


 SUBR	hit_napalm

	;if we're standing, ignore this
	move	*a13(PLYRMODE),a0
	cmpi	MODE_NORMAL,a0

	jreq	cbvjmode_normal

	cmpi	MODE_BLOCK,a0
	jreq	cbvjmode_normal

	calla	wres_collis_off

	move	*a13(PLYRMODE),a0
	cmpi	MODE_ONGROUND,a0
	jrz	cbvjon_grnd
	cmpi	MODE_DEAD,a0
	jrnz	cbvjisdead

cbvjon_grnd
	FACETBL burn_tbl
	calla	change_anim1a

	;set the DEAD_ANIM bit - if this hit kills us, we don't want to
	; go through a normal convulse.
	move	*a13(STATUS_FLAGS),a14,L
	ori	M_DEAD_ANIM,a14
	move	a14,*a13(STATUS_FLAGS),L

	WRSNDX	LBOWDROP_L1,LBOWDROP_L2
	movi	43h,a0
	calla	triple_sound

cbvjisdead	calla	wres_collis_off
	rets

cbvjmode_normal
	;OOPS!  Don't do any damage.
	clr	a14
	move	a14,@hit_damage_pending
	jruc	cbvjisdead



	.end

