
	.file	"react5.asm"
	.title	"wrester reactions to hits"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"plyr.equ"
	.include	"anim.equ"
	.include	"audit.equ"
	.include	"sound.h"
	.include 	"ring.equ"
	.include	"lifebar.h"
	.include	"damage.equ"



	.ref	change_anim1a
	.ref	wres_collis_off
	.ref	block_hit_flail
	.ref	block_hit
	.ref	slide_offscr
	.ref	wres_slave_anim
	.ref	triple_sound,CALL_AVERAGE_MOVE
	.ref	CALL_NASTY_MOVE,PCNT




	.ref	bam_2_head_hit2_anim
	.ref	bam_4_head_hit2_anim
	.ref	bam_4_losebal_anim
	.ref	bam_fall_back_anim
	.ref	bam_get_buzz_anim

	.ref	dnk_2_bncoff_anim
	.ref	dnk_4_bncoff_dizzy_anim
	.ref	dnk_4_bncoff_anim
	.ref	dnk_4_losebal_anim
	.ref	dnk_4_losebal_dizzy_anim
	.ref	dnk_get_buzz_anim
	.ref	dnk_head_hit2_anim
	.ref	dnk_head_hit_dizzy_anim
	.ref	dnk_2_head_hit2_anim,dnk_4_head_hit2_anim
	.ref	dnk_fall_back_anim

	.ref	hrt_fall_back_anim,hrt_4_losebal_dizzy_anim
	.ref	hrt_2_head_hit2_anim,hrt_4_head_hit2_anim
	.ref	hrt_4_head_hit_dizzy_anim
	.ref	hrt_get_buzz_anim,hrt_4_losebal_anim

	.ref	lex_2_head_hit2_anim,lex_4_head_hit2_anim
	.ref	lex_head_hit_dizzy_anim
	.ref	lex_4_losebal_anim
	.ref	lex_fall_back_anim
	.ref	lex_get_buzz_anim

	.ref	rzr_2_head_hit2_anim,rzr_4_head_hit2_anim
	.ref	rzr_4_losebal_anim
	.ref	rzr_fall_back_anim
	.ref	rzr_get_buzz_anim

	.ref	shn_4_head_hit_dizzy_anim
	.ref	shn_2_head_hit3_anim,shn_4_head_hit3_anim
	.ref	shn_fall_back_anim
	.ref	shn_4_losebal_anim
	.ref	shn_get_buzz_anim

	.ref	und_fall_back_anim
	.ref	und_get_buzz_anim
	.ref	und_2_head_hit2_anim,und_4_head_hit2_anim
	.ref	und_head_hit_dizzy_anim
	.ref	und_4_losebal_dizzy_anim
	.ref	und_4_losebal_anim
	.ref	und_4_bncoff_dizzy_anim
	.ref	und_2_bncoff_anim,und_4_bncoff_anim

	.ref	yok_fall_back_anim
	.ref	yok_get_buzz_anim
	.ref	yok_4_losebal_anim







 SUBR	good_run_hit

	move	*a13(OBJ_ZPOS),a0,L
	move	*a10(OBJ_ZPOS),a1,L
	sub	a0,a1
	abs	a1
	srl	16,a1
	move	*a10(WRESTLERNUM),a0
	cmpi	3,a0
	jrnz	hpginotyok


	move	*a10(GETUP_TIME),a0		;delay b4 gaining control
	jrnz	hpgibad




	cmpi	5,a1		;if yoko	;15
	jrlt	hpgigood
hpginotyok	
	cmpi	2,a1		;6
	jrgt	hpgibad

hpgigood	setc
	rets
hpgibad	clrc
	rets

 SUBR	hit_run

	callr	good_run_hit
	jrnc	hpgiskip

	move	*a13(PLYRMODE),a1

	cmpi	MODE_INAIR,a1
	jrz	hpgiisdead
	cmpi	MODE_INAIR2,a1
	jrz	hpgiisdead

	cmpi	MODE_BLOCK,a1
	jrnz	hpginoblock


	calla	block_hit_flail

	movi	[3,0],a0
	move	a0,*a13(OBJ_YVEL),L

	jruc	hpgigotblk

hpginoblock
	move	*a13(PLYRMODE),a0
	cmpi	MODE_ONGROUND,a0
	jrz	hpgiisdead

	GETLIFE
	jrz	hpgiisdead

	WRSNDX	LBOWDROP_L1,LBOWDROP_L2

	SETMODE	NORMAL

	FACETBL	hpgilosebal

	move	*a10(WRESTLERNUM),a1
	cmpi	3,a1
	jrnz	hpginotyok2
	.ref	fall_back_tbl
	FACETBL fall_back_tbl
	calla	change_anim1a


	.ref	hit_damage_pending
	movi	-D_GUTPUSH,a0
	move	a0,@hit_damage_pending


	movi	[3,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	hpgifall_right		;attacker on left
	movi	[-3,0],a2
hpgifall_right
	move	a2,*a13(OBJ_XVEL),L

	jruc	hpgigotblk


hpginotyok2
	calla	change_anim1a

hpgigotblk
	calla	wres_collis_off


	movi	[3,0],a0
	move	a0,*a10(OBJ_YVEL),L

	movi	MODE_NORMAL,a0
	move	a0,*a10(PLYRMODE)

	move	*a10(PLYR_DIZZY),a0
	jrz	hpginotdiz0

	clr	a0
	move	a0,*a10(RUN_TIME)
	SWAP	a10,a13

	FACETBL	hpgibncoff_dizzy
	jruc	hpgicnt

hpginotdiz0
	move	*a10(GETUP_TIME),a0
	jrz	hpgiskipm


	move	*a10(METER_PROC),a0,L
	jrz	hpgiskipm			;skip if we don't have a getup meter
	PUSH	a8,a9,a10
	move	*a0(PA8),a8,L
	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	movi	GETUP_PID,a1
	movi	slide_offscr,a7
	calla	XFERPROC
	PULL	a8,a9,a10

hpgiskipm	clr	a0
	move	a0,*a10(RUN_TIME)
	move	a0,*a13(RUN_TIME)
	move	a0,*a10(GETUP_TIME)


	SWAP	a10,a13

	FACE24TBL bncoff
hpgicnt
	calla	change_anim1a
	SWAP	a10,a13

	movi	[3,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrlt	hpgiright
	neg	a2
hpgiright
	move	a2,*a10(OBJ_XVEL),L

hpgiisdead
	calla	wres_collis_off
	rets
hpgiskip	move	*a13(MOVE_DIR),a14
	.ref	new_victim_movedir
	move	a14,@new_victim_movedir
	calla	wres_collis_off
	rets

hpgilosebal
	.long	hrt_4_losebal_anim		;0 Bret Hart
	.long	rzr_4_losebal_anim		;1 Razor Ramon
	.long	und_4_losebal_anim		;2 Undertaker
	.long	yok_4_losebal_anim		;3 Yokozuna
	.long	shn_4_losebal_anim		;4 Shawn Michaels
	.long	bam_4_losebal_anim		;5 Bam Bam
	.long	dnk_4_losebal_anim		;6 Doink
	.long	dnk_4_losebal_anim		;7 spare
	.long	lex_4_losebal_anim		;8 Lex Luger
	.long	dnk_4_losebal_anim		;9 Referee

 SUBR    bncoff
	.ref	rzr_2_bncoff_anim,rzr_4_bncoff_anim
	.ref	hrt_2_bncoff_anim,hrt_4_bncoff_anim
	.ref	bam_2_bncoff_anim,bam_4_bncoff_anim
	.ref	yok_2_bncoff_anim,yok_4_bncoff_anim
	.ref	shn_2_bncoff_anim,shn_4_bncoff_anim
	.ref	lex_2_bncoff_anim,lex_4_bncoff_anim
	.ref	und_2_bncoff_anim,und_4_bncoff_anim

	.long	hrt_2_bncoff_anim,hrt_4_bncoff_anim	;0 Bret Hart
	.long	rzr_2_bncoff_anim,rzr_4_bncoff_anim	;1 Razor Ramon
	.long	und_2_bncoff_anim,und_4_bncoff_anim	;2 Undertaker
	.long	yok_2_bncoff_anim,yok_4_bncoff_anim	;3 Yokozuna
	.long	shn_2_bncoff_anim,shn_4_bncoff_anim	;4 Shawn Michaels
	.long	bam_2_bncoff_anim,bam_4_bncoff_anim	;5 Bam Bam
	.long	dnk_2_bncoff_anim,dnk_4_bncoff_anim	;6 Doink
	.long	dnk_2_bncoff_anim,dnk_4_bncoff_anim	;7 spare
	.long	lex_2_bncoff_anim,lex_4_bncoff_anim	;8 Lex Luger
	.long	dnk_2_bncoff_anim,dnk_4_bncoff_anim	;9 Referee

 SUBR    bncoff_gate

	.ref	yok_2_bncoff_gate_anim,yok_4_bncoff_gate_anim

	.long	hrt_2_bncoff_anim,hrt_4_bncoff_anim	;0 Bret Hart
	.long	rzr_2_bncoff_anim,rzr_4_bncoff_anim	;1 Razor Ramon
	.long	und_2_bncoff_anim,und_4_bncoff_anim	;2 Undertaker
	.long	yok_2_bncoff_gate_anim,yok_4_bncoff_gate_anim	;3 Yokozuna
	.long	shn_2_bncoff_anim,shn_4_bncoff_anim	;4 Shawn Michaels
	.long	bam_2_bncoff_anim,bam_4_bncoff_anim	;5 Bam Bam
	.long	dnk_2_bncoff_anim,dnk_4_bncoff_anim	;6 Doink
	.long	dnk_2_bncoff_anim,dnk_4_bncoff_anim	;7 spare
	.long	lex_2_bncoff_anim,lex_4_bncoff_anim	;8 Lex Luger
	.long	dnk_2_bncoff_anim,dnk_4_bncoff_anim	;9 Referee


hpgibncoff_dizzy
	.ref	rzr_4_bncoff_dizzy_anim
	.ref	hrt_4_bncoff_dizzy_anim
	.ref	bam_4_bncoff_dizzy_anim
	.ref	yok_4_bncoff_dizzy_anim
	.ref	shn_4_bncoff_dizzy_anim
	.ref	und_4_bncoff_dizzy_anim
	.ref	lex_4_bncoff_dizzy_anim

	.long	hrt_4_bncoff_dizzy_anim		;0 Bret Hart
	.long	rzr_4_bncoff_dizzy_anim		;1 Razor Ramon
	.long	und_4_bncoff_dizzy_anim		;2 Undertaker
	.long	yok_4_bncoff_dizzy_anim		;3 Yokozuna
	.long	shn_4_bncoff_dizzy_anim		;4 Shawn Michaels
	.long	bam_4_bncoff_dizzy_anim		;5 Bam Bam
	.long	dnk_4_bncoff_dizzy_anim		;6 Doink
	.long	dnk_4_bncoff_dizzy_anim		;7 spare
	.long	lex_4_bncoff_dizzy_anim		;8 Lex Luger
	.long	dnk_4_bncoff_dizzy_anim		;9 Referee


 SUBR	hit_puppet

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	katvnoblock

	calla	block_hit
	rets

katvnoblock
	;no need for this check now.  AMODE_PUPPET vs. dead guy won't even
	; register a collision now.

 SUBR	hit_puppet_even_if_dead
	SETMODE	PUPPET

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

	movi	wres_slave_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(GETUP_TIME)

katvdone	calla	wres_collis_off
	rets

 SUBR	hit_puppet_noflail

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	yolsnoblock

	calla	block_hit
	rets

yolsnoblock
	;don't grab dead guys
	move	*a13(PLYRMODE),a14
	cmpi	MODE_DEAD,a14
	jreq	yolsdone

	SETMODE	PUPPET

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

	movi	wres_slave_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(GETUP_TIME)

yolsdone	calla	wres_collis_off
	rets

 SUBR	hit_puppet2


	move	*a13(GETUP_TIME),a0
	jrz	mttvout

	;don't grab dead guys
	move	*a13(PLYRMODE),a14
	cmpi	MODE_DEAD,a14
	jreq	mttvdone

	SETMODE	PUPPET

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

	movi	wres_slave_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(GETUP_TIME)

	calla	wres_collis_off
	rets
mttvout
	move	*a10(ANIMODE),a1
	andni	MODE_STATUS2,a1
	move	a1,*a10(ANIMODE)

mttvdone	calla	wres_collis_off
	rets


 SUBR	hit_puppet_hdgrab

	move	*a13(SAFE_TIME),a0
	jrz	yxqqnoblock

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrne	yxqqnoblock

	calla	block_hit_flail
	rets

yxqqnoblock
	;don't grab dead guys
	move	*a13(PLYRMODE),a14
	cmpi	MODE_DEAD,a14
	jreq	yxqqdone

	clr	a14
	move	a14,*a10(HITBLOCKER)

	move	@PCNT,a14
	move	a14,*a13(HEAD_GRAB_TIME)

	SETMODE	PUPPET

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

	movi	wres_slave_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(GETUP_TIME)

yxqqdone	calla	wres_collis_off
	rets


 SUBR	hit_puppet_toss

	move	*a13(SAFE_TIME),a0
	jrz	fxamnoblock

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	fxamnoblock2

	calla	block_hit
	rets

fxamnoblock
	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	fxamnoblock2

	move	*a13(STICK_VAL_CUR),a14
	andi	0ch,a14
	jrz	fxamnoblock2
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a14,a1
	jrz	fxamnoblock2

	move	*a13(STICK_VAL_CUR),a14
	btst	1,a14
	jrz	fxamnoblock2

	calla	block_hit
	rets


fxamnoblock2


	clr	a14
	move	a14,*a10(HITBLOCKER)


	SETMODE	PUPPET

	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L

	movi	wres_slave_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(GETUP_TIME)

fxamdone	calla	wres_collis_off
	rets

 SUBR	hit_backhand

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	kqywnoblock

	calla	block_hit_flail
	rets

kqywnoblock

	;if it's shawn, do 33h instead of 43h
	movi	33h,a0
	move	*a10(WRESTLERNUM),a14
	subk	W_SHAWN,a14
	jrz	kqywsnd_ok
	movi	43h,a0
kqywsnd_ok	calla	triple_sound

	calla	CALL_AVERAGE_MOVE

	GETLIFE
	jrz	kqywisdead

	WRSNDX	UPRCUT_L2



	SETMODE	NORMAL
	move	*a13(OBJ_YPOSINT),a14
	move	*a13(GROUND_Y),a0
	sub	a0,a14
	cmpi	20,a14
	jrge	kqywfall_back


	FACE24TBL kqywhead_hit2
	calla	change_anim1a
	calla	wres_collis_off

	rets

kqywisdead
	calla	wres_collis_off
	rets

kqywfall_back
	FACETBL	fall_back_tbl

	calla	change_anim1a
	calla	wres_collis_off

	movi	[3,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	kqywfall_right		;attacker on left
	neg	a2
kqywfall_right
	move	a2,*a13(OBJ_XVEL),L

	rets

kqywhead_hit2
	.ref	lex_2_head_hit3_anim,lex_4_head_hit3_anim
	.ref	rzr_2_head_hit3_anim,rzr_4_head_hit3_anim
	.ref	yok_2_head_hit3_anim,yok_4_head_hit3_anim
	.ref	hrt_2_head_hit3_anim,hrt_4_head_hit3_anim

	.long	hrt_2_head_hit3_anim,hrt_4_head_hit3_anim		;0 Bret Hart
	.long	rzr_2_head_hit3_anim,rzr_4_head_hit3_anim		;1 Razor Ramon
	.long	und_2_head_hit2_anim,und_4_head_hit2_anim		;2 Undertaker
	.long	yok_2_head_hit3_anim,yok_4_head_hit3_anim		;3 Yokozuna
	.long	shn_2_head_hit3_anim,shn_4_head_hit3_anim		;4 Shawn Michaels
	.long	bam_2_head_hit2_anim,bam_4_head_hit2_anim		;5 Bam Bam
	.long	dnk_2_head_hit2_anim,dnk_4_head_hit2_anim		;6 Doink
	.long	0,0							;7 spare
	.long	lex_2_head_hit3_anim,lex_4_head_hit3_anim		;8 Lex Luger
	.long	0,0							;9 Referee


 SUBR	hit_earslap

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	jrpnnoblock

	calla	block_hit
	rets

jrpnnoblock

	movi	43h,a0
	calla	triple_sound

	GETLIFE
	jrz	jrpnisdead

	WRSNDX	HDBUTT_L2


	SETMODE	NORMAL

	FACETBL jrpnhead_hit2
	calla	change_anim1a

jrpnisdead
	calla	wres_collis_off
	rets

jrpnhead_hit2
	.ref	lex_4_head_hit4_anim
	.ref	rzr_4_head_hit4_anim
	.ref	yok_4_head_hit4_anim
	.ref	hrt_4_head_hit4_anim
	.ref	und_4_head_hit4_anim
	.ref	bam_4_head_hit4_anim
	.ref	dnk_4_head_hit4_anim
	.ref	shn_4_head_hit4_anim

	.long	hrt_4_head_hit4_anim		;0 Bret Hart
	.long	rzr_4_head_hit4_anim		;1 Razor Ramon
	.long	und_4_head_hit4_anim		;2 Undertaker
	.long	yok_4_head_hit4_anim		;3 Yokozuna
	.long	shn_4_head_hit4_anim		;4 Shawn Michaels
	.long	bam_4_head_hit4_anim		;5 Bam Bam
	.long	dnk_4_head_hit4_anim		;6 Doink
	.long	0							;7 spare
	.long	lex_4_head_hit4_anim		;8 Lex Luger
	.long	0							;9 Referee



 SUBR	hit_buzz

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	lxyqnoblock

	calla	block_hit
	rets

lxyqnoblock



	GETLIFE
	jrz	lxyqisdead

	SETMODE	NORMAL

lxyqisdead
	move	a10,*a13(ATTACH_PROC),L		;proc attached to
	move	a13,*a10(ATTACH_PROC),L



	WRSNDX	PUNCH_L1,PUNCH_L2

	FACETBL	lxyqget_buzz
	calla	change_anim1a
	calla	wres_collis_off

	rets

lxyqget_buzz
	.long	hrt_get_buzz_anim		;0 Bret Hart
	.long	rzr_get_buzz_anim		;1 Razor Ramon
	.long	und_get_buzz_anim		;2 Undertaker
	.long	yok_get_buzz_anim		;3 Yokozuna
	.long	shn_get_buzz_anim		;4 Shawn Michaels
	.long	bam_get_buzz_anim		;5 Bam Bam
	.long	dnk_get_buzz_anim		;6 Doink
	.long	0				;7 spare
	.long	lex_get_buzz_anim		;8 Lex Luger
	.long	0				;9 Referee

 SUBR	hit_haymaker

	move	*a13(PLYRMODE),a1
	cmpi	MODE_BLOCK,a1
	jrnz	arvpnoblock

	calla	block_hit_flail
	rets

arvpnoblock




	WRSNDX	FLYKICK_L1,FLYKICK_L2
	GETLIFE
	jrz	arvpskip

	SETMODE	NORMAL
arvpskip
	FACETBL	arvpfall_back
	calla	change_anim1a

	movi	[4,0],a2
	move	*a10(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	cmp	a0,a1			;offx - defx
	jrgt	arvpgo_rgt			;attacker on left
	neg	a2
arvpgo_rgt
	move	a2,*a13(OBJ_XVEL),L

arvpisdead
	calla	wres_collis_off
	rets

arvpfall_back
	.long	hrt_fall_back_anim		;0 Bret Hart
	.long	rzr_fall_back_anim		;1 Razor Ramon
	.long	und_fall_back_anim		;2 Undertaker
	.long	yok_fall_back_anim		;3 Yokozuna
	.long	shn_fall_back_anim		;4 Shawn Michaels
	.long	bam_fall_back_anim		;5 Bam Bam
	.long	dnk_fall_back_anim		;6 Doink
	.long	0				;7 spare
	.long	lex_fall_back_anim		;8 Lex Luger
	.long	0				;9 Referee



	.end

