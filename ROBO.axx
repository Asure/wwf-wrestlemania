**************************************************************
*
* Software:	Jason Skiles
* Initiated:	4 Oct 93
*
* COPYRIGHT (C) 1993 WILLIAMS ELECTRONICS GAMES, INC.
*
*.Last mod - 12/8/93 11:15
**************************************************************

	.file	"robo.asm"
	.title	"robotron game"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"audit.equ"
	.include	"macros.h"
	.include	"link.equ"

;	.include	"roboimg.glo"
	.include	"roboimg.tbl"
	.include	"fontsimg.glo"

	;kludge
	.ref	robo_p

*****************************************************************************

	;from ADJUST.ASM
	.ref	BINBCD

	;from COLL2.ASM
	.ref	collisions,CCCCount

	;from DCSSOUND.ASM
	.ref	nosounds,SNDSND

	;from DISPLAY.ASM
	.ref	pal_getf,STOPOBJS,fg2bg

	;from MAIN.ASM
	.ref	dpageflip,IRQSKYE

	;from MPROC.ASM
	.ref	KIL1C

	;from STRING.ASM
	.ref	setup_message,print_string_R,mess_objid,copy_string
	.ref	dec_to_asc

	;from UTIL.ASM
	.ref	get_all_buttons_cur,get_stick_val_cur,RNDRNG0,get_start_cur
	.ref	WIPEOUT,CYCLE_TABLE,civani,civanic

	;from WRESTLE.ASM	
	.if DEBUG
	.ref	CPULEFT
	.endif
	.ref	HALT,PCNT

	;for WRESTLE.ASM
	.def	RE_DEADPLAYER,RE_WAVEDONE,RE_ABORT,RE_BACKUP

	.ref	_switch_addr,_switch2_addr,ADD_VOICE

*****************************************************************************

	BSSX	robo_end,	16	;reason for wave end
	BSSX	robotron_score,	32	;UHL score
	.bss	player_dead,	16
	.bss	starts_down,	16	;are both starts down?
	.bss	player_object,	32	;pointer to char1 obj
	.bss	gun_proc,	32	;gun process address
	.bss	robo_wave,	16	;which wave to perform?
	.bss	live_badguys,	16	;wave-preserving badguy count
	.bss	forward,	16	;skip wave
	.bss	backward,	16	;drop back a wave

*****************************************************************************

ROBO_DEBUG	equ	0
WAVE_CONTROL	equ	0
BOG_MONITOR	equ	0

ROBO_PAGE	equ	0*256

ROBJ_MOM	equ	1
ROBJ_DAD	equ	2
ROBJ_KID	equ	3

;termination causes
RE_DEADPLAYER	equ	1
RE_WAVEDONE	equ	2
RE_ABORT	equ	3
RE_BACKUP	equ	4


*****************************************************************************
******************************************************************************
******************************************************************************
	.ref	robo_icon_trigger
	.ref	p1icon_total
	.ref	p2icon_total


 SUBR	robo_check

;	.if DEBUG
;	jruc	eiznrobo_ok
;	.endif
;	jruc	eiznrobo_end

	move	@p1icon_total,a0,L
	move	@robo_icon_trigger,a14		;this is set in wrestle.axx to x matches
	cmp	a0,a14
	jrle	eiznrobo_ok
	move	@p2icon_total,a0,L
	cmp	a0,a14
	jrgt	eiznrobo_end
eiznrobo_ok
	sll	1,a14
	move	a14,@robo_icon_trigger
	callr	reset_roboscore
	callr	robo_sound_init

;	movi	-1,a0
;	calla	ADD_VOICE

	clr	a0
eiznrobo_loop
	JSRP	robo_game
	move	@robo_end,a14
	cmpi	RE_DEADPLAYER,a14
	jreq	eiznplayer_died
	cmpi	RE_WAVEDONE,a14
	jreq	eiznwave_successful
	cmpi	RE_ABORT,a14
	jreq	eiznwave_aborted
	cmpi	RE_BACKUP,a14
	jreq	eiznback_up
	jruc	eiznrobo_end

eiznplayer_died
	jruc	eiznrobo_loop

eiznwave_successful
	inc	a0
	cmpi	16,a0
	jrne	eiznrobo_loop
	jruc	eiznrobo_end

eiznwave_aborted
	jruc	eiznrobo_end

eiznback_up
	dec	a0
	jrnn	eiznrobo_loop
	clr	a0
	jruc	eiznrobo_loop

eiznrobo_end
	RETP


*****************************************************************************
******************************************************************************
******************************************************************************

 SUBRP	reset_roboscore

	clr	a14
	move	a14,@robotron_score,L
	rets


*****************************************************************************
******************************************************************************
******************************************************************************

 SUBR	robo_game

	sla	4,a0
	move	a0,@robo_wave,W

	calla	display_blank
	calla	WIPEOUT			;CLEAN SYSTEM OUT

	clr	a0
	move	a0,@HALT
	move	a0,@dtype		;2d mode
	move	a0,@IRQSKYE
	move	a0,@CCCCount

	movk	1,a0			;page flipping on
	move	a0,@dpageflip

	SLEEPK	1

	movi	SCRNEND,a0		;[256,405]
	move	a0,@SCRNLR,L

	clr	a0
	move	a0,@WORLDTLX,L
	move	a0,@WORLDTLY,L

	SLEEPK	2

	calla	nosounds		;kill the select music

	;draw the frame
	move	@robo_wave,a0
	addi	vnxdborder_colors,a0
	move	*a0,a0,W
	callr	draw_frame

	movk	1,a0
	move	a0,@DISPLAYON

	SLEEPK	2

	clr	a14
	move	a14,@player_dead,W	;clear the done flag

	;initialize the robo palette color cycles
	callr	robo_pal_cycles

	;initialize the badguy count
	clr	a14
	move	a14,@live_badguys,W

	;place the wave count
	callr	wave_count

	;create the score monitor
	CREATE	ROBOSCORE_PID,score

	;set up the player object
	CREATE	ROBOMAN_PID,our_hero		;start the little guy up
	CREATE	PLYRGUN_PID,player_gun	;give him a weapon
	move	a0,@gun_proc,L		;save the address

	;set up some grunts
	move	@robo_wave,a0
	addi	vnxdwave_grunts,a0
	move	*a0,a0,W
	move	@robo_wave,a2
	addi	vnxdgrunt_speeds,a2
	move	*a2,a2,W
	callr	make_grunts

	;make some hulks
	move	@robo_wave,a0
	addi	vnxdwave_hulks,a0
	move	*a0,a0,W
	callr	make_hulks

	;sphereoids
	move	@robo_wave,a0
	addi	vnxdwave_sphereoids,a0
	move	*a0,a0,W
	callr	make_sphereoids

	;quarks
	move	@robo_wave,a0
	addi	vnxdwave_quarks,a0
	move	*a0,a0,W
	callr	make_quarks

	;make a few posts
	move	@robo_wave,a0
	addi	vnxdwave_posts,a0
	move	*a0,a0,W

	move	@robo_wave,a1
	addi	vnxdpost_types,a1
	move	*a1,a1,W

	move	@robo_wave,a2
	addi	vnxdpost_colors,a2
	move	*a2,a2,W

	callr	make_posts

	;and some people
	move	@robo_wave,a0
	move	a0,a1
	move	a0,a2
	addi	vnxdwave_moms,a0
	addi	vnxdwave_dads,a1	
	addi	vnxdwave_kids,a2
	move	*a0,a0,W
	move	*a1,a1,W
	move	*a2,a2,W
	callr	make_humans

	;start the collision checker
	CREATE	COLL_PID,collisions

	;create the both-start-btn watcher
	clr	a0
	move	a0,@starts_down,W
	move	a0,@forward,W
	move	a0,@backward,W
	CREATE	MISC_PID,watch_both_starts

	.if	WAVE_CONTROL
	CREATE	MISC_PID,wave_mover
	.endif

	.if DEBUG
	.if BOG_MONITOR
	CREATE	MISC_PID,bog_o_meter
	.endif
	.endif

	calla	display_unblank

vnxdnot_done
	SLEEPK	1
	move	@live_badguys,a14
	jrz	vnxdwave_done

	move	@player_dead,a14
	jrnz	vnxddead_hero

	move	@starts_down,a14
	jrnz	vnxdwave_abort

	move	@forward,a14
	jrnz	vnxdwave_done

	move	@backward,a14
	jrnz	vnxdback_up
	jruc	vnxdnot_done


vnxdwave_done
	movi	RE_WAVEDONE,a14
	jruc	vnxdquit
vnxddead_hero
	movi	RE_DEADPLAYER,a14
	jruc	vnxdquit
vnxdwave_abort
	movi	RE_ABORT,a14
	jruc	vnxdquit

vnxdback_up
	movi	RE_BACKUP,a14
	jruc	vnxdquit

vnxdquit
	move	a14,@robo_end,W

	;put the wave back in a0 just like we found it
	move	@robo_wave,a0,W
	sra	4,a0
	RETP


vnxdborder_colors
	.if	ROBO_DEBUG
	.word	1
	.endif
	.word	01,06,01,14,07,03,02,08
	.word	00,10,01,06,01,14,07,03

vnxdwave_grunts
	.if	ROBO_DEBUG
	.word	4
	.endif
;	.word	15,17,22,34,20,30,00,30
;	.word	30,25,30,00,30,27,25,30

	.word	15,17,22,34,20,32,00,35
	.word	60,25,35,00,35,27,25,35

vnxdgrunt_speeds
	.if	ROBO_DEBUG
	.word	1000
	.endif
	.word	20,15,15,15,15,15,15,15
	.word	15,15,14,14,14,14,14,13

vnxdwave_posts
	.if	ROBO_DEBUG
	.word	15
	.endif
	.word	05,15,25,25,20,25,00,25
	.word	00,20,25,00,25,05,20,25

vnxdpost_types
	.if	ROBO_DEBUG
	.word	0
	.endif
	.word	0,1,3,8,4,2,0,7
	.word	0,5,0,1,3,8,4,2

vnxdpost_colors
	.if	ROBO_DEBUG
	.word	0Fh
	.endif
	.word	15,14,11,13,14,15,14,11
	.word	14,10,15,14,11,13,14,15

vnxdwave_hulks
	.if	ROBO_DEBUG
	.word	0
	.endif
	.word	00,05,06,07,00,07,12,08
	.word	04,00,08,13,08,20,02,03

vnxdwave_sphereoids
	.if	ROBO_DEBUG
	.word	0
	.endif
	.word	00,01,03,04,01,04,00,05
	.word	05,01,05,00,05,02,01,05

vnxdwave_quarks
	.if	ROBO_DEBUG
	.word	0
	.endif
	.word	00,00,00,00,00,00,10,00
	.word	00,00,00,12,00,00,00,00

vnxdwave_moms
	.if	ROBO_DEBUG
	.word	2
	.endif
	.word	01,01,02,02,15,03,04,03
	.word	03,00,03,03,03,05,00,03

vnxdwave_dads
	.if	ROBO_DEBUG
	.word	2
	.endif
	.word	01,01,02,02,00,03,04,03
	.word	03,22,03,03,03,05,00,03

vnxdwave_kids
	.if	ROBO_DEBUG
	.word	2
	.endif
	.word	00,01,02,02,01,03,04,03
	.word	03,00,03,03,03,05,22,03


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	PLAYER SECTION
*

	;reg use
	; a4  - current position in animation script
	; a5  - head of current animation script
	; a6  - tail of current animation script
	; a8  - player object
	; a9  - motion count accumulator
	; a10 - direction of motion

 SUBRP	our_hero

	;create the player object
	movi	[evahstart_x,0],a0
	movi	[evahstart_y,0],a1
	movi	man_d1,a2
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSPLYR|TYPPLYR,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the global pointer
	move	a8,@player_object,L

	; initiailize direction of motion
	clr	a10

evahnot_done
	PUSHP	a4,a5,a6
	SLEEPK	1
	PULLP	a4,a5,a6

	; move him first
	callr	evahmove_player

	; change his animation frame, if neccesary
	callr	evahchange_frame

	jruc	evahnot_done

evahplayer_dead
	;kill the gun process
	move	@gun_proc,a0,L
	calla	KILL

	movi	BULLET_PID,a0
	calla	KIL1C

	movi	GRUNT_PID,a0
	calla	KIL1C

	movi	HULK_PID,a0
	calla	KIL1C

	movi	HUMAN_PID,a0
	calla	KIL1C

	movi	SPHEREOID_PID,a0
	calla	KIL1C

	movi	ENFORCER_PID,a0
	calla	KIL1C

	movi	SPARK_PID,a0
	calla	KIL1C

	movi	QUARK_PID,a0
	calla	KIL1C

	movi	TANK_PID,a0
	calla	KIL1C

	movi	SHELL_PID,a0
	calla	KIL1C

	calla	STOPOBJS

	;switch frames
	movi	robo_dead,a0,L
	calla	civanic

	;death sound
	movi	RS_DIE,a0
	calla	robo_sound

	SLEEP	120
	movk	1,a14
	move	a14,@player_dead,W

	DIE

******************************************************************************
*
*	player move routine
*

 SUBRP	evahmove_player

	clr	a0
	calla	get_stick_val_cur
	btst	JOYDN,a0
	jrnz	evahmove_down
	btst	JOYUP,a0
	jrnz	evahmove_up
	btst	JOYRGT,a0
	jrnz	evahmove_right
	btst	JOYLFT,a0
	jrnz	evahmove_left
	jruc	evahno_move

evahmove_down
	btst	JOYRGT,a0
	jrnz	evahmove_downright
	btst	JOYLFT,a0
	jrnz	evahmove_downleft

	;increment Y
	move	*a8(OYPOS),a14,W
	inc	a14
	move	a14,*a8(OYPOS),W
	jruc	evahmove_done

evahmove_up
	btst	JOYRGT,a0
	jrnz	evahmove_upright
	btst	JOYLFT,a0
	jrnz	evahmove_upleft

	;decrement Y
	move	*a8(OYPOS),a14,W
	dec	a14
	move	a14,*a8(OYPOS),W
	jruc	evahmove_done

evahmove_right
	;increment X
	move	*a8(OXPOS),a14,W
	inc	a14
	move	a14,*a8(OXPOS),W
	jruc	evahmove_done

evahmove_left
	;decrement X
	move	*a8(OXPOS),a14,W
	dec	a14
	move	a14,*a8(OXPOS),W
	jruc	evahmove_done

evahmove_upright
	; decrement Y and increment X
	move	*a8(OYPOS),a14,W
	dec	a14
	move	a14,*a8(OYPOS),W
	move	*a8(OXPOS),a14,W
	inc	a14
	move	a14,*a8(OXPOS),W
	jruc	evahmove_done

evahmove_upleft
	; decrement Y and decrement X
	move	*a8(OYPOS),a14,W
	dec	a14
	move	a14,*a8(OYPOS),W
	move	*a8(OXPOS),a14,W
	dec	a14
	move	a14,*a8(OXPOS),W
	jruc	evahmove_done

evahmove_downright
	; increment Y and increment X
	move	*a8(OYPOS),a14,W
	inc	a14
	move	a14,*a8(OYPOS),W
	move	*a8(OXPOS),a14,W
	inc	a14
	move	a14,*a8(OXPOS),W
	jruc	evahmove_done

evahmove_downleft
	; increment Y and decrement X
	move	*a8(OYPOS),a14,W
	inc	a14
	move	a14,*a8(OYPOS),W
	move	*a8(OXPOS),a14,W
	dec	a14
	move	a14,*a8(OXPOS),W
	jruc	evahmove_done

evahmove_done
	; check bounds
	move	*a8(OXPOS),a14,W
	cmpi	evahlow_x,a14,W
	jrgt	evahnot_lowx
	; too low on x
	inc	a14
	move	a14,*a8(OXPOS),W

evahnot_lowx
	cmpi	evahhigh_x,a14,W
	jrle	evahnot_highx
	; too high on x
	dec	a14
	move	a14,*a8(OXPOS),W

evahnot_highx

	move	*a8(OYPOS),a14,W
	cmpi	evahlow_y,a14,W
	jrgt	evahnot_lowy
	; too low on y
	inc	a14
	move	a14,*a8(OYPOS),W

evahnot_lowy
	cmpi	evahhigh_y,a14,W
	jrle	evahnot_highy
	; too high on y
	dec	a14
	move	a14,*a8(OYPOS),W

evahnot_highy
evahno_move

evahdone_moving
	rets

******************************************************************************
*
*	player animation routine
*


 SUBRP	evahchange_frame
	;reg use
	; a0  scratch
	; a1  sctatch
	; a3  new direction of motion
	; a10 old direction of motion - update

	clr	a0
	clr	a2
	calla	get_stick_val_cur
	move	a0,a3

	cmp	a0,a10
	jreq	evahsame_direction
	jruc	evahnew_direction

evahnew_direction
	; start a new script.  first use the joystick bits to compute an
	; offset into the script table.
	X32	a0		;multiply by 32
	addi	evahscript_map,a0	;add the address of the script table
	move	*a0,a0,L	;get the address of the script

	; get the tail and head of the image list for this script
	move	*a0+,a6,L
	move	a0,a5

	;a5 now points to the first image in the script.  change to it.
	move	*a5,a0,L
	calla	civanic

	;set the new direction thingie and script pointer
	move	a3,a10
	move	a5,a4

	;set the motion count
	movi	evahmotion_count,a9
	jruc	evahdone_changing

evahsame_direction
	;check the motion count
	dec	a9
	jrnz	evahdone_changing

	;reset the motion count
	movi	evahmotion_count,a9

	;increment the pointer, wrap if neccesary
	addi	20h,a4
	cmp	a4,a6
	jrne	evahno_wrap
	move	a5,a4

evahno_wrap
	;a4 is the new image.  change to it.
	move	*a4,a0,L
	calla	civanic

	jruc	evahdone_changing

evahdone_changing
	rets


******************************************************************************
*
*	player collision routines
*

 SUBR	player_die
	PUSH	a1,a7

	;we hit some kind of bad guy and are dead
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADPLAYER_PID,a1
	movi	evahplayer_dead,a7
	calla	XFERPROC

	PULL	a1,a7
	rets


******************************************************************************
*
*	player configuration data
*

;start position
evahstart_x	equ	197
evahstart_y	equ	123

; motion boundaries
evahlow_x		equ	53
evahhigh_x		equ	347-7
evahlow_y		equ	45
evahhigh_y		equ	231-12

;motion count - controls animation speed
evahmotion_count	equ	2	;there will be trouble if this is zero

; walking animation scripts
evahani_holdstill
	.long	evahani_holdstillx
	.long	man_d1
evahani_holdstillx

evahani_walkup
	.long	evahani_walkupx
	.long	man_u1
	.long	man_u2
	.long	man_u1
	.long	man_u3
evahani_walkupx

evahani_walkdown
	.long	evahani_walkdownx
	.long	man_d1
	.long	man_d2
	.long	man_d1
	.long	man_d3
evahani_walkdownx

evahani_walkright
	.long	evahani_walkrightx
	.long	man_r1
	.long	man_r2
	.long	man_r1
	.long	man_r3
evahani_walkrightx

evahani_walkleft
	.long	evahani_walkleftx
	.long	man_l1
	.long	man_l2
	.long	man_l1
	.long	man_l3
evahani_walkleftx

evahani_bad
	.long	evahani_badx
	.long	robo_bad
evahani_badx

; direction - script mappings: use AND combo of joy bits as index
evahscript_map
	.long	evahani_holdstill
	.long	evahani_walkup
	.long	evahani_walkdown
	.long	evahani_bad
	.long	evahani_walkleft
	.long	evahani_walkleft
	.long	evahani_walkleft
	.long	evahani_bad
	.long	evahani_walkright
	.long	evahani_walkright
	.long	evahani_walkright
	.long	evahani_bad
	.long	evahani_bad
	.long	evahani_bad
	.long	evahani_bad
	.long	evahani_bad
evahscript_mapx


*****************************************************************************
*
*	player gun process
*

	;reg use
	;a9  time till next shot is allowed

 SUBRP	player_gun

	clr	a9
	jruc	rkpasleep

rkpacheck_shot
	movk	1,a0
	calla	get_stick_val_cur
	jrz	rkpasleep			;no shot

	;fire in the indicated direction
	move	a0,a10
	CREATE	BULLET_PID,bullet
	movi	rkparate_of_fire,a9

	;shoot sound
	movi	RS_SHOOT,a0
	callr	robo_sound

	jruc	rkpasleep
	
rkpatoo_soon
	dec	a9
	jruc	rkpasleep

rkpasleep
	SLEEPK	1
	move	a9,a9
	jrnz	rkpatoo_soon
	jruc	rkpacheck_shot

rkpadone
	DIE

rkparate_of_fire	equ	6	;min ticks between shots


*****************************************************************************
*
*	player bullet process
*

	;reg use
	; a4 velocity accumulator (used to pick the correct bullet)
	; a5 vert indicator
	; a8 bullet object
	;a10 (in) stick bits

 SUBRP	bullet

	;create a bullet object
	movi	[500,0],a0
	clr	a1
	movi	bullet_hrz,a2
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSPLYR|TYPBULLET,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the speed and start position
	move	@player_object,a9,L
	clr	a4
	clr	a5
	move	*a9(OXPOS),a0,W
	addk	pjegbstart_xoff,a0
	move	a0,*a8(OXPOS),W
	move	*a9(OYPOS),a0,W
	addk	pjegbstart_yoff,a0
	move	a0,*a8(OYPOS),W

	btst	JOYRGT,a10
	jrz	pjegnot_right
	move	*a8(OXPOS),a14,W	;pos
	addi	pjegbstart_radj,a14
	move	a14,*a8(OXPOS),W
	movi	pjegbullet_speed,a0	;speed
	move	a0,*a8(OXVEL),L
	inc	a4

pjegnot_right

	btst	JOYLFT,a10
	jrz	pjegnot_left
	move	*a8(OXPOS),a14,W	;pos
	addi	pjegbstart_ladj,a14
	move	a14,*a8(OXPOS),W
	movi	-pjegbullet_speed,a0	;speed
	move	a0,*a8(OXVEL),L
	dec	a4

pjegnot_left

	btst	JOYUP,a10
	jrz	pjegnot_up
	move	*a8(OYPOS),a14,W	;pos
	addi	pjegbstart_uadj,a14
	move	a14,*a8(OYPOS),W
	movi	-pjegbullet_speed,a0	;speed
	move	a0,*a8(OYVEL),L
	dec	a4
	inc	a5

pjegnot_up

	btst	JOYDN,a10
	jrz	pjegnot_down
	move	*a8(OYPOS),a14,W	;pos
	addi	pjegbstart_dadj,a14
	move	a14,*a8(OYPOS),W
	movi	pjegbullet_speed,a0	;speed
	move	a0,*a8(OYVEL),L
	inc	a4
	inc	a5

pjegnot_down

	;set the image
	movi	bullet_hrz,a0
	move	a5,a5
	jrz	pjeghoriz
	movi	bullet_vrt,a0
pjeghoriz

	;vert/horz is set.  check for diagonal instead
	cmpi	1,a4
	jreq	pjegimage_set
	cmpi	-1,a4
	jreq	pjegimage_set

	;it's a diagonal.  if a4 is 0, use frontslash
	move	a4,a4
	jrz	pjegfront_slash
	movi	bullet_bck,a0
	jruc	pjegimage_set

pjegfront_slash
	movi	bullet_fnt,a0

pjegimage_set
	calla	civanic

pjegloop
	callr	bounds_check
	move	a0,a0
	jrnz	pjeghit_wall
	SLEEPK	1
	jruc	pjegloop

pjeghit_wall
	;zoinks!  We've hit a wall. back up to be flush against it.
	; a2 holds the number of pixels we have to back up.

	;well, we can overlap on the wall a LITTLE.  say, 2 pixels.
	subk	2,a2

	move	*a8(OXVEL),a0,L
	jrz	pjegy_adjust
	jrn	pjegneg_xvel
	move	*a8(OXPOS),a0,W
	sub	a2,a0
	move	a0,*a8(OXPOS),W
	jruc	pjegy_adjust
pjegneg_xvel
	move	*a8(OXPOS),a0,W
	add	a2,a0
	move	a0,*a8(OXPOS),W

pjegy_adjust
	move	*a8(OYVEL),a0,L
	jrz	pjegkill_bullet
	jrn	pjegneg_yvel
	move	*a8(OYPOS),a0,W
	sub	a2,a0
	move	a0,*a8(OYPOS),W
	jruc	pjegkill_bullet
pjegneg_yvel
	move	*a8(OYPOS),a0,W
	add	a2,a0
	move	a0,*a8(OYPOS),W

pjegkill_bullet
	SLEEPK	1
	calla	DELOBJA8
	DIE


******************************************************************************
*
* player bullet collision routines
*

 SUBR	bullet_die
	PUSH	a1,a7,a9,a10,a11

	;we hit some kind of bad guy and are stopped.
	movi	CLSDEAD,a14
	move	a14,*A8(OID)
	move	*a8(OPLINK),a0,L
	movi	DEADBULLET_PID,a1
	movi	pjegkill_bullet,a7
	calla	XFERPROC

	PULL	a1,a7,a9,a10,a11
	rets


pjegbullet_speed	equ	00080000h	;pixels per frame

pjeglow_x		equ	53
pjeghigh_x		equ	346
pjeglow_y		equ	45
pjeghigh_y		equ	230

;hero anim point is top left.  bullet anim point is center.  use these to
; adjust the starting position of the bullet.
; No.  The bullet anim points are gone now.  Unk.
pjegbstart_xoff	equ	3
pjegbstart_yoff	equ	5

pjegbstart_radj	equ	7
pjegbstart_ladj	equ	-13
pjegbstart_uadj	equ	-14
pjegbstart_dadj	equ	8


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	GRUNT SECTION
*

	;reg use
	; a0 (in) # of grunts
	; a1 scratch
	; a2 (in) initial speed

 SUBRP	make_grunts

	move	a0,a0
	jrz	vtuedone

vtueanother_grunt
	PUSHP	a0

vtueget_pos
	movi	vtuemax_x-vtuemin_x,a0
	calla	RNDRNG0
	move	a0,a9
	movi	vtuemax_y-vtuemin_y,a0
	calla	RNDRNG0
	move	a0,a10

	addi	vtuemin_x,a9
	addi	vtuemin_y,a10

	cmpi	vtuemid_x1,a9
	jrle	vtueplace_grunt
	cmpi	vtuemid_x2,a9
	jrge	vtueplace_grunt
	cmpi	vtuemid_y1,a10
	jrle	vtueplace_grunt
	cmpi	vtuemid_y2,a10
	jrge	vtueplace_grunt
	jruc	vtueget_pos

vtueplace_grunt
	;assign a random starting position
	move	a2,a11
	CREATE	GRUNT_PID,grunt
	PULLP	a0
	dsj	a0,vtueanother_grunt

vtuedone
	rets


vtuemin_x	equ	53	;grunt start outer boundaries
vtuemax_x	equ	347-9
vtuemin_y	equ	45
vtuemax_y	equ	231-13

vtuemid_x1	equ	200-60	;grunt start inner boundaries
vtuemid_x2	equ	200+60
vtuemid_y1	equ	138-60
vtuemid_y2	equ	138+60


*****************************************************************************
*
*	grunt process
*

	STRUCTPD
	WORD	ftrhACCEL_TIMER	;UHW accelerator count
	LONG	ftrhDEATH_SEQUENCE	;UHL anim to use when dying
				;    (set by coll routines)

	;reg use
	; a8 object handle
	; a9 (in)  starting x
	; a9 (use) ticks between moves
	;a10 (in)  starting y
	;a10 (use) ticks until next move
	;a11 (in)  initial speed
	;a11 (use) frame count

 SUBRP	grunt

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W

	;create a grunt object
	move	a9,a0
	sla	16,a0
	move	a10,a1
	sla	16,a1
	movi	grunt_1,a2
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPGRUNT,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;initialize move timer
	move	a11,a9

	;initialize time till next move
	move	a9,a0
	calla	RNDRNG0
	add	a11,a0
	move	a0,a10

	;initialize accel counter
	movi	ftrhacceleration,a14
	move	a14,*a13(ftrhACCEL_TIMER),W

	;initialize frame counter
	clr	a11

ftrhloop
	move	*a13(ftrhACCEL_TIMER),a14,W
	dec	a14
	jrnz	ftrhno_accel

	movi	ftrhacceleration,a14
	cmpi	ftrhtop_speed,a9
	jreq	ftrhno_accel
	dec	a9

ftrhno_accel
	move	a14,*a13(ftrhACCEL_TIMER),W
	SLEEPK	1

	dsj	a10,ftrhloop
	callr	ftrhmove_grunt
	move	a9,a10
	jruc	ftrhloop

ftrhdie
	;we've croaked
	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

	;set up the animation
	move	*a13(ftrhDEATH_SEQUENCE),a9,L
	move	*a8(OCTRL),a0

ftrhdieloop
	move	*a9+,a0,L
	jrz	ftrhdead_n_buried
	calla	civanic
	sleepk	ftrhdeath_speed
	jruc	ftrhdieloop

ftrhdead_n_buried
	calla	DELOBJA8

	DIE

******************************************************************************
*
*	grunt move routine
*

 SUBRP	ftrhmove_grunt
	move	@player_object,a14,L

	;move in X
	move	*a14(OXPOS),a0,W
	move	*a8(OXPOS),a1,W
	sub	a1,a0
	jrz	ftrhdone_movex
	jrn	ftrhmove_left
	jruc	ftrhmove_right

ftrhmove_left
	move	*a8(OXPOS),a1,W
	addi	-ftrhxmotion_inc,a1
	move	a1,*a8(OXPOS),W
	jruc	ftrhdone_movex

ftrhmove_right
	move	*a8(OXPOS),a1,W
	addi	ftrhxmotion_inc,a1
	move	a1,*a8(OXPOS),W
	jruc	ftrhdone_movex

ftrhdone_movex

	move	*a14(OYPOS),a0,W
	move	*a8(OYPOS),a1,W
	sub	a1,a0
	jrz	ftrhdone_movey
	jrn	ftrhmove_up
	jruc	ftrhmove_down

ftrhmove_up
	move	*a8(OYPOS),a1,W
	addi	-ftrhymotion_inc,a1
	move	a1,*a8(OYPOS),W
	jruc	ftrhdone_movey

ftrhmove_down
	move	*a8(OYPOS),a1,W
	addi	ftrhymotion_inc,a1
	move	a1,*a8(OYPOS),W
	jruc	ftrhdone_movey

ftrhdone_movey

	;step through the animation
	inc	a11
	cmpi	4,a11
	jrne	ftrhno_wrap
	clr	a11

ftrhno_wrap
	move	a11,a14
	X32	a14
	addi	ftrhwalk_script,a14
	move	*a14,a0,L
	calla	civanic

	;sound
	movi	RS_GRUNT,a0
	callr	robo_sound
	rets


******************************************************************************
*
*	grunt collision routines
*

 SUBR	grunt_shot
	;hit by player gunfire
	PUSH	a1,a7

	;jeepers.  we're dead.
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W

	move	*a0(OXVEL),a14,L
	jrz	ftrhvert_bullet
	move	*a0(OYVEL),a1,L
	jrz	ftrhhorz_bullet

	add	a1,a14
	jrz	ftrhdiag_13
	jruc	ftrhdiag_24

ftrhhorz_bullet
	movi	ftrhdie_vert,a14
	jruc	ftrhdeath_set
ftrhvert_bullet
	movi	ftrhdie_horz,a14
	jruc	ftrhdeath_set
ftrhdiag_13
	movi	ftrhdie_diag24,a14
	jruc	ftrhdeath_set
ftrhdiag_24
	movi	ftrhdie_diag13,a14
	jruc	ftrhdeath_set

ftrhdeath_set

	move	*a8(OPLINK),a0,L
	move	a14,*a0(ftrhDEATH_SEQUENCE),L
	movi	DEADGRUNT_PID,a1
	movi	ftrhdie,a7

	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	calla	XFERPROC

	movi	ftrhgrunt_points,a0
	callr	score_points

	;die sound
	movi	RS_HIT,a0
	calla	robo_sound

	PULL	a1,a7
	rets


 SUBR	grunt_die
	;hit a mine or something

	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	ftrhdie_vert,a14
	move	a14,*a0(ftrhDEATH_SEQUENCE),L
	movi	DEADGRUNT_PID,a1
	movi	ftrhdie,a7
	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	calla	XFERPROC

	PULL	a1,a7
	rets


******************************************************************************
*
*	grunt configuration data
*


ftrhwalk_script
	.long	grunt_1
	.long	grunt_2
	.long	grunt_1
	.long	grunt_3
ftrhwalk_scriptx

ftrhdie_vert
	.long	grunt_xv1
	.long	grunt_xv2
	.long	grunt_xv3
	.long	grunt_xv4
	.long	0
ftrhdie_vertx

ftrhdie_horz
	.long	grunt_xh1
	.long	grunt_xh2
	.long	grunt_xh3
	.long	grunt_xh4
	.long	0
ftrhdie_horzx

ftrhdie_diag13
	.long	grunt_xdf1
	.long	grunt_xdf2
	.long	grunt_xdf3
	.long	grunt_xdf4
	.long	0
ftrhdie_diag13x

ftrhdie_diag24
	.long	grunt_xdb1
	.long	grunt_xdb2
	.long	grunt_xdb3
	.long	grunt_xdb4
	.long	0
ftrhdie_diag24x

ftrhdeath_speed	equ	3		;death seq anim speed

ftrhacceleration	equ	100		;decrement motion clock every X cycles
ftrhtop_speed	equ	3		;as fast as they get
ftrhxmotion_inc	equ	0004h
ftrhymotion_inc	equ	0004h

ftrhgrunt_points	equ	100		;points for killing a grunt

*****************************************************************************
******************************************************************************
******************************************************************************
*
*	POST SECTION
*
	;reg use
	; a0 - (in) # of posts to create
	; a1 - (in) type (0-8)
	; a2 - (in) color (0-F)

 SUBRP	make_posts

	move	a0,a9		;move the inputs to safer spots
	jrz	sjladone

	move	a1,a10		;type

	move	a2,a11		;color
	sla	8,a11
	or	a2,a11

sjlaanother_post
sjlaget_pos
	movi	sjlamax_y-sjlamin_y,a0
	calla	RNDRNG0
	move	a0,a2			;hide a0
	movi	sjlamax_x-sjlamin_x,a0
	calla	RNDRNG0
	move	a2,a1			;put it back

	addi	sjlamin_x,a0
	addi	sjlamin_y,a1

	cmpi	sjlamid_x1,a0
	jrle	sjlaplace_post
	cmpi	sjlamid_x2,a0
	jrge	sjlaplace_post
	cmpi	sjlamid_y1,a1
	jrle	sjlaplace_post
	cmpi	sjlamid_y2,a1
	jrge	sjlaplace_post
	jruc	sjlaget_pos

sjlaplace_post
	sla	16,a0			;X pos
	sla	16,a1			;Y pos

	;create the post object
	move	a10,a14
	X32	a14
	addi	sjlapost_types,a14
	move	*a14,a14,L
	move	*a14,a2,L		;DON'T advance

	clr	a3
	movi	DMACNZ,a4
	movi	CLSNEUT|TYPPOST,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	move	a10,*a8(OMISC),W	;stuff the type in the object

	move	a11,*a8(OCONST),W

	movi	robo_p,a0
	calla	pal_getf
	move	a0,*a8(OPAL),W

	dsj	a9,sjlaanother_post

sjladone
	rets


sjlamin_x	equ	53	;post start outer boundaries
sjlamax_x	equ	347-17
sjlamin_y	equ	45
sjlamax_y	equ	231-10

sjlamid_x1	equ	200-50	;post start inner boundaries
sjlamid_x2	equ	200+50
sjlamid_y1	equ	138-50
sjlamid_y2	equ	138+50


******************************************************************************
*
*	post collision routines
*

 SUBR	post_die

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	CREATE	DEADPOST_PID,kill_post

	rets


	;reg use
	; a8 - * image
	; a9 - anim pointer

 SUBR	kill_post

	move	*a8(OMISC),a0,W
	X32	a0
	addi	sjlapost_types,a0
	move	*a0,a9,L
	move	*a8(OCTRL),a14,W
	xori	DMACNZ,a14
	ori	DMAWNZ,a14
	move	a14,*a8(OCTRL),W
sjlakploop
	move	*a9+,a0,L
	jrz	sjlakpdone
	calla	civanic
	SLEEPK	sjladie_speed
	jruc	sjlakploop

sjlakpdone
	calla	DELOBJA8
	DIE


sjladie_speed	equ	3

sjlapost_types
	.long	sjlastar		;0
	.long	sjlacrystal	;1
	.long	sjladiamond	;2
	.long	sjlasquare		;3
	.long	sjlarectangle	;4
	.long	sjlaR2084		;5
	.long	sjlapcube		;6
	.long	sjlaspiral		;7
	.long	sjlatriangle	;8
	.long	0
sjlapost_typesx

sjlastar
	.long	star_1,star_2,star_3,0

sjlacrystal
	.long	crystal_1,crystal_2,crystal_3,0

sjladiamond
	.long	diamond_1,diamond_2,diamond_3,0

sjlasquare
	.long	square_1,square_2,square_3,0

sjlarectangle
	.long	rectangle_1,rectangle_2,rectangle_3,0

sjlaR2084
	.long	R2084_1,R2084_2,R2084_3,0

sjlapcube
	.long	pcube_1,pcube_2,pcube_3,0

sjlaspiral
	.long	spiral_1,spiral_2,spiral_3,0

sjlatriangle
	.long	triangle_1,triangle_2,triangle_3,0


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	HUMAN SECTION
*

	.bss	humans_saved,	16

	;reg use
	; a0 # of moms
	; a1 # of dads
	; a2 # of kids 

 SUBRP	make_humans

	;initialize the savecount
	clr	a14
	move	a14,@humans_saved,W

	;first make the moms
	move	a0,a0
	jrz	oaxeskip_moms
oaxemom_loop
	PUSHP	a0,a1,a2
	callr	oaxerand_position
	movi	ROBJ_MOM,a9
	CREATE	HUMAN_PID,human
	PULLP	a0,a1,a2
	dsj	a0,oaxemom_loop

oaxeskip_moms

	;dads
	move	a1,a1
	jrz	oaxeskip_dads
oaxedad_loop
	PUSHP	a1,a2
	callr	oaxerand_position
	movi	ROBJ_DAD,a9
	CREATE	HUMAN_PID,human
	PULLP	a1,a2
	dsj	a1,oaxedad_loop

oaxeskip_dads

	;kids
	move	a2,a2
	jrz	oaxeskip_kids
oaxekid_loop
	PUSHP	a2
	callr	oaxerand_position
	movi	ROBJ_KID,a9
	CREATE	HUMAN_PID,human
	PULLP	a2
	dsj	a2,oaxekid_loop

oaxeskip_kids
	rets


	;reg use
	; no input
	;a10 - (ret) an X val
	;a11 - (ret) a Y val

 SUBRP oaxerand_position
	movi	oaxemax_x-oaxemin_x,a0
	calla	RNDRNG0
	move	a0,a10
	addi	oaxemin_x,a10

	movi	oaxemax_y-oaxemin_y,a0
	calla	RNDRNG0
	move	a0,a11
	addi	oaxemin_y,a11

	rets


******************************************************************************
*
*	human process
*
	STRUCTPD
	LONG	oaxeHITPOST	;UHL post we've hit

	;reg use
	; a8 - hObject
	; a9 - (in)  OID
	; a9 - (use) image table address (oaxemom_table, oaxedad_table...)
	;a10 - (in)  x
	;a10 - (use) direction of motion (1-8)
	;a11 - (in)  y
	;a11 - (use) frame index (0-3)


 SUBRP	human

	cmpi	ROBJ_MOM,a9
	jreq	oaxeinit_mom
	cmpi	ROBJ_DAD,a9
	jreq	oaxeinit_dad
	cmpi	ROBJ_KID,a9
	jreq	oaxeinit_kid

	LOCKUP	;bad objid

oaxeinit_mom
	movi	oaxemom_table,a9
	jruc	oaxemake_obj

oaxeinit_dad
	movi	oaxedad_table,a9
	jruc	oaxemake_obj

oaxeinit_kid
	movi	oaxekid_table,a9
	jruc	oaxemake_obj

oaxemake_obj
	;cweate a widdle people object
	move	a10,a0
	sla	16,a0
	move	a11,a1
	sla	16,a1
	move	*a9(oaxewalk_down),a2,L
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSNEUT|TYPHUMAN,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;choose a direction of motion
	movi	7,a0
	calla	RNDRNG0
	inc	a0
	move	a0,a10

	;set the frame index
	movi	3,a11
	callr	oaxeadvance_frame

oaxemove_loop
	SLEEPK	oaxesleep_time
	callr	oaxerand_dirchange
	callr	oaxemove_onestep
	move	a0,a0
	jrnz	oaxehit_wall
	callr	oaxeadvance_frame
	jruc	oaxemove_loop

oaxehit_wall
	;sleep first because: If you move and bump into both a wall and a
	; post at the same time (could happen), and then you immediately
	; reverse direction, you'll then get flipped AGAIN when you hit
	; the post and get forwarded right through the wall.  yuck.  so
	; instead when you hit a wall, wait a frame and let the post
	; collisions sort themselves out, THEN change direction.
	SLEEPK	1
	callr	oaxereverse_direction
	callr	oaxeadvance_frame
	jruc	oaxemove_loop

oaxehit_post
	callr	post_adjust
	callr	oaxeadvance_frame
	jruc	oaxemove_loop

oaxesaved
	;sound
	movi	RS_RESCUE,a0
	callr	robo_sound

	;increment the save count
	move	@humans_saved,a14,W
	inc	a14
	move	a14,@humans_saved,W

	cmpi	5,a14
	jrle	oaxeno_cap
	movi	5,a14
oaxeno_cap
	dec	a14

	PUSH	a14
	sla	4,a14
	addi	oaxescore_table,a14
	move	*a14,a0,W
	callr	score_points
	PULL	a14

	X32	a14
	addi	oaxesaved_table,a14
	move	*a14,a0,L
	calla	civanic

	SLEEP	120
	calla	DELOBJA8
	jruc	oaxedone

oaxekilled
	movi	skull,a0
	calla	civanic

	SLEEP	120
	calla	DELOBJA8
;	jruc	oaxedone		;fall through

oaxedone
	DIE


******************************************************************************
* Human has bounced into or started inside a post.  Make it better.
* Do this by reversing the direction of the human and moving it forward
* in the new direction until it's clear of the post.
*

	;reg use
	; a0 - X overlap
	; a1 - Y overlap

 SUBRP	post_adjust
	PUSH	a0,a1,a2,a3,a7

	move	*a13(oaxeHITPOST),a7,L

	callr	oaxereverse_direction

	;calculate degree of X and Y overlap
	move	a10,a2
	X32	a2
	addi	oaxewalk_increments,a2
	move	*a2+,a3,W	;y vel
	jrn	oaxeyvel_neg
	jrz	oaxeyvel_zero

oaxeyvel_pos
	move	*a7(OYPOS),a1
	move	*a7(OSIZEY),a14
	add	a14,a1
	move	*a8(OYPOS),a14
	sub	a14,a1
	jruc	oaxedo_xvel
oaxeyvel_zero
	movi	100h,a1
	jruc	oaxedo_xvel
oaxeyvel_neg
	move	*a8(OYPOS),a1
	move	*a8(OSIZEY),a14
	add	a14,a1
	move	*a7(OYPOS),a14
	sub	a14,a1
;	jruc	oaxedo_xvel

oaxedo_xvel
	move	*a2,a3,W	;x vel
	jrn	oaxexvel_neg
	jrz	oaxexvel_zero

oaxexvel_pos
	move	*a7(OXPOS),a0
	move	*a7(OSIZEX),a14
	add	a14,a0
	move	*a8(OXPOS),a14
	sub	a14,a0
	jruc	oaxeovlap_done
oaxexvel_zero
	movi	100h,a0
	jruc	oaxeovlap_done
oaxexvel_neg
	move	*a8(OXPOS),a0
	move	*a8(OSIZEX),a14
	add	a14,a0
	move	*a7(OXPOS),a14
	sub	a14,a0
;	jruc	oaxeovlap_done

oaxeovlap_done
	;a0 and a1 are the X and Y overlaps, respectively.  We need only
	; worry about the smallest of the two and move that many steps.  Note
	; that if velocity in a given direction is zero, the overlap is
	; set to some outrageously large value so that the other direction
	; is all that counts.
	cmp	a1,a0
	jrle	oaxelow_set
	move	a1,a0
oaxelow_set
	;a0 is now the smallest.
	move	a0,a1
	move	a10,a2
	X32	a2
	addi	oaxewalk_increments,a2
	move	*a2+,a3,W	;y vel
	mpys	a3,a1
	move	*a2,a3,W	;x vel
	mpys	a0,a3
	move	a3,a0

	;a0 and a1 now hold X and Y adjustments, respectively.
	move	*a8(OXPOS),a14
	add	a0,a14
	move	a14,*a8(OXPOS)
	move	*a8(OYPOS),a14
	add	a1,a14
	move	a14,*a8(OYPOS)
	
	PULL	a0,a1,a2,a3,a7
	rets


******************************************************************************
*
*	human movement/animation routine
*

	;reg use
	; a0 - scratch
	; a0 (out) - walls hit (0-2)
	; a1 - scratch
	; a8 - (in)  hObject
	; a9 - (in)  image table address (oaxemom_table, oaxedad_table...)
	;a10 - (in)  direction of motion (1-8)
	;a11 - (in)  frame index (0-3)

 SUBRP	oaxemove_onestep

	;move the object
	move	a10,a1
	X32	a1
	addi	oaxewalk_increments,a1
	move	*a1+,a14,W
	move	*a8(OYPOS),a0,W
	add	a14,a0
	move	a0,*a8(OYPOS),W
	move	*a1+,a14,W
	move	*a8(OXPOS),a0,W
	add	a14,a0
	move	a0,*a8(OXPOS),W

	clr	a1
	move	*a8(OXPOS),a0,W

	cmpi	oaxemin_x,a0
	jrge	oaxenot_lowx

	;low x
	inc	a1
	movi	oaxemin_x,a0
	move	a0,*a8(OXPOS),W

oaxenot_lowx
	cmpi	oaxemax_x,a0
	jrle	oaxenot_highx

	;high x
	inc	a1
	movi	oaxemax_x,a0
	move	a0,*a8(OXPOS),W

oaxenot_highx

	move	*a8(OYPOS),a0,W

	cmpi	oaxemin_y,a0
	jrge	oaxenot_lowy

	;low y
	inc	a1
	movi	oaxemin_y,a0
	move	a0,*a8(OYPOS),W

oaxenot_lowy
	cmpi	oaxemax_y,a0
	jrle	oaxenot_highy

	;high y
	inc	a1
	movi	oaxemax_y,a0
	move	a0,*a8(OYPOS),W

oaxenot_highy
	move	a1,a0
	rets


******************************************************************************
* Changes direction one time in oaxedirchange_chance.
*

 SUBRP	oaxerand_dirchange

	;1 chance in oaxedirchange_chance of spontaneously switching direction
	movi	oaxedirchange_chance,a0
	calla	RNDRNG0
	move	a0,a0
	jrnz	oaxedirchange_done
	movi	7,a0
	calla	RNDRNG0
	inc	a0
	move	a0,a10

oaxedirchange_done
	rets


******************************************************************************
* Reverses direction
*

 SUBRP	oaxereverse_direction

	;reverse direction of motion
	subi	4,a10
	jrp	oaxerd_done
	addi	8,a10
oaxerd_done
	rets

******************************************************************************
* Advances the frame
*

 SUBRP	oaxeadvance_frame

	;advance the frame and set the new image
	inc	a11
	cmpi	4,a11
	jrne	oaxeno_wrap
	;wraparound.  reset to start
	clr	a11

oaxeno_wrap
	;calculate the next frame
	move	a9,a0
	move	a10,a1
	sla	4,a1		;mult by 10h
	addi	oaxemotion_table,a1

	;a1 now points to one of the entries in oaxemotion table
	move	*a1,a1,W
	add	a1,a0

	;a0 now points to an entry in oaxexxx_table
	move	a11,a1
	X32	a1		;mult by 20h
	add	a1,a0
	move	*a0,a0,L

	calla	civanic

	rets


******************************************************************************
*
*	human collision routines
*

 SUBR	human_saved
	PUSH	a1,a7

	;change the obj id
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	SAVEDHUMAN_PID,a1
	movi	oaxesaved,a7
	calla	XFERPROC

	PULL	a1,a7
	rets

 SUBR	human_killed
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADHUMAN_PID,a1
	movi	oaxekilled,a7
	calla	XFERPROC

	PULL	a1,a7
	rets


 SUBR	human_hitpost
	PUSH	a1,a7

	move	a0,a1
	move	*a8(OPLINK),a0,L
	move	a1,*a0(oaxeHITPOST),L
	move	*a0(PA8),a8,L
	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	move	*a0(PA11),a11,L
	movi	HUMAN_PID,a1
	movi	oaxehit_post,a7
	calla	XFERPROC

	PULL	a1,a7
	rets

******************************************************************************
*
*	human configuration data
*


oaxemin_x	equ	53	;human start/motion outer boundaries
oaxemax_x	equ	347-11
oaxemin_y	equ	45
oaxemax_y	equ	231-13

oaxesleep_time	equ	7
oaxex_step		equ	1
oaxey_step		equ	1

oaxedirchange_chance	equ	40

oaxewalk_increments
	.word	0,0			;because direction is 1-8, not 0-7
	.word	-oaxey_step,0
	.word	-oaxey_step,oaxex_step
	.word	0,oaxex_step
	.word	oaxey_step,oaxex_step
	.word	oaxey_step,0
	.word	oaxey_step,-oaxex_step
	.word	0,-oaxex_step
	.word	-oaxey_step,-oaxex_step
oaxewalk_incrementsx

oaxemotion_table
	.word	0			;because direction is 1-8, not 0-7
	.word	oaxewalk_up
	.word	oaxewalk_right
	.word	oaxewalk_right
	.word	oaxewalk_right
	.word	oaxewalk_down
	.word	oaxewalk_left
	.word	oaxewalk_left
	.word	oaxewalk_left
oaxemotion_tablex

;image table direction offsets
oaxewalk_left	equ	0000h
oaxewalk_right	equ	oaxewalk_left + (4 * 20h)
oaxewalk_up	equ	oaxewalk_right + (4 * 20h)
oaxewalk_down	equ	oaxewalk_up + (4 * 20h)

oaxescore_table
	.word	1000,2000,3000,4000,5000
oaxescore_tablex

;image tables
oaxemom_table
	.long	mom_l1
	.long	mom_l2
	.long	mom_l1
	.long	mom_l3

	.long	mom_r1
	.long	mom_r2
	.long	mom_r1
	.long	mom_r3

	.long	mom_u1
	.long	mom_u2
	.long	mom_u1
	.long	mom_u3

	.long	mom_d1
	.long	mom_d2
	.long	mom_d1
	.long	mom_d3
oaxemom_tablex

oaxedad_table
	.long	dad_l1
	.long	dad_l2
	.long	dad_l1
	.long	dad_l3

	.long	dad_r1
	.long	dad_r2
	.long	dad_r1
	.long	dad_r3

	.long	dad_u1
	.long	dad_u2
	.long	dad_u1
	.long	dad_u3

	.long	dad_d1
	.long	dad_d2
	.long	dad_d1
	.long	dad_d3
oaxedad_tablex

oaxekid_table
	.long	kid_l1
	.long	kid_l2
	.long	kid_l1
	.long	kid_l3

	.long	kid_r1
	.long	kid_r2
	.long	kid_r1
	.long	kid_r3

	.long	kid_u1
	.long	kid_u2
	.long	kid_u1
	.long	kid_u3

	.long	kid_d1
	.long	kid_d2
	.long	kid_d1
	.long	kid_d3
oaxekid_tablex

oaxesaved_table
	.long	one_k
	.long	two_k
	.long	three_k
	.long	four_k
	.long	five_k
oaxesaved_tablex


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	HULK SECTION
*

	;reg use
	; a0 - (in) # of hulks

 SUBRP	make_hulks

	move	a0,a0
	jrz	xmgydone

xmgyanother_hulk
	PUSHP	a0

xmgyget_pos
	movi	xmgymax_x-xmgymin_x,a0
	calla	RNDRNG0
	move	a0,a9
	movi	xmgymax_y-xmgymin_y,a0
	calla	RNDRNG0
	move	a0,a10

	addi	xmgymin_x,a9
	addi	xmgymin_y,a10

	cmpi	xmgymid_x1,a9
	jrle	xmgyplace_hulk
	cmpi	xmgymid_x2,a9
	jrge	xmgyplace_hulk
	cmpi	xmgymid_y1,a10
	jrle	xmgyplace_hulk
	cmpi	xmgymid_y2,a10
	jrge	xmgyplace_hulk
	jruc	xmgyget_pos

xmgyplace_hulk
	;assign a random starting position
	CREATE	HULK_PID,hulk
	PULLP	a0
	dsj	a0,xmgyanother_hulk

xmgydone
	rets


xmgymin_x	equ	53	;hulk start outer boundaries
xmgymax_x	equ	347-13
xmgymin_y	equ	45
xmgymax_y	equ	231-16

xmgymid_x1	equ	200-50	;hulk start inner boundaries
xmgymid_x2	equ	200+50
xmgymid_y1	equ	138-50
xmgymid_y2	equ	138+50


*****************************************************************************
*
*	hulk process
*

	STRUCTPD
	LONG	awshPD_SCRIPT	;UHL script head
	WORD	awshPD_FRAME_NDX	;UHW frame index

	;reg use
	; a8 - object handle
	; a9 - (in)  starting x
	; a9 - (use) frame change count
	;a10 - (in)  starting y
	;a11 - direction of motion (0-3)

 SUBRP	hulk

	;choose a direction of motion
	movi	3,a0
	calla	RNDRNG0
	move	a0,a11

	X32	a0
	addi	awshanim_list,a0
	move	*a0,a6,L
	move	a6,*a13(awshPD_SCRIPT),L
	clr	a7
	move	a7,*a13(awshPD_FRAME_NDX),W

	;create the hulk object
	move	a9,a0		;x pos
	sla	16,a0
	move	a10,a1		;y pos
	sla	16,a1

	move	a7,a14		;img
	X32	a14
	add	a6,a14
	move	*a14,a2,L

	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPHULK,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the move counter
	movi	awshmove_freq/2,a0
	calla	RNDRNG0
	addi	awshmove_freq,a0
	move	a0,a9

awshloop
	SLEEPK	1
	dsj	a9,awshloop
	callr	awshmove_hulk
	movi	awshmove_freq,a9
	jruc	awshloop


awshbeen_shot	;we immediately wake up here if we've been hit and moved
		; by a player bullet.
	callr	awshdo_bounds_check
	jruc	awshloop

	DIE


******************************************************************************
*
*	hulk move routine
*

	;reg use
	; a0 scratch
	; a1 scratch
	; a8 object handle
	;a11 direction of motion (0-3)

 SUBRP	awshmove_hulk
	;move him
	move	a11,a14
	X32	a14
	addi	awshmove_table,a14
	move	*a14+,a0,W
	move	*a14,a1,W
	move	*a8(OXPOS),a14,W
	add	a0,a14
	move	a14,*a8(OXPOS),W
	move	*a8(OYPOS),a14,W
	add	a1,a14
	move	a14,*a8(OYPOS),W

	;
	;note the fall-through here
	;

	;bounds check
 SUBRP	awshdo_bounds_check
	clr	a1

	clr	a0
	move	*a8(OXPOS),a0,W
	cmpi	awshlow_x,a0
	jrlt	awshtoo_left
	cmpi	awshhigh_x,a0
	jrgt	awshtoo_right
	jruc	awshvert_check

awshtoo_left
	movi	awshlow_x,a14
	move	a14,*A8(OXPOS),W
	ori	M_HIT_LEFT,a1
	jruc	awshvert_check

awshtoo_right
	movi	awshhigh_x,a14
	move	a14,*A8(OXPOS),W
	ori	M_HIT_RIGHT,a1
;	jruc	awshvert_check

awshvert_check
	clr	a0
	move	*a8(OYPOS),a0,W
	cmpi	awshlow_y,a0
	jrlt	awshtoo_high
	cmpi	awshhigh_y,a0
	jrgt	awshtoo_low
	jruc	awshdone_boundcheck

awshtoo_high
	movi	awshlow_y,a14
	move	a14,*A8(OYPOS),W
	ori	M_HIT_TOP,a1
	jruc	awshdone_boundcheck

awshtoo_low
	movi	awshhigh_y,a14
	move	a14,*A8(OYPOS),W
	ori	M_HIT_BOTTOM,a1
	jruc	awshdone_boundcheck

awshdone_boundcheck
	move	a1,a1
	jrnz	awshchange_direction

awshno_wallhit
	movi	awshturn_prob,a0
	PUSH	a1
	calla	RNDRNG0
	PULL	a1
	move	a0,a0
	jrnz	awshdone_moving

	;spontaneous direction change.  random for now
;	jruc	awshchange_direction	;fall through

awshchange_direction
	;turn 90 degrees by randomly incrementing or decrementing direction
	movi	1,a0
	PUSH	a1
	calla	RNDRNG0
	PULL	a1
	move	a0,a0
	jrz	awshinc_direction
	dec	a11
	jruc	awshdir_rangecheck
	
awshinc_direction
	inc	a11

awshdir_rangecheck
	cmpi	-1,a11
	jrne	awshdir_notlow
	movi	3,a11

awshdir_notlow
	cmpi	4,a11
	jrne	awshdir_changed
	clr	a11

awshdir_changed

	;verify that this is a good direction
	cmpi	0,a11
	jreq	awshvalidate_up
	cmpi	1,a11
	jreq	awshvalidate_right
	cmpi	2,a11
	jreq	awshvalidate_down
	cmpi	3,a11
	jreq	awshvalidate_left

awshvalidate_up
	andi	M_HIT_TOP,a1
	jrnz	awshchange_direction
	jruc	awshvalid_dir
awshvalidate_right
	andi	M_HIT_RIGHT,a1
	jrnz	awshchange_direction
	jruc	awshvalid_dir
awshvalidate_down
	andi	M_HIT_BOTTOM,a1
	jrnz	awshchange_direction
	jruc	awshvalid_dir
awshvalidate_left
	andi	M_HIT_LEFT,a1
	jrnz	awshchange_direction
	jruc	awshvalid_dir

awshvalid_dir

	move	a11,a0
	X32	a0
	addi	awshanim_list,a0
	move	*a0,a0,L
	move	a0,*a13(awshPD_SCRIPT),L

awshdone_moving
	
awshnew_frame
	move	*a13(awshPD_FRAME_NDX),a14,W
	move	*a13(awshPD_SCRIPT),a6,L
	inc	a14
	cmpi	4,a14
	jrne	awshno_wrap
	clr	a14
awshno_wrap
	move	a14,*a13(awshPD_FRAME_NDX),W
	X32	a14
	add	a6,a14
	move	*a14,a0,L
	calla	civanic

	rets


******************************************************************************
*
*	hulk collision routines
*

 SUBR	hulk_pushback
	PUSH	a1,a7,a9,a10,a11,a13

	;immediately move the hulk in the direction of the bullet's motion
	move	*a0(OXVEL),a14,L
	sra	17,a14
	move	*a8(OXPOS),a1,W
	add	a14,a1
	move	a1,*a8(OXPOS),W

	move	*a0(OYVEL),a14,L
	sra	17,a14
	move	*a8(OYPOS),a1,W
	add	a14,a1
	move	a1,*a8(OYPOS),W

	;wake up at the collision check
	move	*a8(OPLINK),a0,L
	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	move	*a0(PA11),a11,L
	movi	HULK_PID,a1
	movi	awshbeen_shot,a7
	calla	XFERPROC
	PULL	a1,a7,a9,a10,a11,a13

	rets


******************************************************************************
*
*	hulk configuration data
*

awshstep_x		equ	4
awshstep_y		equ	4

awshlow_x		equ	53	;hulk move boundaries
awshhigh_x		equ	347-13
awshlow_y		equ	45
awshhigh_y		equ	231-16

awshmove_freq	equ	8	;frames between moves

awshturn_prob	equ	30	;chance per frame of spontaneously changing
				;direction
awshmove_table
	.word	0,-awshstep_y
	.word	awshstep_x,0
	.word	0,awshstep_y
	.word	-awshstep_x,0
awshmove_tablex

awshanim_list
	.long	awshanim_vert
	.long	awshanim_right
	.long	awshanim_vert
	.long	awshanim_left
awshanim_listx

awshanim_vert
	.long	hulk_ud1
	.long	hulk_ud2
	.long	hulk_ud1
	.long	hulk_ud3
awshanim_vertx

awshanim_left
	.long	hulk_l1
	.long	hulk_l2
	.long	hulk_l1
	.long	hulk_l3
awshanim_leftx

awshanim_right
	.long	hulk_r1
	.long	hulk_r2
	.long	hulk_r1
	.long	hulk_r3
awshanim_rightx


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	SPHEREOID SECTION
*

	;reg use
	; a0 - (in) # of sphereoids

 SUBRP	make_sphereoids

	move	a0,a0
	jrz	vwrgdone

vwrganother_sphereoid
	PUSHP	a0

vwrgget_pos
	movi	vwrgmax_x-vwrgmin_x,a0
	calla	RNDRNG0
	move	a0,a9
	movi	vwrgmax_y-vwrgmin_y,a0
	calla	RNDRNG0
	move	a0,a10

	addi	vwrgmin_x,a9
	addi	vwrgmin_y,a10

	cmpi	vwrgmid_x1,a9
	jrle	vwrgplace_sphereoid
	cmpi	vwrgmid_x2,a9
	jrge	vwrgplace_sphereoid
	cmpi	vwrgmid_y1,a10
	jrle	vwrgplace_sphereoid
	cmpi	vwrgmid_y2,a10
	jrge	vwrgplace_sphereoid
	jruc	vwrgget_pos

vwrgplace_sphereoid
	;assign a random starting position
	CREATE	SPHEREOID_PID,sphereoid
	PULLP	a0
	dsj	a0,vwrganother_sphereoid

vwrgdone
	rets


vwrgmid_x1	equ	200-80	;sphereoid start inner boundaries
vwrgmid_x2	equ	200+80
vwrgmid_y1	equ	138-80
vwrgmid_y2	equ	138+80


*******************************************************************************
*
*	sphereoid process
*

	STRUCTPD
	LONG	vwrgOXACC		;UHL x acceleration
	LONG	vwrgOYACC		;UHL y acceleration
	WORD	vwrgOCHILDREN	;UHW # of children left to spawn
	WORD	vwrgOSTAGE		;UHW stage in life cycle

	;reg use
	; a7 (use) event timer (related to vwrgOSTAGE)
	; a8 (use) * object
	; a9 (in)  x pos
	; a9 (use) anim counter
	;a10 (in)  y pos
	;a10 (use) frame pointer
	;a11 (use) time until next vel change

 SUBRP	sphereoid

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W

	;create the sphereoid object
	move	a9,a0		;x pos
	sll	16,a0
	move	a10,a1		;y pos
	sll	16,a1
	movi	vwrgstandard_script,a10
	move	*a10+,a2,L	;img
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPSPHEREOID,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;initialize anim counter
	movi	vwrganim_speed,a9

	;set initial velocities and accelerations
	callr	vwrgset_va

	;initialize life cycle and event timer
	movi	vwrgSTAGE_YOUTH,a14
	move	a14,*a13(vwrgOSTAGE),W
	movi	vwrgyouth_time/2,a0
	calla	RNDRNG0
	addi	vwrgyouth_time*3/4,a0
	move	a0,a7

vwrgloop
	PUSHP	a7
	SLEEPK	1
	PULLP	a7
	dec	a9
	jrnz	vwrgno_anim

	;advance the frame
	move	*a10+,a0,L
	jrnz	vwrgnewframe

	;reset script.  depends on the stage
	move	*a13(vwrgOSTAGE),a14,W
	cmpi	vwrgSTAGE_PARENT,a14
	jreq	vwrgset_parent_script
	movi	vwrgstandard_script,a10
	jruc	vwrgscript_set
vwrgset_parent_script
	movi	vwrgparent_script,a10

vwrgscript_set
	move	*a10+,a0,L

vwrgnewframe
	calla	civanic

	;reset the counter
	movi	vwrganim_speed,a9

vwrgno_anim
	;adjust velocities
	callr	vwrgacc_add

	;bounds check
	callr	vwrgin_bounds

	;adjust vel and acc if it's time
	dec	a11
	jrnz	vwrgno_va_change
	callr	vwrgset_va

vwrgno_va_change

	;do event?
	dec	a7
	jrnz	vwrgno_event

	;event.  depends on the stage in the life cycle
	move	*a13(vwrgOSTAGE),a14,W
	cmpi	vwrgSTAGE_YOUTH,a14
	jreq	vwrgyouth_event
	cmpi	vwrgSTAGE_PARENT,a14
	jreq	vwrgparent_event
	;default
	jruc	vwrgdotage_event

vwrgyouth_event
	;switch to middle age
	movi	vwrgSTAGE_PARENT,a14
	move	a14,*a13(vwrgOSTAGE),W

	;set the number of kids to be spawned
	movi	vwrgmax_kids-vwrgmin_kids,a0
	calla	RNDRNG0
	addi	vwrgmin_kids,a0
	move	a0,*a13(vwrgOCHILDREN),W

	;set the spawn timer
	movi	vwrgspawn_time/2,a0
	calla	RNDRNG0
	addi	vwrgspawn_time*3/4,a0
	move	a0,a7
	jruc	vwrgno_event

vwrgparent_event
	;spawn a brat
	PUSHP	a10,a11
	move	*a8(OXVAL),a10,L
	move	*a8(OYVAL),a11,L
	CREATE	ENFORCER_PID,enforcer
	PULLP	a10,a11

	;decrement the kid count
	move	*a13(vwrgOCHILDREN),a14,W
	dec	a14
	jrz	vwrgbarren
	move	a14,*a13(vwrgOCHILDREN),W
	movi	vwrgspawn_time/2,a0
	calla	RNDRNG0
	addi	vwrgspawn_time*3/4,a0
	move	a0,a7
	jruc	vwrgno_event

vwrgbarren
	;that was the last kid.  age
	movi	vwrgSTAGE_DOTAGE,a14
	move	a14,*a13(vwrgOSTAGE),W
	movi	vwrgold_age_time,a7
	jruc	vwrgno_event

vwrgdotage_event
	;nothing fancy.  just die.
	jruc	vwrgdie_peacefully
	
vwrgno_event
	jruc	vwrgloop

vwrgbeen_shot
	;crud.  we've been shot.  score the points
	movi	vwrgsphereoid_points,a0
	callr	score_points

	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

	;clear the velocity
	clr	a14
	move	a14,*a8(OXVEL),L
	move	a14,*a8(OYVEL),L

	;go through the death routine
	movi	vwrgparent_script,a10,L
	move	*a8(OCTRL),a14,W
	xori	DMAWNZ,a14
	ori	DMACNZ,a14
	move	a14,*a8(OCTRL),W
	movi	vwrgdiecolor,a14
	move	a14,*a8(OCONST),W
	movi	robo_p,a0
	calla	pal_getf
	move	a0,*a8(OPAL),W

vwrgdie_loop
	SLEEPK	vwrganim_speed
	move	*a10+,a0,L
	jrz	vwrgscore_msg
	calla	civanic
	jruc	vwrgdie_loop

vwrgscore_msg
	movi	vwrgscore_image,a0
	move	*a8(OCTRL),a1,W
	xori	DMACNZ,a1
	ori	DMAWNZ,a1
	calla	civani
	movi	vwrgscore_xoff,a14
	move	a14,*a8(ODXOFF),W
	movi	vwrgscore_yoff,a14	
	move	a14,*a8(ODYOFF),W
	SLEEP	vwrgscore_time
	jruc	vwrgexpire

vwrgdie_peacefully
	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

vwrgexpire
	calla	DELOBJA8

	DIE


 SUBRP	vwrgset_va

	movi	vwrghigh_v*2,a0
	calla	RNDRNG0
	subi	vwrghigh_v,a0
	move	a0,*a8(OXVEL),L

	movi	vwrghigh_v*2,a0
	calla	RNDRNG0
	subi	vwrghigh_v,a0
	move	a0,*a8(OYVEL),L

	movi	vwrghigh_a*2,a0
	calla	RNDRNG0
	subi	vwrghigh_a,a0
	move	a0,*a13(vwrgOXACC),L

	movi	vwrghigh_a*2,a0
	calla	RNDRNG0
	subi	vwrghigh_a,a0
	move	a0,*a13(vwrgOYACC),L

	movi	vwrgva_change_time,a0
	calla	RNDRNG0
	inc	a0
	move	a0,a11

	rets


 SUBRP	vwrgacc_add
	move	*a13(vwrgOXACC),a14,L
	move	*a8(OXVEL),a0,L
	add	a14,a0
	move	a0,*a8(OXVEL),L

	move	*a13(vwrgOYACC),a14,L
	move	*a8(OYVEL),a0,L
	add	a14,a0
	move	a0,*a8(OYVEL),L

	rets


 SUBRP	vwrgin_bounds

	move	*a8(OXPOS),a14,W
	movi	vwrgmin_x,a0
	cmp	a0,a14
	jrlt	vwrgx_adjust

	movi	vwrgmax_x,a0
	cmp	a0,a14
	jrgt	vwrgx_adjust
	jruc	vwrgy_check

vwrgx_adjust
	move	a0,*a8(OXPOS),W
	clr	a0
	move	a0,*a8(OXVEL),L
	move	a0,*a13(vwrgOXACC),L

vwrgy_check
	move	*a8(OYPOS),a14,W
	movi	vwrgmin_y,a0
	cmp	a0,a14
	jrlt	vwrgy_adjust

	movi	vwrgmax_y,a0
	cmp	a0,a14
	jrgt	vwrgy_adjust
	jruc	vwrgdone_check

vwrgy_adjust
	move	a0,*a8(OYPOS),W
	clr	a0
	move	a0,*a8(OYVEL),L
	move	a0,*a13(vwrgOYACC),L

vwrgdone_check
	rets


******************************************************************************
*
*	sphereoid collision routines
*

 SUBR	sphereoid_die
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADSPHERE_PID,a1
	movi	vwrgbeen_shot,a7
	calla	XFERPROC

	PULL	a1,a7
	rets


******************************************************************************
*
*	sphereoid configuration data
*

vwrganim_speed	equ	3

vwrgva_change_time	equ	180

vwrgsphereoid_points	equ	1000

vwrgyouth_time	equ	180	;time before we have kids
vwrgspawn_time	equ	120	;time between kids
vwrgold_age_time	equ	180	;time between last kid and natural death

vwrgmin_kids	equ	3	;lower limit on spawned enforcers
vwrgmax_kids	equ	7	;upper limit

vwrghigh_v		equ	00004000h
vwrghigh_a		equ	00000C00h

vwrgmin_x	equ	53	;sphereoid move bounds
vwrgmax_x	equ	347-15
vwrgmin_y	equ	45
vwrgmax_y	equ	231-15

vwrgscore_image	equ	one_k	;image on being shot
vwrgdiecolor	equ	0909h	;color on being shot
vwrgscore_time	equ	60	;time to display score value
vwrgscore_xoff	equ	-3	;shotimage offset
vwrgscore_yoff	equ	-5

;life cycle stages
vwrgSTAGE_YOUTH 	equ	0000h
vwrgSTAGE_PARENT	equ	0001h
vwrgSTAGE_DOTAGE	equ	0002h

vwrgstandard_script
	.long	circle_1
	.long	circle_2
	.long	circle_3
	.long	circle_4
	.long	circle_5
	.long	0

vwrgparent_script
	.long	circle_1
	.long	circle_2
	.long	circle_3
	.long	circle_4
	.long	circle_5
	.long	circle_6
	.long	circle_7
	.long	circle_8
	.long	0


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	ENFORCER SECTION
*

	.bss	num_enforcers,		16	;active enforcers
	.bss	picket_duty,		32	;*img of picket enforcer

	;reg use
	;  a8 (use) *img
	;  a9 (use) next move timer (move/fire phase)
	;  a9 (use) frame pointer (death phase)
	; a10 (in)  initial X
	; a10 (use) frame pointer (grow phase)
	; a11 (in)  initial Y
	; a11 (use) next shot timer (move/fire phase)

 SUBRP	enforcer

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W
	move	@num_enforcers,a14,W
	inc	a14
	move	a14,@num_enforcers,W

	;create the enforcer object
	move	a10,a0
	move	a11,a1
	movi	ildngrow_script,a10
	move	*a10+,a2,L
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPENFORCER,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;grow phase

ildngrow_loop
	SLEEPK	ildngrow_speed
	move	*a10+,a0,L
	jrz	ildnmf_phase
	calla	civanic
	jruc	ildngrow_loop

ildnmf_phase
	;begin move/fire phase.  set initial course
	callr	ildnnew_course
	movi	ildnturn_time,a9
	movi	ildnfire_time/2,a0
	calla	RNDRNG0
	addi	ildnfire_time*3/4,a0
	move	a0,a11

ildnmain_loop
	SLEEPK	1
	dec	a9
	jrnz	ildnno_turn


	;picket phase
	;two cases: we're on picket duty, or we're not.
	move	@picket_duty,a14,L
	cmp	a14,a8
	jreq	ildnon_picket

	;we're not on picket duty
	;if we're the only enforcer, quit now.
	move	@num_enforcers,a14,W
	cmpi	1,a14
	jreq	ildnpicket_done

	;there are multiple enforcers.   If there is no picket, see if we
	; take the job.  If there is, quit.
	move	@picket_duty,a14,L
	jrnz	ildnpicket_done

	;there is no picket.
	movi	ildnpicket_prob,a0
	calla	RNDRNG0
	move	a0,a0
	jrnz	ildnpicket_done

	;we're going to take picket duty
	move	a8,@picket_duty,L
	jruc	ildnpicket_done

ildnon_picket
	;we are on picket duty
	;if we're the only enforcer, see if we leave picket mode.
	move	@num_enforcers,a14,W
	cmpi	1,a14
	jrne	ildnmultiple_enforcers

	;we're the only enforcer
	movi	ildnstand_down_prob,a0
	calla	RNDRNG0
	move	a0,a0
	jrnz	ildnpicket_done

	;and we've decided to stand down.
;	clr	a0	;unneccesary.  it's already 0 from above.
	move	a0,@picket_duty,L
	jruc	ildnpicket_done

ildnmultiple_enforcers
	;we may abandon our post
	movi	ildnabandon_post_prob,a0
	calla	RNDRNG0
	move	a0,a0
	jrnz	ildnpicket_done

	;yep.  we quit.
;	clr	a0	;unneccesary.  it's already 0 from above.
	move	a0,@picket_duty,L
;	jruc	ildnpicket_done

ildnpicket_done

	;change course
	callr	ildnnew_course
	movi	ildnturn_time,a9

ildnno_turn

	dec	a11
	jrnz	ildnno_shot

	;fire a spark
	PUSHP	a10
	move	*a8(OXVAL),a10,L
	move	*a8(OYVAL),a11,L
	CREATE	SPARK_PID,spark
	PULLP	a10
	movi	ildnfire_time/2,a0
	calla	RNDRNG0
	addi	ildnfire_time*3/4,a0
	move	a0,a11

ildnno_shot

	;bounds check
	callr	ildnin_bounds

	jruc	ildnmain_loop

ildndie
	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W
	move	@num_enforcers,a14,W
	dec	a14
	move	a14,@num_enforcers,W

	;zero our velocity
	clr	a14
	move	a14,*a8(OXVEL),L
	move	a14,*a8(OYVEL),L

	;set up the animation
ildndieloop
	move	*a9+,a0,L
	jrz	ildndead_n_buried
	calla	civanic
	sleepk	ildndeath_speed
	jruc	ildndieloop

ildndead_n_buried
	calla	DELOBJA8

	DIE


 SUBRP	ildnnew_course

	;two cases.  picket or !picket
	move	@picket_duty,a14,L
	cmp	a8,a14
	jrne	ildnnc_notpicket

ildnnc_picket
	;we're on picket duty.  if we're not against a wall, move toward the
	; nearest one.  if we are against a wall but not in a corner, move
	; toward the nearest corner.  if we're in a corner, sit tight.
;	jruc	ildnnc_done

ildnnc_notpicket
	;velocity is the distance between enforcer and player / 64 / 2
	move	@player_object,a0,L
	move	*a0(OXVAL),a14,L
	move	*a8(OXVAL),a1,L
	sub	a1,a14
	sra	7,a14
	
	move	a14,*a8(OXVEL),L

	move	*a0(OYVAL),a14,L
	move	*a8(OYVAL),a1,L
	sub	a1,a14
	sra	7,a14
	move	a14,*a8(OYVEL),L

ildnnc_done
	rets

 SUBRP	ildnin_bounds

	move	*a8(OXPOS),a14,W
	movi	ildnmin_x,a0
	cmp	a0,a14
	jrlt	ildnx_adjust

	movi	ildnmax_x,a0
	cmp	a0,a14
	jrgt	ildnx_adjust
	jruc	ildny_check

ildnx_adjust
	move	a0,*a8(OXPOS),W
	clr	a0
	move	a0,*a8(OXVEL),L

ildny_check
	move	*a8(OYPOS),a14,W
	movi	ildnmin_y,a0
	cmp	a0,a14
	jrlt	ildny_adjust

	movi	ildnmax_y,a0
	cmp	a0,a14
	jrgt	ildny_adjust
	jruc	ildndone_check

ildny_adjust
	move	a0,*a8(OYPOS),W
	clr	a0
	move	a0,*a8(OYVEL),L

ildndone_check
	rets


******************************************************************************
*
*	enforcer collision routines
*

 SUBR	enforcer_die
	;hit by player gunfire
	PUSH	a1,a7

	;jeepers.  we're dead.
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W

	move	*a0(OXVEL),a14,L
	jrz	ildnvert_bullet
	move	*a0(OYVEL),a1,L
	jrz	ildnhorz_bullet

	add	a1,a14
	jrz	ildndiag_13
	jruc	ildndiag_24

ildnhorz_bullet
	movi	ildndie_vert,a9
	jruc	ildndeath_set
ildnvert_bullet
	movi	ildndie_horz,a9
	jruc	ildndeath_set
ildndiag_13
	movi	ildndie_diag24,a9
	jruc	ildndeath_set
ildndiag_24
	movi	ildndie_diag13,a9
	jruc	ildndeath_set

ildndeath_set
	move	*a8(OPLINK),a0,L
	movi	DEADNFORCER_PID,a1
	movi	ildndie,a7
	move	*a0(PA10),a10,L
	calla	XFERPROC

	movi	ildnenforcer_points,a0
	callr	score_points

	PULL	a1,a7
	rets


******************************************************************************
*
*	enforcer configuration data
*

ildnenforcer_points	equ	100

ildnpicket_prob	equ	4	;one chance in X of becoming the picket if
				; there are multiple enforcers and none is
				; on the job already.

ildnstand_down_prob equ	4	;one chance in X of leaving picket duty if
				; we're the only enforcer left.

ildnabandon_post_prob equ	8	;one chance in X of leaving picket duty if
				; there are other enforcers around.

ildnturn_time	equ	150
ildnfire_time	equ	90

ildngrow_speed	equ	6

ildnmin_x	equ	53	;enforcer move bounds
ildnmax_x	equ	347-9
ildnmin_y	equ	45
ildnmax_y	equ	231-11

ildngrow_script
	.long	enf_g1
	.long	enf_g2
	.long	enf_g3
	.long	enf_g4
	.long	enf_g5
	.long	enf_1
	.long	0
ildngrow_scriptx

ildndie_vert
	.long	enf_xv1
	.long	enf_xv2
	.long	enf_xv3
	.long	enf_xv4
	.long	enf_xv5
	.long	0
ildndie_vertx

ildndie_horz
	.long	enf_xh1
	.long	enf_xh2
	.long	enf_xh3
	.long	enf_xh4
	.long	0
ildndie_horzx

ildndie_diag13
	.long	enf_xf1
	.long	enf_xf2
	.long	enf_xf3
	.long	enf_xf4
	.long	0
ildndie_diag13x

ildndie_diag24
	.long	enf_xb1
	.long	enf_xb2
	.long	enf_xb3
	.long	enf_xb4
	.long	0
ildndie_diag24x

ildndeath_speed	equ	3		;death seq anim speed


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	SPARK SECTION
*

	;reg use
	;  a6 (use) x acc
	;  a7 (use) y acc
	;  a8 (use) *image
	;  a9 (use) anim timer
	; a10 (in)  initial X
	; a10 (use) anim pointer
	; a11 (in)  initial Y
	; a11 (use) life timer

 SUBRP	spark

	;create a spark object
	move	a10,a0
	move	a11,a1

	movi	rypdscript,a10
	move	*a10+,a2,L
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPSPARK,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the initial velocity.
	move	@player_object,a0,L
	move	*a0(OXVAL),a1,L
	move	*a8(OXVAL),a2,L
	sub	a2,a1
	sra	5,a1
	move	a1,*a8(OXVEL),L

	move	*a0(OYVAL),a1,L
	move	*a8(OYVAL),a2,L
	sub	a2,a1
	sra	5,a1
	move	a1,*a8(OYVEL),L

	;and then add a little 'spin' by giving them some acceleration
	movi	rypdhigh_acc*2,a0
	calla	RNDRNG0
	subi	rypdhigh_acc,a0
	move	a0,a6
	movi	rypdhigh_acc*2,a0
	calla	RNDRNG0
	subi	rypdhigh_acc,a0
	move	a0,a7

	movi	rypdanim_time,a9
	movi	rypdlife_time,a11

rypdloop
	PUSHP	a6,a7
	SLEEPK	1
	PULLP	a6,a7

	;bounds check
	callr	rypdin_bounds

	;apply accelerations
	move	*a8(OXVEL),a14,L
	add	a6,a14
	move	a14,*a8(OXVEL),L

	move	*a8(OYVEL),a14,L
	add	a7,a14
	move	a14,*a8(OYVEL),L

	dec	a9
	jrnz	rypdno_anim

	move	*a10+,a0,L
	jrnz	rypdno_wrap
	movi	rypdscript,a10
	move	*a10+,a0,L
rypdno_wrap
	calla	civanic
	movi	rypdanim_time,a9

rypdno_anim
	dec	a11
	jrz	rypdexpire
	jruc	rypdloop

rypdexpire
	calla	DELOBJA8

	DIE


 SUBRP	rypdin_bounds

	move	*a8(OXPOS),a14,W
	movi	rypdmin_x,a0
	cmp	a0,a14
	jrlt	rypdx_adjust

	movi	rypdmax_x,a0
	cmp	a0,a14
	jrgt	rypdx_adjust
	jruc	rypdy_check

rypdx_adjust
	move	a0,*a8(OXPOS),W
	clr	a6
	move	a6,*a8(OXVEL),L

rypdy_check
	move	*a8(OYPOS),a14,W
	movi	rypdmin_y,a0
	cmp	a0,a14
	jrlt	rypdy_adjust

	movi	rypdmax_y,a0
	cmp	a0,a14
	jrgt	rypdy_adjust
	jruc	rypddone_check

rypdy_adjust
	move	a0,*a8(OYPOS),W
	clr	a7
	move	a7,*a8(OYVEL),L

rypddone_check
	rets


******************************************************************************
*
*	spark collision routines
*

 SUBR	spark_die
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADSPARK_PID,a1
	movi	rypdexpire,a7
	calla	XFERPROC

	PULL	a1,a7
	rets


******************************************************************************
*
*	spark configuration data
*

rypdlife_time	equ	180		;how long they last
rypdanim_time	equ	3		;frame rate

rypdhigh_acc	equ	00000200h	;top acceleration

rypdmin_x	equ	53	;spark move bounds
rypdmax_x	equ	347-7
rypdmin_y	equ	45
rypdmax_y	equ	231-7

rypdscript
	.long	spark_1
	.long	spark_2
	.long	spark_3
	.long	spark_4
	.long	0


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	QUARK SECTION
*

	;reg use
	; a0 - (in) # of quarks

 SUBRP	make_quarks

	move	a0,a0
	jrz	ozcmdone

ozcmanother_quark
	PUSHP	a0

ozcmget_pos
	;assign a random starting position
	movi	ozcmmax_x-ozcmmin_x,a0
	calla	RNDRNG0
	move	a0,a9
	movi	ozcmmax_y-ozcmmin_y,a0
	calla	RNDRNG0
	move	a0,a10

	addi	ozcmmin_x,a9
	addi	ozcmmin_y,a10

	cmpi	ozcmmid_x1,a9
	jrle	ozcmplace_quark
	cmpi	ozcmmid_x2,a9
	jrge	ozcmplace_quark
	cmpi	ozcmmid_y1,a10
	jrle	ozcmplace_quark
	cmpi	ozcmmid_y2,a10
	jrge	ozcmplace_quark
	jruc	ozcmget_pos

ozcmplace_quark
	CREATE	QUARK_PID,quark
	PULLP	a0
	dsj	a0,ozcmanother_quark

ozcmdone
	rets


ozcmmid_x1	equ	200-60	;quark start inner boundaries
ozcmmid_x2	equ	200+60
ozcmmid_y1	equ	138-60
ozcmmid_y2	equ	138+60


*******************************************************************************
*
*	quark process
*

	STRUCTPD
	WORD	ozcmOCHILDREN	;UHW # of children left to spawn
	WORD	ozcmOSTAGE		;UHW stage in life cycle

	;reg use
	; a7 (use) event timer (related to ozcmOSTAGE)
	; a8 (use) * object
	; a9 (in)  x pos
	; a9 (use) anim counter
	;a10 (in)  y pos
	;a10 (use) frame pointer
	;a11 (use) time until next vel change

 SUBRP	quark

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W

	;create the quark object
	move	a9,a0		;x pos
	sll	16,a0
	move	a10,a1		;y pos
	sll	16,a1
	movi	ozcmstandard_script,a10
	move	*a10+,a2,L	;img
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPQUARK,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;initialize anim counter
	movi	ozcmanim_speed,a9

	;set initial velocities and accelerations
	callr	ozcmset_v

	;initialize life cycle and event timer
	movi	ozcmSTAGE_YOUTH,a14
	move	a14,*a13(ozcmOSTAGE),W
	movi	ozcmyouth_time,a7

ozcmloop
	PUSHP	a7
	SLEEPK	1
	PULLP	a7
	dec	a9
	jrnz	ozcmno_anim

	;advance the frame
	move	*a10+,a0,L
	jrnz	ozcmnewframe

	;reset script.  depends on the stage
	move	*a13(ozcmOSTAGE),a14,W
	cmpi	ozcmSTAGE_PARENT,a14
	jreq	ozcmset_parent_script
	movi	ozcmstandard_script,a10
	jruc	ozcmscript_set
ozcmset_parent_script
	movi	ozcmparent_script,a10

ozcmscript_set
	move	*a10+,a0,L

ozcmnewframe
	calla	civanic

	;reset the counter
	movi	ozcmanim_speed,a9

ozcmno_anim
	;bounds check
	callr	ozcmin_bounds

	;adjust vel if it's time
	dec	a11
	jrnz	ozcmno_v_change
	callr	ozcmset_v

ozcmno_v_change

	;do event?
	dec	a7
	jrnz	ozcmno_event

	;event.  depends on the stage in the life cycle
	move	*a13(ozcmOSTAGE),a14
	cmpi	ozcmSTAGE_YOUTH,a14
	jreq	ozcmyouth_event
	cmpi	ozcmSTAGE_PARENT,a14
	jreq	ozcmparent_event
	;default
	jruc	ozcmdotage_event

ozcmyouth_event
	;switch to middle age
	movi	ozcmSTAGE_PARENT,a14
	move	a14,*a13(ozcmOSTAGE),W

	;set the number of kids to be spawned
	movi	ozcmmax_kids-ozcmmin_kids,a0
	calla	RNDRNG0
	addi	ozcmmin_kids,a0
	move	a0,*a13(ozcmOCHILDREN),W

	;set the spawn timer
	movi	ozcmspawn_time,a7
	jruc	ozcmno_event

ozcmparent_event
	;spawn a brat
	PUSHP	a10,a11
	move	*a8(OXVAL),a10,L
	move	*a8(OYVAL),a11,L
	CREATE	TANK_PID,tank
	PULLP	a10,a11

	;decrement the kid count
	move	*a13(ozcmOCHILDREN),a14,W
	dec	a14
	jrz	ozcmbarren
	move	a14,*a13(ozcmOCHILDREN),W
	movi	ozcmspawn_time,a7
	jruc	ozcmno_event

ozcmbarren
	;that was the last kid.  age
	movi	ozcmSTAGE_DOTAGE,a14
	move	a14,*a13(ozcmOSTAGE),W
	movi	ozcmold_age_time,a7
	jruc	ozcmno_event

ozcmdotage_event
	;nothing fancy.  just die.
	jruc	ozcmdie_peacefully
	
ozcmno_event
	jruc	ozcmloop


ozcmbeen_shot
	;crud.  we've been shot

ozcmdie_peacefully

	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

	calla	DELOBJA8

	DIE


 SUBRP	ozcmset_v

	movi	ozcmhigh_v,a0
	calla	RNDRNG0
	cmpi	ozcmhigh_v/2,a0
	jrgt	ozcmpositive_x

	subi	ozcmhigh_v,a0

ozcmpositive_x
	move	a0,*a8(OXVEL),L

	movi	ozcmhigh_v,a0
	calla	RNDRNG0
	cmpi	ozcmhigh_v/2,a0
	jrgt	ozcmpositive_y

	subi	ozcmhigh_v,a0

ozcmpositive_y
	move	a0,*a8(OYVEL),L

	movi	ozcmv_change_time/2,a0
	calla	RNDRNG0
	addi	ozcmv_change_time/2,a0
	move	a0,a11

	rets


 SUBRP	ozcmin_bounds

	move	*a8(OXPOS),a1,W
	movi	ozcmmin_x,a0
	move	*a8(ODXOFF),a14,W
	add	a14,a0
	cmp	a0,a1
	jrlt	ozcmx_adjust

	move	*a8(OSIZEX),a0,W
	neg	a0
	move	*a8(ODXOFF),a14,W
	add	a14,a0
	addi	ozcmmax_x,a0
	cmp	a0,a1
	jrgt	ozcmx_adjust
	jruc	ozcmy_check

ozcmx_adjust
	move	a0,*a8(OXPOS),W
	move	*a8(OXVEL),a0,L
	neg	a0
	move	a0,*a8(OXVEL),L

ozcmy_check
	move	*a8(OYPOS),a1,W
	movi	ozcmmin_y,a0
	move	*a8(ODYOFF),a14,W
	add	a14,a0
	cmp	a0,a1
	jrlt	ozcmy_adjust

	move	*a8(OSIZEY),a0,W
	neg	a0
	move	*a8(ODYOFF),a14,W
	add	a14,a0
	addi	ozcmmax_y,a0
	cmp	a0,a1
	jrgt	ozcmy_adjust
	jruc	ozcmdone_check

ozcmy_adjust
	move	a0,*a8(OYPOS),W
	move	*a8(OYVEL),a0,L
	neg	a0
	move	a0,*a8(OYVEL),L

ozcmdone_check
	rets


******************************************************************************
*
*	quark collision routines
*

 SUBR	quark_die
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADQUARK_PID,a1
	movi	ozcmbeen_shot,a7
	calla	XFERPROC

	PULL	a1,a7
	rets


******************************************************************************
*
*	quark configuration data
*

ozcmanim_speed	equ	3

ozcmv_change_time	equ	600	;time between random course changes

ozcmyouth_time	equ	90	;time before we have kids
ozcmspawn_time	equ	90	;time between kids
ozcmold_age_time	equ	120	;time between last kid and natural death

ozcmmin_kids	equ	3	;lower limit on spawned tanks
ozcmmax_kids	equ	7	;upper limit

ozcmhigh_v		equ	0001C000h

ozcmmin_x	equ	53	;quark move bounds
ozcmmax_x	equ	347-0
ozcmmin_y	equ	45
ozcmmax_y	equ	231-0

;life cycle stages
ozcmSTAGE_YOUTH 	equ	0000h
ozcmSTAGE_PARENT	equ	0001h
ozcmSTAGE_DOTAGE	equ	0002h

ozcmstandard_script
	.long	quark_1
	.long	quark_1
	.long	quark_2
	.long	quark_3
	.long	quark_4
	.long	0

ozcmparent_script
	.long	quark_1
	.long	quark_2
	.long	quark_3
	.long	quark_4
	.long	quark_5
	.long	quark_6
	.long	quark_7
	.long	quark_8
	.long	0


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	TANK SECTION
*

	STRUCTPD
	WORD	bpxiOSHOTS		;UHW shots left to be fired

	;reg use
	;  a7 (use) anim timer
	;  a8 (use) *img
	;  a9 (use) next move timer (move/fire phase)
	;  a9 (use) frame pointer (death phase)
	; a10 (in)  initial X
	; a10 (use) frame pointer (grow + move/fire phases)
	; a11 (in)  initial Y
	; a11 (use) next shot timer (move/fire phase)

 SUBRP	tank

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W

	;create the tank object
	move	a10,a0
	move	a11,a1
	movi	bpxigrow_script,a10
	move	*a10+,a2,L
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPTANK,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;grow phase

bpxigrow_loop
	SLEEPK	bpxigrow_speed
	move	*a10+,a0,L
	jrz	bpximf_phase
	calla	civanic
	jruc	bpxigrow_loop

bpximf_phase
	;begin move/fire phase.  set initial course
	callr	bpxiset_v
	movi	bpxianim_time,a7
	movi	bpxiright_script,a10
	movi	bpxifire_time/2,a0
	calla	RNDRNG0
	addi	bpxifire_time*3/4,a0
	move	a0,a11
	movi	bpxinum_shots,a14
	move	a14,*a13(bpxiOSHOTS),W

bpximain_loop
	PUSHP	a7
	SLEEPK	1
	PULLP	a7
	dec	a9
	jrnz	bpxino_turn

	;change course
	callr	bpxiset_v

bpxino_turn

	dec	a11
	jrnz	bpxino_shot

	move	*a13(bpxiOSHOTS),a14,W
	jrz	bpxino_shot

	;fire a shell
	PUSHP	a7,a10,a11
	move	*a8(OXVAL),a10,L
	move	*a8(OYVAL),a11,L
	create	SHELL_PID,tank_shell
	PULLP	a7,a10,a11
	movi	bpxifire_time/2,a0
	calla	RNDRNG0
	addi	bpxifire_time*3/4,a0
	move	a0,a11

bpxino_shot

	;bounds check
	callr	bpxiin_bounds

	;animate?
	dec	a7
	jrnz	bpxino_anim
	move	*a10+,a0,L
	jrnz	bpxino_wrap
	movi	bpxiright_script,a10
	move	*a10+,a0,L

bpxino_wrap
	calla	civanic
	movi	bpxianim_time,a7

bpxino_anim
	jruc	bpximain_loop

bpxidie
	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

	;zero our velocity
	clr	a14
	move	a14,*a8(OXVEL),L
	move	a14,*a8(OYVEL),L

	;set up the animation
bpxidieloop
	move	*a9+,a0,L
	jrz	bpxidead_n_buried
	calla	civanic
	sleepk	bpxideath_speed
	jruc	bpxidieloop

bpxidead_n_buried
	calla	DELOBJA8

	DIE


 SUBRP	bpxiset_v

	movi	bpxihigh_v,a0
	calla	RNDRNG0
	cmpi	bpxihigh_v/2,a0
	jrgt	bpxipositive_x

	subi	bpxihigh_v,a0

bpxipositive_x
	move	a0,*a8(OXVEL),L

	movi	bpxihigh_v,a0
	calla	RNDRNG0
	cmpi	bpxihigh_v/2,a0
	jrgt	bpxipositive_y

	subi	bpxihigh_v,a0

bpxipositive_y
	move	a0,*a8(OYVEL),L

	movi	bpxiv_change_time/2,a0
	calla	RNDRNG0
	addi	bpxiv_change_time/2,a0
	move	a0,a9

	rets


 SUBRP	bpxiin_bounds

	move	*a8(OXPOS),a14,W
	movi	bpximin_x,a0
	cmp	a0,a14
	jrlt	bpxix_adjust

	movi	bpximax_x,a0
	cmp	a0,a14
	jrgt	bpxix_adjust
	jruc	bpxiy_check

bpxix_adjust
	move	a0,*a8(OXPOS),W
	move	*a8(OXVEL),a0,L
	neg	a0
	move	a0,*a8(OXVEL),L

bpxiy_check
	move	*a8(OYPOS),a14,W
	movi	bpximin_y,a0
	cmp	a0,a14
	jrlt	bpxiy_adjust

	movi	bpximax_y,a0
	cmp	a0,a14
	jrgt	bpxiy_adjust
	jruc	bpxidone_check

bpxiy_adjust
	move	a0,*a8(OYPOS),W
	move	*a8(OYVEL),a0,L
	neg	a0
	move	a0,*a8(OYVEL),L

bpxidone_check
	rets


******************************************************************************
*
*	tank collision routines
*

 SUBR	tank_die
	;hit by player gunfire
	PUSH	a1,a7

	;jeepers.  we're dead.
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W

	move	*a0(OXVEL),a14,L
	jrz	bpxivert_bullet
	move	*a0(OYVEL),a1,L
	jrz	bpxihorz_bullet

	add	a1,a14
	jrz	bpxidiag_13
	jruc	bpxidiag_24

bpxihorz_bullet
	movi	bpxidie_vert,a9
	jruc	bpxideath_set
bpxivert_bullet
	movi	bpxidie_horz,a9
	jruc	bpxideath_set
bpxidiag_13
	movi	bpxidie_diag24,a9
	jruc	bpxideath_set
bpxidiag_24
	movi	bpxidie_diag13,a9
	jruc	bpxideath_set

bpxideath_set
	move	*a8(OPLINK),a0,L
	movi	DEADTANK_PID,a1
	movi	bpxidie,a7
	move	*a0(PA10),a10,L
	calla	XFERPROC

	movi	bpxitank_points,a0
	callr	score_points

	PULL	a1,a7
	rets


******************************************************************************
*
*	tank configuration data
*

bpxitank_points	equ	200

bpxinum_shots	equ	21

bpxiv_change_time	equ	250
bpxifire_time	equ	90

bpxihigh_v		equ	00010000h

bpxigrow_speed	equ	6
bpxianim_time	equ	2

bpximin_x	equ	53	;tank move bounds
bpximax_x	equ	347-13
bpximin_y	equ	45
bpximax_y	equ	231-16

bpxigrow_script
	.long	tank_g1
	.long	tank_g2
	.long	tank_g3
	.long	tank_g4
	.long	tank_1
	.long	0

bpxiright_script
	.long	tank_1
	.long	tank_2
	.long	tank_3
	.long	tank_4
	.long	0

bpxidie_vert
	.long	enf_xv1
	.long	enf_xv2
	.long	enf_xv3
	.long	enf_xv4
	.long	enf_xv5
	.long	0

bpxidie_horz
	.long	enf_xh1
	.long	enf_xh2
	.long	enf_xh3
	.long	enf_xh4
	.long	0

bpxidie_diag13
	.long	enf_xf1
	.long	enf_xf2
	.long	enf_xf3
	.long	enf_xf4
	.long	0

bpxidie_diag24
	.long	enf_xb1
	.long	enf_xb2
	.long	enf_xb3
	.long	enf_xb4
	.long	0

bpxideath_speed	equ	3		;death seq anim speed


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	SHELL SECTION
*

	;reg use
	;  a8 (use) *image
	; a10 (in)  initial X
	; a11 (in)  initial Y
	; a11 (use) life timer

 SUBRP	tank_shell

	;create a shell object
	move	a10,a0
	move	a11,a1
	movi	shell,a2
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPSHELL,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the initial velocity.
	move	@player_object,a0,L
	move	*a0(OXVAL),a1,L
	move	*a8(OXVAL),a2,L
	sub	a2,a1
	sra	5,a1
	move	a1,*a8(OXVEL),L

	move	*a0(OYVAL),a1,L
	move	*a8(OYVAL),a2,L
	sub	a2,a1
	sra	5,a1
	move	a1,*a8(OYVEL),L

	movi	myiclife_time,a11

myicloop
	PUSHP	a6,a7
	SLEEPK	1
	PULLP	a6,a7

	;bounds check
	callr	myicin_bounds

	dsj	a11,myicloop

myicexpire
	calla	DELOBJA8

	DIE


 SUBRP	myicin_bounds

	move	*a8(OXPOS),a14,W
	movi	myicmin_x,a0
	cmp	a0,a14
	jrlt	myicx_adjust

	movi	myicmax_x,a0
	cmp	a0,a14
	jrgt	myicx_adjust
	jruc	myicy_check

myicx_adjust
	move	a0,*a8(OXPOS),W
	move	*a8(OXVEL),a0,L
	neg	a0
	move	a0,*a8(OXVEL),L

myicy_check
	move	*a8(OYPOS),a14
	movi	myicmin_y,a0
	cmp	a0,a14
	jrlt	myicy_adjust

	movi	myicmax_y,a0
	cmp	a0,a14
	jrgt	myicy_adjust
	jruc	myicdone_check

myicy_adjust
	move	a0,*a8(OYPOS),W
	move	*a8(OYVEL),a0,L
	neg	a0
	move	a0,*a8(OYVEL),L

myicdone_check
	rets


******************************************************************************
*
*	shell collision routines
*

 SUBR	shell_die
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADSHELL_PID,a1
	movi	myicexpire,a7
	calla	XFERPROC

	PULL	a1,a7
	rets


******************************************************************************
*
*	shell configuration data
*

myiclife_time	equ	240		;how long they last

myicmin_x	equ	53	;shell move bounds
myicmax_x	equ	347-7
myicmin_y	equ	45
myicmax_y	equ	231-7


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	color cycling processes
*

 SUBRP	robo_pal_cycles

	;laser flash cycle
	movi	[0Ah,1],a8		;cycle color A
	movi	robo_p,a9		;pal name
	movi	COLTAB_LFLASH,a10	;use the LFLASH table
	movk	1,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;rgb cycle
	movi	[0Bh,1],a8		;cycle color B
	movi	robo_p,a9		;pal name
	movi	COLTAB_RGB,a10		;use the RGB table
	movk	8,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;decay cycle
	movi	[0Ch,1],a8		;cycle color C
	movi	robo_p,a9		;pal name
	movi	COLTAB_DECAY,a10	;use the DECAY table
	movk	2,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;laser cycle
	movi	[0Dh,1],a8		;cycle color D
	movi	robo_p,a9		;pal name
	movi	COLTAB_LASER,a10	;use the LASER table
	movk	20,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;blue-purple-red cycle
	movi	[0Eh,1],a8		;cycle color E
	movi	robo_p,a9		;pal name
	movi	COLTAB_BLUPURRED,a10	;use the BPR table
	movk	1,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;red-gold cycle
	movi	[0Fh,1],a8		;cycle color F
	movi	robo_p,a9		;pal name
	movi	COLTAB_REDGOLD,a10	;use the RED-GOLD table
	movk	10,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	rets


COLTAB_LFLASH
	.WORD	0380H,1380H,2380H,3380H,4380H,5380H,6380H,7380H,7300H
	.WORD	7280H,7200H,7180H,7080H,7008H,7008H,7010H,7010H,701CH
	.WORD	701CH,601CH,501CH,409CH,309CH,209CH,219CH,029CH,039CH
	.WORD	139CH,239CH,339CH,539CH,739CH,7390H,7380H,6380H,4380H
	.word	-1

;	.word	7C00h,7FFFh,7FFFh,03E0h,7FFFh,7FFFh,001Fh,7FFFh,7FFFh

COLTAB_RGB
	.WORD	07C00H,001FH,77A0h,741Ah
	.word	-1

;	.word	7C00h,03E0h,001Fh

COLTAB_DECAY
	.WORD	001CH,001CH,011CH,021CH,031CH,039CH,239CH,2390H,2388H
	.WORD	2380H,4300H,5280H,7180H,6180H,7080H,7000H,6000H,5000H
	.WORD	4000H,3000H,2000H,1000H
	.word	-1

;	.word	7FFFh,6F7Bh,5EF7h,4E73h,3DEFh,2D6Bh,1CE7h,0C63h,0000h

COLTAB_LASER
	.word	7C1Fh,7FFFh,7F00h
	.word	-1

COLTAB_BLUPURRED
	.WORD	001CH,101CH,201CH,301CH,401CH,501CH,601CH,701CH,7010H
	.WORD	7010H,7008H,7008H,7000H,7000H,7008H,7008H,7010H,7010H
	.WORD	701CH,701CH,601CH,501CH,401CH,301CH,201CH,101CH
;	.word	001Fh,7C1Fh,7C00h
	.word	-1

COLTAB_REDGOLD
	.word	7C00h,7F00h
	.word	-1


*****************************************************************************
******************************************************************************
******************************************************************************
*
*	score stuff
*

	;reg use
	; a11 score

 SUBRP	score

	;initialize score
	move	@robotron_score,a11,L
	callr	uekpprint_score

uekploop
	SLEEPK	1
	move	@robotron_score,a14,L
	cmp	a11,a14
	jrne	uekpscore_change
	jruc	uekploop

uekpscore_change
	move	a14,a11
	callr	uekpzorch_score
	callr	uekpprint_score
	jruc	uekploop

	DIE


 SUBRP	uekpzorch_score
	movi	TYPTEXT|SUBSCOR,a0
	calla	obj_del1c

	rets


 SUBRP	uekpprint_score

	movi	uekpscore_setup,a2
	calla	setup_message
	movi	TYPTEXT|SUBSCOR,a14
	move	a14,@mess_objid
	move	@robotron_score,a0,L
	movi	uekpmax_score,a1
	calla	dec_to_asc
	calla	copy_string
	calla	print_string_R

	rets


uekpscore_setup
	JAM_STR	robotron_ascii,3,1,uekpscore_xpos,uekpscore_ypos,robo_p,0
	.even

uekpmax_score	.equ	9999999		;that's all dec_to_asc can handle

******************************************************************************
* adds to score
* 0a0H amount to add

 SUBRP	score_points

	move	@robotron_score,a14,L
	add	a0,a14
	move	a14,@robotron_score,L
	rets


uekpscore_ypos	.equ	36
uekpscore_xpos	.equ	130

*****************************************************************************
******************************************************************************
******************************************************************************
*
*	miscellaneous functions / processes
*

******************************************************************************
* Draws the frame
* 0a0H=color value
*

 SUBRP	draw_frame


	move	a0,a9
	sla	8,a9
	or	a0,a9

	movi	robo_p,a0
	calla	pal_getf
	move	a0,a10

	movi	flsrframe_data,a11

flsrloop
	move	*a11+,a0,L
	jrz	flsrdone
	move	*a11+,a1,L
	movi	flsrframe_image,a2
	movi	100,a3				;z pos
	movi	DMACAL,a4			;DMA flags
	movi	CLSDEAD,a5			;object ID
	clr	a6				;x vel
	clr	a7				;y vel
	calla	BEGINOBJ

	move	a8,a0
	calla	fg2bg

	move	*a11+,a14,W
	move	a14,*a8(OSIZEX),W
	move	*a11+,a14,W
	move	a14,*a8(OSIZEY),W

	move	a9,*a8(OCONST),W
	move	a10,*a8(OPAL),W

	jruc	flsrloop
flsrdone
	rets


flsrframe_image	equ	man_d1

flsrframe_data
	.long	[53,0],[43,0]	;position X,Y
	.word	294,2		;size X,Y

	.long	[53,0],[231,0]	;position X,Y
	.word	294,2		;size X,Y

	.long	[50,0],[43,0]	;position X,Y
	.word	3,190		;size X,Y

	.long	[347,0],[43,0]	;position X,Y
	.word	3,190		;size X,Y

	.long	0

flsrframe_datax


*****************************************************************************
* Draws the wave indicator
*

 SUBRP	wave_count

	;wave text
	movi	[qjzkwave_x,0],a0			;x pos
	movi	[qjzkwave_y,0],a1			;y pos
	movi	wave_text,a2			;* image
	clr	a3				;z pos
	movi	DMAWNZ,a4			;DMA flags
	clr	a5				;object ID
	clr	a6				;x vel
	clr	a7				;y vel
	calla	BEGINOBJ

	;ones digit
;	move	@robo_wave,a2,W
;	addi	HEXTODEC+0010h,a2		;extra 10 cuz we count from 0
;	move	*a2,a2,W
;	andi	00FFh,a2
	move	@robo_wave,a0,W
	sra	4,a0
	inc	a0
	calla	BINBCD
	move	a0,a2
	andi	0Fh,a2
	X32	a2
	addi	qjzkwave_font,a2

	movi	[qjzkwave_onesx,0],a0		;x pos
	movi	[qjzkwave_y,0],a1			;y pos
	move	*a2,a2,L			;* image
	clr	a3				;z pos
	movi	DMAWNZ,a4			;DMA flags
	clr	a5				;object ID
	clr	a6				;x vel
	clr	a7				;y vel
	calla	BEGINOBJ

	;tens digit
	move	@robo_wave,a0,W
	sra	4,a0
	inc	a0
	calla	BINBCD
	move	a0,a2
	srl	4,a2
	andi	0Fh,a2
	jrz	qjzkdone
	X32	a2
	addi	qjzkwave_font,a2
	movi	[qjzkwave_tensx,0],a0		;x pos
	movi	[qjzkwave_y,0],a1			;y pos
	move	*a2,a2,L			;* image
	clr	a3				;z pos
	movi	DMAWNZ,a4			;DMA flags
	clr	a5				;object ID
	clr	a6				;x vel
	clr	a7				;y vel
	calla	BEGINOBJ

qjzkdone
	rets

qjzkwave_y		.equ	234
qjzkwave_x		.equ	198
qjzkwave_tensx	.equ	184
qjzkwave_onesx	.equ	188

qjzkwave_font
	.long	rsmall_0
	.long	rsmall_1
	.long	rsmall_2
	.long	rsmall_3
	.long	rsmall_4
	.long	rsmall_5
	.long	rsmall_6
	.long	rsmall_7
	.long	rsmall_8
	.long	rsmall_9
qjzkwave_fontx

*****************************************************************************
* Check to see if an object is completely on the playfield
* a8=OBJ
* 0a0H=walls hit (bit 0=top, 1=right, 2=bottom, 3=left)
* 0a2H=degree of overlap (in pixels)
* Trashes scratch

 SUBRP	bounds_check
	PUSH	a1,a3
	clr	a0
	clr	a2
	clr	a3

	;check for hit top
	move	*a8(OYPOS),a1
	move	*a8(ODYOFF),a14
	sub	a14,a1
	cmpi	mvmflow_y,a1
	jrgt	mvmfnot_high
	movi	mvmflow_y,a2
	sub	a1,a2
	ori	M_HIT_TOP,a0
	jruc	mvmfnot_low		;assume won't be both high and low

mvmfnot_high
	;check for hit bottom
	move	*a8(ODYOFF),a14		;subtract the offset AGAIN because
	sub	a14,a1			; we assume the anim point is there
	move	*a8(OSIZEY),a14		; to center the object.
	add	a14,a1
	cmpi	mvmfhigh_y,a1
	jrlt	mvmfnot_low
	move	a1,a2
	subi	mvmfhigh_y,a2
	ori	M_HIT_BOTTOM,a0

mvmfnot_low
	;check for hit left
	move	*a8(OXPOS),a1
	move	*a8(ODXOFF),a14
	sub	a14,a1
	cmpi	mvmflow_x,a1
	jrgt	mvmfnot_left
	movi	mvmflow_x,a3
	sub	a1,a3
	ori	M_HIT_LEFT,a0
	jruc	mvmfdone			;assume won't be both right and left

mvmfnot_left
	move	*a8(ODXOFF),a14
	sub	a14,a1
	move	*a8(OSIZEX),a14
	add	a14,a1
	cmpi	mvmfhigh_x,a1
	jrlt	mvmfdone
	move	a1,a3
	subi	mvmfhigh_x,a3
	ori	M_HIT_RIGHT,a0

mvmfdone
	;a2 is y overlap, a3 is x overlap.  a2 should be greatest of the two.
	cmp	a2,a3
	jrn	mvmfretval_set
	move	a3,a2
mvmfretval_set
	PULL	a1,a3
	rets


;boundaries
mvmflow_x	equ	53
mvmfhigh_x	equ	346
mvmflow_y	equ	45
mvmfhigh_y	equ	230

;ret values
B_HIT_TOP	equ	0
B_HIT_BOTTOM	equ	2
B_HIT_LEFT	equ	3
B_HIT_RIGHT	equ	1

M_HIT_TOP	equ	0001h
M_HIT_BOTTOM	equ	0004h
M_HIT_LEFT	equ	0008h
M_HIT_RIGHT	equ	0002h


*****************************************************************************
* If ever both start buttons are down, this proc sets the robo_done flag
* and dies.
*

 SUBRP	watch_both_starts

mqfwloop
	SLEEPK	4		;no need to check EVERY frame
	clr	a0
	calla	get_start_cur
	jrz	mqfwloop
	movk	1,a0
	calla	get_start_cur
	jrz	mqfwloop

	;both starts are down.
	movk	1,a0
	move	a0,@starts_down

	DIE

*****************************************************************************
* Watches for wave advance/back up key combo.
*

 SUBRP	wave_mover

wmfcloop
	SLEEPK	4
	calla	get_all_buttons_cur
	jrz	wmfcloop

	cmpi	1,a0
	jreq	wmfcback
	cmpi	2,a0
	jreq	wmfcfwd
	jruc	wmfcloop

wmfcback
	move	a0,@backward
	jruc	wmfcdone
wmfcfwd
	move	a0,@forward

wmfcdone
	DIE


	.if DEBUG

*****************************************************************************
* Makes bog meters
*

 SUBRP	bog_o_meter

	movi	robo_p,a0
	calla	pal_getf
	move	a0,a9

	;create the background
	movi	[360,0],a0
	movi	[192,0],a1
	movi	man_d1,a2
	clr	a3
	movi	DMACAL|M_FLIPV,a4
	movi	CLSDEAD,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	move	a9,*a8(OPAL)
	movi	0707h,a14
	move	a14,*a8(OCONST)

	movi	8,a0
	move	a0,*a8(OSIZEX)
	movi	128,a0
	move	a0,*a8(OSIZEY)
	move	a8,a10

	;create the foreground
	movi	[360,0],a0
	movi	[65,0],a1
	movi	man_d1,a2
	movi	1,a3
	movi	DMACAL,a4
	movi	CLSDEAD,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	move	a9,*a8(OPAL)
	movi	0808h,a14
	move	a14,*a8(OCONST)
	
	movi	8,a0
	move	a0,*a8(OSIZEX)

	movi	jjrlscale,a9

jjrlloop
	SLEEPK	2
	move	@CPULEFT,a1
	jrnz	jjrlnobog

	;BOG!!
	nop
jjrlnobog
	mpyu	a9,a1
	srl	16,a1
	move	a1,*a8(OSIZEY)
	cmpi	10h,a1
	jrle	jjrlred

	movi	0707h,a0
	move	a0,*a10(OCONST)
	jruc	jjrlloop
jjrlred
	movi	0101h,a0
	move	a0,*a10(OCONST)
	jruc	jjrlloop

	DIE

jjrlscale	equ	800000h/03DCh

	.endif

*****************************************************************************

robotron_ascii
 .long 0,0,0,0,0,0,0,0								;$00
 .long 0,0,0,0,0,0,0,0								;$08
 .long 0,0,0,0,0,0,0,0								;$10
 .long 0,0,0,0,0,0,0,0								;$18
 .long 0,rfont_exc,0,0,0,0,0,0								;$20	SP! " # $ % & '
 .long rfont_lparens,rfont_rparens,0,0,rfont_comma,0,rfont_period,rfont_fslash	;$28	( ) * + , - . /
 .long rfont_0,rfont_1,rfont_2,rfont_3,rfont_4,rfont_5,rfont_6,rfont_7		;$30	0 1 2 3 4 5 6 7
 .long rfont_8,rfont_9,0,0,0,0,0,0						;$38	8 9 : ; < - > ?
 .long 0,rfont_a,rfont_b,rfont_c,rfont_d,rfont_e,rfont_f,rfont_g		;$40	@ A B C D E F G
 .long rfont_h,rfont_i,rfont_j,rfont_k,rfont_l,rfont_m,rfont_n,rfont_o		;$48	H I J K L M N O
 .long rfont_p,rfont_q,rfont_r,rfont_s,rfont_t,rfont_u,rfont_v,rfont_w		;$50	P Q R S T U V W
 .long rfont_x,rfont_y,rfont_z,0,0,0,0,0					;$58	X Y Z [ \ ] ^ _
 .long 0,rfont_a,rfont_b,rfont_c,rfont_d,rfont_e,rfont_f,rfont_g		;$60	` a b c d e f g
 .long rfont_h,rfont_i,rfont_j,rfont_k,rfont_l,rfont_m,rfont_n,rfont_o		;$68	h i j k l m n o
 .long rfont_p,rfont_q,rfont_r,rfont_s,rfont_t,rfont_u,rfont_v,rfont_w		;$70	p q r s t u v w
 .long rfont_x,rfont_y,rfont_z,0,0,0,0,0					;$78	x y z { | } ~

*****************************************************************************
*
* Sound section
* 0a0H = snd call

RS_GRUNT	.equ	0
RS_DIE		.equ	1
RS_RESCUE	.equ	2
RS_SHOOT	.equ	3
RS_HIT		.equ	4

sp_grunt	.equ	1<<8
sp_shoot	.equ	2<<8
sp_hit		.equ	3<<8
sp_rescue	.equ	4<<8
sp_die		.equ	5<<8

robosnd_tbl
	;	duration, call#
	.word	sp_grunt|6,96		;0 = grunt footstep
	.word	sp_die|96,98		;1 = die
	.word	sp_rescue|39,99		;2 = rescue
	.word	sp_shoot|17,102		;3 = shoot
	.word	sp_hit|23,100		;4 = hit
robosnd_end

	.bss	rs_snd,16		;current sound call (0-3)
	.bss	rs_time,32		;timeout on current call (long)PCNT
	.bss	rs_pri,16		;priority on current call
	.bss	last_grunt_snd,32	;PCNT of last grunt footstep


 SUBRP	robo_sound_init

	clr	a14
	move	a14,@rs_snd
	move	a14,@rs_time,L
	move	a14,@last_grunt_snd,L
	rets


 SUBRP	robo_sound

	PUSH	a2,a3,a4

	;reggies:
	;a0 = call index
	;a1 = priority
	;a2 = duration
	;a3 = call #

	;decode table index
	move	a0,a14
	X32	a14
	addi	robosnd_tbl,a14
	move	*a14,a1,W
	srl	8,a1
	move	*a14+,a2,W
	andi	0FFh,a2
	move	*a14,a3,W

	;check old duration - if there's nothing going on, do the sound.
	move	@rs_time,a4
	move	@PCNT,a14,L
	cmp	a4,a14
	jrgt	mvqwdosnd

	;old sound still going.  check for an override
	TEST	a0
	jrnz	mvqwng
	move	@rs_snd,a14
	jrz	mvqwgrxgr		;special grunt-overriding-grunt case

mvqwng	move	@rs_pri,a14
	cmp	a14,a1
	jrge	mvqwdosnd
	jruc	mvqwdone

mvqwgrxgr	;do new call if old one has 3 or fewer ticks to live
	move	@rs_time,a14,L
	move	@PCNT,a4,L
	sub	a4,a14
	cmpi	3,a4
	jrle	mvqwdosnd
	jruc	mvqwdone

mvqwdosnd	calla	SNDSND
	move	a0,@rs_snd
	move	@PCNT,a14,L
	add	a2,a14
	move	a14,@rs_time,L
	move	a1,@rs_pri

mvqwdone	PULL	a2,a3,a4
	rets


******************************************************************************

	.end

