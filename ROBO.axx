
	.file	"robo.asm"
	.title	"robotron game"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"audit.equ"
	.include	"macros.h"
	.include	"link.equ"

	.include	"roboimg.tbl"
	.include	"fontsimg.glo"

	;kludge
	.ref	robo_p


	;from ADJUST.ASM
	.ref	BINBCD

	;from COLL2.ASM
	.ref	collisions,CCCCount

	;from DCSSOUND.ASM
	.ref	nosounds,SNDSND

	;from DISPLAY.ASM
	.ref	pal_getf,STOPOBJS,fg2bg

	;from MAIN.ASM
	.ref	dpageflip,IRQSKYE

	;from MPROC.ASM
	.ref	KIL1C

	;from STRING.ASM
	.ref	setup_message,print_string_R,mess_objid,copy_string
	.ref	dec_to_asc

	;from UTIL.ASM
	.ref	get_all_buttons_cur,get_stick_val_cur,RNDRNG0,get_start_cur
	.ref	WIPEOUT,CYCLE_TABLE,civani,civanic

	;from WRESTLE.ASM	
	.if DEBUG
	.ref	CPULEFT
	.endif
	.ref	HALT,PCNT

	;for WRESTLE.ASM
	.def	RE_DEADPLAYER,RE_WAVEDONE,RE_ABORT,RE_BACKUP

	.ref	_switch_addr,_switch2_addr,ADD_VOICE


	BSSX	robo_end,	16	;reason for wave end
	BSSX	robotron_score,	32	;UHL score
	.bss	player_dead,	16
	.bss	starts_down,	16	;are both starts down?
	.bss	player_object,	32	;pointer to char1 obj
	.bss	gun_proc,	32	;gun process address
	.bss	robo_wave,	16	;which wave to perform?
	.bss	live_badguys,	16	;wave-preserving badguy count
	.bss	forward,	16	;skip wave
	.bss	backward,	16	;drop back a wave


ROBO_DEBUG	equ	0
WAVE_CONTROL	equ	0
BOG_MONITOR	equ	0

ROBO_PAGE	equ	0*256

ROBJ_MOM	equ	1
ROBJ_DAD	equ	2
ROBJ_KID	equ	3

RE_DEADPLAYER	equ	1
RE_WAVEDONE	equ	2
RE_ABORT	equ	3
RE_BACKUP	equ	4


	.ref	robo_icon_trigger
	.ref	p1icon_total
	.ref	p2icon_total


 SUBR	robo_check

	.if DEBUG
	jruc	hkltrobo_ok
	.endif
	jruc	hkltrobo_end

	move	@p1icon_total,a0,L
	move	@robo_icon_trigger,a14
	cmp	a0,a14
	jrle	hkltrobo_ok
	move	@p2icon_total,a0,L
	cmp	a0,a14
	jrgt	hkltrobo_end
hkltrobo_ok
	sll	1,a14
	move	a14,@robo_icon_trigger
	callr	reset_roboscore
	callr	robo_sound_init


	clr	a0
hkltrobo_loop
	JSRP	robo_game
	move	@robo_end,a14
	cmpi	RE_DEADPLAYER,a14
	jreq	hkltplayer_died
	cmpi	RE_WAVEDONE,a14
	jreq	hkltwave_successful
	cmpi	RE_ABORT,a14
	jreq	hkltwave_aborted
	cmpi	RE_BACKUP,a14
	jreq	hkltback_up
	jruc	hkltrobo_end

hkltplayer_died
	jruc	hkltrobo_loop

hkltwave_successful
	inc	a0
	cmpi	16,a0
	jrne	hkltrobo_loop
	jruc	hkltrobo_end

hkltwave_aborted
	jruc	hkltrobo_end

hkltback_up
	dec	a0
	jrnn	hkltrobo_loop
	clr	a0
	jruc	hkltrobo_loop

hkltrobo_end
	RETP



 SUBRP	reset_roboscore

	clr	a14
	move	a14,@robotron_score,L
	rets



 SUBR	robo_game

	sla	4,a0
	move	a0,@robo_wave,W

	calla	display_blank
	calla	WIPEOUT			;CLEAN SYSTEM OUT

	clr	a0
	move	a0,@HALT
	move	a0,@dtype		;2d mode
	move	a0,@IRQSKYE
	move	a0,@CCCCount

	movk	1,a0			;page flipping on
	move	a0,@dpageflip

	SLEEPK	1

	movi	SCRNEND,a0		;[256,405]
	move	a0,@SCRNLR,L

	clr	a0
	move	a0,@WORLDTLX,L
	move	a0,@WORLDTLY,L

	SLEEPK	2

	calla	nosounds		;kill the select music

	;draw the frame
	move	@robo_wave,a0
	addi	voxpborder_colors,a0
	move	*a0,a0,W
	callr	draw_frame

	movk	1,a0
	move	a0,@DISPLAYON

	SLEEPK	2

	clr	a14
	move	a14,@player_dead,W	;clear the done flag

	;initialize the robo palette color cycles
	callr	robo_pal_cycles

	;initialize the badguy count
	clr	a14
	move	a14,@live_badguys,W

	;place the wave count
	callr	wave_count

	;create the score monitor
	CREATE	ROBOSCORE_PID,score

	;set up the player object
	CREATE	ROBOMAN_PID,our_hero		;start the little guy up
	CREATE	PLYRGUN_PID,player_gun	;give him a weapon
	move	a0,@gun_proc,L		;save the address

	;set up some grunts
	move	@robo_wave,a0
	addi	voxpwave_grunts,a0
	move	*a0,a0,W
	move	@robo_wave,a2
	addi	voxpgrunt_speeds,a2
	move	*a2,a2,W
	callr	make_grunts

	;make some hulks
	move	@robo_wave,a0
	addi	voxpwave_hulks,a0
	move	*a0,a0,W
	callr	make_hulks

	;sphereoids
	move	@robo_wave,a0
	addi	voxpwave_sphereoids,a0
	move	*a0,a0,W
	callr	make_sphereoids

	;quarks
	move	@robo_wave,a0
	addi	voxpwave_quarks,a0
	move	*a0,a0,W
	callr	make_quarks

	;make a few posts
	move	@robo_wave,a0
	addi	voxpwave_posts,a0
	move	*a0,a0,W

	move	@robo_wave,a1
	addi	voxppost_types,a1
	move	*a1,a1,W

	move	@robo_wave,a2
	addi	voxppost_colors,a2
	move	*a2,a2,W

	callr	make_posts

	;and some people
	move	@robo_wave,a0
	move	a0,a1
	move	a0,a2
	addi	voxpwave_moms,a0
	addi	voxpwave_dads,a1	
	addi	voxpwave_kids,a2
	move	*a0,a0,W
	move	*a1,a1,W
	move	*a2,a2,W
	callr	make_humans

	;start the collision checker
	CREATE	COLL_PID,collisions

	;create the both-start-btn watcher
	clr	a0
	move	a0,@starts_down,W
	move	a0,@forward,W
	move	a0,@backward,W
	CREATE	MISC_PID,watch_both_starts

	.if	WAVE_CONTROL
	CREATE	MISC_PID,wave_mover
	.endif

	.if DEBUG
	.if BOG_MONITOR
	CREATE	MISC_PID,bog_o_meter
	.endif
	.endif

	calla	display_unblank

voxpnot_done
	SLEEPK	1
	move	@live_badguys,a14
	jrz	voxpwave_done

	move	@player_dead,a14
	jrnz	voxpdead_hero

	move	@starts_down,a14
	jrnz	voxpwave_abort

	move	@forward,a14
	jrnz	voxpwave_done

	move	@backward,a14
	jrnz	voxpback_up
	jruc	voxpnot_done


voxpwave_done
	movi	RE_WAVEDONE,a14
	jruc	voxpquit
voxpdead_hero
	movi	RE_DEADPLAYER,a14
	jruc	voxpquit
voxpwave_abort
	movi	RE_ABORT,a14
	jruc	voxpquit

voxpback_up
	movi	RE_BACKUP,a14
	jruc	voxpquit

voxpquit
	move	a14,@robo_end,W

	;put the wave back in a0 just like we found it
	move	@robo_wave,a0,W
	sra	4,a0
	RETP


voxpborder_colors
	.if	ROBO_DEBUG
	.word	1
	.endif
	.word	01,06,01,14,07,03,02,08
	.word	00,10,01,06,01,14,07,03

voxpwave_grunts
	.if	ROBO_DEBUG
	.word	4
	.endif

	.word	15,17,22,34,20,32,00,35
	.word	60,25,35,00,35,27,25,35

voxpgrunt_speeds
	.if	ROBO_DEBUG
	.word	1000
	.endif
	.word	20,15,15,15,15,15,15,15
	.word	15,15,14,14,14,14,14,13

voxpwave_posts
	.if	ROBO_DEBUG
	.word	15
	.endif
	.word	05,15,25,25,20,25,00,25
	.word	00,20,25,00,25,05,20,25

voxppost_types
	.if	ROBO_DEBUG
	.word	0
	.endif
	.word	0,1,3,8,4,2,0,7
	.word	0,5,0,1,3,8,4,2

voxppost_colors
	.if	ROBO_DEBUG
	.word	0Fh
	.endif
	.word	15,14,11,13,14,15,14,11
	.word	14,10,15,14,11,13,14,15

voxpwave_hulks
	.if	ROBO_DEBUG
	.word	0
	.endif
	.word	00,05,06,07,00,07,12,08
	.word	04,00,08,13,08,20,02,03

voxpwave_sphereoids
	.if	ROBO_DEBUG
	.word	0
	.endif
	.word	00,01,03,04,01,04,00,05
	.word	05,01,05,00,05,02,01,05

voxpwave_quarks
	.if	ROBO_DEBUG
	.word	0
	.endif
	.word	00,00,00,00,00,00,10,00
	.word	00,00,00,12,00,00,00,00

voxpwave_moms
	.if	ROBO_DEBUG
	.word	2
	.endif
	.word	01,01,02,02,15,03,04,03
	.word	03,00,03,03,03,05,00,03

voxpwave_dads
	.if	ROBO_DEBUG
	.word	2
	.endif
	.word	01,01,02,02,00,03,04,03
	.word	03,22,03,03,03,05,00,03

voxpwave_kids
	.if	ROBO_DEBUG
	.word	2
	.endif
	.word	00,01,02,02,01,03,04,03
	.word	03,00,03,03,03,05,22,03



	;reg use
	; a4  - current position in animation script
	; a5  - head of current animation script
	; a6  - tail of current animation script
	; a8  - player object
	; a9  - motion count accumulator
	; a10 - direction of motion

 SUBRP	our_hero

	;create the player object
	movi	[xljkstart_x,0],a0
	movi	[xljkstart_y,0],a1
	movi	man_d1,a2
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSPLYR|TYPPLYR,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the global pointer
	move	a8,@player_object,L

	; initiailize direction of motion
	clr	a10

xljknot_done
	PUSHP	a4,a5,a6
	SLEEPK	1
	PULLP	a4,a5,a6

	; move him first
	callr	xljkmove_player

	; change his animation frame, if neccesary
	callr	xljkchange_frame

	jruc	xljknot_done

xljkplayer_dead
	;kill the gun process
	move	@gun_proc,a0,L
	calla	KILL

	movi	BULLET_PID,a0
	calla	KIL1C

	movi	GRUNT_PID,a0
	calla	KIL1C

	movi	HULK_PID,a0
	calla	KIL1C

	movi	HUMAN_PID,a0
	calla	KIL1C

	movi	SPHEREOID_PID,a0
	calla	KIL1C

	movi	ENFORCER_PID,a0
	calla	KIL1C

	movi	SPARK_PID,a0
	calla	KIL1C

	movi	QUARK_PID,a0
	calla	KIL1C

	movi	TANK_PID,a0
	calla	KIL1C

	movi	SHELL_PID,a0
	calla	KIL1C

	calla	STOPOBJS

	;switch frames
	movi	robo_dead,a0,L
	calla	civanic

	;death sound
	movi	RS_DIE,a0
	calla	robo_sound

	SLEEP	120
	movk	1,a14
	move	a14,@player_dead,W

	DIE


 SUBRP	xljkmove_player

	clr	a0
	calla	get_stick_val_cur
	btst	JOYDN,a0
	jrnz	xljkmove_down
	btst	JOYUP,a0
	jrnz	xljkmove_up
	btst	JOYRGT,a0
	jrnz	xljkmove_right
	btst	JOYLFT,a0
	jrnz	xljkmove_left
	jruc	xljkno_move

xljkmove_down
	btst	JOYRGT,a0
	jrnz	xljkmove_downright
	btst	JOYLFT,a0
	jrnz	xljkmove_downleft

	;increment Y
	move	*a8(OYPOS),a14,W
	inc	a14
	move	a14,*a8(OYPOS),W
	jruc	xljkmove_done

xljkmove_up
	btst	JOYRGT,a0
	jrnz	xljkmove_upright
	btst	JOYLFT,a0
	jrnz	xljkmove_upleft

	;decrement Y
	move	*a8(OYPOS),a14,W
	dec	a14
	move	a14,*a8(OYPOS),W
	jruc	xljkmove_done

xljkmove_right
	;increment X
	move	*a8(OXPOS),a14,W
	inc	a14
	move	a14,*a8(OXPOS),W
	jruc	xljkmove_done

xljkmove_left
	;decrement X
	move	*a8(OXPOS),a14,W
	dec	a14
	move	a14,*a8(OXPOS),W
	jruc	xljkmove_done

xljkmove_upright
	; decrement Y and increment X
	move	*a8(OYPOS),a14,W
	dec	a14
	move	a14,*a8(OYPOS),W
	move	*a8(OXPOS),a14,W
	inc	a14
	move	a14,*a8(OXPOS),W
	jruc	xljkmove_done

xljkmove_upleft
	; decrement Y and decrement X
	move	*a8(OYPOS),a14,W
	dec	a14
	move	a14,*a8(OYPOS),W
	move	*a8(OXPOS),a14,W
	dec	a14
	move	a14,*a8(OXPOS),W
	jruc	xljkmove_done

xljkmove_downright
	; increment Y and increment X
	move	*a8(OYPOS),a14,W
	inc	a14
	move	a14,*a8(OYPOS),W
	move	*a8(OXPOS),a14,W
	inc	a14
	move	a14,*a8(OXPOS),W
	jruc	xljkmove_done

xljkmove_downleft
	; increment Y and decrement X
	move	*a8(OYPOS),a14,W
	inc	a14
	move	a14,*a8(OYPOS),W
	move	*a8(OXPOS),a14,W
	dec	a14
	move	a14,*a8(OXPOS),W
	jruc	xljkmove_done

xljkmove_done
	; check bounds
	move	*a8(OXPOS),a14,W
	cmpi	xljklow_x,a14,W
	jrgt	xljknot_lowx
	; too low on x
	inc	a14
	move	a14,*a8(OXPOS),W

xljknot_lowx
	cmpi	xljkhigh_x,a14,W
	jrle	xljknot_highx
	; too high on x
	dec	a14
	move	a14,*a8(OXPOS),W

xljknot_highx

	move	*a8(OYPOS),a14,W
	cmpi	xljklow_y,a14,W
	jrgt	xljknot_lowy
	; too low on y
	inc	a14
	move	a14,*a8(OYPOS),W

xljknot_lowy
	cmpi	xljkhigh_y,a14,W
	jrle	xljknot_highy
	; too high on y
	dec	a14
	move	a14,*a8(OYPOS),W

xljknot_highy
xljkno_move

xljkdone_moving
	rets



 SUBRP	xljkchange_frame
	;reg use
	; a0  scratch
	; a1  sctatch
	; a3  new direction of motion
	; a10 old direction of motion - update

	clr	a0
	clr	a2
	calla	get_stick_val_cur
	move	a0,a3

	cmp	a0,a10
	jreq	xljksame_direction
	jruc	xljknew_direction

xljknew_direction
	; start a new script.  first use the joystick bits to compute an
	; offset into the script table.
	X32	a0		;multiply by 32
	addi	xljkscript_map,a0	;add the address of the script table
	move	*a0,a0,L	;get the address of the script

	; get the tail and head of the image list for this script
	move	*a0+,a6,L
	move	a0,a5

	;a5 now points to the first image in the script.  change to it.
	move	*a5,a0,L
	calla	civanic

	;set the new direction thingie and script pointer
	move	a3,a10
	move	a5,a4

	;set the motion count
	movi	xljkmotion_count,a9
	jruc	xljkdone_changing

xljksame_direction
	;check the motion count
	dec	a9
	jrnz	xljkdone_changing

	;reset the motion count
	movi	xljkmotion_count,a9

	;increment the pointer, wrap if neccesary
	addi	20h,a4
	cmp	a4,a6
	jrne	xljkno_wrap
	move	a5,a4

xljkno_wrap
	;a4 is the new image.  change to it.
	move	*a4,a0,L
	calla	civanic

	jruc	xljkdone_changing

xljkdone_changing
	rets



 SUBR	player_die
	PUSH	a1,a7

	;we hit some kind of bad guy and are dead
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADPLAYER_PID,a1
	movi	xljkplayer_dead,a7
	calla	XFERPROC

	PULL	a1,a7
	rets



xljkstart_x	equ	197
xljkstart_y	equ	123

xljklow_x		equ	53
xljkhigh_x		equ	347-7
xljklow_y		equ	45
xljkhigh_y		equ	231-12

xljkmotion_count	equ	2	;there will be trouble if this is zero

xljkani_holdstill
	.long	xljkani_holdstillx
	.long	man_d1
xljkani_holdstillx

xljkani_walkup
	.long	xljkani_walkupx
	.long	man_u1
	.long	man_u2
	.long	man_u1
	.long	man_u3
xljkani_walkupx

xljkani_walkdown
	.long	xljkani_walkdownx
	.long	man_d1
	.long	man_d2
	.long	man_d1
	.long	man_d3
xljkani_walkdownx

xljkani_walkright
	.long	xljkani_walkrightx
	.long	man_r1
	.long	man_r2
	.long	man_r1
	.long	man_r3
xljkani_walkrightx

xljkani_walkleft
	.long	xljkani_walkleftx
	.long	man_l1
	.long	man_l2
	.long	man_l1
	.long	man_l3
xljkani_walkleftx

xljkani_bad
	.long	xljkani_badx
	.long	robo_bad
xljkani_badx

xljkscript_map
	.long	xljkani_holdstill
	.long	xljkani_walkup
	.long	xljkani_walkdown
	.long	xljkani_bad
	.long	xljkani_walkleft
	.long	xljkani_walkleft
	.long	xljkani_walkleft
	.long	xljkani_bad
	.long	xljkani_walkright
	.long	xljkani_walkright
	.long	xljkani_walkright
	.long	xljkani_bad
	.long	xljkani_bad
	.long	xljkani_bad
	.long	xljkani_bad
	.long	xljkani_bad
xljkscript_mapx



	;reg use
	;a9  time till next shot is allowed

 SUBRP	player_gun

	clr	a9
	jruc	vguzsleep

vguzcheck_shot
	movk	1,a0
	calla	get_stick_val_cur
	jrz	vguzsleep			;no shot

	;fire in the indicated direction
	move	a0,a10
	CREATE	BULLET_PID,bullet
	movi	vguzrate_of_fire,a9

	;shoot sound
	movi	RS_SHOOT,a0
	callr	robo_sound

	jruc	vguzsleep
	
vguztoo_soon
	dec	a9
	jruc	vguzsleep

vguzsleep
	SLEEPK	1
	move	a9,a9
	jrnz	vguztoo_soon
	jruc	vguzcheck_shot

vguzdone
	DIE

vguzrate_of_fire	equ	6	;min ticks between shots



	;reg use
	; a4 velocity accumulator (used to pick the correct bullet)
	; a5 vert indicator
	; a8 bullet object
	;a10 (in) stick bits

 SUBRP	bullet

	;create a bullet object
	movi	[500,0],a0
	clr	a1
	movi	bullet_hrz,a2
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSPLYR|TYPBULLET,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the speed and start position
	move	@player_object,a9,L
	clr	a4
	clr	a5
	move	*a9(OXPOS),a0,W
	addk	vncebstart_xoff,a0
	move	a0,*a8(OXPOS),W
	move	*a9(OYPOS),a0,W
	addk	vncebstart_yoff,a0
	move	a0,*a8(OYPOS),W

	btst	JOYRGT,a10
	jrz	vncenot_right
	move	*a8(OXPOS),a14,W	;pos
	addi	vncebstart_radj,a14
	move	a14,*a8(OXPOS),W
	movi	vncebullet_speed,a0	;speed
	move	a0,*a8(OXVEL),L
	inc	a4

vncenot_right

	btst	JOYLFT,a10
	jrz	vncenot_left
	move	*a8(OXPOS),a14,W	;pos
	addi	vncebstart_ladj,a14
	move	a14,*a8(OXPOS),W
	movi	-vncebullet_speed,a0	;speed
	move	a0,*a8(OXVEL),L
	dec	a4

vncenot_left

	btst	JOYUP,a10
	jrz	vncenot_up
	move	*a8(OYPOS),a14,W	;pos
	addi	vncebstart_uadj,a14
	move	a14,*a8(OYPOS),W
	movi	-vncebullet_speed,a0	;speed
	move	a0,*a8(OYVEL),L
	dec	a4
	inc	a5

vncenot_up

	btst	JOYDN,a10
	jrz	vncenot_down
	move	*a8(OYPOS),a14,W	;pos
	addi	vncebstart_dadj,a14
	move	a14,*a8(OYPOS),W
	movi	vncebullet_speed,a0	;speed
	move	a0,*a8(OYVEL),L
	inc	a4
	inc	a5

vncenot_down

	;set the image
	movi	bullet_hrz,a0
	move	a5,a5
	jrz	vncehoriz
	movi	bullet_vrt,a0
vncehoriz

	;vert/horz is set.  check for diagonal instead
	cmpi	1,a4
	jreq	vnceimage_set
	cmpi	-1,a4
	jreq	vnceimage_set

	;it's a diagonal.  if a4 is 0, use frontslash
	move	a4,a4
	jrz	vncefront_slash
	movi	bullet_bck,a0
	jruc	vnceimage_set

vncefront_slash
	movi	bullet_fnt,a0

vnceimage_set
	calla	civanic

vnceloop
	callr	bounds_check
	move	a0,a0
	jrnz	vncehit_wall
	SLEEPK	1
	jruc	vnceloop

vncehit_wall
	;zoinks!  We've hit a wall. back up to be flush against it.
	; a2 holds the number of pixels we have to back up.

	;well, we can overlap on the wall a LITTLE.  say, 2 pixels.
	subk	2,a2

	move	*a8(OXVEL),a0,L
	jrz	vncey_adjust
	jrn	vnceneg_xvel
	move	*a8(OXPOS),a0,W
	sub	a2,a0
	move	a0,*a8(OXPOS),W
	jruc	vncey_adjust
vnceneg_xvel
	move	*a8(OXPOS),a0,W
	add	a2,a0
	move	a0,*a8(OXPOS),W

vncey_adjust
	move	*a8(OYVEL),a0,L
	jrz	vncekill_bullet
	jrn	vnceneg_yvel
	move	*a8(OYPOS),a0,W
	sub	a2,a0
	move	a0,*a8(OYPOS),W
	jruc	vncekill_bullet
vnceneg_yvel
	move	*a8(OYPOS),a0,W
	add	a2,a0
	move	a0,*a8(OYPOS),W

vncekill_bullet
	SLEEPK	1
	calla	DELOBJA8
	DIE



 SUBR	bullet_die
	PUSH	a1,a7,a9,a10,a11

	;we hit some kind of bad guy and are stopped.
	movi	CLSDEAD,a14
	move	a14,*A8(OID)
	move	*a8(OPLINK),a0,L
	movi	DEADBULLET_PID,a1
	movi	vncekill_bullet,a7
	calla	XFERPROC

	PULL	a1,a7,a9,a10,a11
	rets


vncebullet_speed	equ	00080000h	;pixels per frame

vncelow_x		equ	53
vncehigh_x		equ	346
vncelow_y		equ	45
vncehigh_y		equ	230

vncebstart_xoff	equ	3
vncebstart_yoff	equ	5

vncebstart_radj	equ	7
vncebstart_ladj	equ	-13
vncebstart_uadj	equ	-14
vncebstart_dadj	equ	8



	;reg use
	; a0 (in) # of grunts
	; a1 scratch
	; a2 (in) initial speed

 SUBRP	make_grunts

	move	a0,a0
	jrz	vnradone

vnraanother_grunt
	PUSHP	a0

vnraget_pos
	movi	vnramax_x-vnramin_x,a0
	calla	RNDRNG0
	move	a0,a9
	movi	vnramax_y-vnramin_y,a0
	calla	RNDRNG0
	move	a0,a10

	addi	vnramin_x,a9
	addi	vnramin_y,a10

	cmpi	vnramid_x1,a9
	jrle	vnraplace_grunt
	cmpi	vnramid_x2,a9
	jrge	vnraplace_grunt
	cmpi	vnramid_y1,a10
	jrle	vnraplace_grunt
	cmpi	vnramid_y2,a10
	jrge	vnraplace_grunt
	jruc	vnraget_pos

vnraplace_grunt
	;assign a random starting position
	move	a2,a11
	CREATE	GRUNT_PID,grunt
	PULLP	a0
	dsj	a0,vnraanother_grunt

vnradone
	rets


vnramin_x	equ	53	;grunt start outer boundaries
vnramax_x	equ	347-9
vnramin_y	equ	45
vnramax_y	equ	231-13

vnramid_x1	equ	200-60	;grunt start inner boundaries
vnramid_x2	equ	200+60
vnramid_y1	equ	138-60
vnramid_y2	equ	138+60



	STRUCTPD
	WORD	sxovACCEL_TIMER	;UHW accelerator count
	LONG	sxovDEATH_SEQUENCE	;UHL anim to use when dying
				;    (set by coll routines)

	;reg use
	; a8 object handle
	; a9 (in)  starting x
	; a9 (use) ticks between moves
	;a10 (in)  starting y
	;a10 (use) ticks until next move
	;a11 (in)  initial speed
	;a11 (use) frame count

 SUBRP	grunt

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W

	;create a grunt object
	move	a9,a0
	sla	16,a0
	move	a10,a1
	sla	16,a1
	movi	grunt_1,a2
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPGRUNT,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;initialize move timer
	move	a11,a9

	;initialize time till next move
	move	a9,a0
	calla	RNDRNG0
	add	a11,a0
	move	a0,a10

	;initialize accel counter
	movi	sxovacceleration,a14
	move	a14,*a13(sxovACCEL_TIMER),W

	;initialize frame counter
	clr	a11

sxovloop
	move	*a13(sxovACCEL_TIMER),a14,W
	dec	a14
	jrnz	sxovno_accel

	movi	sxovacceleration,a14
	cmpi	sxovtop_speed,a9
	jreq	sxovno_accel
	dec	a9

sxovno_accel
	move	a14,*a13(sxovACCEL_TIMER),W
	SLEEPK	1

	dsj	a10,sxovloop
	callr	sxovmove_grunt
	move	a9,a10
	jruc	sxovloop

sxovdie
	;we've croaked
	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

	;set up the animation
	move	*a13(sxovDEATH_SEQUENCE),a9,L
	move	*a8(OCTRL),a0

sxovdieloop
	move	*a9+,a0,L
	jrz	sxovdead_n_buried
	calla	civanic
	sleepk	sxovdeath_speed
	jruc	sxovdieloop

sxovdead_n_buried
	calla	DELOBJA8

	DIE


 SUBRP	sxovmove_grunt
	move	@player_object,a14,L

	;move in X
	move	*a14(OXPOS),a0,W
	move	*a8(OXPOS),a1,W
	sub	a1,a0
	jrz	sxovdone_movex
	jrn	sxovmove_left
	jruc	sxovmove_right

sxovmove_left
	move	*a8(OXPOS),a1,W
	addi	-sxovxmotion_inc,a1
	move	a1,*a8(OXPOS),W
	jruc	sxovdone_movex

sxovmove_right
	move	*a8(OXPOS),a1,W
	addi	sxovxmotion_inc,a1
	move	a1,*a8(OXPOS),W
	jruc	sxovdone_movex

sxovdone_movex

	move	*a14(OYPOS),a0,W
	move	*a8(OYPOS),a1,W
	sub	a1,a0
	jrz	sxovdone_movey
	jrn	sxovmove_up
	jruc	sxovmove_down

sxovmove_up
	move	*a8(OYPOS),a1,W
	addi	-sxovymotion_inc,a1
	move	a1,*a8(OYPOS),W
	jruc	sxovdone_movey

sxovmove_down
	move	*a8(OYPOS),a1,W
	addi	sxovymotion_inc,a1
	move	a1,*a8(OYPOS),W
	jruc	sxovdone_movey

sxovdone_movey

	;step through the animation
	inc	a11
	cmpi	4,a11
	jrne	sxovno_wrap
	clr	a11

sxovno_wrap
	move	a11,a14
	X32	a14
	addi	sxovwalk_script,a14
	move	*a14,a0,L
	calla	civanic

	;sound
	movi	RS_GRUNT,a0
	callr	robo_sound
	rets



 SUBR	grunt_shot
	;hit by player gunfire
	PUSH	a1,a7

	;jeepers.  we're dead.
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W

	move	*a0(OXVEL),a14,L
	jrz	sxovvert_bullet
	move	*a0(OYVEL),a1,L
	jrz	sxovhorz_bullet

	add	a1,a14
	jrz	sxovdiag_13
	jruc	sxovdiag_24

sxovhorz_bullet
	movi	sxovdie_vert,a14
	jruc	sxovdeath_set
sxovvert_bullet
	movi	sxovdie_horz,a14
	jruc	sxovdeath_set
sxovdiag_13
	movi	sxovdie_diag24,a14
	jruc	sxovdeath_set
sxovdiag_24
	movi	sxovdie_diag13,a14
	jruc	sxovdeath_set

sxovdeath_set

	move	*a8(OPLINK),a0,L
	move	a14,*a0(sxovDEATH_SEQUENCE),L
	movi	DEADGRUNT_PID,a1
	movi	sxovdie,a7

	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	calla	XFERPROC

	movi	sxovgrunt_points,a0
	callr	score_points

	;die sound
	movi	RS_HIT,a0
	calla	robo_sound

	PULL	a1,a7
	rets


 SUBR	grunt_die
	;hit a mine or something

	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	sxovdie_vert,a14
	move	a14,*a0(sxovDEATH_SEQUENCE),L
	movi	DEADGRUNT_PID,a1
	movi	sxovdie,a7
	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	calla	XFERPROC

	PULL	a1,a7
	rets




sxovwalk_script
	.long	grunt_1
	.long	grunt_2
	.long	grunt_1
	.long	grunt_3
sxovwalk_scriptx

sxovdie_vert
	.long	grunt_xv1
	.long	grunt_xv2
	.long	grunt_xv3
	.long	grunt_xv4
	.long	0
sxovdie_vertx

sxovdie_horz
	.long	grunt_xh1
	.long	grunt_xh2
	.long	grunt_xh3
	.long	grunt_xh4
	.long	0
sxovdie_horzx

sxovdie_diag13
	.long	grunt_xdf1
	.long	grunt_xdf2
	.long	grunt_xdf3
	.long	grunt_xdf4
	.long	0
sxovdie_diag13x

sxovdie_diag24
	.long	grunt_xdb1
	.long	grunt_xdb2
	.long	grunt_xdb3
	.long	grunt_xdb4
	.long	0
sxovdie_diag24x

sxovdeath_speed	equ	3		;death seq anim speed

sxovacceleration	equ	100		;decrement motion clock every X cycles
sxovtop_speed	equ	3		;as fast as they get
sxovxmotion_inc	equ	0004h
sxovymotion_inc	equ	0004h

sxovgrunt_points	equ	100		;points for killing a grunt

	;reg use
	; a0 - (in) # of posts to create
	; a1 - (in) type (0-8)
	; a2 - (in) color (0-F)

 SUBRP	make_posts

	move	a0,a9		;move the inputs to safer spots
	jrz	ayhsdone

	move	a1,a10		;type

	move	a2,a11		;color
	sla	8,a11
	or	a2,a11

ayhsanother_post
ayhsget_pos
	movi	ayhsmax_y-ayhsmin_y,a0
	calla	RNDRNG0
	move	a0,a2			;hide a0
	movi	ayhsmax_x-ayhsmin_x,a0
	calla	RNDRNG0
	move	a2,a1			;put it back

	addi	ayhsmin_x,a0
	addi	ayhsmin_y,a1

	cmpi	ayhsmid_x1,a0
	jrle	ayhsplace_post
	cmpi	ayhsmid_x2,a0
	jrge	ayhsplace_post
	cmpi	ayhsmid_y1,a1
	jrle	ayhsplace_post
	cmpi	ayhsmid_y2,a1
	jrge	ayhsplace_post
	jruc	ayhsget_pos

ayhsplace_post
	sla	16,a0			;X pos
	sla	16,a1			;Y pos

	;create the post object
	move	a10,a14
	X32	a14
	addi	ayhspost_types,a14
	move	*a14,a14,L
	move	*a14,a2,L		;DON'T advance

	clr	a3
	movi	DMACNZ,a4
	movi	CLSNEUT|TYPPOST,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	move	a10,*a8(OMISC),W	;stuff the type in the object

	move	a11,*a8(OCONST),W

	movi	robo_p,a0
	calla	pal_getf
	move	a0,*a8(OPAL),W

	dsj	a9,ayhsanother_post

ayhsdone
	rets


ayhsmin_x	equ	53	;post start outer boundaries
ayhsmax_x	equ	347-17
ayhsmin_y	equ	45
ayhsmax_y	equ	231-10

ayhsmid_x1	equ	200-50	;post start inner boundaries
ayhsmid_x2	equ	200+50
ayhsmid_y1	equ	138-50
ayhsmid_y2	equ	138+50



 SUBR	post_die

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	CREATE	DEADPOST_PID,kill_post

	rets


	;reg use
	; a8 - * image
	; a9 - anim pointer

 SUBR	kill_post

	move	*a8(OMISC),a0,W
	X32	a0
	addi	ayhspost_types,a0
	move	*a0,a9,L
	move	*a8(OCTRL),a14,W
	xori	DMACNZ,a14
	ori	DMAWNZ,a14
	move	a14,*a8(OCTRL),W
ayhskploop
	move	*a9+,a0,L
	jrz	ayhskpdone
	calla	civanic
	SLEEPK	ayhsdie_speed
	jruc	ayhskploop

ayhskpdone
	calla	DELOBJA8
	DIE


ayhsdie_speed	equ	3

ayhspost_types
	.long	ayhsstar		;0
	.long	ayhscrystal	;1
	.long	ayhsdiamond	;2
	.long	ayhssquare		;3
	.long	ayhsrectangle	;4
	.long	ayhsR2084		;5
	.long	ayhspcube		;6
	.long	ayhsspiral		;7
	.long	ayhstriangle	;8
	.long	0
ayhspost_typesx

ayhsstar
	.long	star_1,star_2,star_3,0

ayhscrystal
	.long	crystal_1,crystal_2,crystal_3,0

ayhsdiamond
	.long	diamond_1,diamond_2,diamond_3,0

ayhssquare
	.long	square_1,square_2,square_3,0

ayhsrectangle
	.long	rectangle_1,rectangle_2,rectangle_3,0

ayhsR2084
	.long	R2084_1,R2084_2,R2084_3,0

ayhspcube
	.long	pcube_1,pcube_2,pcube_3,0

ayhsspiral
	.long	spiral_1,spiral_2,spiral_3,0

ayhstriangle
	.long	triangle_1,triangle_2,triangle_3,0



	.bss	humans_saved,	16

	;reg use
	; a0 # of moms
	; a1 # of dads
	; a2 # of kids 

 SUBRP	make_humans

	;initialize the savecount
	clr	a14
	move	a14,@humans_saved,W

	;first make the moms
	move	a0,a0
	jrz	stspskip_moms
stspmom_loop
	PUSHP	a0,a1,a2
	callr	stsprand_position
	movi	ROBJ_MOM,a9
	CREATE	HUMAN_PID,human
	PULLP	a0,a1,a2
	dsj	a0,stspmom_loop

stspskip_moms

	;dads
	move	a1,a1
	jrz	stspskip_dads
stspdad_loop
	PUSHP	a1,a2
	callr	stsprand_position
	movi	ROBJ_DAD,a9
	CREATE	HUMAN_PID,human
	PULLP	a1,a2
	dsj	a1,stspdad_loop

stspskip_dads

	;kids
	move	a2,a2
	jrz	stspskip_kids
stspkid_loop
	PUSHP	a2
	callr	stsprand_position
	movi	ROBJ_KID,a9
	CREATE	HUMAN_PID,human
	PULLP	a2
	dsj	a2,stspkid_loop

stspskip_kids
	rets


	;reg use
	; no input
	;a10 - (ret) an X val
	;a11 - (ret) a Y val

 SUBRP stsprand_position
	movi	stspmax_x-stspmin_x,a0
	calla	RNDRNG0
	move	a0,a10
	addi	stspmin_x,a10

	movi	stspmax_y-stspmin_y,a0
	calla	RNDRNG0
	move	a0,a11
	addi	stspmin_y,a11

	rets


	STRUCTPD
	LONG	stspHITPOST	;UHL post we've hit

	;reg use
	; a8 - hObject
	; a9 - (in)  OID
	; a9 - (use) image table address (stspmom_table, stspdad_table...)
	;a10 - (in)  x
	;a10 - (use) direction of motion (1-8)
	;a11 - (in)  y
	;a11 - (use) frame index (0-3)


 SUBRP	human

	cmpi	ROBJ_MOM,a9
	jreq	stspinit_mom
	cmpi	ROBJ_DAD,a9
	jreq	stspinit_dad
	cmpi	ROBJ_KID,a9
	jreq	stspinit_kid

	LOCKUP	;bad objid

stspinit_mom
	movi	stspmom_table,a9
	jruc	stspmake_obj

stspinit_dad
	movi	stspdad_table,a9
	jruc	stspmake_obj

stspinit_kid
	movi	stspkid_table,a9
	jruc	stspmake_obj

stspmake_obj
	;cweate a widdle people object
	move	a10,a0
	sla	16,a0
	move	a11,a1
	sla	16,a1
	move	*a9(stspwalk_down),a2,L
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSNEUT|TYPHUMAN,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;choose a direction of motion
	movi	7,a0
	calla	RNDRNG0
	inc	a0
	move	a0,a10

	;set the frame index
	movi	3,a11
	callr	stspadvance_frame

stspmove_loop
	SLEEPK	stspsleep_time
	callr	stsprand_dirchange
	callr	stspmove_onestep
	move	a0,a0
	jrnz	stsphit_wall
	callr	stspadvance_frame
	jruc	stspmove_loop

stsphit_wall
	;sleep first because: If you move and bump into both a wall and a
	; post at the same time (could happen), and then you immediately
	; reverse direction, you'll then get flipped AGAIN when you hit
	; the post and get forwarded right through the wall.  yuck.  so
	; instead when you hit a wall, wait a frame and let the post
	; collisions sort themselves out, THEN change direction.
	SLEEPK	1
	callr	stspreverse_direction
	callr	stspadvance_frame
	jruc	stspmove_loop

stsphit_post
	callr	post_adjust
	callr	stspadvance_frame
	jruc	stspmove_loop

stspsaved
	;sound
	movi	RS_RESCUE,a0
	callr	robo_sound

	;increment the save count
	move	@humans_saved,a14,W
	inc	a14
	move	a14,@humans_saved,W

	cmpi	5,a14
	jrle	stspno_cap
	movi	5,a14
stspno_cap
	dec	a14

	PUSH	a14
	sla	4,a14
	addi	stspscore_table,a14
	move	*a14,a0,W
	callr	score_points
	PULL	a14

	X32	a14
	addi	stspsaved_table,a14
	move	*a14,a0,L
	calla	civanic

	SLEEP	120
	calla	DELOBJA8
	jruc	stspdone

stspkilled
	movi	skull,a0
	calla	civanic

	SLEEP	120
	calla	DELOBJA8

stspdone
	DIE



	;reg use
	; a0 - X overlap
	; a1 - Y overlap

 SUBRP	post_adjust
	PUSH	a0,a1,a2,a3,a7

	move	*a13(stspHITPOST),a7,L

	callr	stspreverse_direction

	;calculate degree of X and Y overlap
	move	a10,a2
	X32	a2
	addi	stspwalk_increments,a2
	move	*a2+,a3,W	;y vel
	jrn	stspyvel_neg
	jrz	stspyvel_zero

stspyvel_pos
	move	*a7(OYPOS),a1
	move	*a7(OSIZEY),a14
	add	a14,a1
	move	*a8(OYPOS),a14
	sub	a14,a1
	jruc	stspdo_xvel
stspyvel_zero
	movi	100h,a1
	jruc	stspdo_xvel
stspyvel_neg
	move	*a8(OYPOS),a1
	move	*a8(OSIZEY),a14
	add	a14,a1
	move	*a7(OYPOS),a14
	sub	a14,a1

stspdo_xvel
	move	*a2,a3,W	;x vel
	jrn	stspxvel_neg
	jrz	stspxvel_zero

stspxvel_pos
	move	*a7(OXPOS),a0
	move	*a7(OSIZEX),a14
	add	a14,a0
	move	*a8(OXPOS),a14
	sub	a14,a0
	jruc	stspovlap_done
stspxvel_zero
	movi	100h,a0
	jruc	stspovlap_done
stspxvel_neg
	move	*a8(OXPOS),a0
	move	*a8(OSIZEX),a14
	add	a14,a0
	move	*a7(OXPOS),a14
	sub	a14,a0

stspovlap_done
	;a0 and a1 are the X and Y overlaps, respectively.  We need only
	; worry about the smallest of the two and move that many steps.  Note
	; that if velocity in a given direction is zero, the overlap is
	; set to some outrageously large value so that the other direction
	; is all that counts.
	cmp	a1,a0
	jrle	stsplow_set
	move	a1,a0
stsplow_set
	;a0 is now the smallest.
	move	a0,a1
	move	a10,a2
	X32	a2
	addi	stspwalk_increments,a2
	move	*a2+,a3,W	;y vel
	mpys	a3,a1
	move	*a2,a3,W	;x vel
	mpys	a0,a3
	move	a3,a0

	;a0 and a1 now hold X and Y adjustments, respectively.
	move	*a8(OXPOS),a14
	add	a0,a14
	move	a14,*a8(OXPOS)
	move	*a8(OYPOS),a14
	add	a1,a14
	move	a14,*a8(OYPOS)
	
	PULL	a0,a1,a2,a3,a7
	rets



	;reg use
	; a0 - scratch
	; a0 (out) - walls hit (0-2)
	; a1 - scratch
	; a8 - (in)  hObject
	; a9 - (in)  image table address (stspmom_table, stspdad_table...)
	;a10 - (in)  direction of motion (1-8)
	;a11 - (in)  frame index (0-3)

 SUBRP	stspmove_onestep

	;move the object
	move	a10,a1
	X32	a1
	addi	stspwalk_increments,a1
	move	*a1+,a14,W
	move	*a8(OYPOS),a0,W
	add	a14,a0
	move	a0,*a8(OYPOS),W
	move	*a1+,a14,W
	move	*a8(OXPOS),a0,W
	add	a14,a0
	move	a0,*a8(OXPOS),W

	clr	a1
	move	*a8(OXPOS),a0,W

	cmpi	stspmin_x,a0
	jrge	stspnot_lowx

	;low x
	inc	a1
	movi	stspmin_x,a0
	move	a0,*a8(OXPOS),W

stspnot_lowx
	cmpi	stspmax_x,a0
	jrle	stspnot_highx

	;high x
	inc	a1
	movi	stspmax_x,a0
	move	a0,*a8(OXPOS),W

stspnot_highx

	move	*a8(OYPOS),a0,W

	cmpi	stspmin_y,a0
	jrge	stspnot_lowy

	;low y
	inc	a1
	movi	stspmin_y,a0
	move	a0,*a8(OYPOS),W

stspnot_lowy
	cmpi	stspmax_y,a0
	jrle	stspnot_highy

	;high y
	inc	a1
	movi	stspmax_y,a0
	move	a0,*a8(OYPOS),W

stspnot_highy
	move	a1,a0
	rets



 SUBRP	stsprand_dirchange

	;1 chance in stspdirchange_chance of spontaneously switching direction
	movi	stspdirchange_chance,a0
	calla	RNDRNG0
	move	a0,a0
	jrnz	stspdirchange_done
	movi	7,a0
	calla	RNDRNG0
	inc	a0
	move	a0,a10

stspdirchange_done
	rets



 SUBRP	stspreverse_direction

	;reverse direction of motion
	subi	4,a10
	jrp	stsprd_done
	addi	8,a10
stsprd_done
	rets


 SUBRP	stspadvance_frame

	;advance the frame and set the new image
	inc	a11
	cmpi	4,a11
	jrne	stspno_wrap
	;wraparound.  reset to start
	clr	a11

stspno_wrap
	;calculate the next frame
	move	a9,a0
	move	a10,a1
	sla	4,a1		;mult by 10h
	addi	stspmotion_table,a1

	;a1 now points to one of the entries in stspmotion table
	move	*a1,a1,W
	add	a1,a0

	;a0 now points to an entry in stspxxx_table
	move	a11,a1
	X32	a1		;mult by 20h
	add	a1,a0
	move	*a0,a0,L

	calla	civanic

	rets



 SUBR	human_saved
	PUSH	a1,a7

	;change the obj id
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	SAVEDHUMAN_PID,a1
	movi	stspsaved,a7
	calla	XFERPROC

	PULL	a1,a7
	rets

 SUBR	human_killed
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADHUMAN_PID,a1
	movi	stspkilled,a7
	calla	XFERPROC

	PULL	a1,a7
	rets


 SUBR	human_hitpost
	PUSH	a1,a7

	move	a0,a1
	move	*a8(OPLINK),a0,L
	move	a1,*a0(stspHITPOST),L
	move	*a0(PA8),a8,L
	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	move	*a0(PA11),a11,L
	movi	HUMAN_PID,a1
	movi	stsphit_post,a7
	calla	XFERPROC

	PULL	a1,a7
	rets



stspmin_x	equ	53	;human start/motion outer boundaries
stspmax_x	equ	347-11
stspmin_y	equ	45
stspmax_y	equ	231-13

stspsleep_time	equ	7
stspx_step		equ	1
stspy_step		equ	1

stspdirchange_chance	equ	40

stspwalk_increments
	.word	0,0			;because direction is 1-8, not 0-7
	.word	-stspy_step,0
	.word	-stspy_step,stspx_step
	.word	0,stspx_step
	.word	stspy_step,stspx_step
	.word	stspy_step,0
	.word	stspy_step,-stspx_step
	.word	0,-stspx_step
	.word	-stspy_step,-stspx_step
stspwalk_incrementsx

stspmotion_table
	.word	0			;because direction is 1-8, not 0-7
	.word	stspwalk_up
	.word	stspwalk_right
	.word	stspwalk_right
	.word	stspwalk_right
	.word	stspwalk_down
	.word	stspwalk_left
	.word	stspwalk_left
	.word	stspwalk_left
stspmotion_tablex

stspwalk_left	equ	0000h
stspwalk_right	equ	stspwalk_left + (4 * 20h)
stspwalk_up	equ	stspwalk_right + (4 * 20h)
stspwalk_down	equ	stspwalk_up + (4 * 20h)

stspscore_table
	.word	1000,2000,3000,4000,5000
stspscore_tablex

stspmom_table
	.long	mom_l1
	.long	mom_l2
	.long	mom_l1
	.long	mom_l3

	.long	mom_r1
	.long	mom_r2
	.long	mom_r1
	.long	mom_r3

	.long	mom_u1
	.long	mom_u2
	.long	mom_u1
	.long	mom_u3

	.long	mom_d1
	.long	mom_d2
	.long	mom_d1
	.long	mom_d3
stspmom_tablex

stspdad_table
	.long	dad_l1
	.long	dad_l2
	.long	dad_l1
	.long	dad_l3

	.long	dad_r1
	.long	dad_r2
	.long	dad_r1
	.long	dad_r3

	.long	dad_u1
	.long	dad_u2
	.long	dad_u1
	.long	dad_u3

	.long	dad_d1
	.long	dad_d2
	.long	dad_d1
	.long	dad_d3
stspdad_tablex

stspkid_table
	.long	kid_l1
	.long	kid_l2
	.long	kid_l1
	.long	kid_l3

	.long	kid_r1
	.long	kid_r2
	.long	kid_r1
	.long	kid_r3

	.long	kid_u1
	.long	kid_u2
	.long	kid_u1
	.long	kid_u3

	.long	kid_d1
	.long	kid_d2
	.long	kid_d1
	.long	kid_d3
stspkid_tablex

stspsaved_table
	.long	one_k
	.long	two_k
	.long	three_k
	.long	four_k
	.long	five_k
stspsaved_tablex



	;reg use
	; a0 - (in) # of hulks

 SUBRP	make_hulks

	move	a0,a0
	jrz	uhladone

uhlaanother_hulk
	PUSHP	a0

uhlaget_pos
	movi	uhlamax_x-uhlamin_x,a0
	calla	RNDRNG0
	move	a0,a9
	movi	uhlamax_y-uhlamin_y,a0
	calla	RNDRNG0
	move	a0,a10

	addi	uhlamin_x,a9
	addi	uhlamin_y,a10

	cmpi	uhlamid_x1,a9
	jrle	uhlaplace_hulk
	cmpi	uhlamid_x2,a9
	jrge	uhlaplace_hulk
	cmpi	uhlamid_y1,a10
	jrle	uhlaplace_hulk
	cmpi	uhlamid_y2,a10
	jrge	uhlaplace_hulk
	jruc	uhlaget_pos

uhlaplace_hulk
	;assign a random starting position
	CREATE	HULK_PID,hulk
	PULLP	a0
	dsj	a0,uhlaanother_hulk

uhladone
	rets


uhlamin_x	equ	53	;hulk start outer boundaries
uhlamax_x	equ	347-13
uhlamin_y	equ	45
uhlamax_y	equ	231-16

uhlamid_x1	equ	200-50	;hulk start inner boundaries
uhlamid_x2	equ	200+50
uhlamid_y1	equ	138-50
uhlamid_y2	equ	138+50



	STRUCTPD
	LONG	pzxnPD_SCRIPT	;UHL script head
	WORD	pzxnPD_FRAME_NDX	;UHW frame index

	;reg use
	; a8 - object handle
	; a9 - (in)  starting x
	; a9 - (use) frame change count
	;a10 - (in)  starting y
	;a11 - direction of motion (0-3)

 SUBRP	hulk

	;choose a direction of motion
	movi	3,a0
	calla	RNDRNG0
	move	a0,a11

	X32	a0
	addi	pzxnanim_list,a0
	move	*a0,a6,L
	move	a6,*a13(pzxnPD_SCRIPT),L
	clr	a7
	move	a7,*a13(pzxnPD_FRAME_NDX),W

	;create the hulk object
	move	a9,a0		;x pos
	sla	16,a0
	move	a10,a1		;y pos
	sla	16,a1

	move	a7,a14		;img
	X32	a14
	add	a6,a14
	move	*a14,a2,L

	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPHULK,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the move counter
	movi	pzxnmove_freq/2,a0
	calla	RNDRNG0
	addi	pzxnmove_freq,a0
	move	a0,a9

pzxnloop
	SLEEPK	1
	dsj	a9,pzxnloop
	callr	pzxnmove_hulk
	movi	pzxnmove_freq,a9
	jruc	pzxnloop


pzxnbeen_shot	;we immediately wake up here if we've been hit and moved
		; by a player bullet.
	callr	pzxndo_bounds_check
	jruc	pzxnloop

	DIE



	;reg use
	; a0 scratch
	; a1 scratch
	; a8 object handle
	;a11 direction of motion (0-3)

 SUBRP	pzxnmove_hulk
	;move him
	move	a11,a14
	X32	a14
	addi	pzxnmove_table,a14
	move	*a14+,a0,W
	move	*a14,a1,W
	move	*a8(OXPOS),a14,W
	add	a0,a14
	move	a14,*a8(OXPOS),W
	move	*a8(OYPOS),a14,W
	add	a1,a14
	move	a14,*a8(OYPOS),W

	;
	;note the fall-through here
	;

	;bounds check
 SUBRP	pzxndo_bounds_check
	clr	a1

	clr	a0
	move	*a8(OXPOS),a0,W
	cmpi	pzxnlow_x,a0
	jrlt	pzxntoo_left
	cmpi	pzxnhigh_x,a0
	jrgt	pzxntoo_right
	jruc	pzxnvert_check

pzxntoo_left
	movi	pzxnlow_x,a14
	move	a14,*A8(OXPOS),W
	ori	M_HIT_LEFT,a1
	jruc	pzxnvert_check

pzxntoo_right
	movi	pzxnhigh_x,a14
	move	a14,*A8(OXPOS),W
	ori	M_HIT_RIGHT,a1

pzxnvert_check
	clr	a0
	move	*a8(OYPOS),a0,W
	cmpi	pzxnlow_y,a0
	jrlt	pzxntoo_high
	cmpi	pzxnhigh_y,a0
	jrgt	pzxntoo_low
	jruc	pzxndone_boundcheck

pzxntoo_high
	movi	pzxnlow_y,a14
	move	a14,*A8(OYPOS),W
	ori	M_HIT_TOP,a1
	jruc	pzxndone_boundcheck

pzxntoo_low
	movi	pzxnhigh_y,a14
	move	a14,*A8(OYPOS),W
	ori	M_HIT_BOTTOM,a1
	jruc	pzxndone_boundcheck

pzxndone_boundcheck
	move	a1,a1
	jrnz	pzxnchange_direction

pzxnno_wallhit
	movi	pzxnturn_prob,a0
	PUSH	a1
	calla	RNDRNG0
	PULL	a1
	move	a0,a0
	jrnz	pzxndone_moving

	;spontaneous direction change.  random for now

pzxnchange_direction
	;turn 90 degrees by randomly incrementing or decrementing direction
	movi	1,a0
	PUSH	a1
	calla	RNDRNG0
	PULL	a1
	move	a0,a0
	jrz	pzxninc_direction
	dec	a11
	jruc	pzxndir_rangecheck
	
pzxninc_direction
	inc	a11

pzxndir_rangecheck
	cmpi	-1,a11
	jrne	pzxndir_notlow
	movi	3,a11

pzxndir_notlow
	cmpi	4,a11
	jrne	pzxndir_changed
	clr	a11

pzxndir_changed

	;verify that this is a good direction
	cmpi	0,a11
	jreq	pzxnvalidate_up
	cmpi	1,a11
	jreq	pzxnvalidate_right
	cmpi	2,a11
	jreq	pzxnvalidate_down
	cmpi	3,a11
	jreq	pzxnvalidate_left

pzxnvalidate_up
	andi	M_HIT_TOP,a1
	jrnz	pzxnchange_direction
	jruc	pzxnvalid_dir
pzxnvalidate_right
	andi	M_HIT_RIGHT,a1
	jrnz	pzxnchange_direction
	jruc	pzxnvalid_dir
pzxnvalidate_down
	andi	M_HIT_BOTTOM,a1
	jrnz	pzxnchange_direction
	jruc	pzxnvalid_dir
pzxnvalidate_left
	andi	M_HIT_LEFT,a1
	jrnz	pzxnchange_direction
	jruc	pzxnvalid_dir

pzxnvalid_dir

	move	a11,a0
	X32	a0
	addi	pzxnanim_list,a0
	move	*a0,a0,L
	move	a0,*a13(pzxnPD_SCRIPT),L

pzxndone_moving
	
pzxnnew_frame
	move	*a13(pzxnPD_FRAME_NDX),a14,W
	move	*a13(pzxnPD_SCRIPT),a6,L
	inc	a14
	cmpi	4,a14
	jrne	pzxnno_wrap
	clr	a14
pzxnno_wrap
	move	a14,*a13(pzxnPD_FRAME_NDX),W
	X32	a14
	add	a6,a14
	move	*a14,a0,L
	calla	civanic

	rets



 SUBR	hulk_pushback
	PUSH	a1,a7,a9,a10,a11,a13

	;immediately move the hulk in the direction of the bullet's motion
	move	*a0(OXVEL),a14,L
	sra	17,a14
	move	*a8(OXPOS),a1,W
	add	a14,a1
	move	a1,*a8(OXPOS),W

	move	*a0(OYVEL),a14,L
	sra	17,a14
	move	*a8(OYPOS),a1,W
	add	a14,a1
	move	a1,*a8(OYPOS),W

	;wake up at the collision check
	move	*a8(OPLINK),a0,L
	move	*a0(PA9),a9,L
	move	*a0(PA10),a10,L
	move	*a0(PA11),a11,L
	movi	HULK_PID,a1
	movi	pzxnbeen_shot,a7
	calla	XFERPROC
	PULL	a1,a7,a9,a10,a11,a13

	rets



pzxnstep_x		equ	4
pzxnstep_y		equ	4

pzxnlow_x		equ	53	;hulk move boundaries
pzxnhigh_x		equ	347-13
pzxnlow_y		equ	45
pzxnhigh_y		equ	231-16

pzxnmove_freq	equ	8	;frames between moves

pzxnturn_prob	equ	30	;chance per frame of spontaneously changing
				;direction
pzxnmove_table
	.word	0,-pzxnstep_y
	.word	pzxnstep_x,0
	.word	0,pzxnstep_y
	.word	-pzxnstep_x,0
pzxnmove_tablex

pzxnanim_list
	.long	pzxnanim_vert
	.long	pzxnanim_right
	.long	pzxnanim_vert
	.long	pzxnanim_left
pzxnanim_listx

pzxnanim_vert
	.long	hulk_ud1
	.long	hulk_ud2
	.long	hulk_ud1
	.long	hulk_ud3
pzxnanim_vertx

pzxnanim_left
	.long	hulk_l1
	.long	hulk_l2
	.long	hulk_l1
	.long	hulk_l3
pzxnanim_leftx

pzxnanim_right
	.long	hulk_r1
	.long	hulk_r2
	.long	hulk_r1
	.long	hulk_r3
pzxnanim_rightx



	;reg use
	; a0 - (in) # of sphereoids

 SUBRP	make_sphereoids

	move	a0,a0
	jrz	fbctdone

fbctanother_sphereoid
	PUSHP	a0

fbctget_pos
	movi	fbctmax_x-fbctmin_x,a0
	calla	RNDRNG0
	move	a0,a9
	movi	fbctmax_y-fbctmin_y,a0
	calla	RNDRNG0
	move	a0,a10

	addi	fbctmin_x,a9
	addi	fbctmin_y,a10

	cmpi	fbctmid_x1,a9
	jrle	fbctplace_sphereoid
	cmpi	fbctmid_x2,a9
	jrge	fbctplace_sphereoid
	cmpi	fbctmid_y1,a10
	jrle	fbctplace_sphereoid
	cmpi	fbctmid_y2,a10
	jrge	fbctplace_sphereoid
	jruc	fbctget_pos

fbctplace_sphereoid
	;assign a random starting position
	CREATE	SPHEREOID_PID,sphereoid
	PULLP	a0
	dsj	a0,fbctanother_sphereoid

fbctdone
	rets


fbctmid_x1	equ	200-80	;sphereoid start inner boundaries
fbctmid_x2	equ	200+80
fbctmid_y1	equ	138-80
fbctmid_y2	equ	138+80



	STRUCTPD
	LONG	fbctOXACC		;UHL x acceleration
	LONG	fbctOYACC		;UHL y acceleration
	WORD	fbctOCHILDREN	;UHW # of children left to spawn
	WORD	fbctOSTAGE		;UHW stage in life cycle

	;reg use
	; a7 (use) event timer (related to fbctOSTAGE)
	; a8 (use) * object
	; a9 (in)  x pos
	; a9 (use) anim counter
	;a10 (in)  y pos
	;a10 (use) frame pointer
	;a11 (use) time until next vel change

 SUBRP	sphereoid

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W

	;create the sphereoid object
	move	a9,a0		;x pos
	sll	16,a0
	move	a10,a1		;y pos
	sll	16,a1
	movi	fbctstandard_script,a10
	move	*a10+,a2,L	;img
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPSPHEREOID,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;initialize anim counter
	movi	fbctanim_speed,a9

	;set initial velocities and accelerations
	callr	fbctset_va

	;initialize life cycle and event timer
	movi	fbctSTAGE_YOUTH,a14
	move	a14,*a13(fbctOSTAGE),W
	movi	fbctyouth_time/2,a0
	calla	RNDRNG0
	addi	fbctyouth_time*3/4,a0
	move	a0,a7

fbctloop
	PUSHP	a7
	SLEEPK	1
	PULLP	a7
	dec	a9
	jrnz	fbctno_anim

	;advance the frame
	move	*a10+,a0,L
	jrnz	fbctnewframe

	;reset script.  depends on the stage
	move	*a13(fbctOSTAGE),a14,W
	cmpi	fbctSTAGE_PARENT,a14
	jreq	fbctset_parent_script
	movi	fbctstandard_script,a10
	jruc	fbctscript_set
fbctset_parent_script
	movi	fbctparent_script,a10

fbctscript_set
	move	*a10+,a0,L

fbctnewframe
	calla	civanic

	;reset the counter
	movi	fbctanim_speed,a9

fbctno_anim
	;adjust velocities
	callr	fbctacc_add

	;bounds check
	callr	fbctin_bounds

	;adjust vel and acc if it's time
	dec	a11
	jrnz	fbctno_va_change
	callr	fbctset_va

fbctno_va_change

	;do event?
	dec	a7
	jrnz	fbctno_event

	;event.  depends on the stage in the life cycle
	move	*a13(fbctOSTAGE),a14,W
	cmpi	fbctSTAGE_YOUTH,a14
	jreq	fbctyouth_event
	cmpi	fbctSTAGE_PARENT,a14
	jreq	fbctparent_event
	;default
	jruc	fbctdotage_event

fbctyouth_event
	;switch to middle age
	movi	fbctSTAGE_PARENT,a14
	move	a14,*a13(fbctOSTAGE),W

	;set the number of kids to be spawned
	movi	fbctmax_kids-fbctmin_kids,a0
	calla	RNDRNG0
	addi	fbctmin_kids,a0
	move	a0,*a13(fbctOCHILDREN),W

	;set the spawn timer
	movi	fbctspawn_time/2,a0
	calla	RNDRNG0
	addi	fbctspawn_time*3/4,a0
	move	a0,a7
	jruc	fbctno_event

fbctparent_event
	;spawn a brat
	PUSHP	a10,a11
	move	*a8(OXVAL),a10,L
	move	*a8(OYVAL),a11,L
	CREATE	ENFORCER_PID,enforcer
	PULLP	a10,a11

	;decrement the kid count
	move	*a13(fbctOCHILDREN),a14,W
	dec	a14
	jrz	fbctbarren
	move	a14,*a13(fbctOCHILDREN),W
	movi	fbctspawn_time/2,a0
	calla	RNDRNG0
	addi	fbctspawn_time*3/4,a0
	move	a0,a7
	jruc	fbctno_event

fbctbarren
	;that was the last kid.  age
	movi	fbctSTAGE_DOTAGE,a14
	move	a14,*a13(fbctOSTAGE),W
	movi	fbctold_age_time,a7
	jruc	fbctno_event

fbctdotage_event
	;nothing fancy.  just die.
	jruc	fbctdie_peacefully
	
fbctno_event
	jruc	fbctloop

fbctbeen_shot
	;crud.  we've been shot.  score the points
	movi	fbctsphereoid_points,a0
	callr	score_points

	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

	;clear the velocity
	clr	a14
	move	a14,*a8(OXVEL),L
	move	a14,*a8(OYVEL),L

	;go through the death routine
	movi	fbctparent_script,a10,L
	move	*a8(OCTRL),a14,W
	xori	DMAWNZ,a14
	ori	DMACNZ,a14
	move	a14,*a8(OCTRL),W
	movi	fbctdiecolor,a14
	move	a14,*a8(OCONST),W
	movi	robo_p,a0
	calla	pal_getf
	move	a0,*a8(OPAL),W

fbctdie_loop
	SLEEPK	fbctanim_speed
	move	*a10+,a0,L
	jrz	fbctscore_msg
	calla	civanic
	jruc	fbctdie_loop

fbctscore_msg
	movi	fbctscore_image,a0
	move	*a8(OCTRL),a1,W
	xori	DMACNZ,a1
	ori	DMAWNZ,a1
	calla	civani
	movi	fbctscore_xoff,a14
	move	a14,*a8(ODXOFF),W
	movi	fbctscore_yoff,a14	
	move	a14,*a8(ODYOFF),W
	SLEEP	fbctscore_time
	jruc	fbctexpire

fbctdie_peacefully
	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

fbctexpire
	calla	DELOBJA8

	DIE


 SUBRP	fbctset_va

	movi	fbcthigh_v*2,a0
	calla	RNDRNG0
	subi	fbcthigh_v,a0
	move	a0,*a8(OXVEL),L

	movi	fbcthigh_v*2,a0
	calla	RNDRNG0
	subi	fbcthigh_v,a0
	move	a0,*a8(OYVEL),L

	movi	fbcthigh_a*2,a0
	calla	RNDRNG0
	subi	fbcthigh_a,a0
	move	a0,*a13(fbctOXACC),L

	movi	fbcthigh_a*2,a0
	calla	RNDRNG0
	subi	fbcthigh_a,a0
	move	a0,*a13(fbctOYACC),L

	movi	fbctva_change_time,a0
	calla	RNDRNG0
	inc	a0
	move	a0,a11

	rets


 SUBRP	fbctacc_add
	move	*a13(fbctOXACC),a14,L
	move	*a8(OXVEL),a0,L
	add	a14,a0
	move	a0,*a8(OXVEL),L

	move	*a13(fbctOYACC),a14,L
	move	*a8(OYVEL),a0,L
	add	a14,a0
	move	a0,*a8(OYVEL),L

	rets


 SUBRP	fbctin_bounds

	move	*a8(OXPOS),a14,W
	movi	fbctmin_x,a0
	cmp	a0,a14
	jrlt	fbctx_adjust

	movi	fbctmax_x,a0
	cmp	a0,a14
	jrgt	fbctx_adjust
	jruc	fbcty_check

fbctx_adjust
	move	a0,*a8(OXPOS),W
	clr	a0
	move	a0,*a8(OXVEL),L
	move	a0,*a13(fbctOXACC),L

fbcty_check
	move	*a8(OYPOS),a14,W
	movi	fbctmin_y,a0
	cmp	a0,a14
	jrlt	fbcty_adjust

	movi	fbctmax_y,a0
	cmp	a0,a14
	jrgt	fbcty_adjust
	jruc	fbctdone_check

fbcty_adjust
	move	a0,*a8(OYPOS),W
	clr	a0
	move	a0,*a8(OYVEL),L
	move	a0,*a13(fbctOYACC),L

fbctdone_check
	rets



 SUBR	sphereoid_die
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADSPHERE_PID,a1
	movi	fbctbeen_shot,a7
	calla	XFERPROC

	PULL	a1,a7
	rets



fbctanim_speed	equ	3

fbctva_change_time	equ	180

fbctsphereoid_points	equ	1000

fbctyouth_time	equ	180	;time before we have kids
fbctspawn_time	equ	120	;time between kids
fbctold_age_time	equ	180	;time between last kid and natural death

fbctmin_kids	equ	3	;lower limit on spawned enforcers
fbctmax_kids	equ	7	;upper limit

fbcthigh_v		equ	00004000h
fbcthigh_a		equ	00000C00h

fbctmin_x	equ	53	;sphereoid move bounds
fbctmax_x	equ	347-15
fbctmin_y	equ	45
fbctmax_y	equ	231-15

fbctscore_image	equ	one_k	;image on being shot
fbctdiecolor	equ	0909h	;color on being shot
fbctscore_time	equ	60	;time to display score value
fbctscore_xoff	equ	-3	;shotimage offset
fbctscore_yoff	equ	-5

fbctSTAGE_YOUTH 	equ	0000h
fbctSTAGE_PARENT	equ	0001h
fbctSTAGE_DOTAGE	equ	0002h

fbctstandard_script
	.long	circle_1
	.long	circle_2
	.long	circle_3
	.long	circle_4
	.long	circle_5
	.long	0

fbctparent_script
	.long	circle_1
	.long	circle_2
	.long	circle_3
	.long	circle_4
	.long	circle_5
	.long	circle_6
	.long	circle_7
	.long	circle_8
	.long	0



	.bss	num_enforcers,		16	;active enforcers
	.bss	picket_duty,		32	;*img of picket enforcer

	;reg use
	;  a8 (use) *img
	;  a9 (use) next move timer (move/fire phase)
	;  a9 (use) frame pointer (death phase)
	; a10 (in)  initial X
	; a10 (use) frame pointer (grow phase)
	; a11 (in)  initial Y
	; a11 (use) next shot timer (move/fire phase)

 SUBRP	enforcer

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W
	move	@num_enforcers,a14,W
	inc	a14
	move	a14,@num_enforcers,W

	;create the enforcer object
	move	a10,a0
	move	a11,a1
	movi	lcergrow_script,a10
	move	*a10+,a2,L
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPENFORCER,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;grow phase

lcergrow_loop
	SLEEPK	lcergrow_speed
	move	*a10+,a0,L
	jrz	lcermf_phase
	calla	civanic
	jruc	lcergrow_loop

lcermf_phase
	;begin move/fire phase.  set initial course
	callr	lcernew_course
	movi	lcerturn_time,a9
	movi	lcerfire_time/2,a0
	calla	RNDRNG0
	addi	lcerfire_time*3/4,a0
	move	a0,a11

lcermain_loop
	SLEEPK	1
	dec	a9
	jrnz	lcerno_turn


	;picket phase
	;two cases: we're on picket duty, or we're not.
	move	@picket_duty,a14,L
	cmp	a14,a8
	jreq	lceron_picket

	;we're not on picket duty
	;if we're the only enforcer, quit now.
	move	@num_enforcers,a14,W
	cmpi	1,a14
	jreq	lcerpicket_done

	;there are multiple enforcers.   If there is no picket, see if we
	; take the job.  If there is, quit.
	move	@picket_duty,a14,L
	jrnz	lcerpicket_done

	;there is no picket.
	movi	lcerpicket_prob,a0
	calla	RNDRNG0
	move	a0,a0
	jrnz	lcerpicket_done

	;we're going to take picket duty
	move	a8,@picket_duty,L
	jruc	lcerpicket_done

lceron_picket
	;we are on picket duty
	;if we're the only enforcer, see if we leave picket mode.
	move	@num_enforcers,a14,W
	cmpi	1,a14
	jrne	lcermultiple_enforcers

	;we're the only enforcer
	movi	lcerstand_down_prob,a0
	calla	RNDRNG0
	move	a0,a0
	jrnz	lcerpicket_done

	;and we've decided to stand down.
	move	a0,@picket_duty,L
	jruc	lcerpicket_done

lcermultiple_enforcers
	;we may abandon our post
	movi	lcerabandon_post_prob,a0
	calla	RNDRNG0
	move	a0,a0
	jrnz	lcerpicket_done

	;yep.  we quit.
	move	a0,@picket_duty,L

lcerpicket_done

	;change course
	callr	lcernew_course
	movi	lcerturn_time,a9

lcerno_turn

	dec	a11
	jrnz	lcerno_shot

	;fire a spark
	PUSHP	a10
	move	*a8(OXVAL),a10,L
	move	*a8(OYVAL),a11,L
	CREATE	SPARK_PID,spark
	PULLP	a10
	movi	lcerfire_time/2,a0
	calla	RNDRNG0
	addi	lcerfire_time*3/4,a0
	move	a0,a11

lcerno_shot

	;bounds check
	callr	lcerin_bounds

	jruc	lcermain_loop

lcerdie
	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W
	move	@num_enforcers,a14,W
	dec	a14
	move	a14,@num_enforcers,W

	;zero our velocity
	clr	a14
	move	a14,*a8(OXVEL),L
	move	a14,*a8(OYVEL),L

	;set up the animation
lcerdieloop
	move	*a9+,a0,L
	jrz	lcerdead_n_buried
	calla	civanic
	sleepk	lcerdeath_speed
	jruc	lcerdieloop

lcerdead_n_buried
	calla	DELOBJA8

	DIE


 SUBRP	lcernew_course

	;two cases.  picket or !picket
	move	@picket_duty,a14,L
	cmp	a8,a14
	jrne	lcernc_notpicket

lcernc_picket
	;we're on picket duty.  if we're not against a wall, move toward the
	; nearest one.  if we are against a wall but not in a corner, move
	; toward the nearest corner.  if we're in a corner, sit tight.

lcernc_notpicket
	;velocity is the distance between enforcer and player / 64 / 2
	move	@player_object,a0,L
	move	*a0(OXVAL),a14,L
	move	*a8(OXVAL),a1,L
	sub	a1,a14
	sra	7,a14
	
	move	a14,*a8(OXVEL),L

	move	*a0(OYVAL),a14,L
	move	*a8(OYVAL),a1,L
	sub	a1,a14
	sra	7,a14
	move	a14,*a8(OYVEL),L

lcernc_done
	rets

 SUBRP	lcerin_bounds

	move	*a8(OXPOS),a14,W
	movi	lcermin_x,a0
	cmp	a0,a14
	jrlt	lcerx_adjust

	movi	lcermax_x,a0
	cmp	a0,a14
	jrgt	lcerx_adjust
	jruc	lcery_check

lcerx_adjust
	move	a0,*a8(OXPOS),W
	clr	a0
	move	a0,*a8(OXVEL),L

lcery_check
	move	*a8(OYPOS),a14,W
	movi	lcermin_y,a0
	cmp	a0,a14
	jrlt	lcery_adjust

	movi	lcermax_y,a0
	cmp	a0,a14
	jrgt	lcery_adjust
	jruc	lcerdone_check

lcery_adjust
	move	a0,*a8(OYPOS),W
	clr	a0
	move	a0,*a8(OYVEL),L

lcerdone_check
	rets



 SUBR	enforcer_die
	;hit by player gunfire
	PUSH	a1,a7

	;jeepers.  we're dead.
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W

	move	*a0(OXVEL),a14,L
	jrz	lcervert_bullet
	move	*a0(OYVEL),a1,L
	jrz	lcerhorz_bullet

	add	a1,a14
	jrz	lcerdiag_13
	jruc	lcerdiag_24

lcerhorz_bullet
	movi	lcerdie_vert,a9
	jruc	lcerdeath_set
lcervert_bullet
	movi	lcerdie_horz,a9
	jruc	lcerdeath_set
lcerdiag_13
	movi	lcerdie_diag24,a9
	jruc	lcerdeath_set
lcerdiag_24
	movi	lcerdie_diag13,a9
	jruc	lcerdeath_set

lcerdeath_set
	move	*a8(OPLINK),a0,L
	movi	DEADNFORCER_PID,a1
	movi	lcerdie,a7
	move	*a0(PA10),a10,L
	calla	XFERPROC

	movi	lcerenforcer_points,a0
	callr	score_points

	PULL	a1,a7
	rets



lcerenforcer_points	equ	100

lcerpicket_prob	equ	4	;one chance in X of becoming the picket if
				; there are multiple enforcers and none is
				; on the job already.

lcerstand_down_prob equ	4	;one chance in X of leaving picket duty if
				; we're the only enforcer left.

lcerabandon_post_prob equ	8	;one chance in X of leaving picket duty if
				; there are other enforcers around.

lcerturn_time	equ	150
lcerfire_time	equ	90

lcergrow_speed	equ	6

lcermin_x	equ	53	;enforcer move bounds
lcermax_x	equ	347-9
lcermin_y	equ	45
lcermax_y	equ	231-11

lcergrow_script
	.long	enf_g1
	.long	enf_g2
	.long	enf_g3
	.long	enf_g4
	.long	enf_g5
	.long	enf_1
	.long	0
lcergrow_scriptx

lcerdie_vert
	.long	enf_xv1
	.long	enf_xv2
	.long	enf_xv3
	.long	enf_xv4
	.long	enf_xv5
	.long	0
lcerdie_vertx

lcerdie_horz
	.long	enf_xh1
	.long	enf_xh2
	.long	enf_xh3
	.long	enf_xh4
	.long	0
lcerdie_horzx

lcerdie_diag13
	.long	enf_xf1
	.long	enf_xf2
	.long	enf_xf3
	.long	enf_xf4
	.long	0
lcerdie_diag13x

lcerdie_diag24
	.long	enf_xb1
	.long	enf_xb2
	.long	enf_xb3
	.long	enf_xb4
	.long	0
lcerdie_diag24x

lcerdeath_speed	equ	3		;death seq anim speed



	;reg use
	;  a6 (use) x acc
	;  a7 (use) y acc
	;  a8 (use) *image
	;  a9 (use) anim timer
	; a10 (in)  initial X
	; a10 (use) anim pointer
	; a11 (in)  initial Y
	; a11 (use) life timer

 SUBRP	spark

	;create a spark object
	move	a10,a0
	move	a11,a1

	movi	iwiqscript,a10
	move	*a10+,a2,L
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPSPARK,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the initial velocity.
	move	@player_object,a0,L
	move	*a0(OXVAL),a1,L
	move	*a8(OXVAL),a2,L
	sub	a2,a1
	sra	5,a1
	move	a1,*a8(OXVEL),L

	move	*a0(OYVAL),a1,L
	move	*a8(OYVAL),a2,L
	sub	a2,a1
	sra	5,a1
	move	a1,*a8(OYVEL),L

	;and then add a little 'spin' by giving them some acceleration
	movi	iwiqhigh_acc*2,a0
	calla	RNDRNG0
	subi	iwiqhigh_acc,a0
	move	a0,a6
	movi	iwiqhigh_acc*2,a0
	calla	RNDRNG0
	subi	iwiqhigh_acc,a0
	move	a0,a7

	movi	iwiqanim_time,a9
	movi	iwiqlife_time,a11

iwiqloop
	PUSHP	a6,a7
	SLEEPK	1
	PULLP	a6,a7

	;bounds check
	callr	iwiqin_bounds

	;apply accelerations
	move	*a8(OXVEL),a14,L
	add	a6,a14
	move	a14,*a8(OXVEL),L

	move	*a8(OYVEL),a14,L
	add	a7,a14
	move	a14,*a8(OYVEL),L

	dec	a9
	jrnz	iwiqno_anim

	move	*a10+,a0,L
	jrnz	iwiqno_wrap
	movi	iwiqscript,a10
	move	*a10+,a0,L
iwiqno_wrap
	calla	civanic
	movi	iwiqanim_time,a9

iwiqno_anim
	dec	a11
	jrz	iwiqexpire
	jruc	iwiqloop

iwiqexpire
	calla	DELOBJA8

	DIE


 SUBRP	iwiqin_bounds

	move	*a8(OXPOS),a14,W
	movi	iwiqmin_x,a0
	cmp	a0,a14
	jrlt	iwiqx_adjust

	movi	iwiqmax_x,a0
	cmp	a0,a14
	jrgt	iwiqx_adjust
	jruc	iwiqy_check

iwiqx_adjust
	move	a0,*a8(OXPOS),W
	clr	a6
	move	a6,*a8(OXVEL),L

iwiqy_check
	move	*a8(OYPOS),a14,W
	movi	iwiqmin_y,a0
	cmp	a0,a14
	jrlt	iwiqy_adjust

	movi	iwiqmax_y,a0
	cmp	a0,a14
	jrgt	iwiqy_adjust
	jruc	iwiqdone_check

iwiqy_adjust
	move	a0,*a8(OYPOS),W
	clr	a7
	move	a7,*a8(OYVEL),L

iwiqdone_check
	rets



 SUBR	spark_die
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADSPARK_PID,a1
	movi	iwiqexpire,a7
	calla	XFERPROC

	PULL	a1,a7
	rets



iwiqlife_time	equ	180		;how long they last
iwiqanim_time	equ	3		;frame rate

iwiqhigh_acc	equ	00000200h	;top acceleration

iwiqmin_x	equ	53	;spark move bounds
iwiqmax_x	equ	347-7
iwiqmin_y	equ	45
iwiqmax_y	equ	231-7

iwiqscript
	.long	spark_1
	.long	spark_2
	.long	spark_3
	.long	spark_4
	.long	0



	;reg use
	; a0 - (in) # of quarks

 SUBRP	make_quarks

	move	a0,a0
	jrz	bvukdone

bvukanother_quark
	PUSHP	a0

bvukget_pos
	;assign a random starting position
	movi	bvukmax_x-bvukmin_x,a0
	calla	RNDRNG0
	move	a0,a9
	movi	bvukmax_y-bvukmin_y,a0
	calla	RNDRNG0
	move	a0,a10

	addi	bvukmin_x,a9
	addi	bvukmin_y,a10

	cmpi	bvukmid_x1,a9
	jrle	bvukplace_quark
	cmpi	bvukmid_x2,a9
	jrge	bvukplace_quark
	cmpi	bvukmid_y1,a10
	jrle	bvukplace_quark
	cmpi	bvukmid_y2,a10
	jrge	bvukplace_quark
	jruc	bvukget_pos

bvukplace_quark
	CREATE	QUARK_PID,quark
	PULLP	a0
	dsj	a0,bvukanother_quark

bvukdone
	rets


bvukmid_x1	equ	200-60	;quark start inner boundaries
bvukmid_x2	equ	200+60
bvukmid_y1	equ	138-60
bvukmid_y2	equ	138+60



	STRUCTPD
	WORD	bvukOCHILDREN	;UHW # of children left to spawn
	WORD	bvukOSTAGE		;UHW stage in life cycle

	;reg use
	; a7 (use) event timer (related to bvukOSTAGE)
	; a8 (use) * object
	; a9 (in)  x pos
	; a9 (use) anim counter
	;a10 (in)  y pos
	;a10 (use) frame pointer
	;a11 (use) time until next vel change

 SUBRP	quark

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W

	;create the quark object
	move	a9,a0		;x pos
	sll	16,a0
	move	a10,a1		;y pos
	sll	16,a1
	movi	bvukstandard_script,a10
	move	*a10+,a2,L	;img
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPQUARK,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;initialize anim counter
	movi	bvukanim_speed,a9

	;set initial velocities and accelerations
	callr	bvukset_v

	;initialize life cycle and event timer
	movi	bvukSTAGE_YOUTH,a14
	move	a14,*a13(bvukOSTAGE),W
	movi	bvukyouth_time,a7

bvukloop
	PUSHP	a7
	SLEEPK	1
	PULLP	a7
	dec	a9
	jrnz	bvukno_anim

	;advance the frame
	move	*a10+,a0,L
	jrnz	bvuknewframe

	;reset script.  depends on the stage
	move	*a13(bvukOSTAGE),a14,W
	cmpi	bvukSTAGE_PARENT,a14
	jreq	bvukset_parent_script
	movi	bvukstandard_script,a10
	jruc	bvukscript_set
bvukset_parent_script
	movi	bvukparent_script,a10

bvukscript_set
	move	*a10+,a0,L

bvuknewframe
	calla	civanic

	;reset the counter
	movi	bvukanim_speed,a9

bvukno_anim
	;bounds check
	callr	bvukin_bounds

	;adjust vel if it's time
	dec	a11
	jrnz	bvukno_v_change
	callr	bvukset_v

bvukno_v_change

	;do event?
	dec	a7
	jrnz	bvukno_event

	;event.  depends on the stage in the life cycle
	move	*a13(bvukOSTAGE),a14
	cmpi	bvukSTAGE_YOUTH,a14
	jreq	bvukyouth_event
	cmpi	bvukSTAGE_PARENT,a14
	jreq	bvukparent_event
	;default
	jruc	bvukdotage_event

bvukyouth_event
	;switch to middle age
	movi	bvukSTAGE_PARENT,a14
	move	a14,*a13(bvukOSTAGE),W

	;set the number of kids to be spawned
	movi	bvukmax_kids-bvukmin_kids,a0
	calla	RNDRNG0
	addi	bvukmin_kids,a0
	move	a0,*a13(bvukOCHILDREN),W

	;set the spawn timer
	movi	bvukspawn_time,a7
	jruc	bvukno_event

bvukparent_event
	;spawn a brat
	PUSHP	a10,a11
	move	*a8(OXVAL),a10,L
	move	*a8(OYVAL),a11,L
	CREATE	TANK_PID,tank
	PULLP	a10,a11

	;decrement the kid count
	move	*a13(bvukOCHILDREN),a14,W
	dec	a14
	jrz	bvukbarren
	move	a14,*a13(bvukOCHILDREN),W
	movi	bvukspawn_time,a7
	jruc	bvukno_event

bvukbarren
	;that was the last kid.  age
	movi	bvukSTAGE_DOTAGE,a14
	move	a14,*a13(bvukOSTAGE),W
	movi	bvukold_age_time,a7
	jruc	bvukno_event

bvukdotage_event
	;nothing fancy.  just die.
	jruc	bvukdie_peacefully
	
bvukno_event
	jruc	bvukloop


bvukbeen_shot
	;crud.  we've been shot

bvukdie_peacefully

	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

	calla	DELOBJA8

	DIE


 SUBRP	bvukset_v

	movi	bvukhigh_v,a0
	calla	RNDRNG0
	cmpi	bvukhigh_v/2,a0
	jrgt	bvukpositive_x

	subi	bvukhigh_v,a0

bvukpositive_x
	move	a0,*a8(OXVEL),L

	movi	bvukhigh_v,a0
	calla	RNDRNG0
	cmpi	bvukhigh_v/2,a0
	jrgt	bvukpositive_y

	subi	bvukhigh_v,a0

bvukpositive_y
	move	a0,*a8(OYVEL),L

	movi	bvukv_change_time/2,a0
	calla	RNDRNG0
	addi	bvukv_change_time/2,a0
	move	a0,a11

	rets


 SUBRP	bvukin_bounds

	move	*a8(OXPOS),a1,W
	movi	bvukmin_x,a0
	move	*a8(ODXOFF),a14,W
	add	a14,a0
	cmp	a0,a1
	jrlt	bvukx_adjust

	move	*a8(OSIZEX),a0,W
	neg	a0
	move	*a8(ODXOFF),a14,W
	add	a14,a0
	addi	bvukmax_x,a0
	cmp	a0,a1
	jrgt	bvukx_adjust
	jruc	bvuky_check

bvukx_adjust
	move	a0,*a8(OXPOS),W
	move	*a8(OXVEL),a0,L
	neg	a0
	move	a0,*a8(OXVEL),L

bvuky_check
	move	*a8(OYPOS),a1,W
	movi	bvukmin_y,a0
	move	*a8(ODYOFF),a14,W
	add	a14,a0
	cmp	a0,a1
	jrlt	bvuky_adjust

	move	*a8(OSIZEY),a0,W
	neg	a0
	move	*a8(ODYOFF),a14,W
	add	a14,a0
	addi	bvukmax_y,a0
	cmp	a0,a1
	jrgt	bvuky_adjust
	jruc	bvukdone_check

bvuky_adjust
	move	a0,*a8(OYPOS),W
	move	*a8(OYVEL),a0,L
	neg	a0
	move	a0,*a8(OYVEL),L

bvukdone_check
	rets



 SUBR	quark_die
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADQUARK_PID,a1
	movi	bvukbeen_shot,a7
	calla	XFERPROC

	PULL	a1,a7
	rets



bvukanim_speed	equ	3

bvukv_change_time	equ	600	;time between random course changes

bvukyouth_time	equ	90	;time before we have kids
bvukspawn_time	equ	90	;time between kids
bvukold_age_time	equ	120	;time between last kid and natural death

bvukmin_kids	equ	3	;lower limit on spawned tanks
bvukmax_kids	equ	7	;upper limit

bvukhigh_v		equ	0001C000h

bvukmin_x	equ	53	;quark move bounds
bvukmax_x	equ	347-0
bvukmin_y	equ	45
bvukmax_y	equ	231-0

bvukSTAGE_YOUTH 	equ	0000h
bvukSTAGE_PARENT	equ	0001h
bvukSTAGE_DOTAGE	equ	0002h

bvukstandard_script
	.long	quark_1
	.long	quark_1
	.long	quark_2
	.long	quark_3
	.long	quark_4
	.long	0

bvukparent_script
	.long	quark_1
	.long	quark_2
	.long	quark_3
	.long	quark_4
	.long	quark_5
	.long	quark_6
	.long	quark_7
	.long	quark_8
	.long	0



	STRUCTPD
	WORD	afexOSHOTS		;UHW shots left to be fired

	;reg use
	;  a7 (use) anim timer
	;  a8 (use) *img
	;  a9 (use) next move timer (move/fire phase)
	;  a9 (use) frame pointer (death phase)
	; a10 (in)  initial X
	; a10 (use) frame pointer (grow + move/fire phases)
	; a11 (in)  initial Y
	; a11 (use) next shot timer (move/fire phase)

 SUBRP	tank

	;increment the badguy count
	move	@live_badguys,a14,W
	inc	a14
	move	a14,@live_badguys,W

	;create the tank object
	move	a10,a0
	move	a11,a1
	movi	afexgrow_script,a10
	move	*a10+,a2,L
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPTANK,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;grow phase

afexgrow_loop
	SLEEPK	afexgrow_speed
	move	*a10+,a0,L
	jrz	afexmf_phase
	calla	civanic
	jruc	afexgrow_loop

afexmf_phase
	;begin move/fire phase.  set initial course
	callr	afexset_v
	movi	afexanim_time,a7
	movi	afexright_script,a10
	movi	afexfire_time/2,a0
	calla	RNDRNG0
	addi	afexfire_time*3/4,a0
	move	a0,a11
	movi	afexnum_shots,a14
	move	a14,*a13(afexOSHOTS),W

afexmain_loop
	PUSHP	a7
	SLEEPK	1
	PULLP	a7
	dec	a9
	jrnz	afexno_turn

	;change course
	callr	afexset_v

afexno_turn

	dec	a11
	jrnz	afexno_shot

	move	*a13(afexOSHOTS),a14,W
	jrz	afexno_shot

	;fire a shell
	PUSHP	a7,a10,a11
	move	*a8(OXVAL),a10,L
	move	*a8(OYVAL),a11,L
	create	SHELL_PID,tank_shell
	PULLP	a7,a10,a11
	movi	afexfire_time/2,a0
	calla	RNDRNG0
	addi	afexfire_time*3/4,a0
	move	a0,a11

afexno_shot

	;bounds check
	callr	afexin_bounds

	;animate?
	dec	a7
	jrnz	afexno_anim
	move	*a10+,a0,L
	jrnz	afexno_wrap
	movi	afexright_script,a10
	move	*a10+,a0,L

afexno_wrap
	calla	civanic
	movi	afexanim_time,a7

afexno_anim
	jruc	afexmain_loop

afexdie
	;decrement the badguy count
	move	@live_badguys,a14,W
	dec	a14
	move	a14,@live_badguys,W

	;zero our velocity
	clr	a14
	move	a14,*a8(OXVEL),L
	move	a14,*a8(OYVEL),L

	;set up the animation
afexdieloop
	move	*a9+,a0,L
	jrz	afexdead_n_buried
	calla	civanic
	sleepk	afexdeath_speed
	jruc	afexdieloop

afexdead_n_buried
	calla	DELOBJA8

	DIE


 SUBRP	afexset_v

	movi	afexhigh_v,a0
	calla	RNDRNG0
	cmpi	afexhigh_v/2,a0
	jrgt	afexpositive_x

	subi	afexhigh_v,a0

afexpositive_x
	move	a0,*a8(OXVEL),L

	movi	afexhigh_v,a0
	calla	RNDRNG0
	cmpi	afexhigh_v/2,a0
	jrgt	afexpositive_y

	subi	afexhigh_v,a0

afexpositive_y
	move	a0,*a8(OYVEL),L

	movi	afexv_change_time/2,a0
	calla	RNDRNG0
	addi	afexv_change_time/2,a0
	move	a0,a9

	rets


 SUBRP	afexin_bounds

	move	*a8(OXPOS),a14,W
	movi	afexmin_x,a0
	cmp	a0,a14
	jrlt	afexx_adjust

	movi	afexmax_x,a0
	cmp	a0,a14
	jrgt	afexx_adjust
	jruc	afexy_check

afexx_adjust
	move	a0,*a8(OXPOS),W
	move	*a8(OXVEL),a0,L
	neg	a0
	move	a0,*a8(OXVEL),L

afexy_check
	move	*a8(OYPOS),a14,W
	movi	afexmin_y,a0
	cmp	a0,a14
	jrlt	afexy_adjust

	movi	afexmax_y,a0
	cmp	a0,a14
	jrgt	afexy_adjust
	jruc	afexdone_check

afexy_adjust
	move	a0,*a8(OYPOS),W
	move	*a8(OYVEL),a0,L
	neg	a0
	move	a0,*a8(OYVEL),L

afexdone_check
	rets



 SUBR	tank_die
	;hit by player gunfire
	PUSH	a1,a7

	;jeepers.  we're dead.
	movi	CLSDEAD,a14
	move	a14,*a8(OID),W

	move	*a0(OXVEL),a14,L
	jrz	afexvert_bullet
	move	*a0(OYVEL),a1,L
	jrz	afexhorz_bullet

	add	a1,a14
	jrz	afexdiag_13
	jruc	afexdiag_24

afexhorz_bullet
	movi	afexdie_vert,a9
	jruc	afexdeath_set
afexvert_bullet
	movi	afexdie_horz,a9
	jruc	afexdeath_set
afexdiag_13
	movi	afexdie_diag24,a9
	jruc	afexdeath_set
afexdiag_24
	movi	afexdie_diag13,a9
	jruc	afexdeath_set

afexdeath_set
	move	*a8(OPLINK),a0,L
	movi	DEADTANK_PID,a1
	movi	afexdie,a7
	move	*a0(PA10),a10,L
	calla	XFERPROC

	movi	afextank_points,a0
	callr	score_points

	PULL	a1,a7
	rets



afextank_points	equ	200

afexnum_shots	equ	21

afexv_change_time	equ	250
afexfire_time	equ	90

afexhigh_v		equ	00010000h

afexgrow_speed	equ	6
afexanim_time	equ	2

afexmin_x	equ	53	;tank move bounds
afexmax_x	equ	347-13
afexmin_y	equ	45
afexmax_y	equ	231-16

afexgrow_script
	.long	tank_g1
	.long	tank_g2
	.long	tank_g3
	.long	tank_g4
	.long	tank_1
	.long	0

afexright_script
	.long	tank_1
	.long	tank_2
	.long	tank_3
	.long	tank_4
	.long	0

afexdie_vert
	.long	enf_xv1
	.long	enf_xv2
	.long	enf_xv3
	.long	enf_xv4
	.long	enf_xv5
	.long	0

afexdie_horz
	.long	enf_xh1
	.long	enf_xh2
	.long	enf_xh3
	.long	enf_xh4
	.long	0

afexdie_diag13
	.long	enf_xf1
	.long	enf_xf2
	.long	enf_xf3
	.long	enf_xf4
	.long	0

afexdie_diag24
	.long	enf_xb1
	.long	enf_xb2
	.long	enf_xb3
	.long	enf_xb4
	.long	0

afexdeath_speed	equ	3		;death seq anim speed



	;reg use
	;  a8 (use) *image
	; a10 (in)  initial X
	; a11 (in)  initial Y
	; a11 (use) life timer

 SUBRP	tank_shell

	;create a shell object
	move	a10,a0
	move	a11,a1
	movi	shell,a2
	clr	a3
	movi	DMAWNZ,a4
	movi	CLSENMY|TYPSHELL,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	;set the initial velocity.
	move	@player_object,a0,L
	move	*a0(OXVAL),a1,L
	move	*a8(OXVAL),a2,L
	sub	a2,a1
	sra	5,a1
	move	a1,*a8(OXVEL),L

	move	*a0(OYVAL),a1,L
	move	*a8(OYVAL),a2,L
	sub	a2,a1
	sra	5,a1
	move	a1,*a8(OYVEL),L

	movi	bfwzlife_time,a11

bfwzloop
	PUSHP	a6,a7
	SLEEPK	1
	PULLP	a6,a7

	;bounds check
	callr	bfwzin_bounds

	dsj	a11,bfwzloop

bfwzexpire
	calla	DELOBJA8

	DIE


 SUBRP	bfwzin_bounds

	move	*a8(OXPOS),a14,W
	movi	bfwzmin_x,a0
	cmp	a0,a14
	jrlt	bfwzx_adjust

	movi	bfwzmax_x,a0
	cmp	a0,a14
	jrgt	bfwzx_adjust
	jruc	bfwzy_check

bfwzx_adjust
	move	a0,*a8(OXPOS),W
	move	*a8(OXVEL),a0,L
	neg	a0
	move	a0,*a8(OXVEL),L

bfwzy_check
	move	*a8(OYPOS),a14
	movi	bfwzmin_y,a0
	cmp	a0,a14
	jrlt	bfwzy_adjust

	movi	bfwzmax_y,a0
	cmp	a0,a14
	jrgt	bfwzy_adjust
	jruc	bfwzdone_check

bfwzy_adjust
	move	a0,*a8(OYPOS),W
	move	*a8(OYVEL),a0,L
	neg	a0
	move	a0,*a8(OYVEL),L

bfwzdone_check
	rets



 SUBR	shell_die
	PUSH	a1,a7

	movi	CLSDEAD,a14
	move	a14,*a8(OID),W
	move	*a8(OPLINK),a0,L
	movi	DEADSHELL_PID,a1
	movi	bfwzexpire,a7
	calla	XFERPROC

	PULL	a1,a7
	rets



bfwzlife_time	equ	240		;how long they last

bfwzmin_x	equ	53	;shell move bounds
bfwzmax_x	equ	347-7
bfwzmin_y	equ	45
bfwzmax_y	equ	231-7



 SUBRP	robo_pal_cycles

	;laser flash cycle
	movi	[0Ah,1],a8		;cycle color A
	movi	robo_p,a9		;pal name
	movi	COLTAB_LFLASH,a10	;use the LFLASH table
	movk	1,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;rgb cycle
	movi	[0Bh,1],a8		;cycle color B
	movi	robo_p,a9		;pal name
	movi	COLTAB_RGB,a10		;use the RGB table
	movk	8,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;decay cycle
	movi	[0Ch,1],a8		;cycle color C
	movi	robo_p,a9		;pal name
	movi	COLTAB_DECAY,a10	;use the DECAY table
	movk	2,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;laser cycle
	movi	[0Dh,1],a8		;cycle color D
	movi	robo_p,a9		;pal name
	movi	COLTAB_LASER,a10	;use the LASER table
	movk	20,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;blue-purple-red cycle
	movi	[0Eh,1],a8		;cycle color E
	movi	robo_p,a9		;pal name
	movi	COLTAB_BLUPURRED,a10	;use the BPR table
	movk	1,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	;red-gold cycle
	movi	[0Fh,1],a8		;cycle color F
	movi	robo_p,a9		;pal name
	movi	COLTAB_REDGOLD,a10	;use the RED-GOLD table
	movk	10,a11			;cycle speed
	CREATE	CYCPID,CYCLE_TABLE

	rets


COLTAB_LFLASH
	.WORD	0380H,1380H,2380H,3380H,4380H,5380H,6380H,7380H,7300H
	.WORD	7280H,7200H,7180H,7080H,7008H,7008H,7010H,7010H,701CH
	.WORD	701CH,601CH,501CH,409CH,309CH,209CH,219CH,029CH,039CH
	.WORD	139CH,239CH,339CH,539CH,739CH,7390H,7380H,6380H,4380H
	.word	-1


COLTAB_RGB
	.WORD	07C00H,001FH,77A0h,741Ah
	.word	-1


COLTAB_DECAY
	.WORD	001CH,001CH,011CH,021CH,031CH,039CH,239CH,2390H,2388H
	.WORD	2380H,4300H,5280H,7180H,6180H,7080H,7000H,6000H,5000H
	.WORD	4000H,3000H,2000H,1000H
	.word	-1


COLTAB_LASER
	.word	7C1Fh,7FFFh,7F00h
	.word	-1

COLTAB_BLUPURRED
	.WORD	001CH,101CH,201CH,301CH,401CH,501CH,601CH,701CH,7010H
	.WORD	7010H,7008H,7008H,7000H,7000H,7008H,7008H,7010H,7010H
	.WORD	701CH,701CH,601CH,501CH,401CH,301CH,201CH,101CH
	.word	-1

COLTAB_REDGOLD
	.word	7C00h,7F00h
	.word	-1



	;reg use
	; a11 score

 SUBRP	score

	;initialize score
	move	@robotron_score,a11,L
	callr	zlfoprint_score

zlfoloop
	SLEEPK	1
	move	@robotron_score,a14,L
	cmp	a11,a14
	jrne	zlfoscore_change
	jruc	zlfoloop

zlfoscore_change
	move	a14,a11
	callr	zlfozorch_score
	callr	zlfoprint_score
	jruc	zlfoloop

	DIE


 SUBRP	zlfozorch_score
	movi	TYPTEXT|SUBSCOR,a0
	calla	obj_del1c

	rets


 SUBRP	zlfoprint_score

	movi	zlfoscore_setup,a2
	calla	setup_message
	movi	TYPTEXT|SUBSCOR,a14
	move	a14,@mess_objid
	move	@robotron_score,a0,L
	movi	zlfomax_score,a1
	calla	dec_to_asc
	calla	copy_string
	calla	print_string_R

	rets


zlfoscore_setup
	JAM_STR	robotron_ascii,3,1,zlfoscore_xpos,zlfoscore_ypos,robo_p,0
	.even

zlfomax_score	.equ	9999999		;that's all dec_to_asc can handle


 SUBRP	score_points

	move	@robotron_score,a14,L
	add	a0,a14
	move	a14,@robotron_score,L
	rets


zlfoscore_ypos	.equ	36
zlfoscore_xpos	.equ	130



 SUBRP	draw_frame


	move	a0,a9
	sla	8,a9
	or	a0,a9

	movi	robo_p,a0
	calla	pal_getf
	move	a0,a10

	movi	xwlbframe_data,a11

xwlbloop
	move	*a11+,a0,L
	jrz	xwlbdone
	move	*a11+,a1,L
	movi	xwlbframe_image,a2
	movi	100,a3				;z pos
	movi	DMACAL,a4			;DMA flags
	movi	CLSDEAD,a5			;object ID
	clr	a6				;x vel
	clr	a7				;y vel
	calla	BEGINOBJ

	move	a8,a0
	calla	fg2bg

	move	*a11+,a14,W
	move	a14,*a8(OSIZEX),W
	move	*a11+,a14,W
	move	a14,*a8(OSIZEY),W

	move	a9,*a8(OCONST),W
	move	a10,*a8(OPAL),W

	jruc	xwlbloop
xwlbdone
	rets


xwlbframe_image	equ	man_d1

xwlbframe_data
	.long	[53,0],[43,0]	;position X,Y
	.word	294,2		;size X,Y

	.long	[53,0],[231,0]	;position X,Y
	.word	294,2		;size X,Y

	.long	[50,0],[43,0]	;position X,Y
	.word	3,190		;size X,Y

	.long	[347,0],[43,0]	;position X,Y
	.word	3,190		;size X,Y

	.long	0

xwlbframe_datax



 SUBRP	wave_count

	;wave text
	movi	[xjqxwave_x,0],a0			;x pos
	movi	[xjqxwave_y,0],a1			;y pos
	movi	wave_text,a2			;* image
	clr	a3				;z pos
	movi	DMAWNZ,a4			;DMA flags
	clr	a5				;object ID
	clr	a6				;x vel
	clr	a7				;y vel
	calla	BEGINOBJ

	;ones digit
	move	@robo_wave,a0,W
	sra	4,a0
	inc	a0
	calla	BINBCD
	move	a0,a2
	andi	0Fh,a2
	X32	a2
	addi	xjqxwave_font,a2

	movi	[xjqxwave_onesx,0],a0		;x pos
	movi	[xjqxwave_y,0],a1			;y pos
	move	*a2,a2,L			;* image
	clr	a3				;z pos
	movi	DMAWNZ,a4			;DMA flags
	clr	a5				;object ID
	clr	a6				;x vel
	clr	a7				;y vel
	calla	BEGINOBJ

	;tens digit
	move	@robo_wave,a0,W
	sra	4,a0
	inc	a0
	calla	BINBCD
	move	a0,a2
	srl	4,a2
	andi	0Fh,a2
	jrz	xjqxdone
	X32	a2
	addi	xjqxwave_font,a2
	movi	[xjqxwave_tensx,0],a0		;x pos
	movi	[xjqxwave_y,0],a1			;y pos
	move	*a2,a2,L			;* image
	clr	a3				;z pos
	movi	DMAWNZ,a4			;DMA flags
	clr	a5				;object ID
	clr	a6				;x vel
	clr	a7				;y vel
	calla	BEGINOBJ

xjqxdone
	rets

xjqxwave_y		.equ	234
xjqxwave_x		.equ	198
xjqxwave_tensx	.equ	184
xjqxwave_onesx	.equ	188

xjqxwave_font
	.long	rsmall_0
	.long	rsmall_1
	.long	rsmall_2
	.long	rsmall_3
	.long	rsmall_4
	.long	rsmall_5
	.long	rsmall_6
	.long	rsmall_7
	.long	rsmall_8
	.long	rsmall_9
xjqxwave_fontx


 SUBRP	bounds_check
	PUSH	a1,a3
	clr	a0
	clr	a2
	clr	a3

	;check for hit top
	move	*a8(OYPOS),a1
	move	*a8(ODYOFF),a14
	sub	a14,a1
	cmpi	tqehlow_y,a1
	jrgt	tqehnot_high
	movi	tqehlow_y,a2
	sub	a1,a2
	ori	M_HIT_TOP,a0
	jruc	tqehnot_low		;assume won't be both high and low

tqehnot_high
	;check for hit bottom
	move	*a8(ODYOFF),a14		;subtract the offset AGAIN because
	sub	a14,a1			; we assume the anim point is there
	move	*a8(OSIZEY),a14		; to center the object.
	add	a14,a1
	cmpi	tqehhigh_y,a1
	jrlt	tqehnot_low
	move	a1,a2
	subi	tqehhigh_y,a2
	ori	M_HIT_BOTTOM,a0

tqehnot_low
	;check for hit left
	move	*a8(OXPOS),a1
	move	*a8(ODXOFF),a14
	sub	a14,a1
	cmpi	tqehlow_x,a1
	jrgt	tqehnot_left
	movi	tqehlow_x,a3
	sub	a1,a3
	ori	M_HIT_LEFT,a0
	jruc	tqehdone			;assume won't be both right and left

tqehnot_left
	move	*a8(ODXOFF),a14
	sub	a14,a1
	move	*a8(OSIZEX),a14
	add	a14,a1
	cmpi	tqehhigh_x,a1
	jrlt	tqehdone
	move	a1,a3
	subi	tqehhigh_x,a3
	ori	M_HIT_RIGHT,a0

tqehdone
	;a2 is y overlap, a3 is x overlap.  a2 should be greatest of the two.
	cmp	a2,a3
	jrn	tqehretval_set
	move	a3,a2
tqehretval_set
	PULL	a1,a3
	rets


tqehlow_x	equ	53
tqehhigh_x	equ	346
tqehlow_y	equ	45
tqehhigh_y	equ	230

B_HIT_TOP	equ	0
B_HIT_BOTTOM	equ	2
B_HIT_LEFT	equ	3
B_HIT_RIGHT	equ	1

M_HIT_TOP	equ	0001h
M_HIT_BOTTOM	equ	0004h
M_HIT_LEFT	equ	0008h
M_HIT_RIGHT	equ	0002h



 SUBRP	watch_both_starts

hqkiloop
	SLEEPK	4		;no need to check EVERY frame
	clr	a0
	calla	get_start_cur
	jrz	hqkiloop
	movk	1,a0
	calla	get_start_cur
	jrz	hqkiloop

	;both starts are down.
	movk	1,a0
	move	a0,@starts_down

	DIE


 SUBRP	wave_mover

fwpeloop
	SLEEPK	4
	calla	get_all_buttons_cur
	jrz	fwpeloop

	cmpi	1,a0
	jreq	fwpeback
	cmpi	2,a0
	jreq	fwpefwd
	jruc	fwpeloop

fwpeback
	move	a0,@backward
	jruc	fwpedone
fwpefwd
	move	a0,@forward

fwpedone
	DIE


	.if DEBUG


 SUBRP	bog_o_meter

	movi	robo_p,a0
	calla	pal_getf
	move	a0,a9

	;create the background
	movi	[360,0],a0
	movi	[192,0],a1
	movi	man_d1,a2
	clr	a3
	movi	DMACAL|M_FLIPV,a4
	movi	CLSDEAD,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	move	a9,*a8(OPAL)
	movi	0707h,a14
	move	a14,*a8(OCONST)

	movi	8,a0
	move	a0,*a8(OSIZEX)
	movi	128,a0
	move	a0,*a8(OSIZEY)
	move	a8,a10

	;create the foreground
	movi	[360,0],a0
	movi	[65,0],a1
	movi	man_d1,a2
	movi	1,a3
	movi	DMACAL,a4
	movi	CLSDEAD,a5
	clr	a6
	clr	a7
	calla	BEGINOBJW

	move	a9,*a8(OPAL)
	movi	0808h,a14
	move	a14,*a8(OCONST)
	
	movi	8,a0
	move	a0,*a8(OSIZEX)

	movi	gbvqscale,a9

gbvqloop
	SLEEPK	2
	move	@CPULEFT,a1
	jrnz	gbvqnobog

	;BOG!!
	nop
gbvqnobog
	mpyu	a9,a1
	srl	16,a1
	move	a1,*a8(OSIZEY)
	cmpi	10h,a1
	jrle	gbvqred

	movi	0707h,a0
	move	a0,*a10(OCONST)
	jruc	gbvqloop
gbvqred
	movi	0101h,a0
	move	a0,*a10(OCONST)
	jruc	gbvqloop

	DIE

gbvqscale	equ	800000h/03DCh

	.endif


robotron_ascii
 .long 0,0,0,0,0,0,0,0								;$00
 .long 0,0,0,0,0,0,0,0								;$08
 .long 0,0,0,0,0,0,0,0								;$10
 .long 0,0,0,0,0,0,0,0								;$18
 .long 0,rfont_exc,0,0,0,0,0,0								;$20	SP! " # $ % & '
 .long rfont_lparens,rfont_rparens,0,0,rfont_comma,0,rfont_period,rfont_fslash	;$28	( ) * + , - . /
 .long rfont_0,rfont_1,rfont_2,rfont_3,rfont_4,rfont_5,rfont_6,rfont_7		;$30	0 1 2 3 4 5 6 7
 .long rfont_8,rfont_9,0,0,0,0,0,0						;$38	8 9 : ; < - > ?
 .long 0,rfont_a,rfont_b,rfont_c,rfont_d,rfont_e,rfont_f,rfont_g		;$40	@ A B C D E F G
 .long rfont_h,rfont_i,rfont_j,rfont_k,rfont_l,rfont_m,rfont_n,rfont_o		;$48	H I J K L M N O
 .long rfont_p,rfont_q,rfont_r,rfont_s,rfont_t,rfont_u,rfont_v,rfont_w		;$50	P Q R S T U V W
 .long rfont_x,rfont_y,rfont_z,0,0,0,0,0					;$58	X Y Z [ \ ] ^ _
 .long 0,rfont_a,rfont_b,rfont_c,rfont_d,rfont_e,rfont_f,rfont_g		;$60	` a b c d e f g
 .long rfont_h,rfont_i,rfont_j,rfont_k,rfont_l,rfont_m,rfont_n,rfont_o		;$68	h i j k l m n o
 .long rfont_p,rfont_q,rfont_r,rfont_s,rfont_t,rfont_u,rfont_v,rfont_w		;$70	p q r s t u v w
 .long rfont_x,rfont_y,rfont_z,0,0,0,0,0					;$78	x y z { | } ~


RS_GRUNT	.equ	0
RS_DIE		.equ	1
RS_RESCUE	.equ	2
RS_SHOOT	.equ	3
RS_HIT		.equ	4

sp_grunt	.equ	1<<8
sp_shoot	.equ	2<<8
sp_hit		.equ	3<<8
sp_rescue	.equ	4<<8
sp_die		.equ	5<<8

robosnd_tbl
	;	duration, call#
	.word	sp_grunt|6,96		;0 = grunt footstep
	.word	sp_die|96,98		;1 = die
	.word	sp_rescue|39,99		;2 = rescue
	.word	sp_shoot|17,102		;3 = shoot
	.word	sp_hit|23,100		;4 = hit
robosnd_end

	.bss	rs_snd,16		;current sound call (0-3)
	.bss	rs_time,32		;timeout on current call (long)PCNT
	.bss	rs_pri,16		;priority on current call
	.bss	last_grunt_snd,32	;PCNT of last grunt footstep


 SUBRP	robo_sound_init

	clr	a14
	move	a14,@rs_snd
	move	a14,@rs_time,L
	move	a14,@last_grunt_snd,L
	rets


 SUBRP	robo_sound

	PUSH	a2,a3,a4

	;reggies:
	;a0 = call index
	;a1 = priority
	;a2 = duration
	;a3 = call #

	;decode table index
	move	a0,a14
	X32	a14
	addi	robosnd_tbl,a14
	move	*a14,a1,W
	srl	8,a1
	move	*a14+,a2,W
	andi	0FFh,a2
	move	*a14,a3,W

	;check old duration - if there's nothing going on, do the sound.
	move	@rs_time,a4
	move	@PCNT,a14,L
	cmp	a4,a14
	jrgt	miandosnd

	;old sound still going.  check for an override
	TEST	a0
	jrnz	mianng
	move	@rs_snd,a14
	jrz	miangrxgr		;special grunt-overriding-grunt case

mianng	move	@rs_pri,a14
	cmp	a14,a1
	jrge	miandosnd
	jruc	miandone

miangrxgr	;do new call if old one has 3 or fewer ticks to live
	move	@rs_time,a14,L
	move	@PCNT,a4,L
	sub	a4,a14
	cmpi	3,a4
	jrle	miandosnd
	jruc	miandone

miandosnd	calla	SNDSND
	move	a0,@rs_snd
	move	@PCNT,a14,L
	add	a2,a14
	move	a14,@rs_time,L
	move	a1,@rs_pri

miandone	PULL	a2,a3,a4
	rets



	.end

