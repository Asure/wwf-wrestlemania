**************************************************************
*
* Software:		Gilbert the Wonder Hamster
* Initiated:		7/15/94
*
* COPYRIGHT (C) 1992 WILLIAMS ELECTRONICS GAMES, INC.
*
**************************************************************

	.file	"lex.asm"
	.title	"Lex Luger specific game code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"jjxm.h"
	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"plyr.equ"
	.include	"anim.equ"
	.include	"audit.equ"
	.include	"sound.h"
	.include 	"ring.equ"
	.include	"lifebar.h"
	.include	"damage.equ"


******************************************************************************
* EXTERNAL REFERENCES

	.ref	change_anim1a,change_anim2a,master_keep_attached
	.ref	do_taunt,CLIMB_ROPES,bounce_off_ropes,mode_inair2
	.ref	check_secret_moves,mode_choking,DO_REVERSAL_MESS
	.ref	ck_ignore,climb_turnbuckle,execute_walk,PCNT
	.ref	get_opp_plyrmode,ADD_IF_SILENT,DO_REVERSAL,mode_puppet
	.ref	process_ptrs,keep_attached,change_anim1,JUMP_ROPES
	.ref	FIND_AND_KILL_ENDLESS,BONUS_MESS
	.ref	CHECK_COMBO_GO,ck_teammate_pin,raisearm_check
	.ref	BLOCK_WOOSH,drone_change_back,mode_dead,get_opp_process
	.ref	lex_combo_punch_anim,can_pin,round_award
	.ref	lex_combo_kick_anim,set_raisearm_bit
	.ref	blocking_off,std_taunt,std_walk_fast
	.ref	hyper_speed_on
	.ref	in_finish_move
	.ref	p1rounds,p2rounds
	.if NUM_LEX_FINISHES
	.ref	lex_finish1_move
	.if NUM_LEX_FINISHES > 1
	.ref	lex_finish2_move
	.endif
	.endif

******************************************************************************
* ANIMATION SEQUENCES

	.ref	start_run_anim

	.ref	lex_4_block_anim

	.ref	lex_stand2_anim,lex_stand8_anim
	.ref	lex_stand4_anim,lex_stand6_anim
	.ref	lex_torso2_anim,lex_torso8_anim
	.ref	lex_torso4_anim,lex_torso6_anim

	.ref	lex_2_to_4_turn_anim,lex_8_to_6_turn_anim	;stand
	.ref	lex_4_to_2_turn_anim,lex_6_to_8_turn_anim
	.ref	lex_4_to_6_turn_anim,lex_6_to_4_turn_anim
	.ref	lex_2_to_8_turn_anim,lex_8_to_2_turn_anim
	.ref	lex_4_to_8_turn_anim,lex_6_to_2_turn_anim
	.ref	lex_2_to_6_turn_anim,lex_8_to_4_turn_anim

	.ref	lex_2_to_4_turn2_anim,lex_8_to_6_turn2_anim	;torso
	.ref	lex_4_to_2_turn2_anim,lex_6_to_8_turn2_anim
	.ref	lex_4_to_6_turn2_anim,lex_6_to_4_turn2_anim
	.ref	lex_2_to_8_turn2_anim,lex_8_to_2_turn2_anim
	.ref	lex_4_to_8_turn2_anim,lex_6_to_2_turn2_anim
	.ref	lex_2_to_6_turn2_anim,lex_8_to_4_turn2_anim

	.ref	lex_walk1_f2_anim,lex_walk2_f2_anim,lex_walk4_f2_anim
	.ref	lex_walk5_f2_anim,lex_walk6_f2_anim,lex_walk8_f2_anim

	.ref	lex_walk1_f4_anim,lex_walk2_f4_anim,lex_walk4_f4_anim
	.ref	lex_walk5_f4_anim,lex_walk6_f4_anim,lex_walk8_f4_anim

	.ref	lex_run2_anim,lex_hiptoss2_anim

	.ref	lex_2_punch_anim,lex_4_punch_anim
	.ref	lex_2_kick_anim,lex_4_kick_anim

	.ref	lex_2_butt_anim,lex_4_butt_anim
	.ref	lex_4_knee_anim
	.ref	lex_2_stomp_anim,lex_4_stomp_anim

	.ref	lex_2_raise_arm_anim,lex_4_raise_arm_anim

	.ref	lex_3_clobber_anim

	.ref	lex_4_push_anim

	.ref	lex_4_grabfling_anim

	.ref	lex_fall_back_anim

	.ref	lex_flying_kick_anim

	.ref	lex_2_ground_punch_anim,lex_4_ground_punch_anim
	.ref	lex_flying_ground_punch_anim,lex_climb_down_anim
	.ref	lex_buckle_leap_anim,lex_climb_up_anim
	.ref	lex_backbreaker_anim,lex_ohslam_anim

	.ref	lex_3_head_held_stand_anim,lex_2_hair_pickup_anim
	.ref	lex_3_knee_to_head_anim
	.ref	lex_4_hair_pickup_anim,lex_2_grabfling_anim
	.ref	lex_3_head_hold2_anim
	.ref	lex_3_head_hold_anim

	.ref	lex_vsuplex_anim,lex_super_kick_anim
	.ref	lex_4_knee_fall_anim,lex_4_graboh_anim
	.ref	lex_hiptoss_anim,lex_4_bigboot_anim
	.ref	lex_slamdown_anim

	.ref	lex_2_pin_anim,lex_4_pin_anim
	.ref	lex_2_clobber_anim,lex_4_clobber_anim

;	.ref	lex_pkup_chair_anim
	.ref	lex_sliding_elbow_anim
	.ref	lex_knees_to_head_anim
	.ref	lex_grab2elbow_anim

******************************************************************************
* SYMBOLS DEFINED IN THIS FILE

******************************************************************************
* EQUATES FOR THIS FILE

*****************************************************************************
*
* SPECIAL MOVES - History checks

lex_secret_moves
	.long	uijqcharge_clobber
	.long	uijqneck_grab
	.long	uijqgrab_fling
	.long	uijqhip_toss
	.long	uijqgrab_fling2
	.long	uijqhip_toss2
	.long	uijqsliding_elbow
	.long	uijqhammer
	.long	0

******************************************************************************
*
* SPECIAL MOVE PROCESSES - Secret moves
*
;a8=process of wrestler

 SUBR	lex_smove_table
	.long	lex_hdhold_pile
	.long	lex_hdhold_elbow_face
	.long	lex_hdhold_graboh
	.long	lex_grab_toss_air
	.long	lex_hdhold_combo1
	.long	lex_hdhold_combo2
;	.long	lex_hdhold_anti_combo
	.long	std_walk_fast
	.long	std_taunt

	.if NUM_LEX_FINISHES
	.long	lex_finish_move1
	.endif
	.if NUM_LEX_FINISHES > 1
	.long	lex_finish_move2
	.endif
	.long	0

*******************************************************************************
uijqcharge_clobber
	move	*a13(BUT_VAL_UP),a0
	btst	PLAYER_PUNCH_BIT,a0
	jrz	uijqno_punch

;	move	*a13(PLYRMODE),a0
;	cmpi	MODE_ONTURNBKL,a0
;	jrz	uijqno_punch

	move	*a13(PLYRNUM),a0
	.ref	get_punch_dtime
	calla	get_punch_dtime
	cmpi	100,a0			;> min?
	jrlt	uijqno_punch

	callr	uijqscrt_clbr
	setc
	rets

uijqno_punch
	clrc
	rets


uijqscrt_clbr

	move	*a13(PLYRMODE),a0
	cmpi	MODE_HEADHELD,a0
	jrz	uijqout
	cmpi	MODE_HEADHOLD,a0
	jrz	uijqout

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uijqout

;Don't do it when he is on ground
;	calla	get_opp_plyrmode
;	cmpi	MODE_ONGROUND,a0
;	jrz	uijqout
;	cmpi	MODE_DEAD,a0
;	jrz	uijqout
;	move	*a13(PLYRMODE),a0
;	cmpi	MODE_RUNNING,a0
;	jrz	uijqleapclbr
;
;;If stick is toward opponent, then do sliding/leaping buzzer
;	move	*a13(STICK_VAL_CUR),a0
;	move	*a13(NEW_FACING_DIR),a1
;	andi	0ch,a1
;	cmp	a0,a1
;	jrnz	uijqnorm
;
;uijqleapclbr
;;missing noise for a reason !
;	WRSND	W_LUGER,HDBUTT_T1,HDBUTT_T2
;	FACE24	lex,clobber2_anim
;	calla	change_anim1
;	rets
;uijqnorm

	WRSND	W_LUGER,HDBUTT_T1,HDBUTT_T2
	FACE24	lex,clobber_anim
	calla	change_anim1

	rets

*******************************************************************************
uijqhammer
	.word	B_SKICK,			J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 32 		;max
	.long	scrt_hammer

scrt_hammer
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uijqout

	move	*a13(PLYRMODE),a0
	cmpi	MODE_ONTURNBKL,a0
	jrz	uijqout
	cmpi	MODE_OPPOVERHEAD,a0
	jreq	uijqout

	.ref	lex_hammer_anim
	movi	lex_hammer_anim,a0
	calla	change_anim1a

;missing noise for a reason !
	WRSND	W_LUGER,HIPTOSS_T1,PUNCH_T2

	rets

******************************************************************************
uijqneck_grab
	.word	B_SPUNCH,		J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 30		;max
	.long	uijqscrt_neck

uijqscrt_neck
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uijqout

;Don't do it when he is on ground
	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	uijqout
	cmpi	MODE_DEAD,a0
	jrz	uijqout
	cmpi	MODE_HEADHELD,a0
	jrz	uijqout
;Mode held overhead!
	cmpi	MODE_ATTACHED,a0
	jrz	uijqout

	move	*a13(LAST_HEADHOLD),a14,L
	move	@PCNT,a0,L
	sub	a14,a0
	cmpi	2*60,a0
	jrge	uijqok2

;This head grab is too close (in terms of time) to most recent grab
	.ref	lex_3_fake_hold_anim
	movi	lex_3_fake_hold_anim,a0
	calla	change_anim1a
	rets

uijqok2
	move	*a13(CLOSEST_XDIST),a1
	cmpi	90,a1
	jrgt	uijqstd_grab

	movi	lex_3_head_hold2_anim,a0
	calla	change_anim1a
uijqout	rets

uijqstd_grab
	movi	lex_3_head_hold_anim,a0
	calla	change_anim1a
	rets

*******************************************************************************

uijqgrab_fling2
	.word	B_SPUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
	.word	8000h | 10		;max
	.long	uijqscrt_grabfling2

uijqhip_toss2
	.word	B_PUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
	.word	8000h | 10		;max
	.long	uijqscrt_grabfling2

uijqscrt_grabfling2
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uijqout

;If opponent is running, do a hip toss
	calla	get_opp_plyrmode
	cmpi	MODE_BOUNCING,a0
	jrz	uijqyes
	cmpi	MODE_RUNNING,a0
	jrz	uijqyes
	rets

******************************************************************************
uijqgrab_fling
;	.word	B_SPUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
;	.word	8000h | 10		;max
;	.long	uijqscrt_grabfling

	.word	B_SPUNCH,		J_ALL
	.word	J_AWAY,		J_REAL_LR
	.word	J_AWAY,		J_REAL_LR
	.word	8000h | 32		;max
	.long	uijqscrt_grabfling

uijqscrt_grabfling

;Make this miss, rotate to missed opponent,
;and stay rotated to flung opponent, also make
;this work from both angles!

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uijqout

;If opponent is running, do a hip toss
	calla	get_opp_plyrmode
	cmpi	MODE_BOUNCING,a0
	jrz	uijqyes
	cmpi	MODE_RUNNING,a0
	jrnz	uijqnope
uijqyes
	movi	lex_hiptoss_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,HIPTOSS_T1,PUNCH_T2
	rets

uijqnope
;Don't fling when he is on ground
	calla	get_opp_plyrmode
	cmpi	MODE_HEADHELD,a0
	jrz	uijqout
	cmpi	MODE_ONGROUND,a0
	jrz	uijqout
	cmpi	MODE_DEAD,a0
	jrz	uijqout
;Mode held overhead!
	cmpi	MODE_ATTACHED,a0
	jrz	uijqout

;	move	*a13(LAST_FLING_ATTEMPT),a14,L
;	move	@PCNT,a0,L
;	sub	a14,a0
;	cmpi	3*60,a0
;	jrge	uijqok2a
;
;;This head grab is too close (in terms of time) to most recent grab
;	movi	lex_3_clobber_anim,a0
;;	FACE24	lex,punch_anim
;	calla	change_anim1a
;
;	WRSND	W_LUGER,HIPTOSS_T1,PUNCH_T2
;	rets
;
;uijqok2a
;	move	@PCNT,a14,L			;use 32-bit PCNT!
;	move	a14,*a13(LAST_FLING_ATTEMPT),L

	FACE24	lex,grabfling_anim
	calla	change_anim1a

	WRSND	W_LUGER,GRABFLING_T1,PUNCH_T2
	rets

*******************************************************************************
uijqhip_toss
;	.word	B_PUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
;	.word	8000h | 10		;max
;	.long	uijqscrt_hiptoss

	.word	B_PUNCH,		J_ALL
	.word	J_AWAY,		J_REAL_LR
	.word	J_AWAY,		J_REAL_LR
	.word	8000h | 32		;max
	.long	uijqscrt_hiptoss

uijqscrt_hiptoss
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uijqout

;Don't do it when he is on ground
	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	uijqout
;	cmpi	MODE_BLOCK,a0
;	jrz	uijqout
	cmpi	MODE_DEAD,a0
	jrz	uijqout
	cmpi	MODE_HEADHELD,a0
	jrz	uijqout
	cmpi	MODE_INAIR,a0
	jrz	uijqdoit
	cmpi	MODE_INAIR2,a0
	jrz	uijqdoit
;Mode held overhead!
	cmpi	MODE_ATTACHED,a0
	jrz	uijqout

	move	*a13(CLOSEST_DIST),a0
	cmpi	70h,a0
	jrgt	uijqout

uijqdoit
 	movi	lex_hiptoss_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	WRSND	W_LUGER,GRABFLING_T1,PUNCH_T2

	rets

*******************************************************************************
uijqsliding_elbow
	.word	B_PUNCH,		J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 30		;max
	.long	uijqscrt_elbow

uijqscrt_elbow
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	uijqout

	move	*a13(PLYRMODE),a0
	cmpi	MODE_OPPOVERHEAD,a0
	jreq	uijqout

;Don't do it when he is on ground
	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	uijqout
	cmpi	MODE_DEAD,a0
	jrz	uijqout

	;we can do this from a headhold, so...
	calla	FIND_AND_KILL_ENDLESS

 	movi	lex_sliding_elbow_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	WRSND	W_LUGER,UPRCUT_T1,UPRCUT_T2

	rets

	.if NUM_LEX_FINISHES
******************************************************************************
 SUBRP	lex_finish_move1

dyjbTIMEOUT	.equ	TSEC

dyjbend_wait
	SLEEP		TSEC/4
	move		@p1rounds,a0			; Get player 1 rounds
	move		@p2rounds,a1			; Get player 2 rounds
	or		a1,a0				; Put em together
	cmpi		2,a0				; Anybody got 2?
	jrlt		dyjbend_wait			; br = no

dyjbreset
	clr		a11
	move		a11,@in_finish_move
	SLEEPK		1
	WAITSWITCH_DWN	J_UP,0,dyjbreset
	movi		dyjbTIMEOUT,a11
	move		a11,@in_finish_move
	WAITSWITCH_DWN	J_DOWN,0,dyjbreset
	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,dyjbreset
	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,dyjbreset
	WAITSWITCH_DWN	B_PUNCH,J_ALL,dyjbreset

	movi		lex_finish1_move,a0
	move		a0,*a8(SPECIAL_MOVE_ADDR),L
	DIE
	.endif
	

	.if NUM_LEX_FINISHES > 1
******************************************************************************
 SUBRP	lex_finish_move2

duurTIMEOUT	.equ	TSEC

duurend_wait
	SLEEP		TSEC/4
	move		@p1rounds,a0			; Get player 1 rounds
	move		@p2rounds,a1			; Get player 2 rounds
	or		a1,a0				; Put em together
	cmpi		2,a0				; Anybody got 2?
	jrlt		duurend_wait			; br = no

duurreset
	clr		a11
	move		a11,@in_finish_move
	SLEEPK		1
	WAITSWITCH_DWN	J_UP,0,duurreset
	movi		duurTIMEOUT,a11
	move		a11,@in_finish_move
	WAITSWITCH_DWN	J_UP,0,duurreset
	WAITSWITCH_DWN	J_RIGHT,J_UP,duurreset
	WAITSWITCH_DWN	J_RIGHT,J_UP,duurreset
	WAITSWITCH_DWN	B_SPUNCH,J_ALL,duurreset

	movi		lex_finish2_move,a0
	move		a0,*a8(SPECIAL_MOVE_ADDR),L
	DIE
	.endif


*****************************************************************************
 SUBRP	lex_hdhold_pile

douiTIMEOUT	.equ	60

douilp0
	SLEEPK	1
douilp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	douicont
	cmpi	MODE_HEADHELD,a0
	jrnz	douilp0

douicont
	clr	a11

;	WAITSWITCH_DWN	J_AWAY,0,douilp
	WAITSWITCH_DWN	J_TOWARD,0,douilp

	movi	douiTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,douilp
	WAITSWITCH_DWN	B_SPUNCH,J_ALL,douilp
	
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	douislam
	cmpi	MODE_HEADHELD,a0
	jrnz	douilp0
	move	*a8(I_WILL_DIE),A14
	jrnz	douilp0

	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	douilp0				;ignore

	calla	DO_REVERSAL
	calla	DO_REVERSAL_MESS

	;target WHOHITME -- don't hit anyone else
	SMRTTGT	a8,WHOHITME

	;immobilize WHOHITME
	move	*a8(WHOHITME),a0,L
	jruc	douiis_reversal

douislam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	douilp0				;ignore

	movi	42,a10
	CREATE	MESSAGE_PID,BONUS_MESS

	;target WHOIHIT -- don't hit anyone else
	SMRTTGT	a8,WHOIHIT

	;immobilize WHOIHIT
	move	*a8(WHOIHIT),a0,L

douiis_reversal
	movk	15,a14
	move	a14,*a0(IMMOBILIZE_TIME)

	CALLA	FIND_AND_KILL_ENDLESS

	movi	lex_vsuplex_anim,a0
	move	a0,*a8(SPECIAL_MOVE_ADDR),L
	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	SLEEPK	20

	jruc	douilp

*****************************************************************************
 SUBRP	lex_hdhold_elbow_face

guyqTIMEOUT	.equ	60

guyqlp0
	SLEEPK	1
guyqlp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	guyqcont
	cmpi	MODE_HEADHELD,a0
	jrnz	guyqlp0

guyqcont
	clr	a11

;	WAITSWITCH_DWN	J_UP,0,guyqlp
	WAITSWITCH_DWN	J_DOWN,0,guyqlp

	movi	guyqTIMEOUT,a11

	WAITSWITCH_DWN	J_DOWN,0,guyqlp
	WAITSWITCH_DWN	B_SKICK,J_ALL,guyqlp
	
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	guyqslam
	cmpi	MODE_HEADHELD,a0
	jrnz	guyqlp0
	move	*a8(I_WILL_DIE),A14
	jrnz	guyqlp0

	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	guyqlp0				;ignore

	calla	DO_REVERSAL
	calla	DO_REVERSAL_MESS

	;target WHOHITME -- don't hit anyone else
	SMRTTGT	a8,WHOHITME

	;immobilize WHOHITME
	move	*a8(WHOHITME),a0,L

	.ref	lex_grab2elbow_rev_anim

	movi	lex_grab2elbow_rev_anim,a1
	jruc	guyqis_reversal

guyqslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	guyqlp0				;ignore

;fix !!! wrong name - jake
	movk	6,a10
	CREATE	MESSAGE_PID,BONUS_MESS

	;target WHOIHIT -- don't hit anyone else
	SMRTTGT	a8,WHOIHIT

	;immobilize WHOIHIT
	move	*a8(WHOIHIT),a0,L

;This stops Lex from going for foot, then changing his mind!
;	movk	15,a14
;	move	a14,*a8(IMMOBILIZE_TIME)

	movi	lex_grab2elbow_anim,a1
guyqis_reversal
	movk	15,a14
	move	a14,*a0(IMMOBILIZE_TIME)

	move	a1,*a8(SPECIAL_MOVE_ADDR),L
	CALLA	FIND_AND_KILL_ENDLESS

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	SLEEPK	20
	jruc	guyqlp

******************************************************************************
lex_grab_toss_air

ktbgTIMEOUT	.equ	40

ktbglp0
	SLEEPK	1
ktbglp
	clr	a11

	WAITSWITCH_DWN	J_AWAY,0,ktbglp

	movi	ktbgTIMEOUT,a11

	WAITSWITCH_DWN	J_AWAY,0,ktbglp
	WAITSWITCH_DWN	B_PUNCH,J_ALL,ktbglp

	move	*a8(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ktbglp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jreq	ktbglp0

;Don't do it when he is on ground
	SWAP	a8,a13
	calla	get_opp_plyrmode
	SWAP	a8,a13
;Don't do it when he is on ground
	cmpi	MODE_ONGROUND,a0
	jrz	ktbglp
	cmpi	MODE_DEAD,a0
	jrz	ktbglp
	cmpi	MODE_INAIR,a0
	jrz	ktbgdoit2
	cmpi	MODE_INAIR2,a0
	jrz	ktbgdoit2

	move	*a8(CLOSEST_NUM),a0
	X32	a0
	addi	process_ptrs,a0
	move	*a0,a0,L
	move	*a0(ATTACK_TYPE),a0
	cmpi	AT_LEAPING,a0
	jrz	ktbgdoit2

	move	*a8(CLOSEST_DIST),a0
	cmpi	6ch,a0
	jrgt	ktbglp

;	FACE24	lex,hiptoss_anim
	movi	lex_hiptoss_anim,a0
	move	a0,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,GRABFLING_T1,PUNCH_T2

	SLEEPK	20
	jruc	ktbglp

ktbgdoit2
;	FACE24	lex,hiptoss2_anim
	movi	lex_hiptoss2_anim,a0
	move	a0,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,GRABFLING_T1,PUNCH_T2

	SLEEPK	20

	jruc	ktbglp

*****************************************************************************
lex_hdhold_graboh

qjbtTIMEOUT	.equ	60

qjbtlp0
	SLEEPK	1
qjbtlp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	qjbtcont0
	cmpi	MODE_HEADHELD,a0
	jrnz	qjbtlp0
qjbtcont0
	clr	a11

	WAITSWITCH_DWN	J_DOWN,0,qjbtlp

	movi	qjbtTIMEOUT,a11

;	WAITSWITCH_DWN	J_DOWN_TOWARD,0,qjbtlp
	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,qjbtlp
	WAITSWITCH_DWN	B_KICK,J_ALL,qjbtlp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	qjbtdograb
	cmpi	MODE_HEADHELD,a0
	jrnz	qjbtlp0
	move	*a8(I_WILL_DIE),A14
	jrnz	qjbtlp0

	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	qjbtlp0				;ignore

	CALLA	DO_REVERSAL
	CALLA	DO_REVERSAL_MESS

	;target WHOHITME -- don't hit anyone else
	SMRTTGT	a8,WHOHITME

	;immobilize WHOHITME
	move	*a8(WHOHITME),a0,L
	jruc	qjbtis_reversal

qjbtdograb
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	qjbtlp0				;ignore

;	movk	6,a10
;	CREATE	MESSAGE_PID,BONUS_MESS

	;target WHOIHIT -- don't hit anyone else
	SMRTTGT	a8,WHOIHIT

	;immobilize WHOIHIT
	move	*a8(WHOIHIT),a0,L

qjbtis_reversal
	movk	15,a14
	move	a14,*a0(IMMOBILIZE_TIME)

	CALLA	FIND_AND_KILL_ENDLESS

	movi	lex_4_graboh_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,GRABHOLD_T1,GRABHOLD_T2

	SLEEPK	20

	jruc	qjbtlp

*******************************************************************************
*
lex_hdhold_combo1

wzggTIMEOUT	.equ	60

wzgglp0
	SLEEPK	1
wzgglp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrnz	wzgglp0
;	jrz	wzggcont
;	cmpi	MODE_HEADHELD,a0
;	jrnz	wzgglp0
;wzggcont
;In combo mode?
	calla	CHECK_COMBO_GO
	jrlt	wzgglp0

	clr	a11

	WAITSWITCH_DWN	J_TOWARD,0,wzgglp

	movi	wzggTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,wzgglp
	WAITSWITCH_DWN	B_SKICK,J_ALL,wzgglp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	wzggslam
	cmpi	MODE_HEADHELD,a0
	jrnz	wzgglp0

wzggslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	wzgglp0				;ignore

;Start combo rules here
;	move	*a8(ATTACH_PROC),a14,L
;	movk	15,a0
;	move	a0,*a14(IMMOBILIZE_TIME)

	;target WHOIHIT
	SMRTTGT	a8,WHOIHIT

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_combo_punch_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,UPRCUT_T1,UPRCUT_T2

	SLEEPK	20

	jruc	wzgglp

*******************************************************************************
*
lex_hdhold_combo2

alyeTIMEOUT	.equ	60

alyelp0
	SLEEPK	1
alyelp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrnz	alyelp0
;	jrz	alyecont
;	cmpi	MODE_HEADHELD,a0
;	jrnz	alyelp0
;alyecont
;In combo mode?
	calla	CHECK_COMBO_GO
	jrlt	alyelp0

	clr	a11

	WAITSWITCH_DWN	J_TOWARD,0,alyelp

	movi	alyeTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,alyelp
	WAITSWITCH_DWN	B_KICK,J_ALL,alyelp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	alyeslam
	cmpi	MODE_HEADHELD,a0
	jrnz	alyelp0

alyeslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	alyelp0				;ignore

;Start combo rules here
;	move	*a8(ATTACH_PROC),a14,L
;	movk	15,a0
;	move	a0,*a14(IMMOBILIZE_TIME)

	;target WHOIHIT
	SMRTTGT	a8,WHOIHIT

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_combo_kick_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,UPRCUT_T1,UPRCUT_T2

	SLEEPK	20

	jruc	alyelp

;******************************************************************************
;
;;Could start this process only when opponent is in combo mode!
;
;lex_hdhold_anti_combo
;
;alyeTIMEOUT	.equ	30
;
;alyelp0
;	SLEEPK	1
;alyelp
;;Is my opponent in combo mode?
;;	move	*a8(IMMOBILIZE_TIME),a14
;;	jrz	alyelp0
;;	move	*a8(CLOSEST_NUM),a0
;;	X32	a0
;;	addi	process_ptrs,a0
;;	move	*a0,a0,L
;;	move	*a0(COMBO_COUNT),a0
;;	jrz	alyelp0
;
;	clr	a11
;
;	WAITSWITCH_DWN	B_SPUNCH,J_ALL,alyelp
;
;	movk	alyeTIMEOUT,a11
;
;	WAITSWITCH_DWN	B_SPUNCH,J_ALL,alyelp
;	WAITSWITCH_DWN	B_SPUNCH,J_ALL,alyelp
;
;;Is my opponent in combo mode?
;	move	*a8(CLOSEST_NUM),a0
;	X32	a0
;	addi	process_ptrs,a0
;	move	*a0,a0,L
;	move	*a0(COMBO_COUNT),a0
;	jrz	alyelp0
;
;	move	*a8(ANTI_COMBO_TIME),a0		;Time stamp
;	move	@PCNT,a14
;	sub	a0,a14
;	abs	a14
;	cmpi	80,a14
;	jrge	alyelp0
;	move	*a8(I_WILL_DIE),A14
;	jrnz	alyelp0
;
;;Combo breaker message!
;	CALLA	DO_REVERSAL
;	CALLA	DO_REVERSAL_MESS
;
;	clr	a0
;	move	a0,*a8(IMMOBILIZE_TIME)
;
;	move	*a8(WHOHITME),a14,L
;	movk	15,a0
;	move	a0,*a14(IMMOBILIZE_TIME)
;
;	;target WHOHITME
;	SMRTTGT	a8,WHOHITME
;
;	CALLA	FIND_AND_KILL_ENDLESS
;
;	WRSND	W_LUGER,GRABHOLD_T1,GRABHOLD_T2
;	movi	lex_vsuplex_anim,a14
;	move	a14,*a8(SPECIAL_MOVE_ADDR),L
;
;	SLEEPK	20
;
;	jruc	alyelp
;
*****************************************************************************

 SUBR	lex_ani_init

	move	*a13(FACING_DIR),a0
	btst	PLAYER_RIGHT_BIT,a0
	jrnz	vodyp1
;Plyr 2
	movi	lex_stand4_anim,a0
	calla	change_anim1a

	movi	lex_torso4_anim,a0
	calla	change_anim2a

	PUSH	a10
	move	a13,a10
	CREATE	TAUNT_PID,do_taunt
	PULL	a10

	rets

vodyp1
	movi	lex_stand2_anim,a0
	calla	change_anim1a

	movi	lex_torso2_anim,a0
	calla	change_anim2a

	rets


*****************************************************************************
*
* LEX CONTROL CODE
*
* a13 = * Lex process

 SUBR	move_lex


;	movi	100h,a0			;temp fix!
;	move	a0,*a13(ANI_SPEED)

	movi	lex_secret_moves,a11
	calla	check_secret_moves

	move	*a13(PLYRMODE),a0
	sll	5,a0			;x 32
	addi	atlnmode_table,a0
	move	*a0,a0,L
	call	a0
	rets

atlnmode_table
	.long	mode_normal		;0
	.long	mode_running		;1
	.long	mode_inair		;2
	.long	mode_attached		;3
	.long	mode_onground		;4
	.long	mode_bouncing		;5
	.long	mode_onturnbkl		;6
	.long	mode_block		;7
	.long	mode_dizzy		;8
	.long	mode_dead		;9
	.long	mode_oppoverhead	;10
	.long	mode_climbturnbkl	;11
	.long	mode_waitanim		;12
	.long	mode_grapple		;13
	.long	mode_master		;14
	.long	mode_slave		;15
	.long	mode_headhold		;16
	.long	mode_puppet2		;17
	.long	mode_normal		;18
	.long	mode_headheld		;19
	.long	mode_puppet		;20
	.long	mode_inair2		;21
	.long	mode_normal		;22
	.long	mode_normal		;23
	.long	mode_chokehold		;24
	.long	mode_choking		;25

*****************************************************************************

mode_normal

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ezcpno_interrupt

	move	*a13(I_WILL_DIE),a0
	jrz	ezcpnope
	move	*a13(IMMOBILIZE_TIME),a0
	jrnz	ezcpnope
;Player has died amidst a combo...
;Knock me down.
	movi	lex_fall_back_anim,a0
	calla	change_anim1a

;Zero health bar
	movi	-10,a0
	move	*a13(PLYRNUM),a1
	calla	adjust_health

	SETMODE	DEAD

	clr	a0
	move	a0,*a13(I_WILL_DIE)

	rets

ezcpnope
;Perhaps we need to interrupt any sequence when a wrestler has won?

	calla	get_opp_plyrmode
	cmpi	MODE_DEAD,a0
	jrne	ezcpopp_not_dead

	;if closest is a zombie, skip pin section.
	calla	get_opp_process
	move	*a0(STATUS_FLAGS),a14
	btst	B_ZOMBIE,a14
	jrnz	ezcpopp_not_dead
	
	;all opponents are dead.  Check for pins/raisearms

	;if we're attached to someone, don't interrupt it.
	move	*a13(ATTACH_PROC),a1,L
	jrz	ezcpno_attch
	move	*a1(ATTACH_PROC),a2,L
	cmp	a2,a13
	jreq	ezcpno_pin_yet
ezcpno_attch

	;if a teammate has pinned, raise yer arm.
	calla	ck_teammate_pin	
	jrc	ezcpraisearm

	;nobody has pinned.  if we're outside or all our opponents are
	; outside, do a raisearm.
	calla	raisearm_check
	jrc	ezcpraisearm

	;we're inside with a pinnable opponent.  any button pins.
	move	*a13(BUT_VAL_CUR),a0
	jrz	ezcpno_pin_yet

	;we have a press -- make sure opponent is on the ground
	calla	can_pin
	jrnc	ezcpopp_notgnd

	;opponent is on the ground.  pin him.
	FACE24	lex,pin_anim
	calla	change_anim1a

	;set the 'I did a pin' flag.
	move	*a13(STATUS_FLAGS),a14
	ori	M_DID_PIN,a14
	move	a14,*a13(STATUS_FLAGS)

	;if we're a temp drone for auto-pin purposes, turn back into
	; a normal player here.
	calla	drone_change_back

	rets

ezcpraisearm
	FACE24	lex,raise_arm_anim
	calla	change_anim1a
	calla	set_raisearm_bit

	;if we're a temp drone for auto-pin purposes, turn back into
	; a normal player here.
	calla	drone_change_back

	rets

ezcptoo_far_to_pin
ezcpopp_notgnd
ezcpopp_not_dead
ezcpno_pin_yet

	move	*a13(IMMOBILIZE_TIME),a0
	jrnz	ezcpimmobilized

	move	*a13(BUT_VAL_CUR),a0
	btst	PLAYER_BLOCK_BIT,a0	;maybe have to action tables?
	jrz	ezcpnblk			;1 for down transitions
	callr	ezcpblock			;& 1 for current buttons
	jrc	ezcpnblk
	clr	a0
	move	a0,*a13(ATTACK_TYPE)
	rets
ezcpnblk
	move	*a13(BUT_VAL_CUR),a0
	andi	011111b,a0
	cmpi	PLAYER_PUNCH_VAL|PLAYER_KICK_VAL,a0
	jrz	ezcppunchkick

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	ezcpaction_table,a0
	move	*a0,a0,L
	call	a0

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	ezcpno_interrupt


	move	*a13(STICK_VAL_CUR),a0
	move	a0,*a13(MOVE_DIR)
	calla	climb_turnbuckle
	jrnc	ezcpno_climb
	move	*a13(WRESTLERNUM),A5
	MOVI	CLIMB_ROPES,A2
	MOVI	1000,A0
	CALLA	ADD_IF_SILENT
	rets

ezcpimmobilized
	clr	a14
	move	a14,*a13(MOVE_DIR)
ezcpno_climb
	calla	execute_walk

ezcpno_interrupt
	rets

ezcpaction_table
	.long	ezcpz,ezcppunch,ezcpblock,ezcpblock			;0-3
	.long	ezcpsuper_punch,ezcpsuper_punch,ezcpblock,ezcpblock	;4-7
	.long	ezcpkick,ezcppunchkick,ezcpblock,ezcpblock		;8-11
	.long	ezcpsuper_punch,ezcppunchkick,ezcpblock,ezcpblock	;12-15
	.long	ezcpsuper_kick,ezcpsuper_kick,ezcpblock,ezcpblock	;16-19
	.long	ezcpgraboh,ezcpgraboh,ezcpblock,ezcpblock		;20-23
	.long	ezcpsuper_kick,ezcppunchkick,ezcpblock,ezcpblock	;24-27
	.long	ezcpgraboh,ezcpgraboh,ezcpblock,ezcpblock		;28-31

************
ezcpz
	rets


************
ezcppunch		;1
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal	<  40  45	headbutt
* normal	>		normal punch
*
* onground	< 120 120	elbow drop
* onground	>		normal punch
*
* running |
* bouncing			normal punch
*
* onturnbkl	<  40  45	hdbutt
* onturnbkl	>		normal punch
*
* climbturnbkl			punch
*

	JJXM_INIT
	JJXM	NORMAL,		 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;0
	JJXM	RUNNING,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;1
	JJXM	INAIR,		 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;2
	JJXM	ATTACHED,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;3
	JJXM	ONGROUND,	160,140,ezcppunch_lbowdrop,ezcppunch_punch	;4
	JJXM	BOUNCING,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;1
	JJXM	ONTURNBKL,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;6
	JJXM	BLOCK,		 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;7
	JJXM	DIZZY,		 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;8
	JJXM	DEAD,		160,140,ezcppunch_lbowdrop,ezcppunch_punch	;9
	JJXM	OPPOVERHEAD,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;10
	JJXM	CLIMBTURNBKL,		ezcppunch_punch			;11
	JJXM	WAITANIM,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;12
	JJXM	GRAPPLE,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;13
	JJXM	MASTER,		 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;14
	JJXM	SLAVE, 		 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;15
	JJXM	HEADHOLD,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;16
	JJXM	HEADHELD,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;19
	JJXM	CHOKEHOLD,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;16
	JJXM	PUPPET,		 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;20
	JJXM	PUPPET2,	 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;20
	JJXM	INAIR2,		 40, 45,ezcppunch_hdbutt,ezcppunch_punch	;21
	JJXM_END

*****	
ezcppunch_punch
std_punch
	FACE24	lex,punch_anim
	calla	change_anim1a
	WRSND	W_LUGER,PUNCH_T1,PUNCH_T2
	rets

*****
ezcppunch_hdbutt
	FACE24	lex,butt_anim
	calla	change_anim1a
	WRSND	W_LUGER,HDBUTT_T1,HDBUTT_T2
	rets

*****
ezcppunch_lbowdrop
	FACE24	lex,ground_punch_anim
	calla	change_anim1a
	WRSND	W_LUGER,LBOWDROP_T1,LBOWDROP_T2
	rets


************
ezcpblock		;2
std_block
	move	@blocking_off,a0
	jrnz	ezcpno_blocking

	RND_AWARD	a13,BLOCKS_AWD

	movi	lex_4_block_anim,a0
	calla	change_anim1

	calla	BLOCK_WOOSH

	clr	a0
	move	a0,*a13(BLOCK_TIME)
	clrc
	rets

ezcpno_blocking
	setc
	rets


************
ezcpsuper_punch	;4
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal	< 110   75	close in super move
* dizzy			"		"
* normal	>		jumping punch move
* dizzy			"		"
*
* onground	< 136  112	super elbow drop
* onground	>		normal punch
*
* running			hip toss
* bouncing			nothing
*
* onturnbkl	<  60   96	yank down
* onturnbkl	>		normal punch
* climbturnbkl			normal punch
*
	JJXM_INIT
	JJXM	NORMAL,		 70, 45,ezcpspunch_special,ezcpspunch_slap	;0
	JJXM	RUNNING,	 40, 45,ezcpspunch_special,ezcpspunch_slap	;1
	JJXM	INAIR,		 40, 45,ezcpspunch_special,ezcpspunch_slap	;2
	JJXM	ATTACHED,	 40, 45,ezcpspunch_special,ezcpspunch_slap	;3
	JJXM	ONGROUND,	160,140,ezcpspunch_lbowdrop,std_punch	;4
	JJXM	BOUNCING,	110, 45,ezcpspunch_special,ezcpspunch_slap	;5
	JJXM	ONTURNBKL,	 	ezcpspunch_slap			;6
	JJXM	BLOCK,		 70, 45,ezcpspunch_special,ezcpspunch_slap	;7
	JJXM	DIZZY,		 40, 45,ezcpspunch_special,ezcpspunch_slap	;8
	JJXM	DEAD,		160,140,ezcpspunch_lbowdrop,std_punch	;9
	JJXM	OPPOVERHEAD,	110, 35,ezcpspunch_special,ezcpspunch_slap	;10
	JJXM	CLIMBTURNBKL,		std_punch			;11
	JJXM	WAITANIM,	 40, 45,ezcpspunch_special,ezcpspunch_slap	;12
	JJXM	GRAPPLE,	 40, 45,ezcpspunch_special,ezcpspunch_slap	;13
	JJXM	MASTER,		 40, 45,ezcpspunch_special,ezcpspunch_slap	;14
	JJXM	SLAVE, 		 40, 45,ezcpspunch_special,ezcpspunch_slap	;15
	JJXM	HEADHOLD,	 40, 45,ezcpspunch_special,ezcpspunch_slap	;16
	JJXM	HEADHELD,	 40, 45,ezcpspunch_special,ezcpspunch_slap	;19
	JJXM	CHOKEHOLD,	 40, 45,ezcpspunch_special,ezcpspunch_slap	;16
	JJXM	PUPPET,		 40, 45,ezcpspunch_special,ezcpspunch_slap	;20
	JJXM	PUPPET2,	 40, 45,ezcpspunch_special,ezcpspunch_slap	;20
	JJXM	INAIR2,		 40, 45,ezcpspunch_special,ezcpspunch_slap	;21
	JJXM_END

ezcpspunch_rets
	rets

*****
ezcpspunch_slap
	movi	lex_3_clobber_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,PUNCH_T1,PUNCH_T2
	rets

*****
ezcpspunch_special
;	move	*a13(CLOSEST_XDIST),a1
;	cmpi	65,a1
;	jrgt	ezcpspunch_slap

	move	*a13(CLOSEST_XDIST),a1
	cmpi	55,a1
	jrgt	std_punch

	.ref	lex_2_butts_anim
	.ref	lex_4_butts_anim

	FACE24	lex,butts_anim
	calla	change_anim1

	WRSND	W_LUGER,HDBUTT_T1,HDBUTT_T2

	rets

*****
ezcpspunch_lbowdrop

	move	*a13(CLOSEST_NUM),a14
	X32	a14
	addi	process_ptrs,a14
	move	*a14,a14,L
	move	*a14(PLYRMODE),a2
	cmpi	MODE_DEAD,a2
	jrz	ezcpno
	move	*a13(OBJ_XPOS),a1,L 
	move	*a14(OBJ_XPOS),a2,L
	sub	a2,a1
	abs	a1
	srl	16,a1
	cmpi	20h,a1	;28
 	jrlt	ezcpno

;At head or feet!

	move	*a14(OBJ_CONTROL),a14
	andi	M_FLIPH,a14
	move	*a13(OBJ_CONTROL),a0
	andi	M_FLIPH,a0
	cmp	a0,a14
	jrz	ezcpno

;At head!  Do hair pickup.

	FACE24	lex,hair_pickup_anim
	calla	change_anim1
	WRSND	W_LUGER,LBOWDROP_T1,LBOWDROP_T2
	rets

ezcpno
	FACE24	lex,ground_punch_anim
	calla	change_anim1
	WRSND	W_LUGER,LBOWDROP_T1,LBOWDROP_T2
	rets


************
ezcpkick		;8
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal	<  60   50	knee
*		>		normal kick
*
* onground	< 128  128	stomp
* onground	>		normal kick
*
* running |
* bouncing			big boot
*
* onturnbkl |
* climbturnbkl			normal kick
*


	JJXM_INIT
	JJXM	NORMAL,		 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	BLOCK,		 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	DIZZY,		 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	ONGROUND,	160,140,ezcpkick_stomp,ezcpkick_kick
	JJXM	RUNNING, 	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	BOUNCING, 	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	ONTURNBKL,	 	ezcpkick_kick
	JJXM	CLIMBTURNBKL,	 	ezcpkick_kick
	JJXM	INAIR,		 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	ATTACHED,	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	DEAD,		160,140,ezcpkick_stomp,ezcpkick_kick
	JJXM	OPPOVERHEAD,	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	WAITANIM,	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	GRAPPLE,	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	MASTER,		 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	SLAVE,		 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	HEADHOLD,	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	HEADHELD,	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	CHOKEHOLD,	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	PUPPET,		 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	PUPPET2,	 60, 50,ezcpkick_knee,ezcpkick_kick
	JJXM	INAIR2,		 	ezcpkick_TB
	JJXM_END

*****
ezcpkick_TB

	.ref	lex_kick_TB_anim
	movi	lex_kick_TB_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets
*****
ezcpkick_kick
std_kick

	FACE24	lex,kick_anim
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets

*****
ezcpkick_knee
std_knee

	movi	lex_4_knee_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets

*****
ezcpkick_stomp
std_stomp

	FACE24	lex,stomp_anim
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2

	rets

;*****
;ezcpkick_bigboot
;
;	FACE24	lex,kick_anim
;	calla	change_anim1a
;
;	WRSND	W_LUGER,KICK_T1,KICK_T2
;	rets


************
ezcppunchkick	;9

	movi	start_run_anim,a0
	calla	change_anim1a

	rets


************
ezcpsuper_kick	;16
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal	<  60   60	close in super move
* dizzy			"		"
* normal	>		jumping kick move
* dizzy			"		"
*
* onground	< 120  120	super stomp
* onground	>		normal kick
*
* running			big boot
* bouncing			big boot
*
* onturnbkl	<  60   96	jumping kick move
* climbturnbkl		"		"
* onturnbkl	>		normal kick
* climbturnbkl		"		"
*
	JJXM_INIT
	JJXM	NORMAL,		 60, 60,ezcpskick_special,ezcpskick_kick	;0
	JJXM	RUNNING,	 	ezcpskick_bigboot			;1
	JJXM	INAIR,		 60, 60,ezcpskick_special,ezcpskick_kick	;2
	JJXM	ATTACHED,	 60, 60,ezcpskick_special,ezcpskick_kick	;3
	JJXM	ONGROUND,	160,140,std_stomp,std_kick		;4
	JJXM	BOUNCING,	 	ezcpskick_bigboot			;5
	JJXM	ONTURNBKL,	 60, 96,ezcpskick_kick,std_kick		;6
	JJXM	BLOCK,		 60, 60,ezcpskick_special,ezcpskick_kick	;7
	JJXM	DIZZY,		 60, 60,ezcpskick_special,ezcpskick_kick	;8
	JJXM	DEAD,		160,140,std_stomp,std_kick		;9
	JJXM	OPPOVERHEAD,	 60, 60,ezcpskick_special,ezcpskick_kick	;10
	JJXM	CLIMBTURNBKL,	 60, 96,ezcpskick_kick,std_kick		;11
	JJXM	WAITANIM,	 60, 62,ezcpskick_special,ezcpskick_kick	;12
	JJXM	GRAPPLE,	 60, 62,ezcpskick_special,ezcpskick_kick	;13
	JJXM	MASTER,		 60, 62,ezcpskick_special,ezcpskick_kick	;14
	JJXM	SLAVE, 		 60, 62,ezcpskick_special,ezcpskick_kick	;15
	JJXM	HEADHOLD,	 60, 62,ezcpskick_special,ezcpskick_kick	;16
	JJXM	HEADHELD,	 	std_kick
	JJXM	CHOKEHOLD,	 60, 62,ezcpskick_special,ezcpskick_kick	;16
	JJXM	PUPPET,		 60, 62,ezcpskick_special,ezcpskick_kick	;20
	JJXM	PUPPET2,	 60, 62,ezcpskick_special,ezcpskick_kick	;20
	JJXM	INAIR2,		 	ezcpkick_TB
	JJXM_END

*****
ezcpskick_kick
	movi	lex_super_kick_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2
	rets


*****
ezcpskick_special
	move	*a13(STICK_VAL_CUR),a0
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a0,a1
	jrz	ezcpcont2

	movi	lex_4_knee_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2

	rets

ezcpcont2
	movi	lex_4_knee_fall_anim,a0
	calla	change_anim1

	WRSND	W_LUGER,GRABHOLD_T1,GRABHOLD_T2
	rets

*****
ezcpskick_bigboot	;we can probably get away with just a ezcp4 big boot

	movi	lex_4_bigboot_anim,a0		;*****
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	rets

************
ezcpgraboh		;20

	movi	lex_4_graboh_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,GRABHOLD_T1,GRABHOLD_T2

	rets


*****************************************************************************
*
* RUNNING (if GETUP_TIME > 0 then player is out of control)
*

hhbqXRUN_VAL	equ	LEX_XRUN
hhbqZDRIFT_VAL	equ	LEX_ZDRIFT

mode_running

	move	*a13(RUN_TIME),a0		;Used for flying kick
	addk	1,a0
	move	a0,*a13(RUN_TIME)

	move	*a13(USR_VAR1),a0
	jrnz	hhbqno_vel

	calla	bounce_off_ropes

	movi	hhbqXRUN_VAL,a0
	move	*a13(MOVE_DIR),a14
	btst	PLAYER_RIGHT_BIT,a14
	jrnz	hhbqpos
	neg	a0
hhbqpos
	move	@hyper_speed_on,a14
	sll	a14,a0
	move	a0,*a13(OBJ_XVEL),L

hhbqno_vel

	move	*a13(GETUP_TIME),a0		;delay b4 gaining control
	jrnz	hhbqout_of_control

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	hhbqno_interrupt

	move	*a13(STICK_VAL_CUR),a0
	move	*a13(MOVE_DIR),a1
	or	a0,a1
	andi	MOVE_LEFT|MOVE_RIGHT,a1
	cmpi	MOVE_LEFT|MOVE_RIGHT,a1
	jrne	hhbqcontinue_run

	;braking with stick.  abort run.

	SETMODE	NORMAL

hhbqcontinue_run
hhbqno_interrupt

	move	*a13(STICK_VAL_CUR),a0
	clr	a1

	movi	-hhbqZDRIFT_VAL,a1
	btst	PLAYER_UP_BIT,a0
	jrnz	hhbqsetvel

	movi	hhbqZDRIFT_VAL,a1
	btst	PLAYER_DOWN_BIT,a0
	jrnz	hhbqsetvel

	clr	a1
hhbqsetvel
	move	a1,*a13(OBJ_ZVEL),L

;Delay reading buttons after just getting out of an out of control run
;so a mistaken move isn't fired off
	move	*a13(DELAY_BUTNS),a0
	jrnz	hhbqout_of_control

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	hhbqaction_table,a0
	move	*a0,a0,L
	call	a0
hhbqout_of_control
	rets

hhbqaction_table
	.long	hhbqz,hhbqpunch,hhbqblock,hhbqblock			;0-3
	.long	hhbqsuper_punch,hhbqsuper_punch,hhbqblock,hhbqblock	;4-7
	.long	hhbqkick,hhbqpunchkick,hhbqblock,hhbqblock		;8-11
	.long	hhbqsuper_punch,hhbqpunchkick,hhbqblock,hhbqblock	;12-15
	.long	hhbqsuper_kick,hhbqsuper_kick,hhbqblock,hhbqblock	;16-19
	.long	hhbqgraboh,hhbqgraboh,hhbqblock,hhbqblock		;20-23
	.long	hhbqsuper_kick,hhbqpunchkick,hhbqblock,hhbqblock	;24-27
	.long	hhbqgraboh,hhbqgraboh,hhbqblock,hhbqblock		;28-31

************
hhbqz
	rets


************
hhbqpunch		;1
hhbqsuper_punch	;4
hhbqpunchkick
hhbqgraboh
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal			flying clothesline
* dizzy					"
*
* onground	< 176 176	flying ground punch
* onground	>		nothing
*
* running			flying clothesline
* bouncing			flying clothesline
*
* onturnbkl			flying clothesline
* climbturnbkl			flying clothesline
*
	JJXM_INIT
	JJXM	NORMAL,			hhbqpunch_clothesline
	JJXM	BLOCK,			hhbqpunch_clothesline
	JJXM	DIZZY,			hhbqpunch_clothesline
	JJXM	ONGROUND,	176,176,hhbqpunch_bellyflop,hhbqpunch_rets
	JJXM	RUNNING,		hhbqpunch_clothesline
	JJXM	BOUNCING,		hhbqpunch_clothesline
	JJXM	ONTURNBKL,		hhbqpunch_clothesline
	JJXM	CLIMBTURNBKL,		hhbqpunch_clothesline
	JJXM	INAIR,			hhbqpunch_clothesline
	JJXM	ATTACHED,		hhbqpunch_clothesline
	JJXM	ONGROUND,	176,176,hhbqpunch_bellyflop,hhbqpunch_rets
	JJXM	OPPOVERHEAD,		hhbqpunch_clothesline
	JJXM	WAITANIM,		hhbqpunch_clothesline
	JJXM	GRAPPLE,		hhbqpunch_clothesline
	JJXM	MASTER,			hhbqpunch_clothesline
	JJXM	SLAVE,			hhbqpunch_clothesline
	JJXM	HEADHOLD,		hhbqpunch_clothesline
	JJXM	HEADHELD,		hhbqpunch_clothesline
	JJXM	CHOKEHOLD,		hhbqpunch_clothesline
	JJXM	PUPPET,			hhbqpunch_clothesline
	JJXM	PUPPET2,		hhbqpunch_clothesline
	JJXM	INAIR2,			hhbqpunch_clothesline
	JJXM_END

hhbqpunch_rets
	rets

*****
hhbqpunch_clothesline
;TODO - fix this
	;HACK!!!
	jruc	hhbqkick_flyingkick

	rets

*****
hhbqpunch_bellyflop
attack_bellyflop

	movi	lex_flying_ground_punch_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2
	rets


************
hhbqblock		;2

	move	*a13(OBJ_XVEL),a0,L
	sra	1,a0
	move	a0,*a13(OBJ_XVEL),L
	SETMODE	NORMAL
	jruc	std_block
	rets

************
hhbqkick		;8
hhbqsuper_kick	;16
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal			flying kick
* dizzy					"
*
* onground	< 176 176	flying ground punch
* onground	>		nothing
*
* running			flying kick
* bouncing			flying kick
*
* onturnbkl			flying kick
* climbturnbkl			flying kick
*
	JJXM_INIT
	JJXM	NORMAL,			hhbqkick_flyingkick
	JJXM	BLOCK,			hhbqkick_flyingkick
	JJXM	DIZZY,			hhbqkick_flyingkick
	JJXM	ONGROUND,	176,176,attack_bellyflop,hhbqkick_rets
	JJXM	RUNNING,		hhbqkick_flyingkick
	JJXM	BOUNCING,		hhbqkick_flyingkick
	JJXM	ONTURNBKL,		hhbqkick_flyingkick
	JJXM	CLIMBTURNBKL,		hhbqkick_flyingkick
	JJXM	INAIR,			hhbqkick_flyingkick
	JJXM	ATTACHED,		hhbqkick_flyingkick
	JJXM	DEAD,		176,176,attack_bellyflop,hhbqkick_rets
	JJXM	OPPOVERHEAD,		hhbqkick_flyingkick
	JJXM	WAITANIM,		hhbqkick_flyingkick
	JJXM	GRAPPLE,		hhbqkick_flyingkick
	JJXM	MASTER,			hhbqkick_flyingkick
	JJXM	SLAVE,			hhbqkick_flyingkick
	JJXM	HEADHOLD,		hhbqkick_flyingkick
	JJXM	HEADHELD,		hhbqkick_flyingkick
	JJXM	CHOKEHOLD,		hhbqkick_flyingkick
	JJXM	PUPPET,			hhbqkick_flyingkick
	JJXM	PUPPET2,		hhbqkick_flyingkick
	JJXM	INAIR2,			hhbqkick_flyingkick
	JJXM_END

hhbqkick_rets
	rets

*****
hhbqkick_flyingkick

	calla	ck_ignore
	jrc	hhbqz

	SETMODE	INAIR

	movi	lex_flying_kick_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	rets


*****************************************************************************
*

mode_inair	;2

	rets

*****************************************************************************
*

mode_attached	;3

	calla	keep_attached
	jrnz	ifqvstill_attached

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	movi	MODE_NORMAL,a0
	move	a0,*a13(ANIMODE)

ifqvstill_attached
	rets

*****************************************************************************

mode_onground	;4

;	move	*a13(GETUP_TIME),a0
;	jrnz	umicexit
;
;	move	*a13(ANIBASE),a0,L
;	cmpi	lex_faceup_getup_anim,a0
;	jrz	umicexit
;
;	calla	do_roll
;	jrnz	umicexit			;did roll
;
;	movi	lex_faceup_getup_anim,a0
;
;	calla	change_anim1a
;
;umicexit
	rets


*****************************************************************************
*
* bouncing off ropes while running

mode_bouncing	;5

	clr	a0
	move	a0,*a13(OBJ_XVEL),L
	move	a0,*a13(OBJ_ZVEL),L

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	iontnot_ended

	move	*a13(MOVE_DIR),a0
	xori	MOVE_LEFT+MOVE_RIGHT,a0	;flip LEFT/RIGHT
	move	a0,*a13(MOVE_DIR)

	move	*a13(NEW_FACING_DIR),a1
	andi	MOVE_UP+MOVE_DOWN,a1
	or	a0,a1			;LEFT or RIGHT
	move	a1,*a13(FACING_DIR)	;face same dir as run

	movi	lex_run2_anim,a0
	calla	change_anim1a

	SETMODE	RUNNING

;	clr	a0
;	move	a0,*a13(GETUP_TIME)	;in control

iontnot_ended
	rets

*****************************************************************************
*
* STANDING ON TOP OF TURNBUCKLE

mode_onturnbkl	;6

;	;check unint
;	move	*a13(ANIMODE),a14
;	btst	MODE_UNINT_BIT,a14
;	jrnz	tlecno_interrupt

	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_DOWN_BIT,a0
	jrz	tlecno_climbdown

	movi	lex_climb_down_anim,a0
	calla	change_anim1a

	SETMODE	CLIMBTURNBKL

tlecno_interrupt
	rets

tlecno_climbdown
	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	jrz	tlecz

	X32	a0
	addi	tlecaction_table,a0
	move	*a0,a0,L
	call	a0

	move	*a13(PLYRNUM),a5
	movi	JUMP_ROPES,a2
	movi	1000,a0
	calla	ADD_IF_SILENT

	rets


;For standing on top of turnbuckles

tlecaction_table
	.long	tlecz,tlecpunch,tlecblock,tlecblock			;0-3
	.long	tlecsuper_punch,tlecsuper_punch,tlecblock,tlecblock	;4-7
	.long	tleckick,tlecpunchkick,tlecblock,tlecblock		;8-11
	.long	tlecsuper_punch,tlecpunchkick,tlecblock,tlecblock	;12-15
	.long	tlecsuper_kick,tlecsuper_kick,tlecblock,tlecblock	;16-19
	.long	tlecgraboh,tlecgraboh,tlecblock,tlecblock		;20-23
	.long	tlecsuper_kick,tlecpunchkick,tlecblock,tlecblock	;24-27
	.long	tlecgraboh,tlecgraboh,tlecblock,tlecblock		;28-31

;	.long	tlecz			;0
;	.long	tlecpunch			;1
;	.long	tlecblock			;2
;	.long	tlecz			;3
;	.long	tlecsuper_punch		;4
;	.long	tlecz,tlecz,tlecz		;5-7
;	.long	tleckick			;8
;	.long	tlecz,tlecz,tlecz,tlecz,tlecz,tlecz,tlecz	;9-15
;	.long	tlecsuper_kick		;16
;	.long	tlecz,tlecz,tlecz,tlecz,tlecz,tlecz,tlecz	;17-23
;	.long	tlecz,tlecz,tlecz,tlecz,tlecz,tlecz,tlecz,tlecz	;24-31

************
tlecz
tlecblock
	rets

************
tlecpunch		;1
tlecsuper_punch	;4
tlecpunchkick	;5
tleckick		;8
tlecsuper_kick	;16
tlecgraboh		;20

	SETMODE	INAIR

	movi	lex_buckle_leap_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2
	rets

*****************************************************************************
* blocking

mode_block	;7

	move	*a13(BLOCK_TIME),a0
	inc	a0
	move	a0,*a13(BLOCK_TIME)
	cmpi	160,a0
	jrlt	ukxsno

	calla	get_opp_plyrmode
	cmpi	MODE_BLOCK,a0
	jrnz	ukxsno
	move	*a13(CLOSEST_XDIST),a0
	cmpi	61h,a0
	jrge	ukxsno
	cmpi	45,a0
	jrlt	ukxsno
	move	*a13(CLOSEST_ZDIST),a0
	cmpi	30,a0
	jrlt	ukxspunchblock

ukxsno
	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	jrz	ukxsno_interrupt

	;press.  if block isn't down, let mode_normal handle it
	move	a0,a2			;save a0 from ...val_down
	move	*a13(BUT_VAL_CUR),a0
	btst	PLAYER_BLOCK_BIT,a0
	jrz	ukxsset_mode_normal
	move	a2,a0			;restore a0 from ...val_down

	X32	a0
	addi	ukxsaction_table,a0
	move	*a0,a0,L
	call	a0

ukxsno_interrupt
	rets

ukxsset_mode_normal
	SETMODE	NORMAL
	jruc	mode_normal

ukxsaction_table
	.long	ukxsz,ukxspunchblock,ukxsz	;0-2
	.long	ukxspunchblock		;3
	.long	ukxsz,ukxsz,ukxsz,ukxsz,ukxsz,ukxsz,ukxsz	;4-10
	.long	ukxsz,ukxsz,ukxsz,ukxsz,ukxsz,ukxsz,ukxsz	;11-17
	.long	ukxsz,ukxsz,ukxsz,ukxsz,ukxsz,ukxsz,ukxsz	;18-24
	.long	ukxsz,ukxsz,ukxsz,ukxsz,ukxsz,ukxsz,ukxsz	;25-31

************
ukxsz
	rets

************
ukxspunchblock	;3
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* all				push
*

;This push has no collisions set!

	SETMODE	NORMAL

	movi	lex_4_push_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,PUSH_T1,PUSH_T2

	rets


*****************************************************************************
* doink with zero health

mode_dizzy	;8

;	move	*a13(GETUP_TIME),a0
;	jrnz	bbrostill_going
;
;	calla	clear_damage_log
;
;	clr	a0
;	move	a0,*a13(STARS_FLAG)
;
;	SETMODE	NORMAL
;
;bbrostill_going
	rets

*****************************************************************************
* opponent held overhead

mode_oppoverhead ;10

	move	*a13(ATTACH_PROC),a2,L		;proc attached to
	jrz	rrmcnot_attached
	move	*a2(ATTACH_PROC),a0,L
	jrnz	rrmcstill_attached

rrmcnot_attached

	move	*a13(ANIMODE),a0		;* remove this????
	btst	MODE_UNINT_BIT,a0		;*
	jrnz	rrmcno_interrupt			;*

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	CALLA	FIND_AND_KILL_ENDLESS
	SETMODE	NORMAL

	movi	MODE_NORMAL,a0
	move	a0,*a13(ANIMODE)

	rets

rrmcstill_attached
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	rrmcno_interrupt

	move	*a13(FACING_DIR),a0
	andni	MOVE_UP,a0
	ori	MOVE_DOWN,a0
	move	a0,*a13(FACING_DIR)
	move	a0,*a13(NEW_FACING_DIR)

	move	*a13(STICK_VAL_CUR),a0
	jrz	rrmcstand

	move	a0,*a13(MOVE_DIR)
	calla	execute_walk

	.ref	lex_holdoh_anim
	movi	lex_holdoh_anim,a0
	.ref	change_anim2
	calla	change_anim2
 	jruc	rrmcck_butns
rrmcstand
	clr	a0
	move	a0,*a13(MOVE_DIR)
	move	a0,*a13(OBJ_XVEL),L
	move	a0,*a13(OBJ_ZVEL),L

	.ref	lex_stndholdoh_anim
	movi	lex_stndholdoh_anim,a0
	calla	change_anim1

rrmcck_butns

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	rrmcaction_table,a0
	move	*a0,a0,L
	call	a0

rrmcno_interrupt
	rets

rrmcaction_table
	.long	rrmcz,rrmcpunch,rrmcblock,rrmcblock			;0-3
	.long	rrmcsuper_punch,rrmcsuper_punch,rrmcblock,rrmcblock	;4-7
	.long	rrmckick,rrmcpunchkick,rrmcblock,rrmcblock		;8-11
	.long	rrmcsuper_punch,rrmcpunchkick,rrmcblock,rrmcblock	;12-15
	.long	rrmcsuper_kick,rrmcsuper_kick,rrmcblock,rrmcblock	;16-19
	.long	rrmcgraboh,rrmcgraboh,rrmcblock,rrmcblock		;20-23
	.long	rrmcsuper_kick,rrmcpunchkick,rrmcblock,rrmcblock	;24-27
	.long	rrmcgraboh,rrmcgraboh,rrmcblock,rrmcblock		;28-31

;three moves from headhold: backbreaker, slamdown, and ohslam.

************
rrmcz
	rets

************
rrmcpunch		;1
rrmcpunchkick
rrmcblock		;2
rrmckick		;8

;	move	*a13(STICK_VAL_CUR),a0
;	btst	PLAYER_DOWN_BIT,a0
;	jrz	rrmcz

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_ohslam_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,PUNCH_T1,PUNCH_T2

	rets

************
rrmcsuper_punch	;4
rrmcgraboh

	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_UP_BIT,a0
	jrz	rrmcpunch

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_slamdown_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,PUNCH_T1,PUNCH_T2

	rets

************
rrmcsuper_kick	;16

	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_UP_BIT,a0
	jrz	rrmcpunch

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_backbreaker_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,HIPTOSS_T1,PUNCH_T2

	rets

*****************************************************************************
*
* CLIMBING UP OR DOWN THE TURNBUCKLE

mode_climbturnbkl	;11

	rets


*****************************************************************************

mode_waitanim	;12

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	nhjlnot_ended

	move	*a13(CODE_ADDR),a0,L
	call	a0

nhjlnot_ended
	rets


*****************************************************************************

mode_grapple	;13

	rets


*****************************************************************************

mode_master	;14

	calla	master_keep_attached	;keep slave attached to me

	rets

*****************************************************************************

mode_slave	;15
	rets

*****************************************************************************

mode_headhold	;16

;Bozo power move
	.ref	bozo_check
	calla	bozo_check
	jrnc	eaabfail

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	movi	lex_vsuplex_anim,a0
	move	@PCNT,a14
	btst	0,a14
	jrz	eaabtag
	movi	lex_4_graboh_anim,a0
eaabtag	calla	change_anim1a

	rets

eaabfail
	move	*a13(WHOIHIT),a0,L
	move	*a0(PLYRMODE),a0
	cmpi	MODE_HEADHELD,a0
	jrnz	eaabexit

;	move	*a13(ATTACH_PROC),a10,L
;	jrz	eaabexit

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	eaabno_interrupt

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	eaabaction_table,a0
	move	*a0,a0,L
	call	a0

eaabno_interrupt
	rets

eaabexit
	move	*a13(OBJ_ZPOS),a14,L
	subi	[6,0],a14
	move	a14,*a13(OBJ_ZPOS),L

	movk	MOVE_DOWN_RIGHT,a1
	move	*a13(OBJ_CONTROL),a0
	btst	B_FLIPH,a0
	jrz	eaabright
	movk	MOVE_DOWN_LEFT,a1
eaabright	move	a1,*a13(FACING_DIR)
	move	a1,*a13(NEW_FACING_DIR)

	SETMODE	NORMAL

	rets

eaabaction_table
	.long	eaabz,eaabpunch,eaabblock,eaabblock			;0-3
	.long	eaabsuper_punch,eaabsuper_punch,eaabblock,eaabblock	;4-7
	.long	eaabkick,eaabpunchkick,eaabblock,eaabblock		;8-11
	.long	eaabsuper_punch,eaabpunchkick,eaabblock,eaabblock	;12-15
	.long	eaabsuper_kick,eaabsuper_kick,eaabblock,eaabblock	;16-19
	.long	eaabgraboh,eaabgraboh,eaabblock,eaabblock		;20-23
	.long	eaabsuper_kick,eaabpunchkick,eaabblock,eaabblock	;24-27
	.long	eaabgraboh,eaabgraboh,eaabblock,eaabblock		;28-31


************
eaabblock
eaabgraboh
eaabz		;0
	rets

************

eaabpunch		;1
	CALLA	FIND_AND_KILL_ENDLESS
;If stick is toward opponent, then do quick punches
	move	*a13(STICK_VAL_CUR),a0
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a0,a1
	jrnz	eaabtag2
;TODO - change this to uppercuts_to_head_anim!
	WRSND	W_LUGER,KICK_T1,KICK_T2

	.ref	lex_4_knee2_anim
	movi	lex_4_knee2_anim,a0
	calla	change_anim1a
	rets

eaabtag2
;TODO - change this to an uppercut
	WRSND	W_LUGER,KICK_T1,KICK_T2
	movi	lex_4_knee2_anim,a0
	calla	change_anim1a
	rets

************
eaabsuper_punch	;4
	rets

eaabpunchkick
eaabkick
	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_3_knee_to_head_anim,a0
	calla	change_anim1a
	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets

eaabsuper_kick	;16
;If stick is toward opponent, then do quick knees
	move	*a13(STICK_VAL_CUR),a0
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a0,a1
	jrnz	eaabkick

;Quick knees
	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_knees_to_head_anim,a0
	calla	change_anim1a
	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets


*****************************************************************************
mode_puppet2	;17

;	;check for the head-grab breaker
;	move	*a13(BUT_VAL_DOWN),a0
;	btst	PLAYER_KICK_BIT,a0
;	jrz	andwout
;
;	;check the time - must do it within 20 ticks
;	move	*a13(HEAD_GRAB_TIME),a14
;	move	@PCNT,a0
;	sub	a14,a0
;	cmpi	KICK_OUT,a0
;	jrgt	andwout
;
;	move	*a13(PLYR_TYPE),a14
;	cmpi	PTYPE_DRONE,a14
;	jrz	andwout
;     
;	;okay, waste 'im. - detach, setmode, immobilize, and launch the knee
;	move	*a13(ATTACH_PROC),a0,L
;	clr	a14
;	move	a14,*a13(ATTACH_PROC),L
;	move	a14,*a0(ATTACH_PROC),L
;	movi	15,a14
;	move	a14,*a0(IMMOBILIZE_TIME)
;	SETMODE	NORMAL
;	jruc	std_knee
;
;andwout
	rets

*****************************************************************************

mode_headheld	;19
	move	*a13(ANIMODE),a0
	btst	MODE_NOGRAVITY_BIT,a0
	janz	mode_choking


;Bozo reversal
	calla	bozo_check
	jrnc	eilhfail

	PUSH	a8
	move	a13,a8
	CALLA	DO_REVERSAL
	CALLA	DO_REVERSAL_MESS
	PULL	a8

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	movi	lex_vsuplex_anim,a0
	move	@PCNT,a14
	btst	0,a14
	jrz	eilhtag
;	movi	lex_4_graboh_anim,a0
eilhtag	calla	change_anim1a

	rets
eilhfail
;He may have let me go by hitting his block butn!
	move	*a13(ATTACH_PROC),a0,L
	jrnz	eilhno_luck

;Make sure this guy is on ground
	move	*a13(OBJ_YPOSINT),a0
	move	*a13(GROUND_Y),a1
	cmp	a1,a0			;a0-a1
	jrgt	eilhno_luck

;hit_gnd

	movi	lex_3_head_held_stand_anim,a0
	calla	change_anim1a

eilhno_luck
	rets

*****************************************************************************
mode_chokehold	;24
	rets

*****************************************************************************

;dmscVEL	equ	30000h		;38000h	;30000h
;dmscDVEL	equ	21f0eh		;27978h	;21f0eh
;dmscVEL	equ	38000h		;38000h	;30000h
;dmscDVEL	equ	30000h		;27978h	;21f0eh
dmscVEL	equ	3a000h		;38000h	;30000h
dmscDVEL	equ	31000h		;27978h	;21f0eh


 SUBR lex_velocity_table

;		X,	Z

	.long	0,	-dmscVEL	;0 UP

	.long	dmscDVEL,	-dmscDVEL	;1 UP-RIGHT

	.long	dmscVEL,	0	;2 RIGHT

	.long	dmscDVEL,	dmscDVEL	;3 DOWN-RIGHT

	.long	0,	dmscVEL	;4 DOWN

	.long	-dmscDVEL,	dmscDVEL	;5 DOWN-LEFT

	.long	-dmscVEL,	0	;6 LEFT

	.long	-dmscDVEL,	-dmscDVEL	;7 UP-LEFT


*****************************************************************************

 SUBR lex_rotate_anims_table
; old = up right
	.long	lex_stand2_anim		;new = up right
	.long	lex_2_to_4_turn_anim	;    = down right
	.long	lex_2_to_6_turn_anim	;    = down left
	.long	lex_2_to_8_turn_anim	;    = up left

; old = down right
	.long	lex_4_to_2_turn_anim	;new = up right
	.long	lex_stand4_anim		;    = down right
	.long	lex_4_to_6_turn_anim	;    = down left
	.long	lex_4_to_8_turn_anim	;    = up left

; old = down left
	.long	lex_6_to_2_turn_anim	;new = up right
	.long	lex_6_to_4_turn_anim	;    = down right
	.long	lex_stand6_anim		;    = down left
	.long	lex_6_to_8_turn_anim	;    = up left

; old = up left
	.long	lex_8_to_2_turn_anim	;new = up right
	.long	lex_8_to_4_turn_anim	;    = down right
	.long	lex_8_to_6_turn_anim	;    = down left
	.long	lex_stand8_anim		;    = up left


 SUBR lex_leg_anims_table
;  MOVE
;(dnsq1 - UP)				FACING
	.long	lex_walk1_f2_anim	;1 (UP)
	.long	lex_walk1_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk1_f4_anim	;3 (RIGHT)
	.long	lex_walk1_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk1_f4_anim	;5 (DOWN)
	.long	lex_walk1_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk1_f2_anim	;7 (LEFT)
	.long	lex_walk1_f2_anim	;8 (UP-LEFT)

;(dnsq2 - UP-RIGHT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk2_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk2_f2_anim	;3 (RIGHT)
	.long	lex_walk2_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk8_f4_anim	;5 (DOWN)
	.long	lex_walk8_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk4_f2_anim	;7 (LEFT)
	.long	lex_walk4_f2_anim	;8 (UP-LEFT)

;(dnsq3 - RIGHT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk2_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk2_f2_anim	;3 (RIGHT)
	.long	lex_walk4_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk4_f4_anim	;5 (DOWN)
	.long	lex_walk8_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk6_f2_anim	;7 (LEFT)
	.long	lex_walk6_f2_anim	;8 (UP-LEFT)


;(dnsq4 - DOWN-RIGHT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk8_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk4_f4_anim	;3 (RIGHT)
	.long	lex_walk4_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk2_f4_anim	;5 (DOWN)
	.long	lex_walk6_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk2_f2_anim	;7 (LEFT)
	.long	lex_walk6_f2_anim	;8 (UP-LEFT)

;(dnsq5 - DOWN)
	.long	lex_walk5_f2_anim	;1 (UP)
	.long	lex_walk5_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk5_f4_anim	;3 (RIGHT)
	.long	lex_walk5_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk5_f4_anim	;5 (DOWN)
	.long	lex_walk5_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk5_f2_anim	;7 (LEFT)
	.long	lex_walk5_f2_anim	;8 (UP-LEFT)

;(dnsq6 - DOWN-LEFT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk6_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk2_f2_anim	;3 (RIGHT)
	.long	lex_walk6_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk2_f4_anim	;5 (DOWN)
	.long	lex_walk4_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk2_f2_anim	;7 (LEFT)
	.long	lex_walk8_f2_anim	;8 (UP-LEFT)

;(dnsq7 - LEFT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk6_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk6_f2_anim	;3 (RIGHT)
	.long	lex_walk8_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk4_f4_anim	;5 (DOWN)
	.long	lex_walk4_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk2_f2_anim	;7 (LEFT)
	.long	lex_walk2_f2_anim	;8 (UP-LEFT)

;(dnsq8 - UP-LEFT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk4_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk6_f2_anim	;3 (RIGHT)
	.long	lex_walk8_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk6_f4_anim	;5 (DOWN)
	.long	lex_walk2_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk2_f2_anim	;7 (LEFT)
	.long	lex_walk2_f2_anim	;8 (UP-LEFT)


 SUBR lex_torso_anims_table
; old = up right
	.long	lex_torso2_anim		;new = up right
	.long	lex_2_to_4_turn2_anim	;    = down right
	.long	lex_2_to_6_turn2_anim	;    = down left
	.long	lex_2_to_8_turn2_anim	;    = up left

; old = down right
	.long	lex_4_to_2_turn2_anim	;new = up right
	.long	lex_torso4_anim		;    = down right
	.long	lex_4_to_6_turn2_anim	;    = down left
	.long	lex_4_to_8_turn2_anim	;    = up left

; old = down left
	.long	lex_6_to_2_turn2_anim	;new = up right
	.long	lex_6_to_4_turn2_anim	;    = down right
	.long	lex_torso6_anim		;    = down left
	.long	lex_6_to_8_turn2_anim	;    = up left

; old = up left
	.long	lex_8_to_2_turn2_anim	;new = up right
	.long	lex_8_to_4_turn2_anim	;    = down right
	.long	lex_8_to_6_turn2_anim	;    = down left
	.long	lex_torso8_anim		;    = up left


******************************************************************************

	.end

