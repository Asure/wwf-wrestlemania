**************************************************************
*
* Software:		Gilbert the Wonder Hamster
* Initiated:		7/15/94
*
* COPYRIGHT (C) 1992 WILLIAMS ELECTRONICS GAMES, INC.
*
**************************************************************

	.file	"lex.asm"
	.title	"Lex Luger specific game code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"jjxm.h"
	.include	"mproc.equ"		;Mproc equates
	.include	"display.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"game.equ"
	.include	"plyr.equ"
	.include	"anim.equ"
	.include	"audit.equ"
	.include	"sound.h"
	.include 	"ring.equ"
	.include	"lifebar.h"
	.include	"damage.equ"


******************************************************************************
* EXTERNAL REFERENCES

	.ref	change_anim1a,change_anim2a,master_keep_attached
	.ref	do_taunt,CLIMB_ROPES,bounce_off_ropes,mode_inair2
	.ref	check_secret_moves,mode_choking,DO_REVERSAL_MESS
	.ref	ck_ignore,climb_turnbuckle,execute_walk,PCNT
	.ref	get_opp_plyrmode,ADD_IF_SILENT,DO_REVERSAL,mode_puppet
	.ref	process_ptrs,keep_attached,change_anim1,JUMP_ROPES
	.ref	FIND_AND_KILL_ENDLESS,BONUS_MESS
	.ref	CHECK_COMBO_GO,ck_teammate_pin,raisearm_check
	.ref	BLOCK_WOOSH,drone_change_back,mode_dead,get_opp_process
	.ref	lex_combo_punch_anim,can_pin,round_award
	.ref	lex_combo_kick_anim,set_raisearm_bit
	.ref	blocking_off,std_taunt,std_walk_fast
	.ref	hyper_speed_on
	.ref	in_finish_move
	.ref	p1rounds,p2rounds
	.if NUM_LEX_FINISHES
	.ref	lex_finish1_move
	.if NUM_LEX_FINISHES > 1
	.ref	lex_finish2_move
	.endif
	.endif

******************************************************************************
* ANIMATION SEQUENCES

	.ref	start_run_anim

	.ref	lex_4_block_anim

	.ref	lex_stand2_anim,lex_stand8_anim
	.ref	lex_stand4_anim,lex_stand6_anim
	.ref	lex_torso2_anim,lex_torso8_anim
	.ref	lex_torso4_anim,lex_torso6_anim

	.ref	lex_2_to_4_turn_anim,lex_8_to_6_turn_anim	;stand
	.ref	lex_4_to_2_turn_anim,lex_6_to_8_turn_anim
	.ref	lex_4_to_6_turn_anim,lex_6_to_4_turn_anim
	.ref	lex_2_to_8_turn_anim,lex_8_to_2_turn_anim
	.ref	lex_4_to_8_turn_anim,lex_6_to_2_turn_anim
	.ref	lex_2_to_6_turn_anim,lex_8_to_4_turn_anim

	.ref	lex_2_to_4_turn2_anim,lex_8_to_6_turn2_anim	;torso
	.ref	lex_4_to_2_turn2_anim,lex_6_to_8_turn2_anim
	.ref	lex_4_to_6_turn2_anim,lex_6_to_4_turn2_anim
	.ref	lex_2_to_8_turn2_anim,lex_8_to_2_turn2_anim
	.ref	lex_4_to_8_turn2_anim,lex_6_to_2_turn2_anim
	.ref	lex_2_to_6_turn2_anim,lex_8_to_4_turn2_anim

	.ref	lex_walk1_f2_anim,lex_walk2_f2_anim,lex_walk4_f2_anim
	.ref	lex_walk5_f2_anim,lex_walk6_f2_anim,lex_walk8_f2_anim

	.ref	lex_walk1_f4_anim,lex_walk2_f4_anim,lex_walk4_f4_anim
	.ref	lex_walk5_f4_anim,lex_walk6_f4_anim,lex_walk8_f4_anim

	.ref	lex_run2_anim,lex_hiptoss2_anim

	.ref	lex_2_punch_anim,lex_4_punch_anim
	.ref	lex_2_kick_anim,lex_4_kick_anim

	.ref	lex_2_butt_anim,lex_4_butt_anim
	.ref	lex_4_knee_anim
	.ref	lex_2_stomp_anim,lex_4_stomp_anim

	.ref	lex_2_raise_arm_anim,lex_4_raise_arm_anim

	.ref	lex_3_clobber_anim

	.ref	lex_4_push_anim

	.ref	lex_4_grabfling_anim

	.ref	lex_fall_back_anim

	.ref	lex_flying_kick_anim

	.ref	lex_2_ground_punch_anim,lex_4_ground_punch_anim
	.ref	lex_flying_ground_punch_anim,lex_climb_down_anim
	.ref	lex_buckle_leap_anim,lex_climb_up_anim
	.ref	lex_backbreaker_anim,lex_ohslam_anim

	.ref	lex_3_head_held_stand_anim,lex_2_hair_pickup_anim
	.ref	lex_3_knee_to_head_anim
	.ref	lex_4_hair_pickup_anim,lex_2_grabfling_anim
	.ref	lex_3_head_hold2_anim
	.ref	lex_3_head_hold_anim

	.ref	lex_vsuplex_anim,lex_super_kick_anim
	.ref	lex_4_knee_fall_anim,lex_4_graboh_anim
	.ref	lex_hiptoss_anim,lex_4_bigboot_anim
	.ref	lex_slamdown_anim

	.ref	lex_2_pin_anim,lex_4_pin_anim
	.ref	lex_2_clobber_anim,lex_4_clobber_anim

;	.ref	lex_pkup_chair_anim
	.ref	lex_sliding_elbow_anim
	.ref	lex_knees_to_head_anim
	.ref	lex_grab2elbow_anim

******************************************************************************
* SYMBOLS DEFINED IN THIS FILE

******************************************************************************
* EQUATES FOR THIS FILE

*****************************************************************************
*
* SPECIAL MOVES - History checks

lex_secret_moves
	.long	mlpfcharge_clobber
	.long	mlpfneck_grab
	.long	mlpfgrab_fling
	.long	mlpfhip_toss
	.long	mlpfgrab_fling2
	.long	mlpfhip_toss2
	.long	mlpfsliding_elbow
	.long	mlpfhammer
	.long	0

******************************************************************************
*
* SPECIAL MOVE PROCESSES - Secret moves
*
;a8=process of wrestler

 SUBR	lex_smove_table
	.long	lex_hdhold_pile
	.long	lex_hdhold_elbow_face
	.long	lex_hdhold_graboh
	.long	lex_grab_toss_air
	.long	lex_hdhold_combo1
	.long	lex_hdhold_combo2
;	.long	lex_hdhold_anti_combo
	.long	std_walk_fast
	.long	std_taunt

	.if NUM_LEX_FINISHES
	.long	lex_finish_move1
	.endif
	.if NUM_LEX_FINISHES > 1
	.long	lex_finish_move2
	.endif
	.long	0

*******************************************************************************
mlpfcharge_clobber
	move	*a13(BUT_VAL_UP),a0
	btst	PLAYER_PUNCH_BIT,a0
	jrz	mlpfno_punch

;	move	*a13(PLYRMODE),a0
;	cmpi	MODE_ONTURNBKL,a0
;	jrz	mlpfno_punch

	move	*a13(PLYRNUM),a0
	.ref	get_punch_dtime
	calla	get_punch_dtime
	cmpi	100,a0			;> min?
	jrlt	mlpfno_punch

	callr	mlpfscrt_clbr
	setc
	rets

mlpfno_punch
	clrc
	rets


mlpfscrt_clbr

	move	*a13(PLYRMODE),a0
	cmpi	MODE_HEADHELD,a0
	jrz	mlpfout
	cmpi	MODE_HEADHOLD,a0
	jrz	mlpfout

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	mlpfout

;Don't do it when he is on ground
;	calla	get_opp_plyrmode
;	cmpi	MODE_ONGROUND,a0
;	jrz	mlpfout
;	cmpi	MODE_DEAD,a0
;	jrz	mlpfout
;	move	*a13(PLYRMODE),a0
;	cmpi	MODE_RUNNING,a0
;	jrz	mlpfleapclbr
;
;;If stick is toward opponent, then do sliding/leaping buzzer
;	move	*a13(STICK_VAL_CUR),a0
;	move	*a13(NEW_FACING_DIR),a1
;	andi	0ch,a1
;	cmp	a0,a1
;	jrnz	mlpfnorm
;
;mlpfleapclbr
;;missing noise for a reason !
;	WRSND	W_LUGER,HDBUTT_T1,HDBUTT_T2
;	FACE24	lex,clobber2_anim
;	calla	change_anim1
;	rets
;mlpfnorm

	WRSND	W_LUGER,HDBUTT_T1,HDBUTT_T2
	FACE24	lex,clobber_anim
	calla	change_anim1

	rets

*******************************************************************************
mlpfhammer
	.word	B_SKICK,			J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 32 		;max
	.long	scrt_hammer

scrt_hammer
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	mlpfout

	move	*a13(PLYRMODE),a0
	cmpi	MODE_ONTURNBKL,a0
	jrz	mlpfout
	cmpi	MODE_OPPOVERHEAD,a0
	jreq	mlpfout

	.ref	lex_hammer_anim
	movi	lex_hammer_anim,a0
	calla	change_anim1a

;missing noise for a reason !
	WRSND	W_LUGER,HIPTOSS_T1,PUNCH_T2

	rets

******************************************************************************
mlpfneck_grab
	.word	B_SPUNCH,		J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 30		;max
	.long	mlpfscrt_neck

mlpfscrt_neck
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	mlpfout

;Don't do it when he is on ground
	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	mlpfout
	cmpi	MODE_DEAD,a0
	jrz	mlpfout
	cmpi	MODE_HEADHELD,a0
	jrz	mlpfout
;Mode held overhead!
	cmpi	MODE_ATTACHED,a0
	jrz	mlpfout

	move	*a13(LAST_HEADHOLD),a14,L
	move	@PCNT,a0,L
	sub	a14,a0
	cmpi	2*60,a0
	jrge	mlpfok2

;This head grab is too close (in terms of time) to most recent grab
	.ref	lex_3_fake_hold_anim
	movi	lex_3_fake_hold_anim,a0
	calla	change_anim1a
	rets

mlpfok2
	move	*a13(CLOSEST_XDIST),a1
	cmpi	90,a1
	jrgt	mlpfstd_grab

	movi	lex_3_head_hold2_anim,a0
	calla	change_anim1a
mlpfout	rets

mlpfstd_grab
	movi	lex_3_head_hold_anim,a0
	calla	change_anim1a
	rets

*******************************************************************************

mlpfgrab_fling2
	.word	B_SPUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
	.word	8000h | 10		;max
	.long	mlpfscrt_grabfling2

mlpfhip_toss2
	.word	B_PUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
	.word	8000h | 10		;max
	.long	mlpfscrt_grabfling2

mlpfscrt_grabfling2
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	mlpfout

;If opponent is running, do a hip toss
	calla	get_opp_plyrmode
	cmpi	MODE_BOUNCING,a0
	jrz	mlpfyes
	cmpi	MODE_RUNNING,a0
	jrz	mlpfyes
	rets

******************************************************************************
mlpfgrab_fling
;	.word	B_SPUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
;	.word	8000h | 10		;max
;	.long	mlpfscrt_grabfling

	.word	B_SPUNCH,		J_ALL
	.word	J_AWAY,		J_REAL_LR
	.word	J_AWAY,		J_REAL_LR
	.word	8000h | 32		;max
	.long	mlpfscrt_grabfling

mlpfscrt_grabfling

;Make this miss, rotate to missed opponent,
;and stay rotated to flung opponent, also make
;this work from both angles!

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	mlpfout

;If opponent is running, do a hip toss
	calla	get_opp_plyrmode
	cmpi	MODE_BOUNCING,a0
	jrz	mlpfyes
	cmpi	MODE_RUNNING,a0
	jrnz	mlpfnope
mlpfyes
	movi	lex_hiptoss_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,HIPTOSS_T1,PUNCH_T2
	rets

mlpfnope
;Don't fling when he is on ground
	calla	get_opp_plyrmode
	cmpi	MODE_HEADHELD,a0
	jrz	mlpfout
	cmpi	MODE_ONGROUND,a0
	jrz	mlpfout
	cmpi	MODE_DEAD,a0
	jrz	mlpfout
;Mode held overhead!
	cmpi	MODE_ATTACHED,a0
	jrz	mlpfout

;	move	*a13(LAST_FLING_ATTEMPT),a14,L
;	move	@PCNT,a0,L
;	sub	a14,a0
;	cmpi	3*60,a0
;	jrge	mlpfok2a
;
;;This head grab is too close (in terms of time) to most recent grab
;	movi	lex_3_clobber_anim,a0
;;	FACE24	lex,punch_anim
;	calla	change_anim1a
;
;	WRSND	W_LUGER,HIPTOSS_T1,PUNCH_T2
;	rets
;
;mlpfok2a
;	move	@PCNT,a14,L			;use 32-bit PCNT!
;	move	a14,*a13(LAST_FLING_ATTEMPT),L

	FACE24	lex,grabfling_anim
	calla	change_anim1a

	WRSND	W_LUGER,GRABFLING_T1,PUNCH_T2
	rets

*******************************************************************************
mlpfhip_toss
;	.word	B_PUNCH | J_AWAY,	J_REAL_LR | J_UP | J_DOWN
;	.word	8000h | 10		;max
;	.long	mlpfscrt_hiptoss

	.word	B_PUNCH,		J_ALL
	.word	J_AWAY,		J_REAL_LR
	.word	J_AWAY,		J_REAL_LR
	.word	8000h | 32		;max
	.long	mlpfscrt_hiptoss

mlpfscrt_hiptoss
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	mlpfout

;Don't do it when he is on ground
	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	mlpfout
;	cmpi	MODE_BLOCK,a0
;	jrz	mlpfout
	cmpi	MODE_DEAD,a0
	jrz	mlpfout
	cmpi	MODE_HEADHELD,a0
	jrz	mlpfout
	cmpi	MODE_INAIR,a0
	jrz	mlpfdoit
	cmpi	MODE_INAIR2,a0
	jrz	mlpfdoit
;Mode held overhead!
	cmpi	MODE_ATTACHED,a0
	jrz	mlpfout

	move	*a13(CLOSEST_DIST),a0
	cmpi	70h,a0
	jrgt	mlpfout

mlpfdoit
 	movi	lex_hiptoss_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	WRSND	W_LUGER,GRABFLING_T1,PUNCH_T2

	rets

*******************************************************************************
mlpfsliding_elbow
	.word	B_PUNCH,		J_ALL
	.word	J_TOWARD,		J_REAL_LR
	.word	J_TOWARD,		J_REAL_LR
	.word	8000h | 30		;max
	.long	mlpfscrt_elbow

mlpfscrt_elbow
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	mlpfout

	move	*a13(PLYRMODE),a0
	cmpi	MODE_OPPOVERHEAD,a0
	jreq	mlpfout

;Don't do it when he is on ground
	calla	get_opp_plyrmode
	cmpi	MODE_ONGROUND,a0
	jrz	mlpfout
	cmpi	MODE_DEAD,a0
	jrz	mlpfout

	;we can do this from a headhold, so...
	calla	FIND_AND_KILL_ENDLESS

 	movi	lex_sliding_elbow_anim,a0
	calla	change_anim1a

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	WRSND	W_LUGER,UPRCUT_T1,UPRCUT_T2

	rets

	.if NUM_LEX_FINISHES
******************************************************************************
 SUBRP	lex_finish_move1

egdkTIMEOUT	.equ	TSEC

egdkend_wait
	SLEEP		TSEC/4
	move		@p1rounds,a0			; Get player 1 rounds
	move		@p2rounds,a1			; Get player 2 rounds
	or		a1,a0				; Put em together
	cmpi		2,a0				; Anybody got 2?
	jrlt		egdkend_wait			; br = no

egdkreset
	clr		a11
	move		a11,@in_finish_move
	SLEEPK		1
	WAITSWITCH_DWN	J_UP,0,egdkreset
	movi		egdkTIMEOUT,a11
	move		a11,@in_finish_move
	WAITSWITCH_DWN	J_DOWN,0,egdkreset
	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,egdkreset
	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,egdkreset
	WAITSWITCH_DWN	B_PUNCH,J_ALL,egdkreset

	movi		lex_finish1_move,a0
	move		a0,*a8(SPECIAL_MOVE_ADDR),L
	DIE
	.endif
	

	.if NUM_LEX_FINISHES > 1
******************************************************************************
 SUBRP	lex_finish_move2

dfkuTIMEOUT	.equ	TSEC

dfkuend_wait
	SLEEP		TSEC/4
	move		@p1rounds,a0			; Get player 1 rounds
	move		@p2rounds,a1			; Get player 2 rounds
	or		a1,a0				; Put em together
	cmpi		2,a0				; Anybody got 2?
	jrlt		dfkuend_wait			; br = no

dfkureset
	clr		a11
	move		a11,@in_finish_move
	SLEEPK		1
	WAITSWITCH_DWN	J_UP,0,dfkureset
	movi		dfkuTIMEOUT,a11
	move		a11,@in_finish_move
	WAITSWITCH_DWN	J_UP,0,dfkureset
	WAITSWITCH_DWN	J_RIGHT,J_UP,dfkureset
	WAITSWITCH_DWN	J_RIGHT,J_UP,dfkureset
	WAITSWITCH_DWN	B_SPUNCH,J_ALL,dfkureset

	movi		lex_finish2_move,a0
	move		a0,*a8(SPECIAL_MOVE_ADDR),L
	DIE
	.endif


*****************************************************************************
 SUBRP	lex_hdhold_pile

ogtuTIMEOUT	.equ	60

ogtulp0
	SLEEPK	1
ogtulp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	ogtucont
	cmpi	MODE_HEADHELD,a0
	jrnz	ogtulp0

ogtucont
	clr	a11

;	WAITSWITCH_DWN	J_AWAY,0,ogtulp
	WAITSWITCH_DWN	J_TOWARD,0,ogtulp

	movi	ogtuTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,ogtulp
	WAITSWITCH_DWN	B_SPUNCH,J_ALL,ogtulp
	
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	ogtuslam
	cmpi	MODE_HEADHELD,a0
	jrnz	ogtulp0
	move	*a8(I_WILL_DIE),A14
	jrnz	ogtulp0

	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	ogtulp0				;ignore

	calla	DO_REVERSAL
	calla	DO_REVERSAL_MESS

	;target WHOHITME -- don't hit anyone else
	SMRTTGT	a8,WHOHITME

	;immobilize WHOHITME
	move	*a8(WHOHITME),a0,L
	jruc	ogtuis_reversal

ogtuslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	ogtulp0				;ignore

	movi	42,a10
	CREATE	MESSAGE_PID,BONUS_MESS

	;target WHOIHIT -- don't hit anyone else
	SMRTTGT	a8,WHOIHIT

	;immobilize WHOIHIT
	move	*a8(WHOIHIT),a0,L

ogtuis_reversal
	movk	15,a14
	move	a14,*a0(IMMOBILIZE_TIME)

	CALLA	FIND_AND_KILL_ENDLESS

	movi	lex_vsuplex_anim,a0
	move	a0,*a8(SPECIAL_MOVE_ADDR),L
	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	SLEEPK	20

	jruc	ogtulp

*****************************************************************************
 SUBRP	lex_hdhold_elbow_face

dutyTIMEOUT	.equ	60

dutylp0
	SLEEPK	1
dutylp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	dutycont
	cmpi	MODE_HEADHELD,a0
	jrnz	dutylp0

dutycont
	clr	a11

;	WAITSWITCH_DWN	J_UP,0,dutylp
	WAITSWITCH_DWN	J_DOWN,0,dutylp

	movi	dutyTIMEOUT,a11

	WAITSWITCH_DWN	J_DOWN,0,dutylp
	WAITSWITCH_DWN	B_SKICK,J_ALL,dutylp
	
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	dutyslam
	cmpi	MODE_HEADHELD,a0
	jrnz	dutylp0
	move	*a8(I_WILL_DIE),A14
	jrnz	dutylp0

	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	dutylp0				;ignore

	calla	DO_REVERSAL
	calla	DO_REVERSAL_MESS

	;target WHOHITME -- don't hit anyone else
	SMRTTGT	a8,WHOHITME

	;immobilize WHOHITME
	move	*a8(WHOHITME),a0,L

	.ref	lex_grab2elbow_rev_anim

	movi	lex_grab2elbow_rev_anim,a1
	jruc	dutyis_reversal

dutyslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	dutylp0				;ignore

;fix !!! wrong name - jake
	movk	6,a10
	CREATE	MESSAGE_PID,BONUS_MESS

	;target WHOIHIT -- don't hit anyone else
	SMRTTGT	a8,WHOIHIT

	;immobilize WHOIHIT
	move	*a8(WHOIHIT),a0,L

;This stops Lex from going for foot, then changing his mind!
;	movk	15,a14
;	move	a14,*a8(IMMOBILIZE_TIME)

	movi	lex_grab2elbow_anim,a1
dutyis_reversal
	movk	15,a14
	move	a14,*a0(IMMOBILIZE_TIME)

	move	a1,*a8(SPECIAL_MOVE_ADDR),L
	CALLA	FIND_AND_KILL_ENDLESS

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	SLEEPK	20
	jruc	dutylp

******************************************************************************
lex_grab_toss_air

vyslTIMEOUT	.equ	40

vysllp0
	SLEEPK	1
vysllp
	clr	a11

	WAITSWITCH_DWN	J_AWAY,0,vysllp

	movi	vyslTIMEOUT,a11

	WAITSWITCH_DWN	J_AWAY,0,vysllp
	WAITSWITCH_DWN	B_PUNCH,J_ALL,vysllp

	move	*a8(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	vysllp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jreq	vysllp0

;Don't do it when he is on ground
	SWAP	a8,a13
	calla	get_opp_plyrmode
	SWAP	a8,a13
;Don't do it when he is on ground
	cmpi	MODE_ONGROUND,a0
	jrz	vysllp
	cmpi	MODE_DEAD,a0
	jrz	vysllp
	cmpi	MODE_INAIR,a0
	jrz	vysldoit2
	cmpi	MODE_INAIR2,a0
	jrz	vysldoit2

	move	*a8(CLOSEST_NUM),a0
	X32	a0
	addi	process_ptrs,a0
	move	*a0,a0,L
	move	*a0(ATTACK_TYPE),a0
	cmpi	AT_LEAPING,a0
	jrz	vysldoit2

	move	*a8(CLOSEST_DIST),a0
	cmpi	6ch,a0
	jrgt	vysllp

;	FACE24	lex,hiptoss_anim
	movi	lex_hiptoss_anim,a0
	move	a0,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,GRABFLING_T1,PUNCH_T2

	SLEEPK	20
	jruc	vysllp

vysldoit2
;	FACE24	lex,hiptoss2_anim
	movi	lex_hiptoss2_anim,a0
	move	a0,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,GRABFLING_T1,PUNCH_T2

	SLEEPK	20

	jruc	vysllp

*****************************************************************************
lex_hdhold_graboh

azysTIMEOUT	.equ	60

azyslp0
	SLEEPK	1
azyslp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	azyscont0
	cmpi	MODE_HEADHELD,a0
	jrnz	azyslp0
azyscont0
	clr	a11

	WAITSWITCH_DWN	J_DOWN,0,azyslp

	movi	azysTIMEOUT,a11

;	WAITSWITCH_DWN	J_DOWN_TOWARD,0,azyslp
	WAITSWITCH_DWN	J_TOWARD,J_DOWN|J_UP,azyslp
	WAITSWITCH_DWN	B_KICK,J_ALL,azyslp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	azysdograb
	cmpi	MODE_HEADHELD,a0
	jrnz	azyslp0
	move	*a8(I_WILL_DIE),A14
	jrnz	azyslp0

	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	azyslp0				;ignore

	CALLA	DO_REVERSAL
	CALLA	DO_REVERSAL_MESS

	;target WHOHITME -- don't hit anyone else
	SMRTTGT	a8,WHOHITME

	;immobilize WHOHITME
	move	*a8(WHOHITME),a0,L
	jruc	azysis_reversal

azysdograb
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	azyslp0				;ignore

;	movk	6,a10
;	CREATE	MESSAGE_PID,BONUS_MESS

	;target WHOIHIT -- don't hit anyone else
	SMRTTGT	a8,WHOIHIT

	;immobilize WHOIHIT
	move	*a8(WHOIHIT),a0,L

azysis_reversal
	movk	15,a14
	move	a14,*a0(IMMOBILIZE_TIME)

	CALLA	FIND_AND_KILL_ENDLESS

	movi	lex_4_graboh_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,GRABHOLD_T1,GRABHOLD_T2

	SLEEPK	20

	jruc	azyslp

*******************************************************************************
*
lex_hdhold_combo1

pgehTIMEOUT	.equ	60

pgehlp0
	SLEEPK	1
pgehlp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrnz	pgehlp0
;	jrz	pgehcont
;	cmpi	MODE_HEADHELD,a0
;	jrnz	pgehlp0
;pgehcont
;In combo mode?
	calla	CHECK_COMBO_GO
	jrlt	pgehlp0

	clr	a11

	WAITSWITCH_DWN	J_TOWARD,0,pgehlp

	movi	pgehTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,pgehlp
	WAITSWITCH_DWN	B_SKICK,J_ALL,pgehlp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	pgehslam
	cmpi	MODE_HEADHELD,a0
	jrnz	pgehlp0

pgehslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	pgehlp0				;ignore

;Start combo rules here
;	move	*a8(ATTACH_PROC),a14,L
;	movk	15,a0
;	move	a0,*a14(IMMOBILIZE_TIME)

	;target WHOIHIT
	SMRTTGT	a8,WHOIHIT

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_combo_punch_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,UPRCUT_T1,UPRCUT_T2

	SLEEPK	20

	jruc	pgehlp

*******************************************************************************
*
lex_hdhold_combo2

ssfaTIMEOUT	.equ	60

ssfalp0
	SLEEPK	1
ssfalp
	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrnz	ssfalp0
;	jrz	ssfacont
;	cmpi	MODE_HEADHELD,a0
;	jrnz	ssfalp0
;ssfacont
;In combo mode?
	calla	CHECK_COMBO_GO
	jrlt	ssfalp0

	clr	a11

	WAITSWITCH_DWN	J_TOWARD,0,ssfalp

	movi	ssfaTIMEOUT,a11

	WAITSWITCH_DWN	J_TOWARD,0,ssfalp
	WAITSWITCH_DWN	B_KICK,J_ALL,ssfalp

	move	*a8(PLYRMODE),a0
	cmpi	MODE_HEADHOLD,a0
	jrz	ssfaslam
	cmpi	MODE_HEADHELD,a0
	jrnz	ssfalp0

ssfaslam
	move	*a8(IMMOBILIZE_TIME),a14
	jrnz	ssfalp0				;ignore

;Start combo rules here
;	move	*a8(ATTACH_PROC),a14,L
;	movk	15,a0
;	move	a0,*a14(IMMOBILIZE_TIME)

	;target WHOIHIT
	SMRTTGT	a8,WHOIHIT

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_combo_kick_anim,a14
	move	a14,*a8(SPECIAL_MOVE_ADDR),L

	WRSND	W_LUGER,UPRCUT_T1,UPRCUT_T2

	SLEEPK	20

	jruc	ssfalp

;******************************************************************************
;
;;Could start this process only when opponent is in combo mode!
;
;lex_hdhold_anti_combo
;
;ssfaTIMEOUT	.equ	30
;
;ssfalp0
;	SLEEPK	1
;ssfalp
;;Is my opponent in combo mode?
;;	move	*a8(IMMOBILIZE_TIME),a14
;;	jrz	ssfalp0
;;	move	*a8(CLOSEST_NUM),a0
;;	X32	a0
;;	addi	process_ptrs,a0
;;	move	*a0,a0,L
;;	move	*a0(COMBO_COUNT),a0
;;	jrz	ssfalp0
;
;	clr	a11
;
;	WAITSWITCH_DWN	B_SPUNCH,J_ALL,ssfalp
;
;	movk	ssfaTIMEOUT,a11
;
;	WAITSWITCH_DWN	B_SPUNCH,J_ALL,ssfalp
;	WAITSWITCH_DWN	B_SPUNCH,J_ALL,ssfalp
;
;;Is my opponent in combo mode?
;	move	*a8(CLOSEST_NUM),a0
;	X32	a0
;	addi	process_ptrs,a0
;	move	*a0,a0,L
;	move	*a0(COMBO_COUNT),a0
;	jrz	ssfalp0
;
;	move	*a8(ANTI_COMBO_TIME),a0		;Time stamp
;	move	@PCNT,a14
;	sub	a0,a14
;	abs	a14
;	cmpi	80,a14
;	jrge	ssfalp0
;	move	*a8(I_WILL_DIE),A14
;	jrnz	ssfalp0
;
;;Combo breaker message!
;	CALLA	DO_REVERSAL
;	CALLA	DO_REVERSAL_MESS
;
;	clr	a0
;	move	a0,*a8(IMMOBILIZE_TIME)
;
;	move	*a8(WHOHITME),a14,L
;	movk	15,a0
;	move	a0,*a14(IMMOBILIZE_TIME)
;
;	;target WHOHITME
;	SMRTTGT	a8,WHOHITME
;
;	CALLA	FIND_AND_KILL_ENDLESS
;
;	WRSND	W_LUGER,GRABHOLD_T1,GRABHOLD_T2
;	movi	lex_vsuplex_anim,a14
;	move	a14,*a8(SPECIAL_MOVE_ADDR),L
;
;	SLEEPK	20
;
;	jruc	ssfalp
;
*****************************************************************************

 SUBR	lex_ani_init

	move	*a13(FACING_DIR),a0
	btst	PLAYER_RIGHT_BIT,a0
	jrnz	lvndp1
;Plyr 2
	movi	lex_stand4_anim,a0
	calla	change_anim1a

	movi	lex_torso4_anim,a0
	calla	change_anim2a

	PUSH	a10
	move	a13,a10
	CREATE	TAUNT_PID,do_taunt
	PULL	a10

	rets

lvndp1
	movi	lex_stand2_anim,a0
	calla	change_anim1a

	movi	lex_torso2_anim,a0
	calla	change_anim2a

	rets


*****************************************************************************
*
* LEX CONTROL CODE
*
* a13 = * Lex process

 SUBR	move_lex


;	movi	100h,a0			;temp fix!
;	move	a0,*a13(ANI_SPEED)

	movi	lex_secret_moves,a11
	calla	check_secret_moves

	move	*a13(PLYRMODE),a0
	sll	5,a0			;x 32
	addi	drwmmode_table,a0
	move	*a0,a0,L
	call	a0
	rets

drwmmode_table
	.long	mode_normal		;0
	.long	mode_running		;1
	.long	mode_inair		;2
	.long	mode_attached		;3
	.long	mode_onground		;4
	.long	mode_bouncing		;5
	.long	mode_onturnbkl		;6
	.long	mode_block		;7
	.long	mode_dizzy		;8
	.long	mode_dead		;9
	.long	mode_oppoverhead	;10
	.long	mode_climbturnbkl	;11
	.long	mode_waitanim		;12
	.long	mode_grapple		;13
	.long	mode_master		;14
	.long	mode_slave		;15
	.long	mode_headhold		;16
	.long	mode_puppet2		;17
	.long	mode_normal		;18
	.long	mode_headheld		;19
	.long	mode_puppet		;20
	.long	mode_inair2		;21
	.long	mode_normal		;22
	.long	mode_normal		;23
	.long	mode_chokehold		;24
	.long	mode_choking		;25

*****************************************************************************

mode_normal

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	tgqrno_interrupt

	move	*a13(I_WILL_DIE),a0
	jrz	tgqrnope
	move	*a13(IMMOBILIZE_TIME),a0
	jrnz	tgqrnope
;Player has died amidst a combo...
;Knock me down.
	movi	lex_fall_back_anim,a0
	calla	change_anim1a

;Zero health bar
	movi	-10,a0
	move	*a13(PLYRNUM),a1
	calla	adjust_health

	SETMODE	DEAD

	clr	a0
	move	a0,*a13(I_WILL_DIE)

	rets

tgqrnope
;Perhaps we need to interrupt any sequence when a wrestler has won?

	calla	get_opp_plyrmode
	cmpi	MODE_DEAD,a0
	jrne	tgqropp_not_dead

	;if closest is a zombie, skip pin section.
	calla	get_opp_process
	move	*a0(STATUS_FLAGS),a14
	btst	B_ZOMBIE,a14
	jrnz	tgqropp_not_dead
	
	;all opponents are dead.  Check for pins/raisearms

	;if we're attached to someone, don't interrupt it.
	move	*a13(ATTACH_PROC),a1,L
	jrz	tgqrno_attch
	move	*a1(ATTACH_PROC),a2,L
	cmp	a2,a13
	jreq	tgqrno_pin_yet
tgqrno_attch

	;if a teammate has pinned, raise yer arm.
	calla	ck_teammate_pin	
	jrc	tgqrraisearm

	;nobody has pinned.  if we're outside or all our opponents are
	; outside, do a raisearm.
	calla	raisearm_check
	jrc	tgqrraisearm

	;we're inside with a pinnable opponent.  any button pins.
	move	*a13(BUT_VAL_CUR),a0
	jrz	tgqrno_pin_yet

	;we have a press -- make sure opponent is on the ground
	calla	can_pin
	jrnc	tgqropp_notgnd

	;opponent is on the ground.  pin him.
	FACE24	lex,pin_anim
	calla	change_anim1a

	;set the 'I did a pin' flag.
	move	*a13(STATUS_FLAGS),a14
	ori	M_DID_PIN,a14
	move	a14,*a13(STATUS_FLAGS)

	;if we're a temp drone for auto-pin purposes, turn back into
	; a normal player here.
	calla	drone_change_back

	rets

tgqrraisearm
	FACE24	lex,raise_arm_anim
	calla	change_anim1a
	calla	set_raisearm_bit

	;if we're a temp drone for auto-pin purposes, turn back into
	; a normal player here.
	calla	drone_change_back

	rets

tgqrtoo_far_to_pin
tgqropp_notgnd
tgqropp_not_dead
tgqrno_pin_yet

	move	*a13(IMMOBILIZE_TIME),a0
	jrnz	tgqrimmobilized

	move	*a13(BUT_VAL_CUR),a0
	btst	PLAYER_BLOCK_BIT,a0	;maybe have to action tables?
	jrz	tgqrnblk			;1 for down transitions
	callr	tgqrblock			;& 1 for current buttons
	jrc	tgqrnblk
	clr	a0
	move	a0,*a13(ATTACK_TYPE)
	rets
tgqrnblk
	move	*a13(BUT_VAL_CUR),a0
	andi	011111b,a0
	cmpi	PLAYER_PUNCH_VAL|PLAYER_KICK_VAL,a0
	jrz	tgqrpunchkick

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	tgqraction_table,a0
	move	*a0,a0,L
	call	a0

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	tgqrno_interrupt


	move	*a13(STICK_VAL_CUR),a0
	move	a0,*a13(MOVE_DIR)
	calla	climb_turnbuckle
	jrnc	tgqrno_climb
	move	*a13(WRESTLERNUM),A5
	MOVI	CLIMB_ROPES,A2
	MOVI	1000,A0
	CALLA	ADD_IF_SILENT
	rets

tgqrimmobilized
	clr	a14
	move	a14,*a13(MOVE_DIR)
tgqrno_climb
	calla	execute_walk

tgqrno_interrupt
	rets

tgqraction_table
	.long	tgqrz,tgqrpunch,tgqrblock,tgqrblock			;0-3
	.long	tgqrsuper_punch,tgqrsuper_punch,tgqrblock,tgqrblock	;4-7
	.long	tgqrkick,tgqrpunchkick,tgqrblock,tgqrblock		;8-11
	.long	tgqrsuper_punch,tgqrpunchkick,tgqrblock,tgqrblock	;12-15
	.long	tgqrsuper_kick,tgqrsuper_kick,tgqrblock,tgqrblock	;16-19
	.long	tgqrgraboh,tgqrgraboh,tgqrblock,tgqrblock		;20-23
	.long	tgqrsuper_kick,tgqrpunchkick,tgqrblock,tgqrblock	;24-27
	.long	tgqrgraboh,tgqrgraboh,tgqrblock,tgqrblock		;28-31

************
tgqrz
	rets


************
tgqrpunch		;1
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal	<  40  45	headbutt
* normal	>		normal punch
*
* onground	< 120 120	elbow drop
* onground	>		normal punch
*
* running |
* bouncing			normal punch
*
* onturnbkl	<  40  45	hdbutt
* onturnbkl	>		normal punch
*
* climbturnbkl			punch
*

	JJXM_INIT
	JJXM	NORMAL,		 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;0
	JJXM	RUNNING,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;1
	JJXM	INAIR,		 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;2
	JJXM	ATTACHED,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;3
	JJXM	ONGROUND,	160,140,tgqrpunch_lbowdrop,tgqrpunch_punch	;4
	JJXM	BOUNCING,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;1
	JJXM	ONTURNBKL,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;6
	JJXM	BLOCK,		 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;7
	JJXM	DIZZY,		 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;8
	JJXM	DEAD,		160,140,tgqrpunch_lbowdrop,tgqrpunch_punch	;9
	JJXM	OPPOVERHEAD,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;10
	JJXM	CLIMBTURNBKL,		tgqrpunch_punch			;11
	JJXM	WAITANIM,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;12
	JJXM	GRAPPLE,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;13
	JJXM	MASTER,		 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;14
	JJXM	SLAVE, 		 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;15
	JJXM	HEADHOLD,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;16
	JJXM	HEADHELD,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;19
	JJXM	CHOKEHOLD,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;16
	JJXM	PUPPET,		 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;20
	JJXM	PUPPET2,	 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;20
	JJXM	INAIR2,		 40, 45,tgqrpunch_hdbutt,tgqrpunch_punch	;21
	JJXM_END

*****	
tgqrpunch_punch
std_punch
	FACE24	lex,punch_anim
	calla	change_anim1a
	WRSND	W_LUGER,PUNCH_T1,PUNCH_T2
	rets

*****
tgqrpunch_hdbutt
	FACE24	lex,butt_anim
	calla	change_anim1a
	WRSND	W_LUGER,HDBUTT_T1,HDBUTT_T2
	rets

*****
tgqrpunch_lbowdrop
	FACE24	lex,ground_punch_anim
	calla	change_anim1a
	WRSND	W_LUGER,LBOWDROP_T1,LBOWDROP_T2
	rets


************
tgqrblock		;2
std_block
	move	@blocking_off,a0
	jrnz	tgqrno_blocking

	RND_AWARD	a13,BLOCKS_AWD

	movi	lex_4_block_anim,a0
	calla	change_anim1

	calla	BLOCK_WOOSH

	clr	a0
	move	a0,*a13(BLOCK_TIME)
	clrc
	rets

tgqrno_blocking
	setc
	rets


************
tgqrsuper_punch	;4
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal	< 110   75	close in super move
* dizzy			"		"
* normal	>		jumping punch move
* dizzy			"		"
*
* onground	< 136  112	super elbow drop
* onground	>		normal punch
*
* running			hip toss
* bouncing			nothing
*
* onturnbkl	<  60   96	yank down
* onturnbkl	>		normal punch
* climbturnbkl			normal punch
*
	JJXM_INIT
	JJXM	NORMAL,		 70, 45,tgqrspunch_special,tgqrspunch_slap	;0
	JJXM	RUNNING,	 40, 45,tgqrspunch_special,tgqrspunch_slap	;1
	JJXM	INAIR,		 40, 45,tgqrspunch_special,tgqrspunch_slap	;2
	JJXM	ATTACHED,	 40, 45,tgqrspunch_special,tgqrspunch_slap	;3
	JJXM	ONGROUND,	160,140,tgqrspunch_lbowdrop,std_punch	;4
	JJXM	BOUNCING,	110, 45,tgqrspunch_special,tgqrspunch_slap	;5
	JJXM	ONTURNBKL,	 	tgqrspunch_slap			;6
	JJXM	BLOCK,		 70, 45,tgqrspunch_special,tgqrspunch_slap	;7
	JJXM	DIZZY,		 40, 45,tgqrspunch_special,tgqrspunch_slap	;8
	JJXM	DEAD,		160,140,tgqrspunch_lbowdrop,std_punch	;9
	JJXM	OPPOVERHEAD,	110, 35,tgqrspunch_special,tgqrspunch_slap	;10
	JJXM	CLIMBTURNBKL,		std_punch			;11
	JJXM	WAITANIM,	 40, 45,tgqrspunch_special,tgqrspunch_slap	;12
	JJXM	GRAPPLE,	 40, 45,tgqrspunch_special,tgqrspunch_slap	;13
	JJXM	MASTER,		 40, 45,tgqrspunch_special,tgqrspunch_slap	;14
	JJXM	SLAVE, 		 40, 45,tgqrspunch_special,tgqrspunch_slap	;15
	JJXM	HEADHOLD,	 40, 45,tgqrspunch_special,tgqrspunch_slap	;16
	JJXM	HEADHELD,	 40, 45,tgqrspunch_special,tgqrspunch_slap	;19
	JJXM	CHOKEHOLD,	 40, 45,tgqrspunch_special,tgqrspunch_slap	;16
	JJXM	PUPPET,		 40, 45,tgqrspunch_special,tgqrspunch_slap	;20
	JJXM	PUPPET2,	 40, 45,tgqrspunch_special,tgqrspunch_slap	;20
	JJXM	INAIR2,		 40, 45,tgqrspunch_special,tgqrspunch_slap	;21
	JJXM_END

tgqrspunch_rets
	rets

*****
tgqrspunch_slap
	movi	lex_3_clobber_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,PUNCH_T1,PUNCH_T2
	rets

*****
tgqrspunch_special
;	move	*a13(CLOSEST_XDIST),a1
;	cmpi	65,a1
;	jrgt	tgqrspunch_slap

	move	*a13(CLOSEST_XDIST),a1
	cmpi	55,a1
	jrgt	std_punch

	.ref	lex_2_butts_anim
	.ref	lex_4_butts_anim

	FACE24	lex,butts_anim
	calla	change_anim1

	WRSND	W_LUGER,HDBUTT_T1,HDBUTT_T2

	rets

*****
tgqrspunch_lbowdrop

	move	*a13(CLOSEST_NUM),a14
	X32	a14
	addi	process_ptrs,a14
	move	*a14,a14,L
	move	*a14(PLYRMODE),a2
	cmpi	MODE_DEAD,a2
	jrz	tgqrno
	move	*a13(OBJ_XPOS),a1,L 
	move	*a14(OBJ_XPOS),a2,L
	sub	a2,a1
	abs	a1
	srl	16,a1
	cmpi	20h,a1	;28
 	jrlt	tgqrno

;At head or feet!

	move	*a14(OBJ_CONTROL),a14
	andi	M_FLIPH,a14
	move	*a13(OBJ_CONTROL),a0
	andi	M_FLIPH,a0
	cmp	a0,a14
	jrz	tgqrno

;At head!  Do hair pickup.

	FACE24	lex,hair_pickup_anim
	calla	change_anim1
	WRSND	W_LUGER,LBOWDROP_T1,LBOWDROP_T2
	rets

tgqrno
	FACE24	lex,ground_punch_anim
	calla	change_anim1
	WRSND	W_LUGER,LBOWDROP_T1,LBOWDROP_T2
	rets


************
tgqrkick		;8
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal	<  60   50	knee
*		>		normal kick
*
* onground	< 128  128	stomp
* onground	>		normal kick
*
* running |
* bouncing			big boot
*
* onturnbkl |
* climbturnbkl			normal kick
*


	JJXM_INIT
	JJXM	NORMAL,		 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	BLOCK,		 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	DIZZY,		 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	ONGROUND,	160,140,tgqrkick_stomp,tgqrkick_kick
	JJXM	RUNNING, 	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	BOUNCING, 	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	ONTURNBKL,	 	tgqrkick_kick
	JJXM	CLIMBTURNBKL,	 	tgqrkick_kick
	JJXM	INAIR,		 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	ATTACHED,	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	DEAD,		160,140,tgqrkick_stomp,tgqrkick_kick
	JJXM	OPPOVERHEAD,	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	WAITANIM,	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	GRAPPLE,	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	MASTER,		 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	SLAVE,		 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	HEADHOLD,	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	HEADHELD,	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	CHOKEHOLD,	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	PUPPET,		 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	PUPPET2,	 60, 50,tgqrkick_knee,tgqrkick_kick
	JJXM	INAIR2,		 	tgqrkick_TB
	JJXM_END

*****
tgqrkick_TB

	.ref	lex_kick_TB_anim
	movi	lex_kick_TB_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets
*****
tgqrkick_kick
std_kick

	FACE24	lex,kick_anim
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets

*****
tgqrkick_knee
std_knee

	movi	lex_4_knee_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets

*****
tgqrkick_stomp
std_stomp

	FACE24	lex,stomp_anim
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2

	rets

;*****
;tgqrkick_bigboot
;
;	FACE24	lex,kick_anim
;	calla	change_anim1a
;
;	WRSND	W_LUGER,KICK_T1,KICK_T2
;	rets


************
tgqrpunchkick	;9

	movi	start_run_anim,a0
	calla	change_anim1a

	rets


************
tgqrsuper_kick	;16
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal	<  60   60	close in super move
* dizzy			"		"
* normal	>		jumping kick move
* dizzy			"		"
*
* onground	< 120  120	super stomp
* onground	>		normal kick
*
* running			big boot
* bouncing			big boot
*
* onturnbkl	<  60   96	jumping kick move
* climbturnbkl		"		"
* onturnbkl	>		normal kick
* climbturnbkl		"		"
*
	JJXM_INIT
	JJXM	NORMAL,		 60, 60,tgqrskick_special,tgqrskick_kick	;0
	JJXM	RUNNING,	 	tgqrskick_bigboot			;1
	JJXM	INAIR,		 60, 60,tgqrskick_special,tgqrskick_kick	;2
	JJXM	ATTACHED,	 60, 60,tgqrskick_special,tgqrskick_kick	;3
	JJXM	ONGROUND,	160,140,std_stomp,std_kick		;4
	JJXM	BOUNCING,	 	tgqrskick_bigboot			;5
	JJXM	ONTURNBKL,	 60, 96,tgqrskick_kick,std_kick		;6
	JJXM	BLOCK,		 60, 60,tgqrskick_special,tgqrskick_kick	;7
	JJXM	DIZZY,		 60, 60,tgqrskick_special,tgqrskick_kick	;8
	JJXM	DEAD,		160,140,std_stomp,std_kick		;9
	JJXM	OPPOVERHEAD,	 60, 60,tgqrskick_special,tgqrskick_kick	;10
	JJXM	CLIMBTURNBKL,	 60, 96,tgqrskick_kick,std_kick		;11
	JJXM	WAITANIM,	 60, 62,tgqrskick_special,tgqrskick_kick	;12
	JJXM	GRAPPLE,	 60, 62,tgqrskick_special,tgqrskick_kick	;13
	JJXM	MASTER,		 60, 62,tgqrskick_special,tgqrskick_kick	;14
	JJXM	SLAVE, 		 60, 62,tgqrskick_special,tgqrskick_kick	;15
	JJXM	HEADHOLD,	 60, 62,tgqrskick_special,tgqrskick_kick	;16
	JJXM	HEADHELD,	 	std_kick
	JJXM	CHOKEHOLD,	 60, 62,tgqrskick_special,tgqrskick_kick	;16
	JJXM	PUPPET,		 60, 62,tgqrskick_special,tgqrskick_kick	;20
	JJXM	PUPPET2,	 60, 62,tgqrskick_special,tgqrskick_kick	;20
	JJXM	INAIR2,		 	tgqrkick_TB
	JJXM_END

*****
tgqrskick_kick
	movi	lex_super_kick_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2
	rets


*****
tgqrskick_special
	move	*a13(STICK_VAL_CUR),a0
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a0,a1
	jrz	tgqrcont2

	movi	lex_4_knee_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,KICK_T1,KICK_T2

	rets

tgqrcont2
	movi	lex_4_knee_fall_anim,a0
	calla	change_anim1

	WRSND	W_LUGER,GRABHOLD_T1,GRABHOLD_T2
	rets

*****
tgqrskick_bigboot	;we can probably get away with just a tgqr4 big boot

	movi	lex_4_bigboot_anim,a0		;*****
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	rets

************
tgqrgraboh		;20

	movi	lex_4_graboh_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,GRABHOLD_T1,GRABHOLD_T2

	rets


*****************************************************************************
*
* RUNNING (if GETUP_TIME > 0 then player is out of control)
*

dysfXRUN_VAL	equ	LEX_XRUN
dysfZDRIFT_VAL	equ	LEX_ZDRIFT

mode_running

	move	*a13(RUN_TIME),a0		;Used for flying kick
	addk	1,a0
	move	a0,*a13(RUN_TIME)

	move	*a13(USR_VAR1),a0
	jrnz	dysfno_vel

	calla	bounce_off_ropes

	movi	dysfXRUN_VAL,a0
	move	*a13(MOVE_DIR),a14
	btst	PLAYER_RIGHT_BIT,a14
	jrnz	dysfpos
	neg	a0
dysfpos
	move	@hyper_speed_on,a14
	sll	a14,a0
	move	a0,*a13(OBJ_XVEL),L

dysfno_vel

	move	*a13(GETUP_TIME),a0		;delay b4 gaining control
	jrnz	dysfout_of_control

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	dysfno_interrupt

	move	*a13(STICK_VAL_CUR),a0
	move	*a13(MOVE_DIR),a1
	or	a0,a1
	andi	MOVE_LEFT|MOVE_RIGHT,a1
	cmpi	MOVE_LEFT|MOVE_RIGHT,a1
	jrne	dysfcontinue_run

	;braking with stick.  abort run.

	SETMODE	NORMAL

dysfcontinue_run
dysfno_interrupt

	move	*a13(STICK_VAL_CUR),a0
	clr	a1

	movi	-dysfZDRIFT_VAL,a1
	btst	PLAYER_UP_BIT,a0
	jrnz	dysfsetvel

	movi	dysfZDRIFT_VAL,a1
	btst	PLAYER_DOWN_BIT,a0
	jrnz	dysfsetvel

	clr	a1
dysfsetvel
	move	a1,*a13(OBJ_ZVEL),L

;Delay reading buttons after just getting out of an out of control run
;so a mistaken move isn't fired off
	move	*a13(DELAY_BUTNS),a0
	jrnz	dysfout_of_control

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	dysfaction_table,a0
	move	*a0,a0,L
	call	a0
dysfout_of_control
	rets

dysfaction_table
	.long	dysfz,dysfpunch,dysfblock,dysfblock			;0-3
	.long	dysfsuper_punch,dysfsuper_punch,dysfblock,dysfblock	;4-7
	.long	dysfkick,dysfpunchkick,dysfblock,dysfblock		;8-11
	.long	dysfsuper_punch,dysfpunchkick,dysfblock,dysfblock	;12-15
	.long	dysfsuper_kick,dysfsuper_kick,dysfblock,dysfblock	;16-19
	.long	dysfgraboh,dysfgraboh,dysfblock,dysfblock		;20-23
	.long	dysfsuper_kick,dysfpunchkick,dysfblock,dysfblock	;24-27
	.long	dysfgraboh,dysfgraboh,dysfblock,dysfblock		;28-31

************
dysfz
	rets


************
dysfpunch		;1
dysfsuper_punch	;4
dysfpunchkick
dysfgraboh
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal			flying clothesline
* dizzy					"
*
* onground	< 176 176	flying ground punch
* onground	>		nothing
*
* running			flying clothesline
* bouncing			flying clothesline
*
* onturnbkl			flying clothesline
* climbturnbkl			flying clothesline
*
	JJXM_INIT
	JJXM	NORMAL,			dysfpunch_clothesline
	JJXM	BLOCK,			dysfpunch_clothesline
	JJXM	DIZZY,			dysfpunch_clothesline
	JJXM	ONGROUND,	176,176,dysfpunch_bellyflop,dysfpunch_rets
	JJXM	RUNNING,		dysfpunch_clothesline
	JJXM	BOUNCING,		dysfpunch_clothesline
	JJXM	ONTURNBKL,		dysfpunch_clothesline
	JJXM	CLIMBTURNBKL,		dysfpunch_clothesline
	JJXM	INAIR,			dysfpunch_clothesline
	JJXM	ATTACHED,		dysfpunch_clothesline
	JJXM	ONGROUND,	176,176,dysfpunch_bellyflop,dysfpunch_rets
	JJXM	OPPOVERHEAD,		dysfpunch_clothesline
	JJXM	WAITANIM,		dysfpunch_clothesline
	JJXM	GRAPPLE,		dysfpunch_clothesline
	JJXM	MASTER,			dysfpunch_clothesline
	JJXM	SLAVE,			dysfpunch_clothesline
	JJXM	HEADHOLD,		dysfpunch_clothesline
	JJXM	HEADHELD,		dysfpunch_clothesline
	JJXM	CHOKEHOLD,		dysfpunch_clothesline
	JJXM	PUPPET,			dysfpunch_clothesline
	JJXM	PUPPET2,		dysfpunch_clothesline
	JJXM	INAIR2,			dysfpunch_clothesline
	JJXM_END

dysfpunch_rets
	rets

*****
dysfpunch_clothesline
;TODO - fix this
	;HACK!!!
	jruc	dysfkick_flyingkick

	rets

*****
dysfpunch_bellyflop
attack_bellyflop

	movi	lex_flying_ground_punch_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2
	rets


************
dysfblock		;2

	move	*a13(OBJ_XVEL),a0,L
	sra	1,a0
	move	a0,*a13(OBJ_XVEL),L
	SETMODE	NORMAL
	jruc	std_block
	rets

************
dysfkick		;8
dysfsuper_kick	;16
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* normal			flying kick
* dizzy					"
*
* onground	< 176 176	flying ground punch
* onground	>		nothing
*
* running			flying kick
* bouncing			flying kick
*
* onturnbkl			flying kick
* climbturnbkl			flying kick
*
	JJXM_INIT
	JJXM	NORMAL,			dysfkick_flyingkick
	JJXM	BLOCK,			dysfkick_flyingkick
	JJXM	DIZZY,			dysfkick_flyingkick
	JJXM	ONGROUND,	176,176,attack_bellyflop,dysfkick_rets
	JJXM	RUNNING,		dysfkick_flyingkick
	JJXM	BOUNCING,		dysfkick_flyingkick
	JJXM	ONTURNBKL,		dysfkick_flyingkick
	JJXM	CLIMBTURNBKL,		dysfkick_flyingkick
	JJXM	INAIR,			dysfkick_flyingkick
	JJXM	ATTACHED,		dysfkick_flyingkick
	JJXM	DEAD,		176,176,attack_bellyflop,dysfkick_rets
	JJXM	OPPOVERHEAD,		dysfkick_flyingkick
	JJXM	WAITANIM,		dysfkick_flyingkick
	JJXM	GRAPPLE,		dysfkick_flyingkick
	JJXM	MASTER,			dysfkick_flyingkick
	JJXM	SLAVE,			dysfkick_flyingkick
	JJXM	HEADHOLD,		dysfkick_flyingkick
	JJXM	HEADHELD,		dysfkick_flyingkick
	JJXM	CHOKEHOLD,		dysfkick_flyingkick
	JJXM	PUPPET,			dysfkick_flyingkick
	JJXM	PUPPET2,		dysfkick_flyingkick
	JJXM	INAIR2,			dysfkick_flyingkick
	JJXM_END

dysfkick_rets
	rets

*****
dysfkick_flyingkick

	calla	ck_ignore
	jrc	dysfz

	SETMODE	INAIR

	movi	lex_flying_kick_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	rets


*****************************************************************************
*

mode_inair	;2

	rets

*****************************************************************************
*

mode_attached	;3

	calla	keep_attached
	jrnz	avovstill_attached

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	SETMODE	NORMAL

	movi	MODE_NORMAL,a0
	move	a0,*a13(ANIMODE)

avovstill_attached
	rets

*****************************************************************************

mode_onground	;4

;	move	*a13(GETUP_TIME),a0
;	jrnz	ezqrexit
;
;	move	*a13(ANIBASE),a0,L
;	cmpi	lex_faceup_getup_anim,a0
;	jrz	ezqrexit
;
;	calla	do_roll
;	jrnz	ezqrexit			;did roll
;
;	movi	lex_faceup_getup_anim,a0
;
;	calla	change_anim1a
;
;ezqrexit
	rets


*****************************************************************************
*
* bouncing off ropes while running

mode_bouncing	;5

	clr	a0
	move	a0,*a13(OBJ_XVEL),L
	move	a0,*a13(OBJ_ZVEL),L

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	sbyenot_ended

	move	*a13(MOVE_DIR),a0
	xori	MOVE_LEFT+MOVE_RIGHT,a0	;flip LEFT/RIGHT
	move	a0,*a13(MOVE_DIR)

	move	*a13(NEW_FACING_DIR),a1
	andi	MOVE_UP+MOVE_DOWN,a1
	or	a0,a1			;LEFT or RIGHT
	move	a1,*a13(FACING_DIR)	;face same dir as run

	movi	lex_run2_anim,a0
	calla	change_anim1a

	SETMODE	RUNNING

;	clr	a0
;	move	a0,*a13(GETUP_TIME)	;in control

sbyenot_ended
	rets

*****************************************************************************
*
* STANDING ON TOP OF TURNBUCKLE

mode_onturnbkl	;6

;	;check unint
;	move	*a13(ANIMODE),a14
;	btst	MODE_UNINT_BIT,a14
;	jrnz	ybbyno_interrupt

	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_DOWN_BIT,a0
	jrz	ybbyno_climbdown

	movi	lex_climb_down_anim,a0
	calla	change_anim1a

	SETMODE	CLIMBTURNBKL

ybbyno_interrupt
	rets

ybbyno_climbdown
	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	jrz	ybbyz

	X32	a0
	addi	ybbyaction_table,a0
	move	*a0,a0,L
	call	a0

	move	*a13(PLYRNUM),a5
	movi	JUMP_ROPES,a2
	movi	1000,a0
	calla	ADD_IF_SILENT

	rets


;For standing on top of turnbuckles

ybbyaction_table
	.long	ybbyz,ybbypunch,ybbyblock,ybbyblock			;0-3
	.long	ybbysuper_punch,ybbysuper_punch,ybbyblock,ybbyblock	;4-7
	.long	ybbykick,ybbypunchkick,ybbyblock,ybbyblock		;8-11
	.long	ybbysuper_punch,ybbypunchkick,ybbyblock,ybbyblock	;12-15
	.long	ybbysuper_kick,ybbysuper_kick,ybbyblock,ybbyblock	;16-19
	.long	ybbygraboh,ybbygraboh,ybbyblock,ybbyblock		;20-23
	.long	ybbysuper_kick,ybbypunchkick,ybbyblock,ybbyblock	;24-27
	.long	ybbygraboh,ybbygraboh,ybbyblock,ybbyblock		;28-31

;	.long	ybbyz			;0
;	.long	ybbypunch			;1
;	.long	ybbyblock			;2
;	.long	ybbyz			;3
;	.long	ybbysuper_punch		;4
;	.long	ybbyz,ybbyz,ybbyz		;5-7
;	.long	ybbykick			;8
;	.long	ybbyz,ybbyz,ybbyz,ybbyz,ybbyz,ybbyz,ybbyz	;9-15
;	.long	ybbysuper_kick		;16
;	.long	ybbyz,ybbyz,ybbyz,ybbyz,ybbyz,ybbyz,ybbyz	;17-23
;	.long	ybbyz,ybbyz,ybbyz,ybbyz,ybbyz,ybbyz,ybbyz,ybbyz	;24-31

************
ybbyz
ybbyblock
	rets

************
ybbypunch		;1
ybbysuper_punch	;4
ybbypunchkick	;5
ybbykick		;8
ybbysuper_kick	;16
ybbygraboh		;20

	SETMODE	INAIR

	movi	lex_buckle_leap_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2
	rets

*****************************************************************************
* blocking

mode_block	;7

	move	*a13(BLOCK_TIME),a0
	inc	a0
	move	a0,*a13(BLOCK_TIME)
	cmpi	160,a0
	jrlt	rtmlno

	calla	get_opp_plyrmode
	cmpi	MODE_BLOCK,a0
	jrnz	rtmlno
	move	*a13(CLOSEST_XDIST),a0
	cmpi	61h,a0
	jrge	rtmlno
	cmpi	45,a0
	jrlt	rtmlno
	move	*a13(CLOSEST_ZDIST),a0
	cmpi	30,a0
	jrlt	rtmlpunchblock

rtmlno
	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	jrz	rtmlno_interrupt

	;press.  if block isn't down, let mode_normal handle it
	move	a0,a2			;save a0 from ...val_down
	move	*a13(BUT_VAL_CUR),a0
	btst	PLAYER_BLOCK_BIT,a0
	jrz	rtmlset_mode_normal
	move	a2,a0			;restore a0 from ...val_down

	X32	a0
	addi	rtmlaction_table,a0
	move	*a0,a0,L
	call	a0

rtmlno_interrupt
	rets

rtmlset_mode_normal
	SETMODE	NORMAL
	jruc	mode_normal

rtmlaction_table
	.long	rtmlz,rtmlpunchblock,rtmlz	;0-2
	.long	rtmlpunchblock		;3
	.long	rtmlz,rtmlz,rtmlz,rtmlz,rtmlz,rtmlz,rtmlz	;4-10
	.long	rtmlz,rtmlz,rtmlz,rtmlz,rtmlz,rtmlz,rtmlz	;11-17
	.long	rtmlz,rtmlz,rtmlz,rtmlz,rtmlz,rtmlz,rtmlz	;18-24
	.long	rtmlz,rtmlz,rtmlz,rtmlz,rtmlz,rtmlz,rtmlz	;25-31

************
rtmlz
	rets

************
rtmlpunchblock	;3
*
* MODE		   DX  DZ	ACTION
* ----------------------------------------------------
* all				push
*

;This push has no collisions set!

	SETMODE	NORMAL

	movi	lex_4_push_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,PUSH_T1,PUSH_T2

	rets


*****************************************************************************
* doink with zero health

mode_dizzy	;8

;	move	*a13(GETUP_TIME),a0
;	jrnz	whulstill_going
;
;	calla	clear_damage_log
;
;	clr	a0
;	move	a0,*a13(STARS_FLAG)
;
;	SETMODE	NORMAL
;
;whulstill_going
	rets

*****************************************************************************
* opponent held overhead

mode_oppoverhead ;10

	move	*a13(ATTACH_PROC),a2,L		;proc attached to
	jrz	loannot_attached
	move	*a2(ATTACH_PROC),a0,L
	jrnz	loanstill_attached

loannot_attached

	move	*a13(ANIMODE),a0		;* remove this????
	btst	MODE_UNINT_BIT,a0		;*
	jrnz	loanno_interrupt			;*

	clr	a0
	move	a0,*a13(ATTACH_PROC),L

	CALLA	FIND_AND_KILL_ENDLESS
	SETMODE	NORMAL

	movi	MODE_NORMAL,a0
	move	a0,*a13(ANIMODE)

	rets

loanstill_attached
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	loanno_interrupt

	move	*a13(FACING_DIR),a0
	andni	MOVE_UP,a0
	ori	MOVE_DOWN,a0
	move	a0,*a13(FACING_DIR)
	move	a0,*a13(NEW_FACING_DIR)

	move	*a13(STICK_VAL_CUR),a0
	jrz	loanstand

	move	a0,*a13(MOVE_DIR)
	calla	execute_walk

	.ref	lex_holdoh_anim
	movi	lex_holdoh_anim,a0
	.ref	change_anim2
	calla	change_anim2
 	jruc	loanck_butns
loanstand
	clr	a0
	move	a0,*a13(MOVE_DIR)
	move	a0,*a13(OBJ_XVEL),L
	move	a0,*a13(OBJ_ZVEL),L

	.ref	lex_stndholdoh_anim
	movi	lex_stndholdoh_anim,a0
	calla	change_anim1

loanck_butns

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	loanaction_table,a0
	move	*a0,a0,L
	call	a0

loanno_interrupt
	rets

loanaction_table
	.long	loanz,loanpunch,loanblock,loanblock			;0-3
	.long	loansuper_punch,loansuper_punch,loanblock,loanblock	;4-7
	.long	loankick,loanpunchkick,loanblock,loanblock		;8-11
	.long	loansuper_punch,loanpunchkick,loanblock,loanblock	;12-15
	.long	loansuper_kick,loansuper_kick,loanblock,loanblock	;16-19
	.long	loangraboh,loangraboh,loanblock,loanblock		;20-23
	.long	loansuper_kick,loanpunchkick,loanblock,loanblock	;24-27
	.long	loangraboh,loangraboh,loanblock,loanblock		;28-31

;three moves from headhold: backbreaker, slamdown, and ohslam.

************
loanz
	rets

************
loanpunch		;1
loanpunchkick
loanblock		;2
loankick		;8

;	move	*a13(STICK_VAL_CUR),a0
;	btst	PLAYER_DOWN_BIT,a0
;	jrz	loanz

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_ohslam_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,PUNCH_T1,PUNCH_T2

	rets

************
loansuper_punch	;4
loangraboh

	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_UP_BIT,a0
	jrz	loanpunch

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_slamdown_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,PUNCH_T1,PUNCH_T2

	rets

************
loansuper_kick	;16

	move	*a13(STICK_VAL_CUR),a0
	btst	PLAYER_UP_BIT,a0
	jrz	loanpunch

	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_backbreaker_anim,a0
	calla	change_anim1a

	WRSND	W_LUGER,HIPTOSS_T1,PUNCH_T2

	rets

*****************************************************************************
*
* CLIMBING UP OR DOWN THE TURNBUCKLE

mode_climbturnbkl	;11

	rets


*****************************************************************************

mode_waitanim	;12

	move	*a13(ANIMODE),a0
	btst	MODE_END_BIT,a0
	jrz	daaznot_ended

	move	*a13(CODE_ADDR),a0,L
	call	a0

daaznot_ended
	rets


*****************************************************************************

mode_grapple	;13

	rets


*****************************************************************************

mode_master	;14

	calla	master_keep_attached	;keep slave attached to me

	rets

*****************************************************************************

mode_slave	;15
	rets

*****************************************************************************

mode_headhold	;16

;Bozo power move
	.ref	bozo_check
	calla	bozo_check
	jrnc	jwckfail

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	movi	lex_vsuplex_anim,a0
	move	@PCNT,a14
	btst	0,a14
	jrz	jwcktag
	movi	lex_4_graboh_anim,a0
jwcktag	calla	change_anim1a

	rets

jwckfail
	move	*a13(WHOIHIT),a0,L
	move	*a0(PLYRMODE),a0
	cmpi	MODE_HEADHELD,a0
	jrnz	jwckexit

;	move	*a13(ATTACH_PROC),a10,L
;	jrz	jwckexit

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	jwckno_interrupt

	move	*a13(BUT_VAL_DOWN),a0
	andi	011111b,a0
	X32	a0
	addi	jwckaction_table,a0
	move	*a0,a0,L
	call	a0

jwckno_interrupt
	rets

jwckexit
	move	*a13(OBJ_ZPOS),a14,L
	subi	[6,0],a14
	move	a14,*a13(OBJ_ZPOS),L

	movk	MOVE_DOWN_RIGHT,a1
	move	*a13(OBJ_CONTROL),a0
	btst	B_FLIPH,a0
	jrz	jwckright
	movk	MOVE_DOWN_LEFT,a1
jwckright	move	a1,*a13(FACING_DIR)
	move	a1,*a13(NEW_FACING_DIR)

	SETMODE	NORMAL

	rets

jwckaction_table
	.long	jwckz,jwckpunch,jwckblock,jwckblock			;0-3
	.long	jwcksuper_punch,jwcksuper_punch,jwckblock,jwckblock	;4-7
	.long	jwckkick,jwckpunchkick,jwckblock,jwckblock		;8-11
	.long	jwcksuper_punch,jwckpunchkick,jwckblock,jwckblock	;12-15
	.long	jwcksuper_kick,jwcksuper_kick,jwckblock,jwckblock	;16-19
	.long	jwckgraboh,jwckgraboh,jwckblock,jwckblock		;20-23
	.long	jwcksuper_kick,jwckpunchkick,jwckblock,jwckblock	;24-27
	.long	jwckgraboh,jwckgraboh,jwckblock,jwckblock		;28-31


************
jwckblock
jwckgraboh
jwckz		;0
	rets

************

jwckpunch		;1
	CALLA	FIND_AND_KILL_ENDLESS
;If stick is toward opponent, then do quick punches
	move	*a13(STICK_VAL_CUR),a0
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a0,a1
	jrnz	jwcktag2
;TODO - change this to uppercuts_to_head_anim!
	WRSND	W_LUGER,KICK_T1,KICK_T2

	.ref	lex_4_knee2_anim
	movi	lex_4_knee2_anim,a0
	calla	change_anim1a
	rets

jwcktag2
;TODO - change this to an uppercut
	WRSND	W_LUGER,KICK_T1,KICK_T2
	movi	lex_4_knee2_anim,a0
	calla	change_anim1a
	rets

************
jwcksuper_punch	;4
	rets

jwckpunchkick
jwckkick
	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_3_knee_to_head_anim,a0
	calla	change_anim1a
	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets

jwcksuper_kick	;16
;If stick is toward opponent, then do quick knees
	move	*a13(STICK_VAL_CUR),a0
	move	*a13(NEW_FACING_DIR),a1
	andi	0ch,a1
	cmp	a0,a1
	jrnz	jwckkick

;Quick knees
	CALLA	FIND_AND_KILL_ENDLESS
	movi	lex_knees_to_head_anim,a0
	calla	change_anim1a
	WRSND	W_LUGER,KICK_T1,KICK_T2
	rets


*****************************************************************************
mode_puppet2	;17

;	;check for the head-grab breaker
;	move	*a13(BUT_VAL_DOWN),a0
;	btst	PLAYER_KICK_BIT,a0
;	jrz	kpxeout
;
;	;check the time - must do it within 20 ticks
;	move	*a13(HEAD_GRAB_TIME),a14
;	move	@PCNT,a0
;	sub	a14,a0
;	cmpi	KICK_OUT,a0
;	jrgt	kpxeout
;
;	move	*a13(PLYR_TYPE),a14
;	cmpi	PTYPE_DRONE,a14
;	jrz	kpxeout
;     
;	;okay, waste 'im. - detach, setmode, immobilize, and launch the knee
;	move	*a13(ATTACH_PROC),a0,L
;	clr	a14
;	move	a14,*a13(ATTACH_PROC),L
;	move	a14,*a0(ATTACH_PROC),L
;	movi	15,a14
;	move	a14,*a0(IMMOBILIZE_TIME)
;	SETMODE	NORMAL
;	jruc	std_knee
;
;kpxeout
	rets

*****************************************************************************

mode_headheld	;19
	move	*a13(ANIMODE),a0
	btst	MODE_NOGRAVITY_BIT,a0
	janz	mode_choking


;Bozo reversal
	calla	bozo_check
	jrnc	fbhvfail

	PUSH	a8
	move	a13,a8
	CALLA	DO_REVERSAL
	CALLA	DO_REVERSAL_MESS
	PULL	a8

	WRSND	W_LUGER,FLYKICK_T1,FLYKICK_T2

	movi	lex_vsuplex_anim,a0
	move	@PCNT,a14
	btst	0,a14
	jrz	fbhvtag
;	movi	lex_4_graboh_anim,a0
fbhvtag	calla	change_anim1a

	rets
fbhvfail
;He may have let me go by hitting his block butn!
	move	*a13(ATTACH_PROC),a0,L
	jrnz	fbhvno_luck

;Make sure this guy is on ground
	move	*a13(OBJ_YPOSINT),a0
	move	*a13(GROUND_Y),a1
	cmp	a1,a0			;a0-a1
	jrgt	fbhvno_luck

;hit_gnd

	movi	lex_3_head_held_stand_anim,a0
	calla	change_anim1a

fbhvno_luck
	rets

*****************************************************************************
mode_chokehold	;24
	rets

*****************************************************************************

;xifnVEL	equ	30000h		;38000h	;30000h
;xifnDVEL	equ	21f0eh		;27978h	;21f0eh
;xifnVEL	equ	38000h		;38000h	;30000h
;xifnDVEL	equ	30000h		;27978h	;21f0eh
xifnVEL	equ	3a000h		;38000h	;30000h
xifnDVEL	equ	31000h		;27978h	;21f0eh


 SUBR lex_velocity_table

;		X,	Z

	.long	0,	-xifnVEL	;0 UP

	.long	xifnDVEL,	-xifnDVEL	;1 UP-RIGHT

	.long	xifnVEL,	0	;2 RIGHT

	.long	xifnDVEL,	xifnDVEL	;3 DOWN-RIGHT

	.long	0,	xifnVEL	;4 DOWN

	.long	-xifnDVEL,	xifnDVEL	;5 DOWN-LEFT

	.long	-xifnVEL,	0	;6 LEFT

	.long	-xifnDVEL,	-xifnDVEL	;7 UP-LEFT


*****************************************************************************

 SUBR lex_rotate_anims_table
; old = up right
	.long	lex_stand2_anim		;new = up right
	.long	lex_2_to_4_turn_anim	;    = down right
	.long	lex_2_to_6_turn_anim	;    = down left
	.long	lex_2_to_8_turn_anim	;    = up left

; old = down right
	.long	lex_4_to_2_turn_anim	;new = up right
	.long	lex_stand4_anim		;    = down right
	.long	lex_4_to_6_turn_anim	;    = down left
	.long	lex_4_to_8_turn_anim	;    = up left

; old = down left
	.long	lex_6_to_2_turn_anim	;new = up right
	.long	lex_6_to_4_turn_anim	;    = down right
	.long	lex_stand6_anim		;    = down left
	.long	lex_6_to_8_turn_anim	;    = up left

; old = up left
	.long	lex_8_to_2_turn_anim	;new = up right
	.long	lex_8_to_4_turn_anim	;    = down right
	.long	lex_8_to_6_turn_anim	;    = down left
	.long	lex_stand8_anim		;    = up left


 SUBR lex_leg_anims_table
;  MOVE
;(tfvk1 - UP)				FACING
	.long	lex_walk1_f2_anim	;1 (UP)
	.long	lex_walk1_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk1_f4_anim	;3 (RIGHT)
	.long	lex_walk1_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk1_f4_anim	;5 (DOWN)
	.long	lex_walk1_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk1_f2_anim	;7 (LEFT)
	.long	lex_walk1_f2_anim	;8 (UP-LEFT)

;(tfvk2 - UP-RIGHT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk2_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk2_f2_anim	;3 (RIGHT)
	.long	lex_walk2_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk8_f4_anim	;5 (DOWN)
	.long	lex_walk8_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk4_f2_anim	;7 (LEFT)
	.long	lex_walk4_f2_anim	;8 (UP-LEFT)

;(tfvk3 - RIGHT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk2_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk2_f2_anim	;3 (RIGHT)
	.long	lex_walk4_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk4_f4_anim	;5 (DOWN)
	.long	lex_walk8_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk6_f2_anim	;7 (LEFT)
	.long	lex_walk6_f2_anim	;8 (UP-LEFT)


;(tfvk4 - DOWN-RIGHT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk8_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk4_f4_anim	;3 (RIGHT)
	.long	lex_walk4_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk2_f4_anim	;5 (DOWN)
	.long	lex_walk6_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk2_f2_anim	;7 (LEFT)
	.long	lex_walk6_f2_anim	;8 (UP-LEFT)

;(tfvk5 - DOWN)
	.long	lex_walk5_f2_anim	;1 (UP)
	.long	lex_walk5_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk5_f4_anim	;3 (RIGHT)
	.long	lex_walk5_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk5_f4_anim	;5 (DOWN)
	.long	lex_walk5_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk5_f2_anim	;7 (LEFT)
	.long	lex_walk5_f2_anim	;8 (UP-LEFT)

;(tfvk6 - DOWN-LEFT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk6_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk2_f2_anim	;3 (RIGHT)
	.long	lex_walk6_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk2_f4_anim	;5 (DOWN)
	.long	lex_walk4_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk2_f2_anim	;7 (LEFT)
	.long	lex_walk8_f2_anim	;8 (UP-LEFT)

;(tfvk7 - LEFT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk6_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk6_f2_anim	;3 (RIGHT)
	.long	lex_walk8_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk4_f4_anim	;5 (DOWN)
	.long	lex_walk4_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk2_f2_anim	;7 (LEFT)
	.long	lex_walk2_f2_anim	;8 (UP-LEFT)

;(tfvk8 - UP-LEFT)
	.long	lex_walk2_f2_anim	;1 (UP)
	.long	lex_walk4_f2_anim	;2 (UP_RIGHT)
	.long	lex_walk6_f2_anim	;3 (RIGHT)
	.long	lex_walk8_f4_anim	;4 (DOWN_RIGHT)
	.long	lex_walk6_f4_anim	;5 (DOWN)
	.long	lex_walk2_f4_anim	;6 (DOWN_LEFT)
	.long	lex_walk2_f2_anim	;7 (LEFT)
	.long	lex_walk2_f2_anim	;8 (UP-LEFT)


 SUBR lex_torso_anims_table
; old = up right
	.long	lex_torso2_anim		;new = up right
	.long	lex_2_to_4_turn2_anim	;    = down right
	.long	lex_2_to_6_turn2_anim	;    = down left
	.long	lex_2_to_8_turn2_anim	;    = up left

; old = down right
	.long	lex_4_to_2_turn2_anim	;new = up right
	.long	lex_torso4_anim		;    = down right
	.long	lex_4_to_6_turn2_anim	;    = down left
	.long	lex_4_to_8_turn2_anim	;    = up left

; old = down left
	.long	lex_6_to_2_turn2_anim	;new = up right
	.long	lex_6_to_4_turn2_anim	;    = down right
	.long	lex_torso6_anim		;    = down left
	.long	lex_6_to_8_turn2_anim	;    = up left

; old = up left
	.long	lex_8_to_2_turn2_anim	;new = up right
	.long	lex_8_to_4_turn2_anim	;    = down right
	.long	lex_8_to_6_turn2_anim	;    = down left
	.long	lex_torso8_anim		;    = up left


******************************************************************************

	.end

