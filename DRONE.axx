 ;Yoko tb on out of ring opp
 ;UT pin bug
 ;Atk sometimes instead of blk
;Chk dnk buzz blk
 ;No leap head grab
;Don't have drones do power moves unless human has.
;Yoko should take advantage of salt throw
 ;Drones wait in your face for too long.
 ;DL blk for hiptoss
 ;Harder
 ;Atk down opp
 ;Anti run code, esp out of ctrl
 ;Fling and chase
 ;Short repeat atks
 ;Do more head holds from in close - thru block
 ;Seek on a blocker, do headhold
 ;Make multi drones easier...
 ;Too many runs
 ;Break out of runs more...
;Climb TB after a toss out
 ;Sometimes, drone won't move from top of TB
 ;Don't let go charge moves if opp is blocking
 ;In multi-wrestler matches, delay longer on power moves from headhold or revs
;Yoko can run right into drone and knock him down.
;Get smart on big boots, missed/blocked buzzers, etc.
 ;Ignore pushes from opponent, try an attack
;Specific combo paths
 ;Pin fast
 ;Multi wres spacing
 ;Get in ring
 ;Check blocking
;Fix seek_dirdist out of ring
 ;Slide htoss on flying opp
**************************************************************
*
* Software:		Shawn Liptak
* Initiated:		11/21/94
*
* Modified:		Shawn Liptak, 11/21/94	-Started wrestling
*
* COPYRIGHT (C) 1994 WILLIAMS ELECTRONICS GAMES, INC.
*
*.Last mod - 6/29/95 15:42
**************************************************************
	.file	"drone.asm"
	.title	"wrestling drone code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"macros.h"
	.include	"mproc.equ"
	.include	"display.equ"
	.include	"gsp.equ"
	.include	"sys.equ"
	.include	"audit.equ"
	.include	"game.equ"
	.include	"anim.equ"
	.include	"plyr.equ"
	.include	"ring.equ"
	.include	"damage.equ"


;sounds external


;symbols externally defined

	.ref	process_ptrs,PSTATUS,PSTATUS2
	.ref	PCNT,round_tickcount

	.ref	CHECK_COMBO_GO

	.ref	GET_ADJ
	.ref	p1winstreakd,p1rounds,current_round
	.ref	LADDER,CURRENT_LADDER

	.ref	change_anim1a
	.ref	hrt_4_taunt_anim,rzr_4_taunt_anim,und_4_taunt_anim
	.ref	yok_4_taunt_anim,shn_4_taunt_anim,bam_4_taunt_anim
	.ref	dnk_4_taunt_anim,lex_4_taunt_anim


;symbols defined in this file


;uninitialized ram definitions

	BSSX	droneoff	,16		;!0=Drone code off
	.bss	atkcnt_t,AT_NUM*NUM_WRES*16	;# times hit by each atk


;equates for this file

BLKTEST	.equ	0

U_M	.equ	MOVE_UP<<5
D_M	.equ	MOVE_DOWN<<5
L_M	.equ	MOVE_LEFT<<5
R_M	.equ	MOVE_RIGHT<<5

P_M	.equ	PLAYER_PUNCH_VAL
B_M	.equ	PLAYER_BLOCK_VAL
SP_M	.equ	PLAYER_SPUNCH_VAL
K_M	.equ	PLAYER_KICK_VAL
SK_M	.equ	PLAYER_SKICK_VAL

	.if	MODE_NORMAL!=0
	.emsg	"FIX code!"
	.endif


	.text

***************************************************************
* Main drone logic
* A13 = *Plyr process
* Trashes scratch, A2-A10, B2-B7

 SUBR	drone_main

	.if	DEBUG
	move	@droneoff,a0
	jrnz	khncxx
	.endif


	move	*a13(DRN_BUT),a6	;A6=Old buts
	move	*a13(DRN_JOY),a7	;A7=Old joy

	move	*a13(CLOSEST_NUM),a14
	X32	a14
	addi	process_ptrs,a14
	move	*a14,a8,L		;A8=*Closest's proc

;	callr	drone_getcurskillo
;	move	a0,a10
	move	*a13(DRN_SKILL),a10
	X16	a10			;A10=Skill for indexing (*16)

	move	*a13(PLYRMODE),a0
	move	a0,b6			;B6=My pmode
	move	*a8(PLYRMODE),a0
	move	a0,b7			;B7=Opp pmode

;컴컴컴컴컴컴컴			0FHind friendly wrestlers

	movi	process_ptrs,a2
	movk	NUM_WRES,b0
	clr	b2			;B2=Team dist ranking (0-2)
	clr	b3			;B3=# alive on my team (0-3)
khncfflp
	move	*a2+,a3,L
	jrz	khncffnxt

	move	*a13(PLYR_SIDE),a0
	move	*a3(PLYR_SIDE),a1
	cmp	a0,a1
	jrne	khncffnxt			;Not on my team?

	move	*a3(PLYRMODE),a0
	subk	MODE_DEAD,a0
	jreq	khncffnxt

	addk	1,b3

	move	*a13(CLOSEST_NUM),a0
	move	*a3(CLOSEST_NUM),a1
	cmp	a0,a1
	jrne	khncffnxt			;Diff opponents?

	move	*a13(CLOSEST_DIST),a0
	move	*a3(CLOSEST_DIST),a1
	cmp	a1,a0
	jrle	khncffnxt			;I'm closer?

	addk	1,b2
khncffnxt
	dsj	b0,khncfflp

khncffdn
	move	b2,b2
	jrz	khncffdoit			;I'm closest?

	move	*a13(DRN_ACT_p),a9,L
	jrnz	khncffx			;Script running?

	move	*a13(DRN_MODE),a0
	movi	-3,a1
	move	a1,*a13(DRN_MODE)	;Set mode
	addk	3,a0
	jrne	khncnewmdm3		;Init hang back?
	jruc	khncffx

khncffdoit
	move	*a13(DRN_MODE),a0
	addk	3,a0
	jreq	khncnewmd			;Set new mode if in hang back?

khncffx
;컴컴컴컴컴컴컴			>Random mode changes

	move	@PCNT,a0
	sll	32-5,a0
	jrnz	khncnomdchk

	movk	3,a0
	callr	rnd
	jrnz	khncnomdchk

	move	*a13(DRN_MODE),a0
	addk	3,a0
	jrle	khncmcnosc			;Hang back?
khncnewmd
	movk	1,a0			;No aggressive
	move	@CURRENT_LADDER,a1,L	;* to position
	cmpi	LADDER,a1
	jreq	khncmdeasy			;1st ladder?

	movk	3,a0			;Range
	cmpi	13*16,a10
	jrle	khncmdeasy
	addk	1,a0			;More aggressive
khncmdeasy
	callr	rndrng0
	subk	2,a0
	move	a0,*a13(DRN_MODE)	;-2 to 2

	move	*a13(DRN_ACT_p),a9,L
	jrnz	khncmcnosc			;Script running?
	clr	a0
	move	a0,*a13(DRN_JOY)	;So we don't walk into ropes
khncmcnosc
khncnewmdm3
	movk	4,a2			;Rgt side
	move	*a13(OBJ_XPOSINT),a0
	move	*a8(OBJ_XPOSINT),a1
	sub	a1,a0
	jrge	khncmetorgt
	movk	12,a2			;Left side
khncmetorgt
	move	a2,*a13(DRN_SEEKDIR)

	movk	4,a0
	callr	rndrng0

	move	*a13(DRN_MODE),a1
	addk	3,a1
	jrne	khncnodhb			;!Hang back?
	ANDK	1,a0			;Make 3-4 or 4-5
	addk	2,a0
	move	b2,a14
	add	a14,a0
khncnodhb
	move	a0,*a13(DRN_SEEKDIST)

khncnomdchk
;컴컴컴컴컴컴컴			>Passive mode movement

	.if	BLKTEST
	jruc	khncskmv
	.endif

	move	*a13(DRN_MODE),a0
	jrge	khncskmv			;Aggressive?

	move	b6,b6
	jrnz	khncskmv			;!MODE_NORMAL?

	move	*a13(DRN_ACT_p),a9,L
	jrnz	khncskmv			;Script running?

	callr	drone_seekdirdist
khncskmv

;컴컴컴컴컴컴컴			0DecH charge delay

	move	*a13(DRN_BUTCHRG),a0
	jrz	khncnochrgn

	move	*a13(DRN_BUTCHRGDLY),a0
	subk	1,a0
	move	a0,*a13(DRN_BUTCHRGDLY)
khncnochrgn

;컴컴컴컴컴컴컴			>Handle getup time

	move	*a13(GETUP_TIME),a2
	jrle	khncnogt

	movi	99,a0
	callr	rndrng0
	move	a10,a1
	addi	khncgetup_t,a1
	move	*a1,a1
	cmp	a1,a0
	jrge	khncdsabt

	subk	5,a2
	jrge	khncgtok
	clr	a2
khncgtok	move	a2,*a13(GETUP_TIME)
	jruc	khncdsabt
khncnogt
;컴컴컴컴컴컴컴

	move	*a13(DRN_DELAY),a0
	subk	1,a0
	jrle	khncnewact
	move	a0,*a13(DRN_DELAY)
	jruc	khncx
khncnewact
;컴컴컴컴컴컴컴			0BHlock detection

	move	*a8(ATTACK_TIME),a4
	move	@round_tickcount,a5
	sub	a5,a4			;-=Attack passed, +=Ticks till atk
	jrlt	khncnoblk

	movi	50,a1			;Closer Z allowed
	move	*a8(ATTACK_TYPE),a2
	cmpi	AT_PUSH,a2
	jreq	khncnoblk
	cmpi	AT_MSL,a2
	jreq	khncmsla			;Missile atk?

	move	*a13(CLOSEST_XDIST),a0
	cmpi	180,a0
	jrgt	khncnoblk			;Too far?
	movi	100,a1
khncmsla
	move	*a13(CLOSEST_ZDIST),a0
	cmp	a1,a0
	jrgt	khncnoblk			;Too far?

	move	*a8(GETUP_TIME),a14
	jrgt	khncnoblk			;Out of control?

	cmpi	MODE_INAIR2,b7
	jreq	khncnoblk			;Jumping on me?

	btst	PLAYER_BLOCK_BIT,a6
	jrnz	khncx			;Trying to blk?

	move	*a8(ATTACK_TYPE),a2
	move	*a13(PLYRNUM),a0
	movi	AT_NUM,a3
	mpys	a0,a3
	add	a2,a3
	X16	a3
	addi	atkcnt_t,a3

	move	*a3,a2			;# missed blks
	cmpi	10,a2
	jrlo	khncacok
	movk	9,a2
khncacok
	X16	a2
	addi	khncblkatk_t,a2
	move	*a2,a2			;Get %

	move	b3,a14			;# on team
	subk	1,a14
	X32	a14
	sub	a14,a2			;Decrease per extra bud

	.if	BLKTEST
	movi	100,a2
	.endif

	movi	99,a0
	callr	rndrng0
	move	a10,a1
	addi	khncblkbase_t,a1
	move	*a1,a1			;Base %
	add	a2,a1
	cmp	a1,a0
	jrge	khncmissb

	cmpi	15,a4
	jrge	khncminbtok		;Min time OK?
	movk	15,a4
khncminbtok

	move	*a8(ATTACK_TYPE),a2
	cmpi	AT_LEAPING,a2
	jrne	khncsblk			;!Leaping?

;	LOCKUP

	move	a10,a0
	srl	4+2,a0			;Skill/4
	callr	rndrng0
	move	a0,a0
	jrz	khncsblk			;Skip?

	move	a5,*a8(ATTACK_TIME)	;Cancel

	movi	slhtoss,a9		;Flip opp
	jruc	khncnewsc

khncsblk
	move	a4,*a13(DRN_DELAY)

	movk	B_M,a0			;Block it
	move	a0,*a13(DRN_BUT)

	cmpi	AT_PUPPET,a2
	jrne	khncdsabt			;!Hip toss?

	movi	B_M+L_M+D_M,a0		;Special block
	move	a4,a3			;Dly
	clr	a9
	jruc	khncsetbx

khncmissb
	move	a5,*a8(ATTACK_TIME)	;Cancel

	move	*a3,a0			;Cnt+1
	addk	1,a0
	move	a0,*a3

khncnoblk
;컴컴컴컴컴컴컴			>New script selection

	move	*a13(DRN_ACT_p),a9,L	;A9=*Script
	jrnz	khnccact			;Continue script?


	clr	a2

	cmpi	MODE_BLOCK,b6
	jrne	khncnps			;!Blking?
	btst	PLAYER_PUNCH_BIT,a6
	jrnz	khncnps			;Already pushed?

	.if	BLKTEST
	jruc	khncnps
	.endif

	movk	7,a0
	callr	rndrng0
	move	a0,a0
	jrnz	khncnps			;Skip 88%?

	move	*a13(CLOSEST_XDIST),a0
	cmpi	110,a0
	jrgt	khncnps			;Too far?
	move	*a13(CLOSEST_ZDIST),a0
	cmpi	40,a0
	jrgt	khncnps			;Too far?
	movk	P_M+B_M,a2		;Push
	move	a2,*a13(DRN_BUT)
	jruc	khncx
khncnps
	move	a2,*a13(DRN_BUT)

	.if	BLKTEST
	jruc	khncx
	.endif


	move	b6,b6
	jrnz	khncdoact			;!MODE_NORMAL?


	move	*a13(INRING),a0
	jrz	khncringok			;In ring?

	move	*a8(INRING),a0
	jrnz	khncringok			;Opp out?

	movi	drn_enterring,a9
	jruc	khncnewsc
khncringok

	move	*a13(DRN_MODE),a3
	cmpi	-3,a3
	jrle	khncpass			;Hang back?

	cmpi	MODE_ONGROUND,b7
	jreq	khncdoact			;Atk?

	cmpi	MODE_INAIR2,b7
	jrne	khnconia
	movi	drn_opinair,a9		;Avoid jump
	jruc	khncnewsc
khnconia
	cmpi	MODE_RUNNING,b7
	jrne	khnconrun
	move	*a13(CLOSEST_XDIST),a0
	cmpi	80,a0
	jrle	khnconrun			;Too close?
	movi	drn_oprun,a9		;Anti run
	jruc	khncnewsc
khnconrun

	move	*a8(ATTACK_TIME),a0
	move	@round_tickcount,a1
	sub	a1,a0			;-=Attack passed, +=Ticks till atk
	addk	15,a0
	jrge	khncdoact			;Get him after his atk (or no blk)?


	move	a3,a3
	jrlt	khncpass			;Passive?

	movk	1fh,a0			;.6 sec
	cmpi	22*16,a10
	jrle	khnccat
	movk	7,a0			;.15 sec
khnccat
	callr	rnd
	jrz	khncdoactpup		;Atk?


khncpass
	move	@PCNT,a0		;Chk every 16
	addk	1,a0
	sll	32-4,a0
	jrnz	khncx

	move	*a13(CLOSEST_XDIST),a1
	move	*a13(CLOSEST_ZDIST),a14
	cmp	a1,a14
	jrge	khncbig
	move	a1,a14
khncbig
	cmpi	70,a14
	jrlt	khncdoactpup		;Opp in my face? Attack!

	move	*a13(DRN_MODE),a0
	addk	3,a0
	jrle	khncx			;Hang back?

	move	@CURRENT_LADDER,a1,L	;* to position
	cmpi	LADDER,a1
	jreq	khncnratk			;1st ladder?

	movk	0fh,a0			;Avg 4.8 sec
	callr	rnd
	jrz	khncdoactpup		;Random attack?
khncnratk
	move	b7,b7
	jrz	khncx			;Opp MODE_NORMAL? Be passive
khncdoactpup
	move	b7,a0
	subk	MODE_PUPPET2,a0
	jreq	khncx
	subk	MODE_PUPPET-MODE_PUPPET2,a0
	jreq	khncx
	cmpi	MODE_HEADHELD,b7
	jreq	khncx
	cmpi	MODE_HEADHOLD,b7
	jreq	khncx
	cmpi	MODE_ATTACHED,b7
	jreq	khncx


khncdoact
	cmpi	MODE_HEADHELD,b6
	jreq	khncslctscrpt

	cmpi	MODE_ONGROUND,b6
	jrne	khncnognd
	movi	drn_roll,a9
	jruc	khncnewsc
khncnognd
	cmpi	MODE_INAIR2,b6
	jrne	khncnin2
	movi	drn_inair,a9
	jruc	khncnewsc
khncnin2
	cmpi	MODE_ONTURNBKL,b6
	jrne	khncnotb
	movi	drn_ontb,a9
	jruc	khncnewsc
khncnotb
	cmpi	MODE_DEAD,b6
	jrne	khncndead
	movk	7,a0
	callr	rnd
	jruc	khncbutx
khncndead
	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrnz	khncunint			;Wait?

	cmpi	MODE_RUNNING,b6
	jrne	khncnrun
	movi	drn_run,a9
	jruc	khncnewsc
khncnrun

	cmpi	MODE_HEADHOLD,b6
	jrne	khncnhh

	clr	a0
	move	a0,*a13(DRN_BUTCHRG)	;Clr it

;	movk	1,a0			;50%
;	callr	rnd
;	jrnz	khncnhh			;Don't combo?

	move	a8,a3
	move	a13,a8
	calla	CHECK_COMBO_GO
	jrlt	khncncmb			;Can't combo?
	move	a3,a8
	movi	drn_combo,a9
	jruc	khncnewsc
khncncmb
	move	a3,a8
khncnhh

	move	*a13(DRN_BUTCHRG),a0
	jrz	khncnochrg

	move	*a13(DRN_BUTCHRGDLY),a0
	jrgt	khncnochrg

	cmpi	MODE_BLOCK,b7
	jreq	khncnochrg			;Opp blking?

	move	*a13(DRN_BUTCHRG_p),a9,L ;Fire charge
	jruc	khncnewsc
khncnochrg

	move	*a13(CLOSEST_DIST),a14
	cmpi	200,a14
	jrgt	khncopnb

	cmpi	MODE_BLOCK,b7
	jrne	khncopnb			;!Blking?

	move	*a13(CLOSEST_ZDIST),a14
	cmpi	40,a14
	jrgt	khncbsk

	move	*a13(CLOSEST_XDIST),a14
	cmpi	60,a14
	jrgt	khncbsk

	move	*a8(STICK_REL_CUR),a14
	cmpi	MOVE_DOWN_LEFT,a14
	jreq	khncbht			;Hip toss blocked?

	movi	M_shrtblkr,a9		;Attack
	jruc	khncgetscrpt
khncbht
	movi	M_shrtblkrdl,a9		;Attack
	jruc	khncgetscrpt
;	movi	hgrab,a9
;	jruc	khncnewsc
khncbsk
	movi	drn_seekclose,a9
	jruc	khncnewsc
khncopnb

	cmpi	MODE_DEAD,b7
	jrne	khncopnd

	movi	drn_oppdead,a9
	jruc	khncnewsc
khncopnd

khncslctscrpt

	move	*a13(WRESTLERNUM),a0
	X32	a0
	addi	khncwnshort_t,a0

	move	*a13(CLOSEST_XDIST),a1
	move	*a13(CLOSEST_ZDIST),a14
	X2	a14			;Z*2
	cmp	a1,a14
	jrge	khnchavebig
	move	a1,a14
khnchavebig
	cmpi	100,a14
	jrlt	khncshrt
	addi	khncwnmed_t-khncwnshort_t,a0

	cmpi	180,a14
	jrlt	khncmed
	addi	khncwnlong_t-khncwnmed_t,a0
khncshrt
khncmed
	move	*a0,a9,L		;Get * mode list
	move	b6,a2
	move	b7,a3
khncmdlp
	movb	*a9,a0			;My mode #
	addk	8,a9
	movb	*a9,a14			;His mode #
	addk	8,a9
	move	*a9+,a1,L		;* script * table

	move	a0,a0
	jrn	khncinochk			;Don't chk?
	cmp	a2,a0
	jrne	khncmdlp			;Not in mode?
khncinochk
	move	a14,a14
	jrn	khncdef			;Don't chk?
	cmp	a3,a14
	jrne	khncmdlp			;Not in mode?
khncdef
	move	a1,a9
khncgetscrpt
	move	*a9+,a0			;# entries
	jrge	khncneok			;!Headhold?
	abs	a0
	cmpi	26*16,a10
	jrle	khncneok			;!Hard?
	movk	1,a0			;Only do first two
khncneok
	callr	rndrng0
	X32	a0
	add	a0,a9
	move	*a9,a9,L		;* new script

khncnewsc
	move	b6,a0
	move	a0,*a13(DRN_SPMODE)


	cmpi	MODE_HEADHOLD,b6
	jrne	khncnhh2

	movk	1,a3
	cmpi	2,b3
	jrlt	khnchhd1			;1 opp?
	movk	22,a3
khnchhd1
	movi	sklhhdly_t,a0
	add	a10,a0			;+Offset
	move	*a0,a0
	callr	rndrng0
	add	a0,a3			;Increase per extra bud

	move	@PCNT,a0
	sll	32-1,a0
	jrz	khnchhdok			;Skip half of checks?

	cmpi	65,a3
	jrle	khnchhdok
	movi	65,a3			;Max delay
khnchhdok
	clr	a0
	jruc	khncsetbx
khncnhh2

	cmpi	MODE_HEADHELD,b6
	jrne	khncnhhe

	clr	a0
	move	a0,*a13(DRN_BUTCHRG)	;Clr it

	move	b3,a3			;# on team
	movk	7,a0
	callr	rnd
	jrz	khnchrskmp			;12% skip multiplyr delay?
	subk	1,a3
	X16	a3
khnchrskmp
	movi	sklhrdly_t,a0
	add	a10,a0			;+Offset
	move	*a0,a0
	callr	rndrng0
	addk	1,a0
	add	a0,a3			;Increase per extra bud

	move	@PCNT,a0
	sll	32-1,a0
	jrz	khnchrdok			;Skip half of checks?

	cmpi	70,a3
	jrle	khnchrdok
	movi	70,a3			;Max delay
khnchrdok
	clr	a0
	jruc	khncsetbx
khncnhhe


khnccact
;컴컴컴컴컴컴컴			0CHhk script for mode change

	move	b6,a0
	jrz	khncmdsame			;MODE_NORMAL?
	subk	MODE_BLOCK,a0
	jreq	khncmdsame			;Treat as normal?
	move	*a13(DRN_SPMODE),a0
	move	b6,a1
	cmp	a1,a0
	.if	DEBUG
	jreq	khncdsok
	jruc	khncdsabt			;For breakpoint
khncdsok
	.endif
	jrne	khncdsabt

khncmdsame

	.if	DEBUG
	move	a9,a9			;Chk script *
	jrz	khnca9ok
	cmpi	0ff000000h,a9
	jrhs	khnca9ok
	LOCKUP
khnca9ok
	.endif

;컴컴컴컴컴컴컴			>Read script command
khncscplp
	move	*a9+,a0
	jrge	khncnocd			;No command?

	sll	32-14,a0
	jrc	khncdsdone			;Done? (Bit 14)

	srl	32-14,a0

;컴컴컴컴컴컴컴			>Seek until 0

	subk	1,a0			;khnc1
	jrne	khncnseekt0

	callr	drone_seek
	jrz	khncscplp

	subk	16,a9
	jruc	khncdsdone
khncnseekt0
;컴컴컴컴컴컴컴			>Random skill lvl abort

	subk	1,a0			;khnc2
	jrne	khncnrnda

	cmpi	MODE_BLOCK,b7
	jreq	khncdsabt			;Blking?

	move	*a9+,a3,L		;*Skill table

;	clr	a2
;	cmpi	sklrev_t,a3
;	jrne	khncnsr
;	move	b3,a2			;# on team
;	subk	1,a2
;	X16	a2
;khncnsr
	movi	99,a0
	callr	rndrng0
	add	a10,a3			;+Offset
	move	*a3,a1
;	sub	a2,a1			;Decrease per extra bud
	cmp	a1,a0
	jrlt	khncscplp			;Cont?

	movk	10,a0
	move	a0,*a13(DRN_DELAY)

	jruc	khncdsabt
khncnrnda
;컴컴컴컴컴컴컴			>Wait till interruptable

	subk	1,a0			;khnc3
	jrne	khncnwtint

	move	*a13(ANIMODE),a0
	btst	MODE_UNINT_BIT,a0
	jrz	khncscplp			;Off?

	subk	16,a9
	jruc	khncdsdone
khncnwtint
;컴컴컴컴컴컴컴			0AbHort if opponent blocking

	subk	1,a0			;khnc4
	jrne	khncnaib

	cmpi	MODE_BLOCK,b7
	jrne	khncscplp			;!Blocked?

	jruc	khncdsabt
khncnaib
;컴컴컴컴컴컴컴			0CaHll code

	subk	1,a0			;khnc5
	jrne	khncncall

	move	*a9+,a0,L
	call	a0

	jruc	khncscplp
khncncall
;컴컴컴컴컴컴컴			>Rnd jump

	subk	1,a0			;khnc6
	jrne	khncnrj

	movi	99,a0
	callr	rndrng0
	move	*a9+,a1			;Percent to jmp
	move	*a9+,a2,L
	cmp	a1,a0
	jrge	khncscplp

	move	a2,a9			;New addr
	jruc	khncscplp
khncnrj
;컴컴컴컴컴컴컴			>Jump

	subk	1,a0			;khnc7
	jrne	khncnjmp

	move	*a9+,a9,L
	jruc	khncscplp
khncnjmp
;컴컴컴컴컴컴컴			0CaHll function

	exgpc	a9
	jruc	khncscplp
khncnocd
;컴컴컴컴컴컴컴			>Joy & button bits

	move	*a9+,a3			;Get delay
khncsetbx
	move	a0,a1
	sll	32-5,a0
	srl	32-5,a0
	move	a0,*a13(DRN_BUT)

	srl	5,a1
	move	*a13(FACING_DIR),a14
	btst	PLAYER_RIGHT_BIT,a14
	jrnz	khncrgt			;Facing rgt?

	move	a1,a0			;0FHlip L&R bits
	move	a1,a14
	sll	32-2,a1
	srl	3,a0			;Bit0=Rgt
	sll	32-3,a14
	srl	31,a14
	sll	1,a14			;Bit1=Left
	or	a0,a1
	or	a14,a1
	rl	2,a1
khncrgt
	move	a1,*a13(DRN_JOY)

	move	a3,*a13(DRN_DELAY)
	move	a3,a3
	jrgt	khncdsdone
khncdsabt
	clr	a9
khncdsdone
	move	a9,*a13(DRN_ACT_p),L

khncunint

;컴컴컴컴컴컴컴			0CaHlc ctrl bit transitions
khncx
	move	*a13(DRN_BUT),a0
khncbutx
	move	*a13(DRN_BUTCHRG),a14
	or	a14,a0
	move	a0,*a13(DRN_BUT)
	xor	a0,a6
	move	a6,a1
	and	a0,a6
	move	a6,*a13(DRN_BUTDT)
	andn	a0,a1
	move	a1,*a13(DRN_BUTUT)

	move	*a13(DRN_JOY),a0
	xor	a0,a7
	move	a7,a1
	and	a0,a7
	move	a7,*a13(DRN_JOYDT)
	andn	a0,a1
	move	a1,*a13(DRN_JOYUT)

khncxx
	rets


SKLM	.macro	w,ad
	.word	:w:,:w:+:ad:,:w:+:ad:*2,:w:+:ad:*3,:w:+:ad:*4
	.endm

khncgetup_t				;% to bump getup time
	SKLM	10,2
	SKLM	20,2
	SKLM	30,2
	SKLM	40,2
	SKLM	50,2
	SKLM	60,10
khncblkbase_t				;Base % to block
	SKLM	10,2
	SKLM	20,2
	SKLM	30,1
	SKLM	35,1
	SKLM	40,1
	SKLM	47,7
khncblkatk_t				;% to block per attack
	.word	0,10,20,30,40
	.word	50,50,50,50,50
sklhhdly_t				;Max delay on hh
	SKLM	150,-6
	SKLM	120,-6
	SKLM	90,-6
	SKLM	60,-6
	SKLM	28,-3
	SKLM	13,-2
sklhrdly_t				;Max delay on hh rev
	SKLM	150,-6
	SKLM	120,-6
	SKLM	90,-6
	SKLM	60,-6
	SKLM	28,-3
	SKLM	13,-2
sklrep_t				;% to repeat
	SKLM	20,4
	SKLM	45,2
	SKLM	55,4
	SKLM	75,2
	SKLM	85,2
	SKLM	90,3


;khncgetup_t				;% to bump getup time
;	SKLM	5,2
;	SKLM	15,1
;	SKLM	20,2
;	SKLM	30,2
;	SKLM	40,4
;	SKLM	60,10
;khncblkbase_t				;Base % to block
;	SKLM	5,1
;	SKLM	10,2
;	SKLM	20,2
;	SKLM	30,1
;	SKLM	35,2
;	SKLM	45,7
;khncblkatk_t				;% to block per attack
;	.word	0,10,20,30,40
;	.word	50,50,50,50,50
;sklhhdly_t				;Max delay on hh
;	SKLM	200,-10
;	SKLM	150,-6
;	SKLM	120,-9
;	SKLM	70,-8
;	SKLM	35,-4
;	SKLM	15,-2
;sklhrdly_t				;Max delay on hh rev
;	SKLM	200,-10
;	SKLM	150,-6
;	SKLM	120,-8
;	SKLM	70,-4
;	SKLM	50,-7
;	SKLM	15,-2
;sklrep_t				;% to repeat
;	SKLM	6,3
;	SKLM	20,4
;	SKLM	40,2
;	SKLM	50,4
;	SKLM	70,4
;	SKLM	90,3
;

khncwnshort_t
	.long	bret_s_t,raz_s_t,utak_s_t,yoko_s_t
	.long	shawn_s_t,bam_s_t,doink_s_t,doink_s_t,lex_s_t
khncwnmed_t
	.long	bret_m_t,raz_m_t,utak_m_t,yoko_m_t
	.long	shawn_m_t,bam_m_t,doink_m_t,doink_m_t,lex_m_t
khncwnlong_t
	.long	bret_l_t,raz_l_t,utak_l_t,yoko_l_t
	.long	shawn_l_t,bam_l_t,doink_l_t,doink_l_t,lex_l_t


*******************************
* Drone script commands

BBL	.macro	w,w2,l
	.byte	:w:,:w2:
	.long	:l:
	.endm

DS_CODE	.macro
	.word	8000h+0
	.endm
DS_CODEEND	.macro
	exgpc	a9
	.endm
DS_SEEKTIL0	.macro
	.word	8000h+1
	.endm
DS_RNDA	.macro	l
	.word	8000h+2
	.long	:l:
	.endm
DS_WTINT	.macro
	.word	8000h+3
	.endm
DS_ABIFBLK	.macro
	.word	8000h+4
	.endm
DS_CALL	.macro	a
	.word	8000h+5
	.long	:a:
	.endm
DS_RJMP	.macro	p,a
	.word	8000h+6,:p:
	.long	:a:
	.endm
DS_JMP	.macro	a
	.word	8000h+7
	.long	:a:
	.endm
DS_SLP1	.macro
	.word	0c000h+0
	.endm
DS_END	.macro
	.word	0,0
	.endm

;컴컴컴컴컴컴컴		0BaHm bam scripts

bam_s_t
	BBL	MODE_HEADHOLD,-1,baM_hh
	BBL	MODE_HEADHELD,-1,baM_hhr
	BBL	MODE_OPPOVERHEAD,-1,baM_ooh
	BBL	-1,MODE_ONGROUND,M_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,baM_n
bam_m_t
	BBL	MODE_HEADHOLD,-1,baM_hh
	BBL	MODE_HEADHELD,-1,baM_hhr
	BBL	MODE_OPPOVERHEAD,-1,baM_ooh
	BBL	-1,MODE_ONGROUND,Mm_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,baMm_n

baM_n	;Normal
	.word	(baM_n_-$)/32-1
	.long	noisrun
	.long	noisp,noissp,noisk,noissk
	.long	noisspx
	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noishtoss,noishtoss
	.long	noisllsk		;Karate kick
	.long	noisspsk		;Pickup
baM_n_
baMm_n
	.word	(baMm_n_-$)/32-1
	.long	drn_seek
	.long	drn_retreat
	.long	noisrun
	.long	noissp
	.long	noissk
;	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noisllsk		;Karate kick
	.long	noisspsk		;Pickup
	.long	noisfast		;Hyper
	.long	noischrg
baMm_n_
baM_hh	;Holding head
;	.word	1-1
;	.long	noisbahh
	.word	-(7-1)
	.long	noisbahhpg		;Pogo
	.long	noislrrsp		;Pile drvr
	.long	noisjk		;Pickup
	.long	noisbahhrsk	;Knees (3 way)
	.long	noisbahhrsk
	.long	noisbahhrsk
	.long	noisspsk2		;Neck brkr

baM_hhr ;Head held reversals
	.word	4-1
	.long	noisk
	.long	noisbahhpg		;Pogo
	.long	noislrrsp		;Pile drvr
	.long	noisspsk2		;Neck brkr

baM_ooh ;Holding opp over head
	.word	2-1
	.long	noisk		;Slam
	.long	noisdsk		;Back brkr

noisbahhrsk ;Knees
	.word	R_M+SK_M,2
	.word	0,5
;FIX! - chk timing
	.word	SK_M,4, 0,4, SK_M,4, 0,4, SK_M,4, 0,4
	DS_RNDA	sklrep_t
	.word	SK_M,4, 0,4, SK_M,4, 0,4, SK_M,4, 0,4
	DS_RJMP	33,noisk		;Hip toss
	DS_RJMP	33,noissp		;Pickup
	.word	D_M+SP_M,0	;Pile drvr
noisbahhpg
	.word	U_M,2, D_M,2, 0,2, D_M,2, SK_M,50
	DS_RNDA	sklrep_t
	.word	SP_M,2, 0,2, SP_M,2, 0,2, SP_M,2, 0,15	;Repeat
	DS_RNDA	sklrep_t
	.word	SP_M,2, 0,2, SP_M,2, 0,2, SP_M,2, 0,15
	.word	SP_M,2, 0,2, SP_M,2, 0,2, SP_M,0


;컴컴컴컴컴컴컴		>Undertaker scripts

utak_s_t
	BBL	MODE_HEADHOLD,-1,utM_hh
	BBL	MODE_HEADHELD,-1,M_hhr
	BBL	MODE_CHOKEHOLD,-1,utM_chold
	BBL	-1,MODE_ONGROUND,M_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,utM_n
utak_m_t
	BBL	MODE_HEADHOLD,-1,utM_hh
	BBL	MODE_HEADHELD,-1,M_hhr
	BBL	MODE_CHOKEHOLD,-1,utM_chold
	BBL	-1,MODE_ONGROUND,Mm_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,utMm_n

utM_n	;Normal
;	.word	1-1
;	.long	noisut
	.word	(utM_n_-$)/32-1
	.long	noisrun
	.long	noisspx
	.long	noisp,noissp,noisk,noissk
	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noishtoss,noishtoss
	.long	noisucut
	.long	noisutshootps	;Fire push ghosts
	.long	noisutshootpl	;Fire pull ghosts
	.long	noisuttombhit	;Smash with tombstone
	.long	noisjp		;Slide choke
utM_n_
utMm_n
;	.word	1-1
;	.long	noisutshootpl
	.word	(utMm_n_-$)/32-1
	.long	drn_seek
	.long	drn_retreat
	.long	noisrun
	.long	noissp
	.long	noissk
;	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noisutshootps	;Fire push ghosts
	.long	noisutshootpl	;Fire pull ghosts
	.long	noisuttombhit	;Smash with tombstone
	.long	noisjp		;Slide choke
	.long	noisfast		;Hyper
	.long	noischrg
utMm_n_
utM_hh	;Holding head
	.word	-(6-1)
	.long	noisuddsk		;Pile drv
	.long	noislrrsp		;Neck brkr
	.long	noisuthhrp
	.long	noisucut
	.long	noisuttombhit	;Smash with tombstone
	.long	noisutdk		;Face slam

utM_chold ;Choke holding opp by neck
	.word	7-1
	.long	noisp
	.long	noisk
	.long	noishtoss
	.long	noisucut
	.long	noisutchup
	.long	noisutdk		;Face slam
	.long	noisdsk		;Slam

noisutshootps
	.word	R_M,2, 0,2, R_M,2, K_M,2, 0,2
	DS_WTINT
	DS_ABIFBLK
	.word	P_M+K_M,0
noisutshootpl
	.word	L_M,2, 0,2, L_M,2, K_M,2, 0,2
	DS_WTINT
	DS_ABIFBLK
	.word	D_M+SP_M,0
noisuttombhit
	.word	R_M,2, 0,2, R_M,2, SK_M,5
	DS_RNDA	sklrep_t
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,4, 0,4	;Repeat
	DS_ABIFBLK
	DS_RNDA	sklrep_t
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,4, 0,4
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,0

noisuthhrp ;Knee & hdbuts
	.word	R_M+P_M,2
	.word	0,5
;FIX! - chk timing
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4
	DS_RNDA	sklrep_t
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4
	DS_RJMP	50,noissp		;Fly kick
	.word	K_M,0		;Neck brkr

noisutchup ;Knee & hdbuts
	.word	U_M+P_M,2
	.word	0,5
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4
	DS_RNDA	sklrep_t
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4
	DS_RJMP	50,noissp		;Fly kick
	.word	K_M,0		;Neck brkr

noisutdk	;Face slam
	.word	D_M,2, 0,2, D_M+K_M,2
	DS_RNDA	sklrep_t
	.word	0,5
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4	;Repeat
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4
	DS_RNDA	sklrep_t
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,0


;컴컴컴컴컴컴컴		0DHoink scripts

doink_s_t
	BBL	MODE_HEADHOLD,-1,doM_hh
	BBL	MODE_HEADHELD,-1,doM_hhr
	BBL	-1,MODE_ONGROUND,M_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,doM_n
doink_m_t
	BBL	MODE_HEADHOLD,-1,doM_hh
	BBL	MODE_HEADHELD,-1,doM_hhr
	BBL	-1,MODE_ONGROUND,Mm_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,doMm_n

doM_n	;Normal
;	.word	1-1
;	.long	noiseslap
	.word	(doM_n_-$)/32-1
	.long	noisrun
	.long	noisspx
	.long	noisp,noissp,noisk,noissk
	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noishtoss,noishtoss
	.long	noisucut
	.long	noisdoham		;Hammer
	.long	noisdoeslap	;Ear slap
	.long	noisdopbig		;Boxing punch
doM_n_
doMm_n
;	.word	1-1
;	.long	noisdoeslap
	.word	(doMm_n_-$)/32-1
	.long	drn_seek
	.long	drn_retreat
	.long	noisrun
	.long	noissp
	.long	noissk
;	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noisdoham		;Hammer
	.long	noisdoeslap	;Ear slap
	.long	noisfast		;Hyper
	.long	noischrg
doMm_n_
doM_hh	;Holding head
	.word	-(8-1)
	.long	noisuddskk		;Face slam
	.long	noislrrsp		;Pile drv
	.long	noisdoham
	.long	noisj2p		;Buzz
	.long	noishhp3k		;P & fly kick
	.long	noishhp4
	.long	noishhp3pd		;P & pdrv
	.long	noishhsk3pd	;K & pdrv

doM_hhr ;Head held reversals
	.word	3-1
	.long	noisk
	.long	noisuddskk		;Face slam
	.long	noislrrsp		;Pile drv

noisdoham	.word	R_M,2, 0,2, R_M,2, SK_M,10
	DS_RNDA	sklrep_t
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,4, 0,4	;Repeat
	DS_RNDA	sklrep_t
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,4, 0,4
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,0

noisdoeslap
	.word	D_M,2, D_M+R_M,2, R_M,2, P_M,2
	DS_RNDA	sklrep_t
	.word	0,5, P_M,5, 0,5, P_M,5			;Repeat
	DS_RNDA	sklrep_t
	.word	0,5, P_M,5, 0,5, P_M,5
	.word	0,5, P_M,5, 0,5, P_M,0

noisdopbig	.word	P_M,2, 0,2, P_M,2, 0,2, P_M,2, 0,2, P_M,2, 0,2
	.word	P_M,2, 0,2, P_M,2, 0,2, P_M,0


;컴컴컴컴컴컴컴		>Yoko scripts

yoko_s_t
	BBL	MODE_HEADHOLD,-1,yoM_hh
	BBL	MODE_HEADHELD,-1,M_hhr
	BBL	MODE_OPPOVERHEAD,-1,yoM_ooh
	BBL	-1,MODE_ONGROUND,M_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,yoM_n
yoko_m_t
	BBL	MODE_HEADHOLD,-1,yoM_hh
	BBL	MODE_HEADHELD,-1,M_hhr
	BBL	MODE_OPPOVERHEAD,-1,yoM_ooh
	BBL	-1,MODE_ONGROUND,Mm_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,yoMm_n

yoM_n	;Normal
;	.word	1-1
;	.long	noiseslap
	.word	(yoM_n_-$)/32-1
	.long	noisrun
	.long	noisspx
	.long	noisp,noissp,noisk,noissk
	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noishtoss,noishtoss
	.long	noisjp		;Speed jab
	.long	noisrrsk		;Scissor squish
	.long	noisrrp		;Gut push
	.long	noisspsk		;Pickup
yoM_n_
yoMm_n
;	.word	1-1
;	.long	noisdoeslap
	.word	(yoMm_n_-$)/32-1
	.long	drn_seek
	.long	drn_retreat
	.long	noisrun
	.long	noissp
	.long	noissk
;	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noisjp		;Speed jab
	.long	noisrrsk		;Scissor squish
	.long	noisspsk		;Pickup
	.long	noisfast		;Hyper
	.long	noischrg
yoMm_n_
yoM_hh	;Holding head
	.word	-(4-1)
	.long	noisuddsk		;Vert suplex
	.long	noislrrsp		;Scissor squish
	.long	noisj2p		;Salt
	.long	noisucut

yoM_ooh ;Holding opp over head
	.word	2-1
	.long	noisk		;Slam
	.long	noisdsk		;Spinning slam


;컴컴컴컴컴컴컴		>Shawn scripts

shawn_s_t
	BBL	MODE_HEADHOLD,-1,shM_hh
	BBL	MODE_HEADHELD,-1,shM_hhr
	BBL	-1,MODE_ONGROUND,M_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,shM_n
shawn_m_t
	BBL	MODE_HEADHOLD,-1,shM_hh
	BBL	MODE_HEADHELD,-1,shM_hhr
	BBL	-1,MODE_ONGROUND,Mm_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,shMm_n

shM_n	;Normal
;	.word	1-1
;	.long	noiseslap
	.word	(shM_n_-$)/32-1
	.long	noisrun
	.long	noisspx
	.long	noisp,noissp,noisk,noissk
	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noishtoss,noishtoss
	.long	noisllsk		;Karate kick
	.long	noisjkk		;Speed kick
	.long	noisrrsk		;Frankensteiner
	.long	noisrrk		;Sliding kick toss
	.long	noisjisp		;Flipslam
shM_n_
shMm_n
;	.word	1-1
;	.long	noisdoeslap
	.word	(shMm_n_-$)/32-1
	.long	drn_seek
	.long	drn_retreat
	.long	noisrun
	.long	noissp
	.long	noissk
;	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noisllsk		;Karate kick
	.long	noisjkk		;Speed kick
	.long	noisrrsk		;Frankensteiner
	.long	noisrrk		;Sliding kick toss
	.long	noisjisp		;Flipslam
	.long	noisfast		;Hyper
	.long	noischrg
shMm_n_
shM_hh	;Holding head
	.word	-(8-1)
	.long	noisuddsk		;German suplex
	.long	noislrrsp		;Frankensteiner
	.long	noisrsk4k		;Quick knees
	.long	noisjkk		;Speed kick
	.long	noisrp		;Arm break
	.long	noisrsp4		;Jmp head butt
	.long	noisrk4		;Speed kick
	.long	noislrrk		;Kick toss

shM_hhr ;Head held reversals
	.word	5-1
	.long	noisk
	.long	noisuddsk		;German suplex
	.long	noislrrsp		;Frankensteiner
	.long	noislrrk		;Kick toss
	.long	noisjkk		;Speed kick


;컴컴컴컴컴컴컴		0BHret scripts

bret_s_t
	BBL	MODE_HEADHOLD,-1,brM_hh
	BBL	MODE_HEADHELD,-1,brM_hhr
	BBL	-1,MODE_ONGROUND,M_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,brM_n
bret_m_t
	BBL	MODE_HEADHOLD,-1,brM_hh
	BBL	MODE_HEADHELD,-1,brM_hhr
	BBL	-1,MODE_ONGROUND,Mm_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,brMm_n

brM_n	;Normal
;	.word	1-1
;	.long	noiseslap
	.word	(brM_n_-$)/32-1
	.long	noisrun
	.long	noisp,noissp,noisk,noissk
	.long	noisspx
	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noishtoss,noishtoss
	.long	noisucut
	.long	noisddp		;Leap ucut
	.long	noisjp		;Face rake
	.long	noisj2sp		;Rolling ucut
	.long	noisllsk		;Fast kick
brM_n_
brMm_n
;	.word	1-1
;	.long	noisdoeslap
	.word	(brMm_n_-$)/32-1
	.long	drn_seek
	.long	drn_retreat
	.long	noisrun
	.long	noissp
	.long	noissk
;	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noisddp		;Leap ucut
	.long	noisjp		;Face rake
	.long	noisj2sp		;Rolling ucut
	.long	noisllsk		;Fast kick
	.long	noisfast		;Hyper
	.long	noischrg
brMm_n_
brM_hh	;Holding head
	.word	-(7-1)
	.long	noisuddsk		;Neck DDT
	.long	noislrrsp		;Pile drvr
	.long	noisucut
	.long	noisjpx		;Face drvr
;FIX! - broke
	.long	noisrp		;Arm break
	.long	noisrsp		;Head to knee
;FIX! - broke
	.long	noislrrp		;Knee to face

brM_hhr ;Head held reversals
	.word	4-1
	.long	noisk
	.long	noisuddsk		;Neck DDT
	.long	noislrrsp		;Pile drvr
	.long	noislrrp		;Kick toss
;	.long	noisjk		;


;컴컴컴컴컴컴컴		>Razor scripts

raz_s_t
	BBL	MODE_HEADHOLD,-1,rzM_hh
	BBL	MODE_HEADHELD,-1,rzM_hhr
	BBL	-1,MODE_ONGROUND,M_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,rzM_n
raz_m_t
	BBL	MODE_HEADHOLD,-1,rzM_hh
	BBL	MODE_HEADHELD,-1,rzM_hhr
	BBL	-1,MODE_ONGROUND,Mm_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,rzMm_n

rzM_n	;Normal
;	.word	1-1
;	.long	noiseslap
	.word	(rzM_n_-$)/32-1
	.long	noisrun
	.long	noisp,noissp,noisk,noissk
	.long	noisspx
	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noishtoss,noishtoss
	.long	noisucut
	.long	noisjp		;Down slash
	.long	noisrrk		;Rug slam
rzM_n_
rzMm_n
;	.word	1-1
;	.long	noisdoeslap
	.word	(rzMm_n_-$)/32-1
	.long	drn_seek
	.long	drn_retreat
	.long	noisrun
	.long	noissp
	.long	noissk
;	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noisjp		;Down slash
	.long	noisfast		;Hyper
	.long	noischrg
rzMm_n_
rzM_hh	;Holding head
	.word	-(6-1)
	.long	noisuddsk		;P drvr
	.long	noislrrsp		;Razors edge
	.long	noisrzup4		;Slashes up
	.long	noisrzdp4		;Slashes dn
	.long	noisucut
	.long	noisrzuddksp	;Rug shake

rzM_hhr ;Head held reversals
	.word	4-1
	.long	noisk
	.long	noisuddsk		;P drvr
	.long	noislrrsp		;Razors edge
	.long	noisrzuddksp	;Rug shake

noisrzup4	.word	U_M+P_M,5, 0,5, P_M,5, 0,5, P_M,5, 0,5, P_M,0
noisrzdp4	.word	D_M+P_M,5, 0,5, P_M,5, 0,5, P_M,5, 0,5, P_M,0

noisrzuddksp
	.word	U_M,2, D_M,2, 0,2, D_M,2, K_M,2
	DS_RNDA	sklrep_t
	.word	0,20
	.word	SP_M,6, 0,6, SP_M,6, 0,6, SP_M,6, 0,6	;Repeat
	DS_RNDA	sklrep_t
	.word	SP_M,6, 0,6, SP_M,6, 0,6, SP_M,0


;컴컴컴컴컴컴컴		>Lex scripts

lex_s_t
	BBL	MODE_HEADHOLD,-1,lxM_hh
	BBL	MODE_HEADHELD,-1,lxM_hhr
	BBL	MODE_OPPOVERHEAD,-1,lxM_ooh
	BBL	-1,MODE_ONGROUND,M_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,lxM_n
lex_m_t
	BBL	MODE_HEADHOLD,-1,lxM_hh
	BBL	MODE_HEADHELD,-1,lxM_hhr
	BBL	MODE_OPPOVERHEAD,-1,lxM_ooh
	BBL	-1,MODE_ONGROUND,Mm_og
	BBL	-1,MODE_ONTURNBKL,M_opptbkl
	BBL	-1,MODE_CLIMBTURNBKL,M_opptbkl
	WL	-1,lxMm_n

lxM_n	;Normal
;	.word	1-1
;	.long	noiseslap
	.word	(lxM_n_-$)/32-1
	.long	noisrun
	.long	noisp,noissp,noisk,noissk
	.long	noisspx
	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noishtoss,noishtoss
	.long	noisrrp		;Elbow to gut
	.long	noisspsk		;Pickup
lxM_n_
lxMm_n
;	.word	1-1
;	.long	noisdoeslap
	.word	(lxMm_n_-$)/32-1
	.long	drn_seek
	.long	drn_retreat
	.long	noisrun
	.long	noissp
	.long	noissk
;	.long	noishgrab,noishgrab,noishgrab
	.long	noisflng
	.long	noisrrp		;Elbow to gut
	.long	noisspsk		;Pickup
	.long	noisfast		;Hyper
	.long	noischrg
lxMm_n_
lxM_hh	;Holding head
	.word	-(4-1)
	.long	noisuddsk		;Neck DDT
	.long	noislrrsp		;Vert suplex
	.long	noisrsk4k		;Quick knees & v suplex
	.long	noisjk		;Pickup

lxM_hhr ;Head held reversals
	.word	4-1
	.long	noisk
	.long	noisuddsk		;Neck DDT
	.long	noislrrsp		;V suplex
	.long	noisjk		;Pickup

lxM_ooh ;Holding opp over head
	.word	3-1
	.long	noisp		;Slam
	.long	noisusp		;Head slam
	.long	noisusk		;Back brkr


;컴컴컴컴컴컴컴		>Generic scripts

M_og	;Opp on gnd
	.word	6-1
;	.long	drn_retreat
	.long	noisp,noissp,noisk,noissk
	.long	noisoghg,noisoghg
Mm_og
	.word	3-1
	.long	drn_seek
;FIX! - Rzr rug shake
	.long	noisseeksp
	.long	noisseeksk

M_opptbkl ;Opp on turnbkl
	.word	1-1
	.long	noisrun

M_hhr	;Head held reversals
	.word	3-1
	.long	noisk
	.long	noisuddsk
	.long	noislrrsp

M_shrtblkr
	.word	2-1
	.long	noishgrab
	.long	noishtoss
M_shrtblkrdl
	.word	2-1
	.long	noishgrab
	.long	noisspx
;	.long	drn_climbtb

noisspx	.word	SP_M,2, 0,2, SP_M,2, 0,2, SP_M,2
	.word	SP_M,2, 0,2, SP_M,2, 0,2, SP_M,0

noisp	.word	P_M,0
noissp	.word	SP_M,0
noisk	.word	K_M,0
noissk	.word	SK_M,0
noisspsk	.word	SP_M+SK_M,0
noisrp	.word	R_M+P_M,0
noisrsp	.word	R_M+SP_M,0
noisrsp4	.word	R_M+SP_M,4, 0,4, SP_M,4, 0,4, SP_M,4, 0,4, SP_M,0
noisrk4	.word	R_M+K_M,4, 0,4, K_M,4, 0,4, K_M,4, 0,4, K_M,0
noisrsk	.word	R_M+SK_M,0
noisrsk4k	.word	R_M+SK_M,4, 0,4, SK_M,4, 0,4, SK_M,4, 0,4, SK_M,4, K_M,0
noisrrp	.word	R_M,2, 0,2, R_M,2, P_M,0
noisrrk	.word	R_M,2, 0,2, R_M,2, K_M,0
noisrrsk	.word	R_M,2, 0,2, R_M,2, SK_M,0
noisllsk	.word	L_M,2, 0,2, L_M,2, SK_M,0
noisdp	.word	D_M+P_M,0
noisdk	.word	D_M+K_M,0
noisdsk	.word	D_M+SK_M,0
noisddp	.word	D_M,2, 0,2, D_M,2, P_M,0
noisusp	.word	U_M+SP_M,0
noisusk	.word	U_M+SK_M,0

noisrun
	DS_CALL	drone_chkrun
	.word	P_M+K_M,0	;Skipped if bad
	DS_END
hgrab
noishgrab	.word	R_M,2, 0,2, R_M,2, SP_M,0
noisflng	.word	L_M,2, 0,2, L_M+SP_M,0		;Grab fling
slhtoss	.word	L_M,2, 0,2, L_M,2, P_M,0	;Hip toss
noishtoss	.word	L_M,2, 0,2, L_M+P_M,0		;Hip toss
noisucut	.word	D_M+SP_M,0			;Uppercut

noisseeksp
	DS_SEEKTIL0
	.word	SP_M,0
noisseeksk
	DS_SEEKTIL0
	.word	SK_M,0
noischrg
	DS_CALL	drone_chrg
	DS_END

noisoghg	;Opp on gnd head grab
	.word	D_M,2, SP_M,0

noisfast	;Works once
	.word	L_M,2, L_M+D_M,2, D_M,2, D_M+R_M,2
	.word	R_M,2, R_M+U_M,2, U_M,2, U_M+L_M,0

;noislrrspp	;Pile drvr/Neck brkr
;	.word	L_M,2, R_M,2, 0,2, R_M,2, SP_M,2
;	DS_RNDA	sklrep_t
;	.word	0,5
;	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4	;Repeat
;	DS_RNDA	sklrep_t
;	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4
;	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,0

noishhp3k	;Punch*3, kick
	.asg	6,T
	.word	R_M+P_M,T, R_M,T, R_M+P_M,T, R_M,T
	.word	R_M+P_M,T, R_M,T, R_M+P_M,T, R_M,T
	.word	R_M+K_M,2
	.word	0,10
	DS_END

noishhp4	;Punch*4
	.asg	6,T
	.word	R_M+P_M,T, R_M,T, R_M+P_M,T, R_M,T
	.word	R_M+P_M,T, R_M,T, R_M+P_M,T, R_M,T
	.word	R_M+P_M,T, R_M,T, R_M+P_M,2
	.word	0,10
	DS_END

noishhp3pd	;Punch*3, piledriver
	.asg	6,T
	.word	R_M+P_M,T, R_M,T, R_M+P_M,T, R_M,T
	.word	R_M+P_M,T, R_M,T, R_M+P_M,T, R_M,T+5
	.word	D_M+SP_M,2
	.word	0,10
	DS_END

noishhsk3pd ;Knee*3, piledriver
	.asg	6,T
	.word	R_M+SK_M,T, R_M,T, R_M+SK_M,T, R_M,T
	.word	R_M+SK_M,T, R_M,T, R_M+SK_M,T, R_M,T
	.word	SP_M,2
	.word	0,10
	DS_END

noisj2p	.word	L_M+D_M,2
noisjp	.word	D_M,2, D_M+R_M,2, R_M,2, P_M,0
noisj2sp	.word	L_M+D_M,2
noisjsp	.word	D_M,2, D_M+R_M,2, R_M,2, SP_M,0
noisjk	.word	D_M,2, D_M+R_M,2, R_M,2, K_M,0
noisjkk	.word	D_M,2, D_M+R_M,2, R_M,2, K_M,4
	.word	0,4, K_M,4, 0,4, K_M,4
	.word	0,4, K_M,4, 0,4, K_M,0
noisjisp	.word	U_M,2, U_M+R_M,2, R_M,2, SP_M,0

noisjpx	.word	D_M,2, D_M+R_M,2, R_M,2, P_M,0
	DS_RNDA	sklrep_t
;FIX - chk
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,0

	;Reversals

noisuddsk
	.word	U_M,2, D_M,2, 0,2, D_M,2, SK_M,0

noisuddskk	;Face slam/Pile drv
	.word	U_M,2, D_M,2, 0,2, D_M,2, SK_M,2
	DS_RNDA	sklrep_t
	.word	0,8
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,4, 0,4	;Repeat
	DS_RNDA	sklrep_t
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,4, 0,4
	.word	K_M,4, 0,4, K_M,4, 0,4, K_M,0

noisuddsksp ;Repeat SP
	.word	U_M,2, D_M,2, 0,2, D_M,2, SK_M,2
	DS_RNDA	sklrep_t
	.word	SP_M,4, 0,4, SP_M,4, 0,4, SP_M,0
noislrrp
	.word	L_M,2, R_M,2, 0,2, R_M,2, P_M,0
noislrrsp
	.word	L_M,2, R_M,2, 0,2, R_M,2, SP_M,0
noislrrk
	.word	L_M,2, R_M,2, 0,2, R_M,2, K_M,0
noisspsk2
	.word	SP_M,2, SK_M,2, SP_M,2, SK_M,0


********************************

bret_l_t
raz_l_t
utak_l_t
yoko_l_t
shawn_l_t
bam_l_t
doink_l_t
lex_l_t
	BBL	-1,MODE_ONGROUND,noismdog
	WL	-1,noismdn
noismdn
;	.word	1-1
;	.long	drn_climbtb
	.word	4-1
	.long	drn_seek
	.long	noisrun
	.long	drn_climbtb
	.long	drn_taunt
noismdog
	.word	3-1
	.long	drn_seek
	.long	drn_seek
	.long	noisrun


*******************************

 SUBRP	drone_chrg

	move	*a13(DRN_BUTCHRG),a14
	jrnz	iienx			;Already charging?

	movk	20h,a0
	callr	rnd

	move	*a13(WRESTLERNUM),a1
	X64	a1
	addi	iienwres_t,a1
	add	a0,a1
	move	*a1,a1,L		;Get * script

	move	*a1+,a0
	move	a0,*a13(DRN_BUTCHRG)
;FIX!-all same
	move	*a1+,a0
	move	a0,*a13(DRN_BUTCHRGDLY)
	move	a1,*a13(DRN_BUTCHRG_p),L

iienx
	rets

iienwres_t
	.long	iienbrt,iienbrt
	.long	iienraz,iienraz2
	.long	iienut,iienut2
	.long	iienyok,iienyok
	.long	iienshn,iienshn2
	.long	iienbam,iienbam2
	.long	iiendnk,iiendnk2
	.long	0,0
	.long	iienlex,iienlex2

iienraz
	.word	SP_M,TSEC*2		;Slashes
	DS_JMP	iienrun
iienut
	.word	P_M,TSEC*2		;Neck breaker
	DS_JMP	iienrun
iienyok
	.word	P_M,TSEC*2		;Salt throw
	DS_CODE
	movk	1,a0
	move	a0,*a13(DRN_MODE)	;Aggressive
	DS_CODEEND
	DS_JMP	iienrun
iienshn
	.word	P_M,TSEC*2		;Suplex
	DS_JMP	iienrun
iienbam
	.word	P_M,TSEC*2		;Fire punch
	DS_CODE
	clr	a0
	move	a0,*a13(DRN_BUTCHRG)	;Fire it
	DS_CODEEND
	.word	0,4
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4	;Repeat
	DS_RNDA	sklrep_t
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4
	DS_RNDA	sklrep_t
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,4, 0,4
	.word	P_M,4, 0,4, P_M,4, 0,4, P_M,0
iienbam2
	.word	SP_M,TSEC*2		;Neck brkr
	DS_JMP	iienrun
iiendnk
	.word	P_M,TSEC*2		;Buzzer
	DS_JMP	iienrun
iiendnk2
	.word	P_M,TSEC*2		;Buzzer (leap)
	.word	R_M,2
	DS_CODE
	clr	a0
	move	a0,*a13(DRN_BUTCHRG)	;Fire it
	DS_CODEEND
	.word	R_M,2			;Need?
	DS_END
iienlex
	.word	P_M,TSEC*2		;Smash
	DS_JMP	iienrun
iienlex2
iienbrt
iienraz2
iienut2
iienshn2
	.word	SK_M,TSEC*2		;Flying kick
;	DS_JMP	iienrun

iienrun
	DS_CALL	drone_chkrun
	.word	P_M+K_M,0		;Run & end
	DS_CODE

	move	*a13(OBJ_XPOSINT),a0
	move	*a8(OBJ_XPOSINT),a14
	sub	a14,a0
	abs	a0
	cmpi	150,a0
	jrge	iienrx			;X too far?

	move	*a13(OBJ_ZPOSINT),a1
	move	*a8(OBJ_ZPOSINT),a14
	sub	a14,a1
	abs	a1
	cmpi	40,a1
	jrge	iienrx			;Z too far?

	clr	a0
	move	a0,*a13(DRN_BUTCHRG)	;Fire it
iienrx
	DS_CODEEND
	DS_END



*******************************

 SUBRP	drn_combo

	DS_CODE

	move	*a13(WRESTLERNUM),a0
	X32	a0
	addi	yngnwres_t,a0
	move	a9,a1
	move	*a0,a9,L		;Get * script
	jump	a1			;Ret

yngnwres_t
	.long	yngnbrt,yngnraz,yngnut,yngnyok
	.long	yngnshn,yngnbam,yngndnk,yngndnk,yngnlex
yngnbrt
	.word	R_M,2, 0,2, R_M,2, 0,2
	DS_RJMP	50,yngnbrt2
	.word	P_M,2
	DS_JMP	yngncstrt
yngnbrt2
	.word	SK_M,2
	DS_JMP	yngncstrt

yngnraz
	.word	R_M,2, 0,2, R_M,2, 0,2
	DS_RJMP	50,yngnraz2
	.word	SP_M,2
	DS_JMP	yngncstrt
yngnraz2
	.word	K_M,2
	DS_JMP	yngncstrt

yngnut
	.word	R_M,2, 0,2, R_M,2, 0,2
	DS_RJMP	50,yngnut2
	.word	SK_M,2
	DS_JMP	yngncstrt
yngnut2
	.word	K_M,2
	DS_JMP	yngncstrt

yngnyok
	.word	R_M,2, 0,2, R_M,2, 0,2
	DS_RJMP	50,yngnyok2
	.word	SP_M,2
	DS_JMP	yngncstrt
yngnyok2
	.word	P_M,2
	DS_JMP	yngncstrt

yngnshn
	.word	R_M,2, 0,2, R_M,2, 0,2
	DS_RJMP	50,yngnshn2
	.word	P_M,2
	DS_JMP	yngncstrt
yngnshn2
	.word	K_M,2
	DS_JMP	yngncstrt

yngnbam
	.word	R_M,2, 0,2, R_M,2, 0,2
	DS_RJMP	50,yngnbam2
	.word	SP_M,2
	DS_JMP	yngncstrt
yngnbam2
	.word	P_M,2
	DS_JMP	yngncstrt

yngndnk
	.word	R_M,2, 0,2, R_M,2, 0,2
	DS_RJMP	50,yngndnk2
	.word	SP_M,2
	DS_JMP	yngncstrt
yngndnk2
	.word	SK_M,2
	DS_JMP	yngncstrt

yngnlex
	.word	R_M,2, 0,2, R_M,2, 0,2
	DS_RJMP	50,yngnlex2
	.word	SK_M,2
	DS_JMP	yngncstrt
yngnlex2
	.word	K_M,2
	DS_JMP	yngncstrt

	.asg	6,T
yngncstrt
	.word	0,2
	DS_RJMP	25,yngncsk
	DS_RJMP	25,yngncp
	DS_RJMP	25,yngnck
yngncsp
	.word	SP_M,T, 0,T, SP_M,T, 0,T
	.word	SP_M,T, 0,T, SP_M,T, 0,T
	DS_RNDA	sklrep_t
	DS_JMP	yngncstrt
yngncsk
	.word	SK_M,T, 0,T, SK_M,T, 0,T
	.word	SK_M,T, 0,T, SK_M,T, 0,T
	DS_RNDA	sklrep_t
	DS_JMP	yngncstrt
yngncp
	.word	P_M,T, 0,T, P_M,T, 0,T
	.word	P_M,T, 0,T, P_M,T, 0,T
	DS_RNDA	sklrep_t
	DS_JMP	yngncstrt
yngnck
	.word	K_M,T, 0,T, K_M,T, 0,T
	.word	K_M,T, 0,T, K_M,T, 0,T
	DS_RNDA	sklrep_t
	DS_JMP	yngncstrt



*******************************

fiwklp
	DS_CODEEND
	DS_SLP1

 SUBRP	drn_seek

	DS_CODE

	move	b6,a0
	jrz	fiwkskok
	subk	MODE_BLOCK,a0
	jrne	fiwkx
fiwkskok
	movi	3fh,a0
	callr	rnd
	jrz	fiwkx
	callr	drone_seek
	jrnz	fiwklp
fiwkx
	DS_CODEEND
	DS_END


*******************************
* Get real close

xsrelp
	DS_CODEEND
	DS_SLP1

 SUBRP	drn_seekclose

	DS_CODE

	move	b6,a0
	jrz	xsreskok
	subk	MODE_BLOCK,a0
	jrne	xsrex
xsreskok
	movi	3fh,a0
	callr	rnd
	jrz	xsrex

	move	*a13(OBJ_XPOSINT),a14
	movk	32,a2
	move	*a8(OBJ_XPOSINT),a0
	cmp	a0,a14
	jrge	xsretorgt
	neg	a2
xsretorgt
	add	a2,a0

	move	*a8(OBJ_ZPOSINT),a1
	movk	23,a2
	callr	drone_seekxz
	jrnz	xsrelp
xsrex
	DS_CODEEND
	DS_END


*******************************

 SUBRP	drn_retreat

	DS_CODE
	movk	4,a0			;Far
	move	a0,*a13(DRN_SEEKDIST)
mrovlp
	callr	drone_seekdirdist
	DS_CODEEND
	DS_SLP1
	DS_CODE
	movk	1fh,a0
	callr	rnd
	jrnz	mrovlp

	DS_CODEEND
	DS_END


*******************************
* Check if running would be OK
* A9+32 if bad

 SUBRP	drone_chkrun

	move	*a13(OBJ_XPOSINT),a0
	move	*a13(OBJ_ZPOSINT),a1

	move	*a13(FACING_DIR),a2

	move	*a13(INRING),a14
	jrnz	cwouout			;Out of ring?

;컴컴컴컴컴컴컴

	cmpi	MODE_ONGROUND,b7
	jreq	cwoux
;	subk	MODE_INAIR2,a14
;	jreq	cwoux			;Jumping on me?

	move	*a13(CLOSEST_ZDIST),a1
	cmpi	70,a1
	jrge	cwoux			;Z far enough?

	subk	30,a1
	jrle	cwoux			;Z close enough?

	move	*a13(CLOSEST_XDIST),a0
	cmpi	150,a0
	jrge	cwoux			;X far enough?

;	btst	PLAYER_RIGHT_BIT,a2
;	jrz	cwoul			;Facing left?

	jruc	cwoubad

;컴컴컴컴컴컴컴

cwouout
	btst	PLAYER_RIGHT_BIT,a2
	jrz	cwouol			;Facing left?

	cmpi	RING_X_CENTER+500,a0
	jrge	cwoubad			;Hit rgt crowd?

	cmpi	RING_TOP-10,a1
	jrlt	cwourrok
	cmpi	RING_BOT+10,a1
	jrgt	cwourrok
	cmpi	RING_X_CENTER,a0
	jrge	cwourrok
	cmpi	RING_X_CENTER-300,a0
	jrge	cwoubad			;Hit rgt ring?
cwourrok
	jruc	cwoux
cwouol
	cmpi	RING_X_CENTER-500,a0
	jrle	cwoubad			;Hit lft crowd?

	cmpi	RING_TOP-10,a1
	jrlt	cwoulrok
	cmpi	RING_BOT+10,a1
	jrgt	cwoulrok
	cmpi	RING_X_CENTER,a0
	jrle	cwoulrok
	cmpi	RING_X_CENTER+300,a0
	jrle	cwoubad			;Hit lft ring?
cwoulrok
;FIX!
	jruc	cwoux

;컴컴컴컴컴컴컴

cwoubad
	addk	32,a9			;Skip script run buttons

cwoux
	rets


*******************************
* Control runner

zrntrsk
	movk	10,a2
	callr	drone_seek2
	andni	MOVE_LEFT+MOVE_RIGHT,a0		;0 lft & rgt
	move	a0,*a13(DRN_JOY)
	DS_CODEEND
	DS_SLP1

 SUBRP	drn_run

	DS_CODE
	move	b6,a0
	subk	MODE_RUNNING,a0
	jreq	zrntmdok
	subk	MODE_BOUNCING-MODE_RUNNING,a0
	jrne	zrntabrt
zrntmdok
	movi	01ffh,a0		;9.6 sec
	callr	rnd
	jrz	zrntbrkrun			;Breakout?

	move	*a13(OBJ_XVEL+16),a4
	sll	3,a4			;*8
	move	*a8(OBJ_XVEL+16),a1
	sll	5,a1			;*32

	move	*a8(OBJ_XPOSINT),a0
	add	a1,a0
	move	*a13(OBJ_XPOSINT),a1
	add	a4,a1
	move	a1,a3
	sub	a1,a0
	move	a4,a1
	xor	a0,a1
	abs	a0

	move	*a13(CLOSEST_ZDIST),a2

	move	*a13(INRING),a14
	jrz	zrntinr			;In ring?

	cmpi	300,a0
	jrgt	zrntering			;Too far?

	move	a1,a1
	jrn	zrntbrkrun			;Running away?

	subk	30,a2
	jrgt	zrntbrkseek		;Too far?

	jruc	zrntcont

zrntinr
	move	a4,a4
	jrn	zrntlrp			;Towards L rope?

	cmpi	RING_X_CENTER+210,a3
	jrlt	zrntrpok			;Won't hit R rope?
	jruc	zrntchkopp
zrntlrp
	cmpi	RING_X_CENTER-210,a3
	jrgt	zrntrpok			;Won't hit L rope?
zrntchkopp
	move	*a8(GETUP_TIME),a14
	jrgt	zrntrpok			;Out of control?

	cmpi	MODE_ONGROUND,b7
	jreq	zrntrpok

	cmpi	300,a0
	jrgt	zrntrpok			;Opp X far?

	cmpi	MODE_RUNNING,b7
	jreq	zrntoprun

	cmpi	180,a0
	jrgt	zrntrpok			;Opp X far?
zrntoprun
	cmpi	90,a2
	jrlt	zrntbrkrun			;Opp Z close?

zrntrpok

	move	a1,a1
	jrn	zrntrsk			;Running away?
zrntcont
	cmpi	MODE_INAIR2,b7
	jreq	zrntbrkrun


	subk	30,a2
	jrgt	zrntrsk			;Z too far?

	cmpi	250,a0
	jrgt	zrntrsk			;X too far?

	move	a0,a2

	movi	120,a0
	callr	rndrng0
	addi	130,a0

	cmp	a0,a2
	jrgt	zrntrsk			;X too far?


;	movk	3,a0
;	callr	rnd
;	jrz	zrntigpup			;25% ignore?

	move	b7,a0
	subk	MODE_PUPPET2,a0
	jreq	zrntbrkrun
	subk	MODE_PUPPET-MODE_PUPPET2,a0
	jreq	zrntbrkrun
	cmpi	MODE_HEADHELD,b7
	jreq	zrntbrkrun
	cmpi	MODE_HEADHOLD,b7
	jreq	zrntbrkrun
	cmpi	MODE_ATTACHED,b7
	jreq	zrntbrkrun
;zrntigpup

	move	*a13(DRN_BUTCHRG),a0
	jrz	zrntnchrg
	move	*a13(DRN_BUTCHRGDLY),a0
	jrgt	zrntnchrg
	clr	a0
	move	a0,*a13(DRN_BUTCHRG)	;Fire it
	jruc	zrntabrt
zrntnchrg
	DS_CODEEND
	DS_RJMP	33,zrntk
	DS_RJMP	33,zrntsk
	.word	SP_M,0
zrntk	.word	K_M,0
zrntsk	.word	SK_M,0

zrntabrt
	DS_CODEEND
	DS_END

zrntbrkrun
	DS_CODEEND
	.word	L_M,0

zrntering
	DS_CODEEND
	.word	L_M,2
	DS_JMP	drn_enterring

zrntbrkseek
	DS_CODEEND
	.word	L_M,2
	DS_JMP	drn_seek



*******************************
* Opponent running

 SUBRP	drn_oprun

	DS_CODE

	movk	7,a0
	callr	rnd
	jrnz	fuksabrt			;Skip?

	move	*a8(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
;	move	a1,a3
	sub	a1,a0
	move	*a8(OBJ_XVEL+16),a1
;	move	a1,a4
	xor	a0,a1
	abs	a0

	move	*a13(CLOSEST_ZDIST),a2
	cmp	a0,a2
	jrgt	fuksabrt			;Z dist > X dist?

	move	a1,a1
	jrn	fuksrun			;Running away?

	move	*a8(GETUP_TIME),a14
	jrgt	fuksrun			;Out of control?

fuksabrt
	DS_CODEEND
	DS_END

fuksrun
	DS_CODEEND
	.word	P_M+K_M,0	;Run



*******************************

 SUBRP	drn_roll

	DS_CODE
	callr	drone_chrg

	jruc	njpcstrt

njpclp
	DS_CODEEND
	DS_SLP1
	DS_CODE
njpcstrt
	cmpi	MODE_ONGROUND,b6
	jrne	njpcx

	move	*a13(CLOSEST_XDIST),a0
	cmpi	150,a0
	jrgt	njpcx			;Safe dist?

	move	*a13(CLOSEST_ZDIST),a0
	cmpi	70,a0
	jrgt	njpcx			;Safe dist?

	clr	a2
	callr	drone_seek2
	jrz	njpcx

	movk	3,a1			;Flip up & down
	xor	a1,a0
	move	a0,*a13(DRN_JOY)

	movi	7fh,a0
	callr	rnd
	jrnz	njpclp

njpcx
	DS_CODEEND
	.word	B_M,TSEC-10		;Block while standing
	DS_END


*******************************
* Climb closest turnbuckle

 SUBRP	drn_climbtb

	DS_CODE
	move	b3,a0
	subk	1,a0
	jrle	xwvilp			;Only 1 on team?

	movk	1,a0
	callr	rnd
	jrnz	xwvix			;Skip 50%?

xwvilp
	DS_CODEEND
	DS_SLP1

 SUBRP	drn_ontb

	DS_CODE

	move	b7,a2

	cmpi	MODE_ONTURNBKL,b6
	jrne	xwvinot

	subk	MODE_ONTURNBKL,a2
	jreq	xwvidn			;He's up, so get dn?

	jruc	xwvijmp

xwvinot
	move	*a13(INRING),a0
	jrnz	xwviering			;!In ring?

	move	*a13(WRESTLERNUM),a0
	subk	3,a0
	jrne	xwviny			;!Yoko?
	move	*a8(INRING),a0
	jrnz	xwvix			;!In ring?
xwviny
	subk	MODE_ONTURNBKL,a2
	jreq	xwvix
	subk	MODE_INAIR2-MODE_ONTURNBKL,a2
	jreq	xwvix

	movi	RING_X_CENTER-225,a0
	move	*a13(OBJ_XPOSINT),a1
	cmpi	RING_X_CENTER,a1
	jrle	xwvilrp
	movi	RING_X_CENTER+225,a0
xwvilrp
	move	a0,a3
	movi	RING_TOP,a1
	movk	32,a2
	callr	drone_seekxz		;Seek to visinity
	jrnz	xwvinclose

	move	a3,a0
	movi	RING_TOP-10,a1
	clr	a2
	callr	drone_seekxz		;Push into turnbuckle

xwvinclose
	move	*a13(CLOSEST_XDIST),a0
	cmpi	120,a0
	jrgt	xwvilp			;Safe dist?

	move	*a13(CLOSEST_ZDIST),a0
	cmpi	70,a0
	jrgt	xwvilp			;Safe dist?

xwvix
	DS_CODEEND
	DS_END

xwvijmp
	clr	a6			;So we get dn transition
	DS_CODEEND
	.word	K_M,0
xwvidn
	DS_CODEEND
	.word	D_M,0
xwviering
	DS_CODEEND
	DS_JMP	drn_enterring


*******************************
* I'm in air

eieflp
	DS_CODEEND
	DS_SLP1

 SUBRP	drn_inair

	DS_CODE
	clr	a2
	callr	drone_seek2

	cmpi	MODE_INAIR2,b6
	jreq	eieflp

	DS_CODEEND
	DS_END


*******************************
* Opponent in air

 SUBRP	drn_opinair

	DS_CODE

	movk	1,a0
	callr	rnd
	jrnz	dwrirun			;Skip?
dwrilp
	cmpi	MODE_INAIR2,b7
	jrne	dwrik

	DS_CODEEND
	DS_SLP1
	DS_CODE

	move	*a13(CLOSEST_XDIST),a1
	move	*a13(CLOSEST_ZDIST),a14
	cmp	a1,a14
	jrge	dwribig
	move	a1,a14
dwribig
	cmpi	150,a14
	jrgt	dwrilp			;Opp far?
dwrik
	DS_CODEEND
	.word	K_M,0			;Jump up

dwrirun
	DS_CODEEND
	.word	L_M+P_M+K_M,2	;Run away
	DS_END



*******************************
* Enter ring at closest entry point

bhmhlp
	DS_CODEEND
	DS_SLP1

 SUBRP	drn_enterring

	DS_CODE

	move	*a8(INRING),a0
	jrnz	bhmhx			;Opp out?

	move	*a13(INRING),a0
	jrz	bhmhx			;In ring?

	move	*a13(OBJ_XPOSINT),a14

	movi	RING_Z_CENTER,a1
	movi	RING_X_CENTER-260,a0
	cmp	a0,a14
	jrle	bhmhsk
	movi	RING_X_CENTER+260,a0
	cmp	a0,a14
	jrge	bhmhsk

	move	*a13(OBJ_ZPOSINT),a14

	movi	RING_X_CENTER,a0
	movi	RING_TOP-10,a1
	cmp	a1,a14
	jrle	bhmhsk
	movi	RING_BOT+10,a1
bhmhsk
	movk	10,a2
	callr	drone_seekxz		;Seek to visinity
	jrnz	bhmhlp

	DS_CODEEND
	.word	P_M+SP_M+K_M+SK_M+B_M,0	;Enter

bhmhx
	DS_CODEEND
	DS_END


*******************************

 SUBRP	drn_taunt

	DS_CODE

	move	*a8(OBJ_ZPOSINT),a0
	move	*a13(OBJ_ZPOSINT),a1
	sub	a1,a0
	cmpi	100,a0
	jrlt	xmaox

	move	*a8(OBJ_XPOSINT),a0
	move	*a13(OBJ_XPOSINT),a1
	sub	a1,a0
	abs	a0
	cmpi	300,a0
	jrgt	xmaox

;Time to execute high-risk move!
	movi	8000h+6*60,a0
	move	a0,*a13(RISK)

	move	*a13(WRESTLERNUM),a0
	X32	a0
	addi	xmaotaunt_t,a0
	move	*a0,a0,L
	calla	change_anim1a
xmaox
	DS_CODEEND
	DS_END


xmaotaunt_t
	.long	hrt_4_taunt_anim		;0 Bret Hart
	.long	rzr_4_taunt_anim		;1 Razor Ramon
	.long	und_4_taunt_anim		;2 Undertaker
	.long	yok_4_taunt_anim		;3 Yokozuna
	.long	shn_4_taunt_anim		;4 Shawn Michaels
	.long	bam_4_taunt_anim		;5 Bam Bam
	.long	dnk_4_taunt_anim		;6 Doink
	.long	0		  		;7 spare
	.long	lex_4_taunt_anim		;8 Lex Luger


*******************************

 SUBRP	drn_oppdead

	DS_CODE
cgaflp
	clr	a0			;Close
	move	a0,*a13(DRN_SEEKDIR)
	move	a0,*a13(DRN_SEEKDIST)

	callr	drone_seekdirdist
	DS_CODEEND
	DS_SLP1
	DS_CODE

	movk	32,a1
	move	*a13(WRESTLERNUM),a0
	subk	2,a0
	jreq	cgafut			;Undertaker?
	movi	90,a1
cgafut
	move	*a13(CLOSEST_DIST),a0
	cmp	a1,a0
	jrle	cgafx
;	jruc	cgafnotut

	move	*a8(ANIMODE),a0		;Let ut walk through dead opps
	ori	MODE_OVERLAP,a0
	move	a0,*a8(ANIMODE)
cgafnotut

	move	*a13(DRN_JOY),a0
	jrnz	cgaflp
cgafx
	DS_CODEEND
	.word	P_M,0



*******************************
* Push stick to move drone towards his opp dir/dist seek position
* A7 = Old joy bits
* A8 = *Closest opp proc
* Trashes scratch, A2-A5

 SUBRP	drone_seekdirdist

	move	*a13(DRN_SEEKDIR),a4	;0-f
	move	*a13(DRN_SEEKDIST),a3	;0-4
	move	a3,a0
	X16	a3
	X4	a0
	add	a0,a3			;*20

	move	a4,a2
	callr	gkaqdrn_getxz
	jrnz	gkaqok

	movk	7,b0
	move	a4,a5
gkaqlp
	addk	1,a4
	sll	32-4,a4
	srl	32-4,a4
	move	a4,a2
	callr	gkaqdrn_getxz
	jrnz	gkaqnewok

	subk	1,a5
	sll	32-4,a5
	srl	32-4,a5
	move	a5,a2
	callr	gkaqdrn_getxz
	jrnz	gkaqnew5ok

	dsj	b0,gkaqlp

	clr	a0
	move	a0,*a13(DRN_JOY)

	rets

gkaqnew5ok
	move	a5,a4
gkaqnewok
	move	a4,*a13(DRN_SEEKDIR)
gkaqok
	movk	30,a2
	callr	drone_seekxz
	jrnz	gkaqx

	move	*a13(DRN_MODE),a0
	addk	1,a0
	jrge	gkaqx			;Was -1? Skip dir change

	move	a7,*a13(DRN_JOY)	;Restore to lessen glitch

	movk	3,a0			;>Get rnd +-2/3
	callr	rnd
	subk	1,a0
	jrnz	gkaqrnz
	subk	2,a0			;0 into -2
gkaqrnz
	addk	1,a0
	jrgt	gkaqrpos
	subk	2,a0
gkaqrpos

	add	a0,a4
	sll	32-4,a4
	srl	32-4,a4
	move	a4,*a13(DRN_SEEKDIR)

gkaqx
	rets


gkaqdrn_getxz
	add	a3,a2
	X16	a2
	addi	gkaqsine_t,a2

	move	*a2(4*16),a14
	move	*a8(OBJ_XPOSINT),a0
	add	a14,a0

	cmpi	RING_X_CENTER-220,a0
	jrlt	gkaqxzbad
	cmpi	RING_X_CENTER+220,a0
	jrgt	gkaqxzbad

	move	*a2,a14
	move	*a8(OBJ_ZPOSINT),a1
	add	a14,a1

	cmpi	RING_TOP,a1
	jrlt	gkaqxzbad
	cmpi	RING_BOT,a1
	jrle	gkaqxzok
gkaqxzbad
	clr	a2			;Set Z
gkaqxzok
	move	a2,a2
	rets

gkaqsine_t
	.word	-50,-46,-35,-19
	.word	0,19,35,46,50,46,35,19
	.word	0,-19,-35,-46,-50,-46,-35,-19

	.word	-100,-92,-71,-38
	.word	0,38,71,92,100,92,71,38
	.word	0,-38,-71,-92,-100,-92,-71,-38

	.word	-150,-139,-106,-57
	.word	0,57,106,139,150,139,106,57
	.word	0,-57,-106,-139,-150,-139,-106,-57

	.word	-200,-185,-141,-76
	.word	0,76,141,185,200,185,141,76
	.word	0,-76,-141,-185,-200,-185,-141,-76

	.word	-250,-231,-177,-95
	.word	0,95,177,231,250,231,177,95
	.word	0,-95,-177,-231,-250,-231,-177,-95

	.word	-300,-277,-212,-115
	.word	0,114,212,277,300,277,212,114
	.word	0,-114,-212,-277,-300,-277,-212,-115


*******************************
* Push stick to move drone towards closest opp

 SUBRP	drone_seek

	movi	70,a2

 SUBRP	drone_seek2

	move	*a8(OBJ_XPOSINT),a0
	move	*a8(OBJ_ZPOSINT),a1


*******************************
* Push stick to move drone towards an XZ location
* A0 = X to seek
* A1 = Z
* A2 = Range to stop
* A13= *Plyr proc
*0A0H = Joy bits set or 0 (Pass CC)
* Trashes scratch

 SUBRP	drone_seekxz

	move	a3,b0

	move	*a13(OBJ_XPOSINT),a3
	sub	a0,a3

	clr	a0

	move	a3,a14
	abs	a3
	sub	a2,a3
	jrle	uevyonx
	move	a14,a14
	jrlt	uevynolft
	subk	4,a0			;Left

uevynolft	addk	8,a0			;Rgt
uevyonx
	move	*a13(OBJ_ZPOSINT),a3

	sub	a1,a3
	move	a3,a14
	abs	a3
	sub	a2,a3
	jrle	uevyonz
	move	a14,a14
	jrlt	uevynoup
	subk	1,a0			;Up

uevynoup	addk	2,a0			;Dn
uevyonz
	move	a0,*a13(DRN_JOY)

	move	b0,a3
	move	a0,a0
	rets


*******************************
* Adjust drone skill level (called each round)
* 8 is typical starting difficulty
* A13 = *Plyr proc
* Trashes scratch, A2-A5

 SUBR	drone_calcskill

	move	*a13(PLYR_TYPE),a0
	jrz	dpmfx			;Human?

	move	*a13(DRN_SKILLRNDM),a0

	move	@current_round,a3	;1-3
	subk	1,a3
	jrgt	dpmfn1st

	movk	4,a0
	callr	rndrng0
	subk	2,a0
	move	a0,*a13(DRN_SKILLRNDM)
dpmfn1st
	move	@CURRENT_LADDER,a5,L	;* to position
	subi	LADDER,a5
	sra	5,a5			;/32 (Gives 0-6)
	move	a5,a1
	sra	1,a1			;0-3
	add	a1,a5			;A5=0-9
	add	a0,a5			;-2 to 2 randomness

	clr	a4
	move	@PSTATUS2,a0
	btst	0,a0
	jrnz	dpmfp1
	movk	16,a4
dpmfp1
	movi	p1winstreakd,a0
	add	a4,a0
	move	*a0,a0
	jrlt	dpmfloser			;Lost? (-2 per match lost)



	X2	a0
;	add	a0,a5			;+1 per match won
;	add	a0,a5			;+1 per match won



	add	a0,a5			;+1 per match won
dpmfloser
	X2	a0
	add	a0,a5			;+2 per match won

;	X2	a0
;	add	a0,a5			;+2 per match won
;dpmfloser
;	X2	a0
;	add	a0,a5			;+4 per match won

	movi	p1rounds,a0
	add	a4,a0
	move	*a0,a0
	add	a0,a5			;+3 per round won
	X2	a0
	add	a0,a5

	X2	a3
	sub	a3,a5			;-2 per round past 1st


	movk	ADJDIFF,a0		;Get difficulty level (1-10)
	calla	GET_ADJ
	subk	2,a0
	X2	a0
	add	a0,a5			;+8 default
	jrge	dpmfminok
	clr	a5
dpmfminok
	cmpi	29,a5
	jrle	dpmfmaxok
	movk	29,a5
dpmfmaxok
	move	a5,*a13(DRN_SKILL)

;컴컴컴컴컴컴컴			0CHlr attack count

	clr	a0
	movi	atkcnt_t,a1
	movi	AT_NUM*NUM_WRES,b0
dpmfaclp
	move	a0,*a1+
	dsj	b0,dpmfaclp

dpmfx
	rets



********************************
* Get random # with mask
* A0 = Mask
*0A0H = Rnd # (Pass CC)
* Trashes scratch

 SUBRP	rnd

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	and	a1,a0
	rets


********************************
* Quickly produce a random # in range 0-X
* A0 = X
*0A0H = Random # (0 to A0) (No CC)
* Trashes scratch

 SUBRP	rndrng0

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	addk	1,a0
	mpyu	a1,a0		;Condition codes not valid!

	rets






********************************

	.end

